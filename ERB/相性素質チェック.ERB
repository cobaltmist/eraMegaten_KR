;-------------------------------------------------
;   素質のチェック
;-------------------------------------------------
; Input:
;  ARG:0				チェック対象者
;  ARG:1				素質、または相性番号
;  ARG:2				ARG:1の番号を素質か相性でチェックする
;						0－素質番号チェック(剣撃 0	飛具 1	打撃 2	戦技 3	回復 4	支援 5	防御 6	火炎 7	氷結 8	電撃 9	衝撃 10	神経 11	精神 12	破魔 13	呪殺 14	地変 15	水撃 16	疾風 17	重力 18	核熱 19	万能 20	サイコ 21	自動効果 22)
;						1－相性番号チェック(剣撃 0	飛具 1	打撃 2	戦技 3	火炎 4	氷結 5	電撃 6	衝撃 7	神経 8	精神 9	破魔 10	呪殺 11	地変 12	水撃 13	疾風 14	重力 15	核熱 16	万能 17)
;  ARG:3				設定を無視できるか	1でできる
;						（基本的に無視できるのはスキルのみにしておく）
;-------------------------------------------------
@상성소질체크,ARG,ARG:1,ARG:2,ARG:3 = -1
#DIMS L_소질명
;설정이 무효라면 아무것도 안 한다
SIF BATTLE_SETTING_IS_TALENT() == 0 && ARG:3 < 0
	RETURN 0
SIF ARG < 0
	RETURN 0
IF ARG:2 == 0
	SIF ARG:1 > 22
		RETURN 0
	L_소질명	= %GET_SUCCESSION(ARG:1)%
ELSEIF ARG:2 == 1
	SIF ARG:1 > 19
		RETURN 0
	L_소질명	= %GET_TYPE(ARG:1)%
ELSE
	RETURN 0
ENDIF
IF ABL:ARG:종족 == 0
	;초기페르소나 또는 초기링크악마의 소질 체크
	IF TALENT:ARG:페르소나구사자 && ABL:ARG:초기페르소나 > 0
		;SIF TALENT:(ABL:ARG:초기페르소나 CFLAG:ARG:초기페르소나넘버):L_소질명;PRINTFORM {LOCAL};LOCAL = ABL:ARG:초기페르소나
		IF  EXISTCSV((ABL:ARG:초기페르소나), 0)
			SIF CSVTALENT((ABL:ARG:초기페르소나),GETNUM(TALENT,L_소질명),0)
				RETURN 1
		ENDIF
	ENDIF
	IF (TALENT:ARG:데빌시프터 || TALENT:ARG:식노 || TALENT:ARG:악마빙의자) && CFLAG:ARG:초기링크악마 > 0
		SIF CSVTALENT((CFLAG:ARG:초기링크악마),GETNUM(TALENT,L_소질명),0)
			RETURN 1
	ENDIF
ELSE
	;本人の素質チェック
	SIF TALENT:ARG:L_소질명
		RETURN 1
ENDIF
RETURN 0

