@INDIVIDUAL_EVENT_K812_AN_SOULSURRENDER,ARG
#DIMS 呼び名
#DIMS アーミヤ
#DIM ポジション確保
#DIM LCOUNT
VARSET ポジション確保
SELECTCASE CFLAG:MASTER:당신의전공분야
	CASE 1
		呼び名 = サマナー
	CASE 3,6
		呼び名 = バスター
	CASE 5
		呼び名 = 喰奴
	CASE 7
		呼び名 = ハンター
	CASE 8
		呼び名 = シフター
	CASE 10
		呼び名 = たけし
	CASEELSE
		呼び名 = %CALLNAME:MASTER%
ENDSELECT
アーミヤ = %CALLNAME:ARG%
CALL 疑似的画面クリア()
SETCOLOR COLOR("BROWN")
PRINTL
PRINTL
PRINTFORMW %アーミヤ%は夢を見ていた。
PRINTL
PRINTW 打ち合う鉄と共に響く怒号。
PRINTW 痛みと共に吐き出される悲痛の声。
PRINTW 肉が焼ける臭いと熱狂を感じさせる炎。
PRINTL
PRINTW ここは戦場──────見たことも、感じたこともないはずの惨劇の場。
PRINTFORMW その真ん中に、%アーミヤ%は周囲の熱狂に置いていかれたかのように一人立っていた。
PRINTL
PRINTW ……いや、違う。
PRINTFORMW もう一人、%アーミヤ%と同じように喧騒の波に取り残された者がいた。
PRINTL
PRINTW どこか薄幸で、あまりにも儚くて、人波に巻き込まれれば崩れてしまいそうな程に脆そうな……そんな印象がする、頭に角が生えた女の人。
PRINTFORMW 頭に角が生えた────『サルカズ』と呼ばれる種族の彼女は%アーミヤ%の視線に気づき、微笑みを返した。
PRINTL
PRINTFORMW 知っている。 %アーミヤ%は彼女を知っている。 初めてあったはずなのに。 彼女の名前を。 彼女の存在を。
PRINTW 彼女の声から伝わる優しさが。 温もりが。 悲しみが。 決意が。
PRINTFORMW 覚えているのだ。 %アーミヤ%ではなく、『アーミヤ』が。
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「アーミヤ」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW 名前を呼ばれた。
IF アーミヤ == "アーミヤ"
	PRINTW 自身のオリジンの名前であり、しかし同時に生みの親であるプロフェッサー・ヴィクトルから与えられたその名前を。
ELSE
	PRINTFORMW 自身のオリジンの名前であり、しかし新たな名である%アーミヤ%が定義されると同時に、過去に埋もれてしまったその名前を。
ENDIF
PRINTL
PRINTW 「ッ……！」
PRINTFORMW %アーミヤ%は反射的に、警戒するように構えを取った。
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「………」
SETCOLOR COLOR("BROWN")
PRINTW そんな警戒の意を見せられたサルカズの女性の、どことなく寂しそうな、だけど慈しみも感じるような微笑みは変わらず。
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「アーミヤ」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW そして『アーミヤ』の名前を呼ぶその声も、変わらず。
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「あなたは目覚めなければならないわ」
PRINTW 「たとえ今のあなたが、資格無き継承者だったとしても」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW その声が終わると同時に、周囲の景色が一変した。
PRINTL
PRINTW 肌が熱くなるほどの熱気。
PRINTW 近くに何かが爆発し、それと共に鳴り響く崩落する鉄骨の音。
PRINTW そして……
SETCOLOR COLOR("RED")
PRINTW 揺らめく炎の奥から伝わる、数多の殺意の視線。
SETCOLOR COLOR("BROWN")
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「乗り越えなさい。 かつてのあなたが乗り越えた試練を、今のあなたで────」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW その冷酷にも思える言葉と共に、炎の陰からは規律正しく前進する軍隊。
PRINTW そして、それらを率いる、龍の角が生えた一人の女性が現れた……。
PRINTL
PRINTW この数を相手に一人で迎え撃つのはあまりにも無謀だ…。 とても乗り越えられるものではない…！
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「────いいえ、大丈夫よ。 かつてのあなたも、今のあなたも、一人で戦ってたわけじゃないんだから」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTFORMW サルカズの女性の言葉と同時に、%アーミヤ%の地面に魔法の陣が描かれ、そして周囲に光の柱が立ち上がった！
;イメージは某究極幻想14漆黒編のアレ
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「率いなさい、今のあなたの仲魔を。 今までのあなたがそうしたように」
SETCOLOR COLOR("BROWN")
PRINTFORMW %CALLNAME:MASTER%の仲魔が光の柱から連鎖的に召喚された！
PRINTFORMW ……しかし肝心の%CALLNAME:MASTER%の姿が見当たらない…！
SETCOLOR COLOR("BROWN")
CALL 色変えAN("テレジア")
PRINTW 「これはあなたが越えなければならない戦いよ、アーミヤ。 あなた自身の手で、仲魔を導きなさい」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTFORMW どうやら%CALLNAME:MASTER%の召喚までは望めないようだ……。 こうなれば今ある仲魔で乗り越えるしかない。
PRINTL
PRINTW 「っ……皆さんっ、よろしくお願いします！」
PRINTL
PRINTFORMW %アーミヤ%の号令と共に、仲魔達が立ち上がる！
PRINTL
RESETCOLOR
PRINTW タルラ と 帝国の先鋒達 が 立ちはだかった！


;＠だけをハブる機械かよ！？
ポジション確保 = CFLAG:MASTER:포지션
CALL REMOVE_POSITION, CFLAG:MASTER:포지션

FLAG:도주불가플래그 = 1
FLAG:회화불능플래그 = 1
FLAG:선제기습취소 = 1

;Talulah戦。 もう歩けないタル～な闘士タルラの方ではない。
;まぁCSV用意してないからアークナイツ勢と同じく既存の改造物なんだがな、ぶぇへへ
CALL SET_ENEMY,9,[[キャラ:청룡]],70,1,3
NAME:(CHARANUM-1) = タルラ
CALLNAME:(CHARANUM-1) = タルラ
CFLAG:(CHARANUM-1):ＨＰ보정 += 7500
CFLAG:(CHARANUM-1):ＭＰ보정 += 70000
CFLAG:(CHARANUM-1):행동횟수 = 3
CFLAG:(CHARANUM-1):COOP피데미지보정 = -20
CFLAG:(CHARANUM-1):마법피데미지보정 = -50
CSTR:(CHARANUM-1):사고패턴 = TALULAH
CSTR:(CHARANUM-1):종족명 = 龍人
BASE:(CHARANUM-1):힘 += 20
BASE:(CHARANUM-1):인내력 += 20
BASE:(CHARANUM-1):속도 += 20
BASE:(CHARANUM-1):검격 = 0
MAXBASE:(CHARANUM-1):검격 = 0
BASE:(CHARANUM-1):비구 = 50
MAXBASE:(CHARANUM-1):비구 = 50
BASE:(CHARANUM-1):타격 = 50
MAXBASE:(CHARANUM-1):타격 = 50
BASE:(CHARANUM-1):화염 = 0
MAXBASE:(CHARANUM-1):화염 = 0
BASE:(CHARANUM-1):빙결 = 125
MAXBASE:(CHARANUM-1):빙결 = 125
BASE:(CHARANUM-1):전격 = 100
MAXBASE:(CHARANUM-1):전격 = 100
BASE:(CHARANUM-1):충격 = 75
MAXBASE:(CHARANUM-1):충격 = 75
BASE:(CHARANUM-1):신경 = 100
MAXBASE:(CHARANUM-1):신경 = 100
BASE:(CHARANUM-1):정신 = 100
MAXBASE:(CHARANUM-1):정신 = 100
BASE:(CHARANUM-1):파마 = 150
MAXBASE:(CHARANUM-1):파마 = 150
BASE:(CHARANUM-1):주살 = 0
MAXBASE:(CHARANUM-1):주살 = 0
BASE:(CHARANUM-1):수격 = 150
MAXBASE:(CHARANUM-1):수격 = 150
BASE:(CHARANUM-1):질풍 = 100
MAXBASE:(CHARANUM-1):질풍 = 100
BASE:(CHARANUM-1):중력 = 75
MAXBASE:(CHARANUM-1):중력 = 75
BASE:(CHARANUM-1):핵열 = 50
MAXBASE:(CHARANUM-1):핵열 = 50
BASE:(CHARANUM-1):HAPPY = -100
BASE:(CHARANUM-1):PANIC = -100
BASE:(CHARANUM-1):CHARM = -100
BASE:(CHARANUM-1):SLEEP = -100
BASE:(CHARANUM-1):FLAME = -100
CSTR:(CHARANUM-1):전용기1 = 日輪
CSTR:(CHARANUM-1):전용기2 = 灼熱の息吹
ABL:(CHARANUM-1):스킬1  = [[스킬:전용기1]]
ABL:(CHARANUM-1):스킬2  = [[스킬:전용기2]]
ABL:(CHARANUM-1):스킬3  = [[스킬:브레이브재퍼]]
ABL:(CHARANUM-1):스킬4  = [[스킬:데들리번]]
ABL:(CHARANUM-1):스킬5  = [[스킬:히트라이저]]
ABL:(CHARANUM-1):스킬6  = [[스킬:데・쿤다]]
ABL:(CHARANUM-1):스킬7  = [[스킬:컨센트레이트]]
ABL:(CHARANUM-1):스킬8  = [[스킬:테트라코와스]]
ABL:(CHARANUM-1):스킬9  = [[스킬:데・카쟈]]
ABL:(CHARANUM-1):스킬10  = [[스킬:화염가드브레이크]]
ABL:(CHARANUM-1):스킬11  = [[스킬:주살가드브레이크]]
ABL:(CHARANUM-1):스킬12  = [[스킬:검격가드브레이크]]
ABL:(CHARANUM-1):스킬14  = [[스킬:비구가드브레이크]]
ABL:(CHARANUM-1):스킬13  = [[스킬:브레이브스텝]]
CALL SYNC_STATUS,(CHARANUM-1)
CALL HEALTH_CHARA,(CHARANUM-1)

;帝国砲撃誘導機。
CALL SET_ENEMY,14,[[キャラ:T95메두사]],68,1,1
NAME:(CHARANUM-1) = 帝国砲撃誘導機
CALLNAME:(CHARANUM-1) = 帝国砲撃誘導機
CFLAG:(CHARANUM-1):ＨＰ보정 += 4500
CFLAG:(CHARANUM-1):ＭＰ보정 += 70000
CFLAG:(CHARANUM-1):마법피데미지보정 = -30
CSTR:(CHARANUM-1):사고패턴 = 帝国砲撃誘導機
CSTR:(CHARANUM-1):전용기1 = 爆撃（ＡＮ）
BASE:(CHARANUM-1):힘 += 10
BASE:(CHARANUM-1):인내력 += 30
BASE:(CHARANUM-1):속도 += 10
BASE:(CHARANUM-1):검격 = 50
MAXBASE:(CHARANUM-1):검격 = 50
BASE:(CHARANUM-1):비구 = 125
MAXBASE:(CHARANUM-1):비구 = 125
BASE:(CHARANUM-1):타격 = 50
MAXBASE:(CHARANUM-1):타격 = 50
BASE:(CHARANUM-1):전술 = 50
MAXBASE:(CHARANUM-1):전술 = 50
BASE:(CHARANUM-1):충격 = 125
MAXBASE:(CHARANUM-1):충격 = 125
BASE:(CHARANUM-1):화염 = 100
MAXBASE:(CHARANUM-1):화염 = 100
BASE:(CHARANUM-1):전격 = 150
MAXBASE:(CHARANUM-1):전격 = 150
BASE:(CHARANUM-1):수격 = 150
MAXBASE:(CHARANUM-1):수격 = 150
BASE:(CHARANUM-1):핵열 = 100
MAXBASE:(CHARANUM-1):핵열 = 100
;マシンなので精神系バステは受けない…受けないが、SLEEPは普通に通す謎
BASE:(CHARANUM-1):HAPPY = -100
BASE:(CHARANUM-1):PANIC = -100
BASE:(CHARANUM-1):CHARM = -100
ABL:(CHARANUM-1):스킬1  = [[스킬:전용기1]]
ABL:(CHARANUM-1):스킬2  = [[스킬:스쿤・다]]
ABL:(CHARANUM-1):스킬3  = 0
ABL:(CHARANUM-1):스킬4  = 0
ABL:(CHARANUM-1):스킬5  = 0
ABL:(CHARANUM-1):스킬6  = 0
ABL:(CHARANUM-1):스킬7  = 0
ABL:(CHARANUM-1):스킬8  = 0
CALL SYNC_STATUS,(CHARANUM-1)
CALL HEALTH_CHARA,(CHARANUM-1)


FOR LCOUNT,7,17
	;タルラと帝国砲撃誘導機のポジションは触らないようにする
	SIF LCOUNT == 9 || LCOUNT == 14
		CONTINUE
	;帝国精鋭先鋒
	CALL SET_ENEMY,LCOUNT,[[キャラ:터미네이터]],65,1,1
	NAME:(CHARANUM-1) = 帝国精鋭先鋒
	CALLNAME:(CHARANUM-1) = 帝国精鋭先鋒
	CFLAG:(CHARANUM-1):ＨＰ보정 += 2400
	CFLAG:(CHARANUM-1):ＭＰ보정 += 20000
	CSTR:(CHARANUM-1):사고패턴 = 帝国精鋭先鋒
	CSTR:(CHARANUM-1):종족명 = ウルサス
	BASE:(CHARANUM-1):힘 += 15
	BASE:(CHARANUM-1):인내력 += 15
	BASE:(CHARANUM-1):속도 += 10
	BASE:(CHARANUM-1):검격 = 75
	MAXBASE:(CHARANUM-1):검격 = 75
	BASE:(CHARANUM-1):비구 = 75
	MAXBASE:(CHARANUM-1):비구 = 75
	BASE:(CHARANUM-1):타격 = 75
	MAXBASE:(CHARANUM-1):타격 = 75
	BASE:(CHARANUM-1):전술 = 75
	MAXBASE:(CHARANUM-1):전술 = 75
	BASE:(CHARANUM-1):화염 = 100
	MAXBASE:(CHARANUM-1):화염 = 100
	BASE:(CHARANUM-1):빙결 = 100
	MAXBASE:(CHARANUM-1):빙결 = 100
	BASE:(CHARANUM-1):충격 = 100
	MAXBASE:(CHARANUM-1):충격 = 100
	BASE:(CHARANUM-1):신경 = 75
	MAXBASE:(CHARANUM-1):신경 = 75
	BASE:(CHARANUM-1):정신 = 75
	MAXBASE:(CHARANUM-1):정신 = 75
	BASE:(CHARANUM-1):주살 = 100
	MAXBASE:(CHARANUM-1):주살 = 100
	BASE:(CHARANUM-1):지변 = 100
	MAXBASE:(CHARANUM-1):지변 = 100
	BASE:(CHARANUM-1):수격 = 100
	MAXBASE:(CHARANUM-1):수격 = 100
	BASE:(CHARANUM-1):핵열 = 100
	MAXBASE:(CHARANUM-1):핵열 = 100
	ABL:(CHARANUM-1):스킬1  = [[스킬:그랜드턱]]
	ABL:(CHARANUM-1):스킬2  = [[스킬:낭아천주참]]
	ABL:(CHARANUM-1):스킬3  = [[스킬:삼단쏘기]]
	ABL:(CHARANUM-1):스킬4  = [[스킬:타루・카쟈]]
	ABL:(CHARANUM-1):스킬5  = [[스킬:마카・카쟈]]
	ABL:(CHARANUM-1):스킬6  = [[스킬:스쿠・카쟈]]
	ABL:(CHARANUM-1):스킬7  = [[스킬:마의도눔]]
	ABL:(CHARANUM-1):스킬8  = [[스킬:메・디아라마]]
	CALL SYNC_STATUS,(CHARANUM-1)
	CALL HEALTH_CHARA,(CHARANUM-1)
NEXT



CALL BATTLE_START

FLAG:도주불가플래그 = 0
FLAG:회화불능플래그 = 0
FLAG:선제기습취소 = 0

;もし元々＠がいたポジションが既に埋まっていた場合の処理
SELECTCASE POS(ポジション確保)
	CASE ARG
		;アーミヤだった場合、上もしくは下のポジション番号のキャラを外す
		SIF CPOS(ARG) == 1
			LCOUNT = ポジション確保 + 1
		SIF CPOS(ARG) == 6
			LCOUNT = ポジション確保 - 1
		CALL REMOVE_POSITION,LCOUNT
		;ポジション変更メッセージ表示用に値を設定
		LCOUNT = -10
	CASE IS > 0
		CALL REMOVE_POSITION,ポジション確保
		;ポジション変更メッセージ表示用に値を設定
		LCOUNT = -10
ENDSELECT
CALL INSERT_POSITION,ポジション確保,MASTER

CALL 疑似的画面クリア()
PRINTL …
PRINTL ……
PRINTL ………
PRINTL …………
PRINTL
SETCOLOR COLOR("BROWN")
PRINTFORMW %アーミヤ%達の奮闘により、仲魔達はなんとか敵を打ち倒すことが出来た。
PRINTW サルカズの女性が言う『試練』を達成したからなのか、夢の風景がその役割を終えるかのようにひび割れ始めた……。
PRINTL
PRINTFORMW そして空間の崩壊は%アーミヤ%の足元の地面にも及び、足場を失った%アーミヤ%と仲魔たちは虚空の彼方へと落ちて行った……。
RESETCOLOR
PRINTL
PRINTL …
PRINTL ……
PRINTL ………
PRINTL …………
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「試練達成おめでとう、アーミヤ」
IF アーミヤ != "アーミヤ"
	PRINTFORMW 「あぁ、いえ……今のあなたは%アーミヤ%、だったわね」
ENDIF
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW 気が付けば白色で染められた空間と、いつぞやのサルカズの女性が視界に入っていた。
PRINTW いつの間にか謎の空間に跳ばされていたようだ……。
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「本来であれば今のあなたに逢う事はなかったのだけれど…」
PRINTW 「今回は変わった金髪のかっこいい人にお願いされてね、少しだけお節介を焼く事になったの」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW サルカズの女性はそう語りながら、こちらへと近づいてきた。
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「お手、少し触るわね」
SETCOLOR COLOR("BROWN")
PRINTFORMW 彼女はそう言いながら、%アーミヤ%の右手を両手で優しく包み、そして互いの目の前へと運び移す。
PRINTL
CALL 色変えAN("テレジア")
PRINTFORMW 「%アーミヤ%。 あなたはこれから先、大きな困難の壁にぶつかる時が来るわ」
PRINTFORMW 「その困難に挑む時、今のあなたでは力及ばず、太刀打ちできないかもしれない」
PRINTL
PRINTFORMW 「だから……今あなたに眠っている力、その一つを目覚めさせるわ」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTFORMW どうやらこのサルカズの女性は、%アーミヤ%のオリジンに深く関係のある人物のようだ……
PRINTW 一体何者なのだろうか…？
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「んー？ ふふっ、そうね……親代わりのお姉ちゃん、とでも言っておきましょうか」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW なるほど……道理でどことなく、安心感を覚えた事に納得できた。
PRINTFORMW しかし……ここからどうやって、%アーミヤ%に眠る力を解放できるのだろうか？
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「そうね…先ほど力を目覚めさせるとは言ったけど」
PRINTW 「実を言うと、今の私が与えられるのはきっかけだけで、解放まではできないのよね」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW それ詐欺では？
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「もう！ そんな酷い事を言うのはやめて」
PRINTW 「確かに私からは解放させてあげられないのは悪かったけど、過程はどうあれ力の解放自体は起こるのだからいいでしょう？」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW そう言ってサルカズの女性は頬を膨らませながら捲し立てた。
PRINTFORMW %アーミヤ%としてはただ単に軽い冗談を投げてみただけだが、やはりこのサルカズの女性は雰囲気に反して感性豊かのようだ。
PRINTW それはさておき、彼女では力を目覚めさせられないというのなら、やはり他の要因で解放するのだろうか？
PRINTL
CALL 色変えAN("テレジア")
PRINTFORMW 「いいえ、力の解放は%アーミヤ%、あなた自身の手で行うのよ」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTFORMW まさかの丸投げである。 %アーミヤ%は困惑した。
PRINTL
CALL 色変えAN("テレジア")
PRINTFORMW 「大丈夫。 目を閉じて、心を落ち着かせて、声に耳を澄ませなさい。 私の言う通りにすれば、きっと上手くいくわ」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTFORMW サルカズの女性の言葉に若干の猜疑心を抱くも、%アーミヤ%は渋々その言葉に従い、精神を集中させた……。
RESETCOLOR
PRINTL
PRINTL …
PRINTL ……
PRINTL ………
PRINTL …………
PRINTL
SETCOLOR 0x7f7fff
PRINTFORMW %アーミヤ%の脳裏に様々なビジョンが浮かび上がっていく……
PRINTL
PRINTL
PRINTW 炎に焼かれる大地と、その光景に憤怒の叫びを上げるサルカズの男の姿がいた。
PRINTL
PRINTW 守るべき民を、憎悪によってその手で焼き尽くしてしまった龍人の女の姿がいた。
PRINTL
PRINTW 誤った道を歩んだ親愛なる者を止める為に、ひたすらに突き進む赤き剣の持ち主の姿がいた。
PRINTL
PRINTW そして、死なずの悪神が放つ炎を斬り捨てた、小さな魔王の姿がいた。
PRINTL
PRINTW …
CLEARLINE 1
PRINTW ……
CLEARLINE 1
PRINTW ………
CLEARLINE 1
PRINTL
PRINTFORMW 存在しない記憶のビジョンでしかなかった魔王は、いつしか鏡合わせのように%アーミヤ%の前に立っていた。
PRINTL
PRINTL
PRINTFORMW 少しの間を置いた後に、小さな魔王は%アーミヤ%に剣の柄を差し出し、そして囁いた。
PRINTL
PRINTFORMW ……声は聞こえなかった。
PRINTL
PRINTFORMW だが、%アーミヤ%は直感的に、その言葉を理解した。
PRINTL
PRINTL
SETCOLOR COLOR("BROWN")
PRINTW 『次はお前の番だ』
SETCOLOR 0x7f7fff
PRINTL
PRINTL
PRINTFORM ……%アーミヤ%は、オリジンから憤怒の剣───
SETCOLOR COLOR("GRAY")
PRINTFORM 『影霄』
SETCOLOR 0x7f7fff
PRINTW を受け継いだ。
RESETCOLOR
PRINTL
PRINTL …
PRINTL ……
PRINTL ………
PRINTL …………
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「ね？ 私の言った通り、上手くいったでしょう？」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW 追想からの目覚めと同時に、サルカズの女性の声が響く。
PRINTW 色々と不思議な光景を見せられた上に、記憶の中にいるオリジンから力を受け取りはしたが……
PRINTW …実の所、あまり実感が沸かない。
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「あら、そう？ 私から見たら随分と様変わりしたと思うけど」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW サルカズの女性はああ言っているが、本当にそうだろうか？
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「ほら、右手をごらんなさい」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW 言われるがままに右手を見やれば、いつの間にか黒い甲冑が右腕まで覆われていた。
PRINTFORMW ───そして、オリジンから受け継いだ『影霄』もまた、%アーミヤ%の右手の中に握られてあった。
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「身体に馴染み過ぎて変化した事に気づかなかったみたいね、悪魔だから感覚的に鈍くなっちゃうのかしら」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW 彼女の言う通り、あまりにも馴染み過ぎて違和感を感じられなかった。
PRINTW 改めて確認するように自身の身体を動かすと、身体が剣を持った戦い方に違和感なく付いてこれている。 戦闘方法に合わせて身体が最適化されたかのようだ。
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「その様子だと身体への負担もあまり大きくなさそう…うん、帰る前に心配事が一つ減って安心したわ」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW しばらく身体を動かしていたが、どうやらサルカズの女性は帰ってしまうらしい。
PRINTW 試練を吹っ掛けられたと思ったら、新たな力への目覚めを手助けしてくれたりしたが…
PRINTW 結局何者だったのかは分からないまま別れるというのに、なぜか不思議と寂しい思いを抱いてしまう。
PRINTL
CALL 色変えAN("テレジア")
PRINTW 「ふふっ、そんなに寂しがらなくても大丈夫よ。 一生のお別れって訳じゃないんだから」
PRINTFORMW 「本来であれば会う事のない出会いだったけど…あなたとお話出来て楽しかったわ、%アーミヤ%」
PRINTW 「色々大変だとは思うけど、誰かの助けになれるように頑張りなさい」
PRINTW 「あ、もし金髪のお兄さんに出会えたら、『よろしく』って伝えてあげてね？」
SETCOLOR COLOR("BROWN")
PRINTL
PRINTW その言葉を最後に、サルカズの女性の気配が消えてしまった。
PRINTL
PRINTFORMW それと同時に%アーミヤ%の意識も遠のいてしまった……。
CALL 疑似的画面クリア()
RESETCOLOR
PRINTL …
PRINTL ……
PRINTL ………
PRINTL …………
PRINTL
RESETCOLOR
PRINTFORML %アーミヤ%は前衛クラスにクラスチェンジ出来るようになった…！
PRINTFORMW これに伴い、アーミヤのスキル強化イベントにてクラスチェンジ機能が解禁された！


@SET_ACTION_TALULAH, ARG
#LOCALSIZE 2
#LOCALSSIZE 1
#DIM LCOUNT
#DIM 反射チェック
#DIM 相性チェック
#DIM 強化解除チェック
#DIM 弱体解除チェック
IF CFLAG:ARG:１more플래그 > 0
	VARSET 反射チェック,0
	VARSET 相性チェック,0
	VARSET 強化解除チェック,0
	VARSET 弱体解除チェック,0
	VARSET LOCALS
	;ワンモア時、自身に不利な状況を改善する手段を取る
	;反射解除　＞　相性貫通　＞　強化解除　＞ 弱体解除 ＞ コンセントレイト　の優先順位
	FOR LCOUNT,1,7
		LOCAL = POS(LCOUNT)
		SIF LOCAL < 0
			CONTINUE
		;反射チェック立ってない時に同時反射してる奴がいたらフラグが立つ
		SIF 反射チェック == 0 && CFLAG:LOCAL:마법반사플래그 > 0 && CFLAG:LOCAL:물리반사플래그 > 0
			反射チェック = 1
		IF 相性チェック == 0
			IF CFLAG:ARG:주살가드킬 == 0 && (MAXBASE:LOCAL:주살 < 100 || MAXBASE:LOCAL:주살 == 999)
				LOCALS = 呪殺
			ENDIF
			IF CFLAG:ARG:검격가드킬 == 0  && (MAXBASE:LOCAL:검격 < 100 || MAXBASE:LOCAL:검격 == 999)
				LOCALS = 剣撃
			ENDIF
			IF CFLAG:ARG:비구가드킬 == 0 && (MAXBASE:LOCAL:비구 < 100 || MAXBASE:LOCAL:비구 == 999)
				LOCALS = 飛具
			ENDIF
			IF CFLAG:ARG:화염가드킬 == 0 && (MAXBASE:LOCAL:화염 < 100 || MAXBASE:LOCAL:화염 == 999)
				LOCALS = 火炎
			ENDIF
			SIF LOCALS != ""
				相性チェック = 1
		ENDIF
		強化解除チェック = ( CFLAG:LOCAL:공격강화 + CFLAG:LOCAL:방어강화 + CFLAG:LOCAL:명중강화 + CFLAG:LOCAL:회피강화 + CFLAG:LOCAL:마법위력강화 + CFLAG:LOCAL:마법효과강화 ) / 6
	NEXT
	IF 強化解除チェック >= 48
		強化解除チェック = 1
	ELSE
		強化解除チェック = 0
	ENDIF
	IF GROUPMATCH(-16,CFLAG:ARG:공격강화,CFLAG:ARG:방어강화,CFLAG:ARG:마법위력강화,CFLAG:ARG:마법효과강화,CFLAG:ARG:명중강화,CFLAG:ARG:회피강화)
		弱体解除チェック = 1
	ENDIF
	IF 反射チェック == 1
		CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:테트라코와스]]
	ELSEIF 相性チェック == 1
		SELECTCASE LOCALS
			CASE "剣撃"
				CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:검격가드브레이크]]
			CASE "火炎"
				CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:화염가드브레이크]]
			CASE "呪殺"
				CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:주살가드브레이크]]
		ENDSELECT
	ELSEIF 強化解除チェック == 1
		CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:데・카쟈]]
	ELSEIF 弱体解除チェック == 1
		CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:데・쿤다]]
	ELSE
		CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:컨센트레이트]]
	ENDIF
ELSEIF CFLAG:ARG:행동한횟수 == 0
	IF 현HP비율(ARG) < 50
		IF RAND:3 == 0
			CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:화염가드킬]]
		ELSEIF RAND:2 == 0
			CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:브레이브재퍼]]
		ELSE
			CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:컨센트레이트]]
		ENDIF
	ELSE
		SELECTCASE FLAG:경과턴수 % 5
			CASE 0
				CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:데들리번]]
			CASE 1
				CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:컨센트레이트]]
			CASE 2
				CFLAG:ARG:타겟 = CPOS(ARG)
				CFLAG:ARG:입력행동 = [[스킬:히트라이저]]
			CASE 3
				CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:브레이브재퍼]]
			CASEELSE
				CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:브레이브스텝]]
		ENDSELECT
	ENDIF
ELSEIF CFLAG:ARG:행동한횟수 == 1
	IF SKILLGAGE_H_GET(ARG,[[스킬:전용기2]]) >= 19
		CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:전용기2]]
	ELSEIF SKILLGAGE_H_GET(ARG,[[스킬:전용기1]]) >= 40 
		CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:전용기1]]
	ELSE
		IF 현HP비율(ARG) < 75
			SELECTCASE FLAG:경과턴수 % 5
				CASE 0
					CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:데들리번]]
				CASE 1
					CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:컨센트레이트]]
				CASE 2
					CFLAG:ARG:타겟 = CPOS(ARG)
					CFLAG:ARG:입력행동 = [[스킬:히트라이저]]
				CASE 3
					CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:브레이브재퍼]]
				CASEELSE
					CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:브레이브스텝]]
			ENDSELECT
		ELSE
			;HPが75以上の時は追加行動しない
			CFLAG:ARG:입력행동 = -1
		ENDIF
	ENDIF
ELSE
	IF SKILLGAGE_H_GET(ARG,[[스킬:전용기1]]) >= 40
		CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:전용기1]]
	ELSEIF SKILLGAGE_H_GET(ARG,[[스킬:전용기2]]) >= 19
		CALL CHECK_ACTIONABLE_AND_SET, ARG, [[스킬:전용기2]]
	ELSE
		;基本的に行動はしない
		CFLAG:ARG:입력행동 = -1
	ENDIF
ENDIF

@SET_ACTION_帝国砲撃誘導機,ARG

IF CFLAG:ARG:１more플래그 > 0
	;ワンモア時はスク・ンダを唱えるだけ
	CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:스쿤・다]]
ELSE
	IF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"爆撃（ＡＮ）")) > 0
		CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:전용기1]]
	ELSE
		;対象を見つけられなかった時は無防備
		CFLAG:ARG:입력행동 = -1
		CFLAG:ARG:방어플래그 = 0
	ENDIF
ENDIF

@SET_ACTION_帝国精鋭先鋒,ARG
#DIM LCOUNT,2
#DIM CHARA
#DIM 能力強化可能フラグ,6
#DIM COUNTER,7
IF CFLAG:ARG:１more플래그 > 0
	;ワンモア時はタルラにドナム付ける　＞　回復　＞　能力強化
	VARSET COUNTER
	VARSET 能力強化可能フラグ,0
	;フィールド全体のエネミーを検索
	FOR LCOUNT,7,17
		;ポジションからキャラ番号獲得
		CHARA = POS(LCOUNT)
		;存在しており、尚且つDYINGではない
		IF CHARA > -1 && GET_STATE(CFLAG:CHARA:상태이상) != "DYING"
			;50未満なら回復対象として数える
			SIF 현HP비율(CHARA) > 50
				COUNTER:0 ++
			;そのキャラの戦闘能力を確認し、最大値で尚且つ要素数が5未満ならばカジャフラグを立てる
			FOR LCOUNT:1,1,7
				IF CFLAG:CHARA:(GET_BATTLESTATUS(LCOUNT:1) + "強化") < 32 && 能力強化可能フラグ:0 < 5
					;COUNTER:1に該当するカジャのスキル番号を代入
					SELECTCASE LCOUNT:1
						CASE 1
							COUNTER:1 = [[스킬:타루・카쟈]]
						CASE 2,4
							COUNTER:1 = [[스킬:스쿠・카쟈]]
						CASE 3
							COUNTER:1 = [[스킬:라쿠・카쟈]]
						CASE 5
							COUNTER:1 = [[스킬:마카・카쟈]]
						CASE 6
							COUNTER:1 = [[스킬:사마・카쟈]]
					ENDSELECT
					;既にそのカジャがいずれかの配列に代入されてたら跳ばす
					SIF MATCH(能力強化可能フラグ,COUNTER:1)
						CONTINUE
					;登録されていないカジャの場合、能力強化可能フラグ:0の値を１増加
					能力強化可能フラグ:0 ++
					;COUNTER:1に代入されたスキル番号を指定された能力強化可能フラグに代入する
					能力強化可能フラグ:(能力強化可能フラグ:0) = COUNTER:1
				ENDIF
			NEXT
		ENDIF
	NEXT
	;回復対象が二人以上の場合は回復する
	IF COUNTER:0 >= 2
		CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:메・디아라마]]
	ENDIF
	;が、能力強化可能フラグが立ってたら能力強化を優先する
	IF 能力強化可能フラグ:0 > 0
		;発狂中は天上の舞でタルラの支援を優先する
		IF 현HP비율(ARG) < 50
			CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:천상의춤]]
		ELSE
			CALL CHECK_ACTIONABLE_AND_SET,ARG,能力強化可能フラグ:(RAND:(能力強化可能フラグ:0))
		ENDIF
	ENDIF

	;それはそれとしてタルラが生存中の場合は魔のドナムを付与するのを優先する
	CHARA = FINDCHARA(CSTR:사고패턴,"TALULAH")
	IF CHARA > -1 && CFLAG:CHARA:타인집중플래그 == 0
		CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:마의도눔]]
	ENDIF
ELSE
	;ポジションに応じて行動を変化
	SELECTCASE CFLAG:ARG:포지션
		CASE IS > 11
			IF 현HP비율(ARG) < 50
				SELECTCASE RAND:2
					CASE 0
						CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:그랜드턱]]
					CASE 1
						CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:삼단쏘기]]
				ENDSELECT
			ELSE
				SELECTCASE RAND:4
					CASE 0
						CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:그랜드턱]]
					CASE 1
						CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:타루・카쟈]]
					CASE 2
						CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:마카・카쟈]]
					CASE 3
						CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:스쿠・카쟈]]
				ENDSELECT
			ENDIF
		CASE IS < 12
			;前列の場合の行動
			SELECTCASE RAND:2
				CASE 0
					CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:낭아천주참]]
				CASE 1
					CALL CHECK_ACTIONABLE_AND_SET,ARG,[[스킬:삼단쏘기]]
			ENDSELECT
	ENDSELECT
ENDIF

@EVENT_BATTLE_ACTEND_INDIVIDUALEVENT_K812_1_AN
#DIM CHARA
#DIM 解説フラグ
#DIM タルラ強化フラグ
#DIM 先鋒強化フラグ,8
#DIM LCOUNT

IF FLAG:경과턴수 == 0 && (タルラ強化フラグ != 0 || MATCH(先鋒強化フラグ,1))
	タルラ強化フラグ = 0
	解説フラグ = 0
	VARSET 先鋒強化フラグ,0
ENDIF

CHARA = FINDCHARA(CSTR:사고패턴,"TALULAH")


IF CHARA > -1 && GET_STATE(CFLAG:CHARA:상태이상) != "DYING"
	IF 解説フラグ == 0 && GETBIT(CFLAG:CHARA:받음플래그,19)
		;解説フラグが立ってなくて、尚且つタルラが魔法ダメージを受けた時に解説を設ける
		PRINTFORMW どうやらタルラには魔法ダメージが効きづらいようだ…！
		解説フラグ = 1
	ENDIF
	IF 현HP비율(CHARA) <= 50 && タルラ強化フラグ == 0
		SETCOLOR COLOR("RED")
		PRINTL
		PRINTFORMW %CALLNAME:CHARA%は妖しげな笑みを浮かべると同時に、圧倒するプレッシャーを解き放った！
		RESETCOLOR
		PRINTFORML %CALLNAME:CHARA%の受ける魔法ダメージがほとんど軽減されるようになった！
		PRINTFORMW %CALLNAME:CHARA%の受ける物理ダメージが大きく軽減されるようになった！
		PRINTL
		タルラ強化フラグ = 1
		CFLAG:CHARA:COOP피데미지보정 = -30
		CFLAG:CHARA:마법피데미지보정 = -90
		CFLAG:CHARA:물리피데미지보정 = -50
	ENDIF
ENDIF


FOR LCOUNT,0,8
	CHARA = FINDCHARA(CSTR:사고패턴,"帝国精鋭先鋒") + LCOUNT
	SIF CHARA < 0 || GET_STATE(CFLAG:CHARA:상태이상) == "DYING"
		CONTINUE
	IF 현HP비율(CHARA) <= 50 && 先鋒強化フラグ:LCOUNT == 0
		SETCOLOR COLOR("YELLOW")
		PRINTL
		PRINTFORMW 追い詰められた%CALLNAME:CHARA%は不屈の心で自身の士気を引き上げた！
		RESETCOLOR
		PRINTFORMW %CALLNAME:CHARA%の与える物理ダメージが大きく上昇した！
		先鋒強化フラグ:LCOUNT = 1
		CFLAG:CHARA:물리여데미지보정 = 50
	ENDIF
NEXT

@EVENT_BATTLE_TURNEND_INDIVIDUALEVENT_K812_1_AN
#DIM CHARA
#DIM 攻撃対象
#DIM LCOUNT

CHARA = FINDCHARA(CSTR:사고패턴,"帝国砲撃誘導機")
IF CHARA > -1 && GET_STATE(CFLAG:CHARA:상태이상) != "DYING"
	PRINTL
	PRINTFORML %CALLNAME:CHARA%は攻撃対象を探している……
	$LAUNCHER_TARGET_LOOP
	攻撃対象 ++
	SIF POS(攻撃対象) < 0
		GOTO LAUNCHER_TARGET_LOOP
	PRINT どうやら次の攻撃対象は
	SETCOLOR COLOR("YELLOW")
	PRINTFORM %CALLNAME:POS(攻撃対象)%
	RESETCOLOR
	PRINTW のようだ…！
	CALL SKILLGAGE_H_SET(CHARA,PU_SKILLNUM_GET(CHARA,"爆撃（ＡＮ）"),攻撃対象) 
	SIF 攻撃対象 >= 6
		攻撃対象 = 0
ENDIF

CHARA = FINDCHARA(CSTR:사고패턴,"TALULAH")
IF CHARA > -1 && GET_STATE(CFLAG:CHARA:상태이상) != "DYING"
	IF SKILLGAGE_F_GET(CHARA,PU_SKILLNUM_GET(CHARA,"日輪")) >= 40
		SETCOLOR COLOR("RED")
		PRINTW 嫌な予感がする………！
		RESETCOLOR
	ENDIF
ENDIF

@色変えAN,ARGS
SELECTCASE ARGS
	CASE "アーミヤ"
		SETCOLOR COLOR("BROWN")
	CASE "テレジア"
		SETCOLOR 0xa898ab
ENDSELECT