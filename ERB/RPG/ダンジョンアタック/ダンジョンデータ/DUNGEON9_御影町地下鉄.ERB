
;=======================================================================
;地下鉄
;ボス・なし
;던전ナンバー9　ＭＡＰ0~2
;던전플래그:9:0 이누가미撃破
;던전플래그:9:1 クリアフラグ
;던전플래그:9:2 プラザマウォール消失の日数판정トリガー
;던전플래그:9:3 開始메시지
;던전플래그:9:4 コインロッカーベイビーフラグ
;=======================================================================




;=======================================================================
;攻略조건
;=======================================================================
@PREREQUISITE_DUNGEON_9
;에코빌딩クリア+P1ルート未クリア+プラズマウォール消失をクリアしてない
;または主人がエルミン出身+P1ルート未クリア+プラズマウォール消失をクリアしてない
IF (던전플래그:1:1 == 1 || CFLAG:MASTER:72 == 21) && 던전플래그:11:7 != 1 && 이벤트플래그:24:0 == 0
	SIF 던전플래그:9:2 == 0
		던전플래그:9:2 = DAY
	RETURN 1
ELSE
	RETURN 0
ENDIF


;=======================================================================
;던전名
;=======================================================================
@GET_DUNGEON_NAME_9
RESULTS = 미카게쵸지하철역


;=======================================================================
;フ로어名표시
;=======================================================================
@FLOORNAME_9
PRINTL 미카게쵸지하철역
SELECTCASE FLAG:현M
	CASE 0
		PRINTL 플랫폼 주변
	CASE 1
		PRINTL 서쪽 통로
	CASE 2
		PRINTL 동쪽 통로
	CASEELSE
		PRINTL
ENDSELECT

;=======================================================================
;特殊タイル표시
;=======================================================================
@TILE_MARK_9_2_7_2
RESULTS = Ｔ


;=======================================================================
;スタート地点입력
;=======================================================================
@START_DUNGEON_9,ARG
FLAG:현M = 0
FLAG:현X = 13
FLAG:현Y = 0
;CALL AUTOMAP

;=======================================================================
;スタート時이벤트
;=======================================================================
@EVENT_DUNGEON_START_9
IF 던전플래그:9:3++ == 0
	CALL SHOW_PICTURE, "D"
	IF CFLAG:MASTER:72 == 21
		CALL MESSAGE_WINDOW_D, "", @"＞미카게쵸라고 하는 마을에서 이계화 사건이 발생했다/＞미카게쵸라면 %CALLNAME:MASTER%이 다니던 성 엘민학원이 있는 거리다/＞미카게쵸의 주위에는 결계가 쳐져서 출입 할 수 없게 되었지만、/＞결계에 소규모 구멍을 뚫고 침입하는 일은 가능하다"
		CALL MESSAGE_WINDOW_D, "", @"＞쓸데없이 눈에 띄이는 것을 피하기 위해서, 이번에는 지하철 선로를 이용해 침입하기로 했다/＞……이제 팬텀의 다크서머너가 된 %CALLNAME:MASTER%이 원래대로 복구된 학교에 다닐 수 있을거라 생각되지는 않지만/＞그래도 이 일이 눈에 띄인 이상 맡지 않을 수 없었다"
		CALL MESSAGE_WINDOW_D, "", @"＞……라고는 하지만、정만으로 움직인 것은 아니다/＞%CALLNAME:MASTER%이 미카게쵸에 익숙하다는 것은、이번 일을 하는데 있어서 우위를 차지할 수 있는 한 수가 될 것이다……"
	ELSE
		CALL MESSAGE_WINDOW_D, "", "＞미카게쵸라고 하는 마을에서 이계화 사건이 발생했다/＞해결하면 상당한 보상금이 나온다고 한다/＞미카게쵸의 주위에는 결계가 쳐져서 출입 할 수 없게 되었지만、/＞결계에 소규모 구멍을 뚫고 침입하는 일은 가능하다"
		CALL MESSAGE_WINDOW_D, "", "＞쓸데없이 눈에 띄이는 것을 피하기 위해, 이번에는 지하철 선로를 이용해 침입하기로 했다/＞그러나 보상금이 나온다고 하는 말은、/＞이 이계화 사건의 조기 해결을 바라고 있는 스폰서가 있다는 것일까……?"
		CALL MESSAGE_WINDOW_D, "", @"＞……그것은 생각할 필요가 없는 일이다/＞%조사처리(CALLNAME:MASTER,"는")% 그 이상 생각하는 것을 그만두었다"
	ENDIF
ENDIF

;=======================================================================
;指定したマスに進入가능か
;=======================================================================
@CAN_ENTER_9,ARG,ARG:1
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT


;=======================================================================
;フ로어데이터
;=======================================================================
@MAKE_FLOOR_9
SELECTCASE FLAG:현M
	CASE 0
		FLAG:최대X = 17
		FLAG:최대Y = 9
		;이벤트の設置
		CALL MAKE_FLOOR_LINE, 0,"00000000000008000"
		CALL MAKE_FLOOR_LINE, 1,"0611         1  0"
		CALL MAKE_FLOOR_LINE, 2,"0  1         1  0"
		CALL MAKE_FLOOR_LINE, 3,"0  11111111111  0"
		CALL MAKE_FLOOR_LINE, 4,"0  11111111111  0"
		CALL MAKE_FLOOR_LINE, 5,"0  11111111111  0"
		CALL MAKE_FLOOR_LINE, 6,"0            1  0"
		CALL MAKE_FLOOR_LINE, 7,"0            1160"
		CALL MAKE_FLOOR_LINE, 8,"00000000000000000"
		CALL SET_TILE,13,3,던전플래그:9:0,0,4
		CALL SET_TILE,13,3,던전플래그:9:0,2,4

		;メガテン系イベント以外不許可の場合
		SIF GETBIT(FLAG:EXTRA이벤트설정, 0) == 0 || GETBIT(FLAG:EXTRA이벤트필터, 10) == 1
			RETURN 0
		CALL SET_TILE,8,3,던전플래그:9:4,0,4
		
	;A=自動床タイル
	CASE 1
		FLAG:최대X = 19
		FLAG:최대Y = 13
		;						 0123456789012345678
		CALL MAKE_FLOOR_LINE, 0,"0000000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"811 11111 111A  110"
		CALL MAKE_FLOOR_LINE, 2,"0 1 1   111  111 10"
		CALL MAKE_FLOOR_LINE, 3,"0 1 1111  A111 1110"
		CALL MAKE_FLOOR_LINE, 4,"0 11   1         10"
		CALL MAKE_FLOOR_LINE, 5,"0  111 11   1111 10"
		CALL MAKE_FLOOR_LINE, 6,"0 11 1  1 111  1 10"
		CALL MAKE_FLOOR_LINE, 7,"0 1  11 1 1  111 10"
		CALL MAKE_FLOOR_LINE, 8,"811   1 1 1  A   10"
		CALL MAKE_FLOOR_LINE, 9,"0   111 1111 A11110"
		CALL MAKE_FLOOR_LINE,10,"0   1   1  111 1  0"
		CALL MAKE_FLOOR_LINE,11,"0   11111      7  0"
		CALL MAKE_FLOOR_LINE,12,"0000000000000000000"
		DA:13:1 = 11
		DA:10:3 = 21
		DA:13:8 = 31
		DA:13:9 = 41

	;A=自動床タイル
	CASE 2
		FLAG:최대X = 19
		FLAG:최대Y = 13
		;						 0123456789012345678
		CALL MAKE_FLOOR_LINE, 0,"0000000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"0      1          0"
		CALL MAKE_FLOOR_LINE, 2,"71111111111       0"
		CALL MAKE_FLOOR_LINE, 3,"0      1  111   118"
		CALL MAKE_FLOOR_LINE, 4,"011111 1    1   1 0"
		CALL MAKE_FLOOR_LINE, 5,"01   1 1    1 111 0"
		CALL MAKE_FLOOR_LINE, 6,"0111 111    1A1 1 0"
		CALL MAKE_FLOOR_LINE, 7,"0  1   11     1 1 0"
		CALL MAKE_FLOOR_LINE, 8,"01 111  1111  1 1 0"
		CALL MAKE_FLOOR_LINE, 9,"0111 11       1 118"
		CALL MAKE_FLOOR_LINE,10,"0     11111 111   0"
		CALL MAKE_FLOOR_LINE,11,"0111111   111 1   0"
		CALL MAKE_FLOOR_LINE,12,"0000000000000000000"
		DA:13:6 = 11

	CASEELSE





ENDSELECT



;=======================================================================
;[5]조사　実行時の이벤트
;=======================================================================
@EVENT_CHECK_DUNGEON_9
;==========
;出입の処理
;==========
IF DA:(FLAG:현X):(FLAG:현Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
	
;=======
;階段
;=======
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 6
	CALL UPSTARIS_9
	RETURN RESULT

ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 7
	CALL DOWNSTARIS_9
	RETURN RESULT


ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 9
	CALL DUNGEON_TERMINAL
	RETURN 1
ELSE
	CALL MESSAGE_WINDOW_D, "", "＞아무 일도 일어나지 않았다"
	RETURN 0
ENDIF

;=======================================================================
;タイル進入時の이벤트
;=======================================================================
@EVENT_ENTER_DUNGEON_9,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出입の処理
;==========
IF DA:(FLAG:현X):(FLAG:현Y) == 8
	IF FLAG:현M > 0 && !던전플래그:9:1
		CALL MESSAGE_WINDOW_D, "", "＞외부의 빛이 보이기 시작했다/＞이 앞은 결계에 갇힌 미카게쵸 내부로 이어져 있을 것이다……/＞팬텀에 미카게쵸 침입 성공의 보고를 넣자"
		CALL MESSAGE_WINDOW_D, "", @"＞아무래도 이곳의 침입에 있어서는、/＞%CALLNAME:MASTER%이 제일 먼저 도착한 것 같다/＞이제 미카게쵸와 관련된 이야기가 있으면, 이쪽으로 전해질 일이 많아질 것이다"
		던전플래그:9:1 = 1
	ELSE
		CALL DUNGEON_EXIT
	ENDIF
	RETURN 1
;==========
;이벤트床
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 1 && DA:(FLAG:현X):(FLAG:현Y) / 10 > 0
	IF FLAG:현M == 1
		SELECTCASE DA:(FLAG:현X):(FLAG:현Y) / 10
			CASE 1
				CALL SHOW_FORCEMOVE, "LLL", "D", , ,  "돌연 바닥이 움직이기 시작했다！"
			CASE 2
				CALL SHOW_FORCEMOVE, "RRR", "D", , ,  "돌연 바닥이 움직이기 시작했다！"
			CASE 3
				CALL SHOW_FORCEMOVE, "UR<2>U<2>L<3>DL<2>D<2>", "D", "", "돌연 바닥이 움직이기 시작했다！"
			CASE 4
				CALL SHOW_FORCEMOVE, "DLLU", "D", , , "돌연 바닥이 움직이기 시작했다！"
		ENDSELECT
	ELSE
		CALL SHOW_FORCEMOVE, "LU<3>L<2>UL<3>", "D", , , "돌연 바닥이 움직이기 시작했다！"
	ENDIF
	RETURN 1
;=======
;階段
;=======
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 6
	CALL UPSTARIS_9
	RETURN RESULT

ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 7
	CALL DOWNSTARIS_9
	RETURN RESULT


;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1


;========================
;이벤트
;========================
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 4
SELECTCASE FLAG:현M
	CASE 0
		IF 던전플래그:9:0 == 0
			CALL MESSAGE_WINDOW_D, "", "＞지하철의 선로상을 걸어, 간신히 지하철 미카게쵸역 플랫폼이 보이는……/＞그런데, 그 앞을 한 마리의 짐승이 가로막고 서 있다"
			CALL MESSAGE_WINDOW_D, "마수 이누가미", "동서의 통로를 봉쇄하라는 말을 듣기는 했지만、/설마 거기서 인간이 올거라고는 미처 생각하지 못했군/인간、어떻게 네가 들어올 수 있었나？"
			CALL MESSAGE_WINDOW_D, "", "＞마수 이누가미가 １체 나타났다！"
			FLAG:도주불가플래그 = 1
			FLAG:회화불능플래그 = 1
			FLAG:선제기습취소 = 1
			CALL SET_ENEMY,9,853,15,0,1
			CFLAG:(CHARANUM-1):행동횟수 = 2
			CFLAG:(CHARANUM-1):ＨＰ보정 += 500
			BASE:(CHARANUM-1):힘 += 5
			CALL SYNC_STATUS,(CHARANUM-1)
			CALL HEALTH_CHARA,(CHARANUM-1)
			CALL BATTLE_START
			FLAG:도주불가플래그 = 0
			FLAG:회화불능플래그 = 0
			FLAG:선제기습취소 = 0
			DA:(FLAG:현X):(FLAG:현Y) = 1
			던전플래그:9:0 = 1
			CALL SHOW_PICTURE, "D"
			CALL MESSAGE_WINDOW_D, "", "＞마수 이누가미를 토벌 한 것으로 인해, 미카게쵸에의 침입이 가능하게 되었다"
			CALL MESSAGE_WINDOW_D, "", "＞떠나려 하자 시야의 가장자리에 피어스를 입은 남자 고등학생이 쓰러져 있는 것을 깨달았다"
			CALL MESSAGE_WINDOW_D, "", "＞운나쁘게 혼자서 이누가미랑 조우라도 한걸까……/> 교복은 피에 젖어있지만 아직도 숨은 붙어있는것 같다"						
			CALL INPUT_YN_M , "남자라도 뭔가에는 쓸 수 있겠지" , "방치한다"
			IF RESULT == 0
			CALL MESSAGE_WINDOW_D, "", "＞상처투성이의 모습을 관찰하고 있으니 어딘가 이상한 힘을 느낀다 /> 조금이라도 전력이 된다면 ……라는 생각을 하고 의식이 없는 남학생을 회수하기로 했다."


			CALL ADD_NEW_COMPANION, 4654, 500
			;전투참가可能で加入するが、조교で전투참가불가능になる
				IF RESULT == 1
					;戦闘参加可能で加入するが、調教で戦闘参加不可能になる
					CFLAG:GETCHARA(4654):전투참가불가능 = -1
					LOCAL = GETCHARA(4654)
					$INPUT_LOOP
					PRINTL 피어스의 소년（페르소나１주인공）의 이름을 입력해주세요
					INPUTS
					NAME:LOCAL = %RESULTS%
					PRINTL 피어스의 소년（페르소나１주인공）의 애칭을 입력해주세요
					INPUTS
					CALLNAME:LOCAL = %RESULTS%
					PRINTFORML 이름　%NAME:LOCAL%
					PRINTFORML 애칭　%CALLNAME:LOCAL%
					PRINTL 이걸로 좋습니까？？
					CALL INPUT_YN
					SIF RESULT == 1
						GOTO INPUT_LOOP
				ENDIF
			ELSE
			CALL MESSAGE_WINDOW_D, "", "＞남학생을 그 자리에 방치하고 떠났다"
			ENDIF
		ENDIF
	CASEELSE
ENDSELECT

	IF FLAG:현M == 0 && FLAG:현X == 8 && FLAG:현Y == 3
		;メガテン系イベント以外不許可の場合
		SIF GETBIT(FLAG:EXTRA이벤트설정, 0) == 0 || GETBIT(FLAG:EXTRA이벤트필터, 10) == 1
			RETURN 0
		IF 던전플래그:9:4 == 0
			CALL MESSAGE_WINDOW_D, "", "＞코인 로커가 있다/＞어쩐지 불쾌한 분위기다……"
			CALL MESSAGE_WINDOW_D, "???", "……아아……오꺄아~♪……아아~♬"
			CALL MESSAGE_WINDOW_D, "", "＞안에서 어렴풋이 아기 울음소리 같은 것이 들려온다"
			CALL MESSAGE_WINDOW_D, "", "＞열어볼까?"
			CALL INPUT_SELECT_D("[1] 연다/[2] 열지 않는다")
			IF RESULT == 1
				CALL MESSAGE_WINDOW_D, "", "＞로커가 녹슨 소리를 내며 열렸다/＞……썩은 냄새가 난다/＞동시에 뭔가가 축축한 소리를 내며 땅바닥에 떨어졌다"
				CALL MESSAGE_WINDOW_D, "？", "마……마……?"
				CALL MESSAGE_WINDOW_D, "", "＞아기다/＞그러나 온몸의 살은 썩어 문드러졌고、고름과 구더기를 흘리고 있다./＞아무리 생각해도 이 세상의 존재는 아니다……!"
				CALL MESSAGE_WINDOW_D, "", "＞활짝 열린 사물함에서 같은 것들이 텀벙텀벙 떨어졌다/＞그리고 작은 손을 내밀어 이쪽에 매달려 왔다!"
				CALL MESSAGE_WINDOW_D, "악마", "맘마아……맘마아아아아아아아아아아아아아아!!!"
				CALL MESSAGE_WINDOW_D, "", "＞괴이 코인로커 베이비가 10체 나타났다!"
					IF FINDCHARA_B([[キャラ:이자나미]]) == 2 && TALENT:MASTER:모성 == 0
						CALL MESSAGE_WINDOW_D, "", "＞………/＞……/＞…"
						CALL MESSAGE_WINDOW_D, "", @"＞전투 상태에 들어가기 직전、누군가가 양자의 앞을 가로막았다/＞%CALLNAME:MASTER%의 동료마인 이자나미다"
						CALL MESSAGE_WINDOW_D, "이자나미", "기다리세요/이 아이들에게 칼을 들이대지 마십시오!/일단 떨어지십시오!"
						CALL MESSAGE_WINDOW_D, "", @"＞이자나미는 뭔가 생각이 있는 것 같다/＞대체 무엇을 할 작정인가……/＞%조사처리(CALLNAME:MASTER,"는")% 일단 지켜보기로 했다"
						CALL MESSAGE_WINDOW_D, "이자나미", "착한 아이로군요/……그럼"
						CALL MESSAGE_WINDOW_D, "", "＞이자나미는 악마들을 향해 돌아서며 활짝 웃었다/그리고 옷이 더러워지는 것도 아랑곳하지 않고 땅에 무릎을 꿇는다/두 팔이 악마들을 맞이하듯 벌려졌다"
						CALL MESSAGE_WINDOW_D, "이자나미", "기다리게 해버렸네요、아가들/엄마가 데리려왔어요/자아、같이 돌아갑시다"
						CALL MESSAGE_WINDOW_D, "악마", "아……아……맘마……?"
						CALL MESSAGE_WINDOW_D, "", "＞악마들은 이자나미 곁으로 기어갔다/＞주뼛주뼛、확인하듯이 그녀의 손이나 무릎 등을 만진다/＞이자나미는 그런 악마들의 머리와 등을 다정하게 어루만지고 있었다"
						CALL MESSAGE_WINDOW_D, "이자나미", "에에、엄마에요/지금까지 너희들을 버려둔 것、용서해줘……라고 말하진 않겠어요/말할 수 있을 리가 없죠"
						CALL MESSAGE_WINDOW_D, "이자나미", "하지만、엄마가 싫지 않다면、아직 좋아한다면……/앞으로 쭉 함께할 것을、제발 허락해 주실 수는 없을까요?"
						CALL MESSAGE_WINDOW_D, "갓난아기", "아아……우아아……우아아아아아아아아아앙……/맘마아아아……!"
						CALL MESSAGE_WINDOW_D, "", "＞악마들은 이자나미에 매달려 통곡하고 있다……/"
						CALL MESSAGE_WINDOW_D, "이자나미", "아아……착하지、사랑스러운 아이/지금까지 잘 해왔습니다……장하다、장하다~"
						CALL MESSAGE_WINDOW_D, "", "＞악마、아니、이젠 아기라고 불러도 되겠지/＞그들의 모습은 그렇게 정화되어 있었다"
						CALL MESSAGE_WINDOW_D, "", "＞어머니에 대한 원한、괴로움、갈망……/＞아기의 영혼을 검게 물들이던 욕구가『엄마』에 의해 채워지고 있다/＞그 결과、영혼이 제 모습을 찾은 것이다"
						CALL MESSAGE_WINDOW_D, "", "＞이자나미는 계속 우는 아기들을 달래고 있다/그 모습은 바로 어머니였다……"
						CALL MESSAGE_WINDOW_D, "", "＞………/＞……/＞…"
						CALL MESSAGE_WINDOW_D, "", "＞정신을 차려 보니 아기들이 사라져 있었다……"
						CALL MESSAGE_WINDOW_D, "이자나미", "황천으로 돌아간 모양입니다/지금쯤은 제 본체의 곁에서 맘 편히 지내고 있겠지요"
						;ググったら黄泉の国は汚いらしい。そんなとこに赤ちゃんを送ってええんやろか…
						CALL MESSAGE_WINDOW_D, "", "＞……뭔가 떨어져 있다"
						CALL MESSAGE_WINDOW_D, "", "＞보옥륜을 1개 손에 넣었다"
						ITEM:보옥륜 += 1
					ELSEIF TALENT:MASTER:모성 == 1
						CALL MESSAGE_WINDOW_D, "", "＞………/＞……/＞…"
						CALL MESSAGE_WINDOW_D, "", @"＞%조사처리(CALLNAME:MASTER,"는")% 전투 태세에 들어가지 않았다/＞그러기는커녕 매달리는 아기들을 끌어안았다"
						CALL MESSAGE_WINDOW_D, "", "＞이런 곳에 남겨져 분명히 슬펐을 것이다/＞어둡고 좁은 장소에 처박혀 힘들었겠지/＞그리고 무엇보다도 어머니가 곁에 없어 외로웠을 것이다"
						CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%도 아이를 낳아 기른 경험이 있다/＞유기된 갓난아이들의 억울한 마음/＞그것을 무시하고 칼을 들이댈 수 있을 리가 없었다"
						CALL MESSAGE_WINDOW_D, "", "＞늦어서 미안해、엄마가 데리러 왔어/＞이제 두 번 다시 혼자 두지 않을 테니까/＞그러니 안심하고 코- 자도 돼"
						CALL MESSAGE_WINDOW_D, "갓난아기", "맘마……맘마아……"
						CALL MESSAGE_WINDOW_D, "", "＞아기들의 표정이 편안해졌다/＞모습이 희미해져 간다……"
						CALL MESSAGE_WINDOW_D, "", "＞……사라져 버렸다/＞썩은 냄새도 싫은 공기도 사라져 있다/＞저 아이들의 영혼은 구원된 것일까……"
						CALL MESSAGE_WINDOW_D, "", "＞……활짝 열린 사물함 안쪽에 뭔가가 있다/＞이건……향일까?"
						CALL MESSAGE_WINDOW_D, "", "＞보옥륜을 1개 손에 넣었다"
						ITEM:보옥륜 += 1
					ELSE
						FLAG:도주불가플래그 = 1
						FLAG:회화불능플래그 = 1
						FLAG:선제기습취소 = 1
						;スライム10体
						FOR LOCAL, 7, 17
							CALL SET_ENEMY,LOCAL,[[キャラ:슬라임]],15,0
							CFLAG:(CHARANUM-1):ＨＰ보정 += 150
							NAME:(CHARANUM-1) = CL베이비
							CALLNAME:(CHARANUM-1) = CL베이비
							CSTR:(CHARANUM-1):30 = 『코인로커 베이비』
							CSTR:(CHARANUM-1):31 = 　
							CSTR:(CHARANUM-1):32 = 어떤 젊은 여자가 마음에 둔 남자와 교제 끝에、아이를 임신했다
							CSTR:(CHARANUM-1):33 = 그걸 안 남자는 실종。여자는 돈도 없고 의지할 사람도 없었기 때문에 어찌할 바를 모르고、
							CSTR:(CHARANUM-1):34 = 마침내 역의 화장실에서 아이를 출산하고 말았다
							CSTR:(CHARANUM-1):35 = 곤란한 여성은 아기를 종이봉투에 넣어 역의 코인로커에 방치。그냥 그대로 도망쳐 버렸다
							CSTR:(CHARANUM-1):36 = 　
							CSTR:(CHARANUM-1):37 = 그로부터 몇 년、떠올리고 싶지 않은 기억도 이제 희미해졌을 때쯤、여성은 그 역을 이용하게 되었다
							CSTR:(CHARANUM-1):38 = 코인로커 앞까지 온 순간、그 옆에서 어린 아이가 울고 있는 것을 발견했다.
							CSTR:(CHARANUM-1):39 = 보다 못한 여자는 아이에게 말을 건다「얘야、거기서 뭐 하고 있니? 엄마는 어디 계셔?」
							CSTR:(CHARANUM-1):40 = 　
							CSTR:(CHARANUM-1):41 = 　
							CSTR:(CHARANUM-1):42 = 　
							CSTR:(CHARANUM-1):43 = 　
							CSTR:(CHARANUM-1):44 = 　
							CSTR:(CHARANUM-1):45 = 　　　　　　　　　　　　　　　　　　　　　　　　　　　「바로 너야」
							CALL SYNC_STATUS,(CHARANUM-1)
							CALL HEALTH_CHARA,(CHARANUM-1)
						NEXT
						CALL BATTLE_START
						FLAG:도주불가플래그 = 0
						FLAG:회화불능플래그 = 0
						FLAG:선제기습취소 = 0
						CALL SHOW_PICTURE, "D"
						CALL MESSAGE_WINDOW_D, "악마", "마……마……"
						CALL MESSAGE_WINDOW_D, "", "＞악마들은 힘이 다했다"
						CALL MESSAGE_WINDOW_D, "", "＞보옥을 1개 손에 넣었다"
						ITEM:보옥 += 1
					ENDIF
						DA:(FLAG:현X):(FLAG:현Y) = 1
						던전플래그:9:4 = 1
			ELSE
				CALL MESSAGE_WINDOW_D, "", "＞그 자리에서 벗어났다……"
			ENDIF
		ENDIF
	ENDIF
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 9
	CALL DUNGEON_TERMINAL
	RETURN 1
ENDIF
RETURN 0


;=======================================================================
;階段置き場
;=======================================================================
@UPSTARIS_9
SELECTCASE FLAG:현M
	CASE 0
		SIF FLAG:현X == 1 && FLAG:현Y == 1
			CALL DUNGEON_UPSTAIRS,1,15,11
		SIF FLAG:현X == 15 && FLAG:현Y == 7
			CALL DUNGEON_UPSTAIRS,2,0,2
	CASEELSE
ENDSELECT
RETURN 1

@DOWNSTARIS_9
SELECTCASE FLAG:현M
	CASE 1
		SIF FLAG:현X == 15 && FLAG:현Y == 11
			CALL DUNGEON_DOWNSTAIRS,0,1,1
	CASE 2
		SIF FLAG:현X == 0 && FLAG:현Y == 2
			CALL DUNGEON_DOWNSTAIRS,0,15,7
	CASEELSE
ENDSELECT
RETURN 1

;=======================================================================
;인카운트율
;=======================================================================
@ENCOUNT_RATE_9
SELECTCASE FLAG:현M
	CASE 0
		FLAG:인카운트율 = FLAG:미조우걸음수*5
	CASEELSE
		FLAG:인카운트율 = FLAG:미조우걸음수*5
ENDSELECT
SIF FLAG:미조우걸음수 < 5
	FLAG:인카운트율 = 0
;=======================================================================
;필요레벨눈安
;권장LV 雑魚최저LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_9
RETURN 15, 12, 15, 15
;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_9
IF RAND:100 < FLAG:인카운트율
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:미조우걸음수 += 1
	RETURN 0
ENDIF
;=======================================================================
;적の종족の종류数
;=======================================================================
@CLASS_NUM_9
RETURN CLASS_NUM_COMMON("初級")
;=======================================================================
;적の総数
;ARGは出現する적の종류です
;RAND(X, Y)はX～Y-1のうち중 하나になるRANDです
;なお、ここで帰った数字は下限を종족数、上限を10として自動的に調整されます
;=======================================================================
@ENEMY_NUM_9, ARG
RETURN ENEMY_NUM_COMMON("初級", ARG)
;=======================================================================
;적の종류
;=======================================================================
@ENEMY_9, ARG
SELECTCASE RAND:100
	CASE IS < 10
		;10%で임프Lv9（12）
		RETURN 459, 12
	CASE IS < 20
		;10%で좀비캅Lv9（12）
		RETURN 1095, 12
	CASE IS < 30
		;10%で나하트코볼트
		RETURN 467, 14
	CASE IS < 40
		;10%　용왕야토노카미LV22 
		RETURN [[キャラ:야토노카미]], 14
	CASE IS < 55
		;15%で아체리Lv10（13）
		RETURN 608, 13
	CASE IS < 70
		;15%で라미아Lv10（13）
		RETURN 702, 13
	CASE IS < 85
		;15%で하나코lv13
		RETURN 1003, 13
	CASE IS < 100
		;15%で드라이어드(15)
		RETURN 502, 15
ENDSELECT

;=======================================================================
;윤간참가악마
;=======================================================================
@DUNGEON_RAPE_DEVIL_9
;랜덤にどの악마が出るか決定
;RETURN CSV번호 , 人数 , 레벨
SELECTCASE RAND:3
	CASE 0
		RETURN [[キャラ:좀비캅]] , 3 + RAND:10 , CSVBASE([[キャラ:좀비캅]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 1
		RETURN [[キャラ:나하트코볼트]] , 3 + RAND:10 , CSVBASE([[キャラ:나하트코볼트]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 2
		RETURN [[キャラ:임프]] , 3 + RAND:10 , CSVBASE([[キャラ:임프]] , GETNUM(BASE , "LV") , 0) + RAND:3
ENDSELECT




