;=======================================================================
;미혹의숲
;던전ナンバー12
;=======================================================================
;
;フラグ
;던전플래그:12:0 = まいの話フラグ(1.話聞いてマキを救うことにした 10.話聞いてこの件は切り上げた)
;던전플래그:12:1 = マキ이벤트플래그(1.発生済)
;던전플래그:12:2 = 에리이벤트플래그(1.発生済)
;던전플래그:12:3 = 아야세이벤트플래그(1.発生済)
;던전플래그:12:4 = マキ一時離脱フラグ(1.離脱しなかった 2.離脱した)
;던전플래그:12:5 = まい連れ出しフラグ(1.連れ出した)
;
;=======================================================================
;攻略조건
;=======================================================================
@PREREQUISITE_DUNGEON_12
;Deva Yugaで소녀を救う方法を探すを選んでいない
SIF 던전플래그:11:7 != 2
	RETURN 0
;クリア済み
SIF 던전플래그:12:0 == 10
	RETURN 0
;마키がいない
SIF GETCHARA([[キャラ:마키]], 0) == -1
	RETURN 0
RETURN 1
;=======================================================================
;던전名
;=======================================================================
@GET_DUNGEON_NAME_12
RESULTS = 미혹의숲


;=======================================================================
;플로어名표시
;=======================================================================
@FLOORNAME_12
PRINT 미혹의숲


;=======================================================================
;特殊タイル표시
;=======================================================================
@TILE_MARK_12_0_18_4
RESULTS = →
@TILE_MARK_12_0_18_17
RESULTS = →
@TILE_MARK_12_0_3_18
RESULTS = ↓

@TILE_MARK_12_1_0_5
RESULTS = ←
@TILE_MARK_12_1_0_17
RESULTS = ←

@TILE_MARK_12_2_3_0
RESULTS = ↑
@TILE_MARK_12_2_4_0
RESULTS = ↑
@TILE_MARK_12_2_8_0
RESULTS = ↑


;=======================================================================
;スタート地点입력
;=======================================================================
@START_DUNGEON_12,ARG
FLAG:현M = 0
FLAG:현X = 15
FLAG:현Y = 0
;CALL AUTOMAP

;=======================================================================
;スタート時이벤트
;=======================================================================
@EVENT_DUNGEON_START_12
#LOCALSSIZE 1
IF 던전플래그:(FLAG:현던전):1 == 0
	CALL SHOW_PICTURE, "NONFLOORD", @"%CALLNAME:MASTER%のアジト"
	CALL MESSAGE_WINDOW, "", @"＞칸도리를 쓰러뜨린 후、%조사처리(CALLNAME:MASTER,"는")% 팬텀・소사이어티의 눈을 피해、/＞그가 일으킨 사건에 대한 조사를 계속하고 있었다/＞그 결과、불확실한 추측도 섞여있지만 몇가지 사실이 드러났다", , "로그를 남기지 않는다/버튼을 이용한다/재이용하지 않는다", "CENTER", 72, 4
	CALL MESSAGE_WINDOW, "", "＞첫번째로 Deva시스템이란、인간의 심상세계와 현실세계를 연결하는 기계이다/＞그리고 이것으로 인해、개인의 심상세계가 주는 영향이、/＞현실 세계를 침식할 위험이 있다/＞그것은、분명 데바・유가의 현현이겠지", , "로그를 남기지 않는다/버튼을 이용한다/재이용하지 않는다", "CENTER", 72, 4
	CALL MESSAGE_WINDOW, "", "＞시스템에 접속된 소녀「소노무라 마키」를 시스템에서 분리하려면/＞그녀의 심상세계에 놓여진 시스템과의 접점을 제거할 필요가 있다", , "로그를 남기지 않는다/버튼을 이용한다/재이용하지 않는다", "CENTER", 72, 4
	CALL MESSAGE_WINDOW, "", @"＞심상 세계에 침임하는 것은 사이코 다이버의 도움을 받는다면 가능하다/＞하지만、그 전에 %플레이어와()% 행동을 함께하고 있는 소노무라 마키에게/＞현상을 설명해야할 것이다/＞……그녀가、시스템에 의해 구축된 가상의 가상의 존재라는 사실을……", , "로그를 남기지 않는다/버튼을 이용한다/재이용하지 않는다", "CENTER", 72, 4
	CALL MESSAGE_WINDOW, "", "………………/…………/……", , "로그를 남기지 않는다/버튼을 이용한다/재이용하지 않는다", "CENTER", 72, 4
	LOCAL = GETCHARA([[キャラ:마키]], 0)
	IF 함락(LOCAL) >= 1
		CALL MESSAGE_WINDOW, "", @"＞……%CALLNAME:MASTER%의 이야기를 들은 마키는 얼굴이 파랗게 질렸다/＞하지만、마음을 다잡고 %CALLNAME:MASTER%의 눈을 똑바로 응시한다", , "로그를 남기지 않는다/버튼을 이용한다/재이용하지 않는다", "CENTER", 72, 4
		CALL MESSAGE_WINDOW, "마키", "내가 어떻게든 할 수 밖에 없겠……지요/＞그뿐이라면、알겠어요/＞……힘을 빌려주세요", , "로그를 남기지 않는다/버튼을 이용한다/재이용하지 않는다", "CENTER", 72, 4
		던전플래그:(FLAG:현던전):4 = 1
	ELSE
		CALL MESSAGE_WINDOW, "", @"＞……%CALLNAME:MASTER%의 이야기를 들은 마키는、얼굴이 새파래져서 떨기 시작했다", , "로그를 남기지 않는다/버튼을 이용한다/재이용하지 않는다", "CENTER", 72, 4
		CALL MESSAGE_WINDOW, "마키", "……결국、나의 탓……이었어/전부……/……보지 말아줘……나、나 따위……", , "로그를 남기지 않는다/버튼을 이용한다/재이용하지 않는다", "CENTER", 72, 4
		CALL MESSAGE_WINDOW, "", "＞눈에 커다란 눈물이 고인 마키가 마키고함을 지르는 것과 동시에、/＞마키의 모습이 천천히 사라져갔다/＞정신세계로 도망친 모양이다……", , "로그를 남기지 않는다/버튼을 이용한다/재이용하지 않는다", "CENTER", 72, 4
		;마키一時離脱
		CFLAG:LOCAL:이자리에없는플래그 = 1
		SIF CPOS(LOCAL) > 0
			CALL REMOVE_POSITION, CPOS(LOCAL)
		던전플래그:(FLAG:현던전):4 = 2
	ENDIF
	CALL MESSAGE_WINDOW, "", @"＞%조사처리(CALLNAME:MASTER,"는")% 조속히、사이코 다이브의 계획을 세웠다……", , "로그를 남기지 않는다/버튼을 이용한다/재이용하지 않는다", "CENTER", 72, 4
	CALL MESSAGE_WINDOW, "", "………………/…………/……", , "로그를 남기지 않는다/버튼을 이용한다/재이용하지 않는다", "CENTER", 72, 4
	CALL SHOW_PICTURE, "EMPTY"
	CALL SHOW_PICTURE, "D"
	CALL MESSAGE_WINDOW_D, "", "＞돌입한、소노무라 마키의 심상 세계/＞그곳은 그녀의 미혹을 나타내려는 것처럼 깊고、어두운 숲이었다",
ENDIF

;=======================================================================
;指定したマスに進入가능か
;=======================================================================
@CAN_ENTER_12,ARG,ARG:1
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT
;=======================================================================
;플로어데이터
;=======================================================================
@MAKE_FLOOR_12
SELECTCASE FLAG:현M
	CASE 0
	;北東
		
		FLAG:최대X = 19
		FLAG:최대Y = 19
		CALL MAKE_FLOOR_LINE, 0,"0000000000000008000"
		CALL MAKE_FLOOR_LINE, 1,"0000000011110111110"
		CALL MAKE_FLOOR_LINE, 2,"0011111010010111110"
		CALL MAKE_FLOOR_LINE, 3,"0010001010010002000"
		CALL MAKE_FLOOR_LINE, 4,"0011111110011111119"
		CALL MAKE_FLOOR_LINE, 5,"0000100000000001000"
		CALL MAKE_FLOOR_LINE, 6,"0011111110000001000"
		CALL MAKE_FLOOR_LINE, 7,"0010100010000001000"
		CALL MAKE_FLOOR_LINE, 8,"0010111110000001000"
		CALL MAKE_FLOOR_LINE, 9,"0000002000000001000"
		CALL MAKE_FLOOR_LINE,10,"0000001000000001000"
		CALL MAKE_FLOOR_LINE,11,"0000000000000001000"
		CALL MAKE_FLOOR_LINE,12,"0111111111141111000"
		CALL MAKE_FLOOR_LINE,13,"0100000001000000000"
		CALL MAKE_FLOOR_LINE,14,"0111110001000000000"
		CALL MAKE_FLOOR_LINE,15,"0101010001411110000"
		CALL MAKE_FLOOR_LINE,16,"0111110001000011100"
		CALL MAKE_FLOOR_LINE,17,"0001000001111410119"
		CALL MAKE_FLOOR_LINE,18,"0009000000000000000"

		;이벤트の設置
;		CALL SET_TILE,12,7,던전플래그:(FLAG:현던전):1,0,4
	CASE 1
		;北西
		FLAG:최대X = 19
		FLAG:최대Y = 19
		CALL MAKE_FLOOR_LINE, 0,"0000000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"0111111111100000000"
		CALL MAKE_FLOOR_LINE, 2,"0000400010100000000"
		CALL MAKE_FLOOR_LINE, 3,"0000101110100000000"
		CALL MAKE_FLOOR_LINE, 4,"9111101000100000000"
		CALL MAKE_FLOOR_LINE, 5,"0104001000100000000"
		CALL MAKE_FLOOR_LINE, 6,"0101001000100000000"
		CALL MAKE_FLOOR_LINE, 7,"0101001000100000000"
		CALL MAKE_FLOOR_LINE, 8,"0101111011100000000"
		CALL MAKE_FLOOR_LINE, 9,"0100100010100000000"
		CALL MAKE_FLOOR_LINE,10,"0111101111100000000"
		CALL MAKE_FLOOR_LINE,11,"0000000010000000000"
		CALL MAKE_FLOOR_LINE,12,"0001141110000000000"
		CALL MAKE_FLOOR_LINE,13,"0121001000000000000"
		CALL MAKE_FLOOR_LINE,14,"0001141000000000000"
		CALL MAKE_FLOOR_LINE,15,"0000001111111111110"
		CALL MAKE_FLOOR_LINE,16,"0000000000000000010"
		CALL MAKE_FLOOR_LINE,17,"9111111111111111110"
		CALL MAKE_FLOOR_LINE,18,"0000000000000000000"
	
	CASE 2
		;南西
		FLAG:최대X = 19
		FLAG:최대Y = 19
		CALL MAKE_FLOOR_LINE, 0,"0009000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"0014101211101111110"
		CALL MAKE_FLOOR_LINE, 2,"0011100000100000010"
		CALL MAKE_FLOOR_LINE, 3,"0002001110100111110"
		CALL MAKE_FLOOR_LINE, 4,"0011101110100111110"
		CALL MAKE_FLOOR_LINE, 5,"0010000200100111110"
		CALL MAKE_FLOOR_LINE, 6,"0011411111100111110"
		CALL MAKE_FLOOR_LINE, 7,"0001000000111111110"
		CALL MAKE_FLOOR_LINE, 8,"0111110000000000010"
		CALL MAKE_FLOOR_LINE, 9,"0101010001000001110"
		CALL MAKE_FLOOR_LINE,10,"0101110001000004010"
		CALL MAKE_FLOOR_LINE,11,"0100001111000001010"
		CALL MAKE_FLOOR_LINE,12,"0111001010011111140"
		CALL MAKE_FLOOR_LINE,13,"0101001010040001000"
		CALL MAKE_FLOOR_LINE,14,"0111111010010004000"
		CALL MAKE_FLOOR_LINE,15,"0100000011411411110"
		CALL MAKE_FLOOR_LINE,16,"0111100001040000010"
		CALL MAKE_FLOOR_LINE,17,"0011100011111111110"
		CALL MAKE_FLOOR_LINE,18,"0000000000000000000"
	
		;이벤트の設置
		;CALL SET_TILE,3,1,던전플래그:(FLAG:현던전):0,0,4
		CALL SET_TILE,7,4,던전플래그:(FLAG:현던전):1,0,4
		CALL SET_TILE,12,1,던전플래그:(FLAG:현던전):2,0,4
		CALL SET_TILE,6,1,던전플래그:(FLAG:현던전):3,0,4

	CASEELSE





ENDSELECT



;=======================================================================
;[5]조사　実行時の이벤트
;=======================================================================
@EVENT_CHECK_DUNGEON_12
;==========
;出口の処理
;==========
IF DA:(FLAG:현X):(FLAG:현Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;이벤트の処理
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 4
	SELECTCASE FLAG:현M
		CASE 2
			IF FLAG:현X == 3 && FLAG:현Y == 1
			;마이の部屋
				CALL MESSAGE_WINDOW_D, "", "＞과자의 집이 있다……"
			ELSEIF FLAG:현X == 7 && FLAG:현Y == 4
			;마키の引きこもってる部屋
			ENDIF
	ENDSELECT
	RETURN 1
ELSE
	CALL MESSAGE_WINDOW_D, "", "＞아무 것도 일어나지 않았다"
	RETURN 1
ENDIF

;=======================================================================
;タイル進入時の이벤트
;=======================================================================
@EVENT_ENTER_DUNGEON_12,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出口の処理
;==========
IF DA:(FLAG:현X):(FLAG:현Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;이벤트、ワープの処理
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 4
	SELECTCASE FLAG:현M
		CASE 0
			IF FLAG:현X == 11 && FLAG:현Y == 12
				FLAG:현X = 15
				FLAG:현Y = 5
			ELSEIF FLAG:현X == 10 && FLAG:현Y == 15
				FLAG:현X = 11
			ELSEIF FLAG:현X == 13 && FLAG:현Y == 17
				FLAG:현X = 12
			ENDIF
		CASE 1
			IF FLAG:현X == 4 && FLAG:현Y == 2
				FLAG:현Y = 3
			ELSEIF FLAG:현X == 3 && FLAG:현Y == 5
				FLAG:현Y = 6
			ELSEIF FLAG:현X == 5 && FLAG:현Y == 12
				FLAG:현X = 4
			ELSEIF FLAG:현X == 5 && FLAG:현Y == 14
				FLAG:현X = 6
			ENDIF
		CASE 2
			IF FLAG:현X == 4 && FLAG:현Y == 6
				FLAG:현X = 3
			ELSEIF FLAG:현X == 15 && FLAG:현Y == 10
				FLAG:현Y = 11
			ELSEIF FLAG:현X == 17 && FLAG:현Y == 12
				FLAG:현Y = 11
			ELSEIF FLAG:현X == 11 && FLAG:현Y == 13
				FLAG:현Y = 12
			ELSEIF FLAG:현X == 15 && FLAG:현Y == 14
				FLAG:현Y = 15
			ELSEIF FLAG:현X == 13 && FLAG:현Y == 15
				FLAG:현X = 14
			ELSEIF FLAG:현X == 10 && FLAG:현Y == 15
				FLAG:현X = 9
			ELSEIF FLAG:현X == 11 && FLAG:현Y == 16
				FLAG:현Y = 15
			ELSEIF FLAG:현X == 3 && FLAG:현Y == 1
				;마이の部屋
				IF 던전플래그:(FLAG:현던전):0 == 0
					CALL MESSAGE_WINDOW_D, "", "＞어두운 숲을 헤쳐 나가기를, 얼마나 지났을까？/＞갑자기 시야가 열린다라고 생각했더니 작은 광장으로 나왔다/＞그리고 거기에는 동화에 나오는 것 같은 과자의 집이 있고,/＞그 전에는 무서운듯이 몸을 움츠리면서 이쪽을 주시하는 작은 여자 아이가 있었다……"
					IF 던전플래그:(FLAG:현던전):4 == 1
						CALL MESSAGE_WINDOW_D, "여자 아이", "다、당신、『마키』의……"
						CALL MESSAGE_WINDOW_D, "", @"＞여자 아이가 %CALLNAME:MASTER%를 보고, 조금 얼굴을 붉혔다"
					ELSE
						CALL MESSAGE_WINDOW_D, "여자 아이", "다、당신은、『마키』를 다치게 한……"
					ENDIF
					CALL MESSAGE_WINDOW_D, "여자 아이", "저는、마이라고 합니다/＞……이 세계를 만든 것은 저입니다/＞이야기하고 싶은 것이 있습니다/＞들어주세요"
					CALL MESSAGE_WINDOW_D, "", "………………/…………/……"
					CALL MESSAGE_WINDOW_D, "", "＞소노무라 마키의 정신세계를 관리하는 인격인, 마이에 의하면/＞DEVA 시스템과의 접점은 마키의 인격중 가장 파괴적인 인격이 확보하고 있어서、/＞그 인격에 의해 이 세계는 파괴 되려고 하고 있다고 하는 것이다"
					CALL MESSAGE_WINDOW_D, "", "＞그리고 이 파괴는 DEVA 시스템을 개입시켜 현실로 반영되어、/＞마키의 정신 세계 뿐만이 아니라 현실 세계를 파괴할 우려가 있다고 한다"
					CALL MESSAGE_WINDOW_D, "마이", "부탁해。『마키』를 도와줘…"
					CALL MESSAGE_WINDOW_D, "", "＞DEVA 시스템은 이 세계에서의 사건을 현실로 반영한다/＞따라서 마키의 정신 세계의 파괴는 현실 세계의 파괴를 일으키게 될 것이다/＞그러나, DEVA 시스템과 마키를 물리적으로 떼어내는 것으로＞/현실 세계의 파괴는 막을 수 있을 것이다"
					CALL INPUT_YN_D("마키를 구한다", "마키를 버려서, 위험을 미리 막는다")
					IF RESULT == 0
						CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%의 의사가 변함없는 것을 확인하고、/＞마이는 이 숲속에 소노무라 마키가(정확하게는 그 인격 가운데 하나가) 있다는 것을 가르쳐 주었다"
						CALL MESSAGE_WINDOW_D, "", "＞그녀는 DEVA 시스템을 통해 자신이 한 일로、/＞자신을 자책하여 틀어박혀 버렸다고 한다/＞그러나, DEVA 시스템을 얻은 파괴적인 인격에게 도달하려면 , 그녀의 협력이 필요한 것 같다"
						CALL INCREASE_LD,5
						던전플래그:(FLAG:현던전):0 = 1
					ELSE
						CALL MESSAGE_WINDOW_D, "", @"＞%조사처리(CALLNAME:MASTER,"는")% 정신세계로부터 탈출한 다음 팬텀에 보고를 실시해、/＞이 건을 끝내기로 했다"
						CALL MESSAGE_WINDOW_D, "", "잠시 지나면, 이 대참사도, 매우 흔히 있던 사고, 흔히 있던 비극으로서 인지되게 되는 것일까"
						CALL MESSAGE_WINDOW_D, "", "………………/…………/……"
						CALL SHOW_PICTURE, "EMPTY"
						CALL SHOW_PICTURE, "NONFLOORD", @"%CALLNAME:MASTER%의 방"
						CALL MESSAGE_WINDOW_D, "", "/＞……잠시 후에, 팬텀으로부터 DEVA 시스템의 해체의 보고와/＞그 조사에 의해 얻을 수 있던 기술에 대한 자료가 도착했다/＞하지만, 거기에는 소녀에 대한 기술은 아무것도 없었다……"
						CALL MESSAGE_WINDOW_D, "", "이 건의 보수로서 ￥100000이 지불되었다"
						MONEY += 100000
						던전플래그:(FLAG:현던전):0 = 10
						던전플래그:11:7 = 1
						CALL INCREASE_LC,5
						CALL INCREASE_LD,-5
						FLAG:탈출 = 1
					ENDIF
				ELSEIF 던전플래그:(FLAG:현던전):1 == 1 && 던전플래그:(FLAG:현던전):5 == 0
					;마키이벤트클리어後
					CALL MESSAGE_WINDOW_D, "마이", "……고마워요……"
					CALL MESSAGE_WINDOW_D, "", @"＞마키의 상태가 좋아진 것을 아는 것일까/＞마이는 가냘픈 목소리로 %CALLNAME:MASTER%에 감사를 표했다"
					CALL MESSAGE_WINDOW_D, "마이", "……이것으로 당분간은 괜찮다고 생각되요……/＞……그 사이에, 가장 나쁜 나를……"
					CALL MESSAGE_WINDOW_D, "", "＞……그 동안。마이를 계속 혼자 여기에 남겨둬야 할까?"
					CALL INPUT_YN_D("숲의 밖을 권한다", "방치한다")
					IF RESULT == 0
						CALL MESSAGE_WINDOW_D, "마이", "……좋은거야……？　저어……"
						CALL MESSAGE_WINDOW_D, "", "＞주저하는 소녀의 손을 가볍게 잡는다/＞마이는 풀어 버리려고 하지는 않았다"
						CALL ADD_NEW_COMPANION,4592,0
						던전플래그:(FLAG:현던전):5 = 1
					ENDIF
				ELSE
					CALL MESSAGE_WINDOW_D, "", "＞과자의 집이 있다……"
				ENDIF
			ELSEIF FLAG:현X == 7 && FLAG:현Y == 4
				;マキの引きこもってる部屋
				CALL MESSAGE_WINDOW_D, "", @"＞숲이 조금 열려 있는 듯한 공간에、/＞병원에 놓여 있는 것 같은 소박한 침대가 놓여있다/＞\@던전플래그:(FLAG:현던전):4 == 1? 침대 위에는、 부자연스러울 정도로 새하얀 시트에 감싸인、한 명의 소녀가 누워 있었고 # 침대 위에는、새하얀 시트에 감싸인、똑같은 모습의 두 사람의 소녀가 누워 있었고\@/＞소녀의 얼굴에는 새하얀 가면이 씌어져 있었다……"
				CALL MESSAGE_WINDOW_D, "가면의 소녀", "……당신은、누구？/우으응、누구라도 좋아요……/절 내버려두세요……"
				CALL MESSAGE_WINDOW_D, "", "＞……소녀는 오로지 자신을 책망하고、절망에 대해 이야기하기 시작했다/＞소녀는 그녀의 절망이 현실 세계에 침식하고 있는 것을 모르고 있는 것 같다…"
				IF FINDCHARA_B(4519,0) == 2
					;마키がパーティにいる
					CALL MESSAGE_WINDOW_D, "마키", "……이게、나？/아니、이것도、나……/너도 나라면、얘기를 들어줘！"
				ELSEIF FINDCHARA_B(4514,0) == 2
					;에리がパーティにいる
					CALL MESSAGE_WINDOW_D, "에리", "Maki……/당신 잘못이 아니에요……"
				ELSEIF FINDCHARA_B(4515,0) == 2
					;아야세がパーティにいる
					CALL MESSAGE_WINDOW_D, "아야세", "……소노무라……"
				ENDIF
				CALL MESSAGE_WINDOW_D, "", @"＞%조사처리(CALLNAME:MASTER,"는")%、소녀를 격려하고、일으켜 세우기 위해 이래저래 설득하기 시작했다……/＞%CALLNAME:MASTER%의 열변에 닿았는지/＞소녀는 조용히、목에 걸고 있었던 컴팩트를 일행에게 건네주자/＞천천히 희미해지고、대기가 눈 녹듯 사라졌다。"
				CALL MESSAGE_WINDOW_D, "", "＞컴팩트를 들여다보면、그 내면에는 신사같은 것이 들어가 있다"
				LOCAL = GETCHARA(4519, 0)
				IF 던전플래그:(FLAG:현던전):4 == 2
					CALL MESSAGE_WINDOW_D, "", "＞그리고、침대에 자고 있던 다른 소녀……/＞마키는 천천히 얼굴을 가리고 있었던、가면을 버리고 일어섰다……"
					CALL MESSAGE_WINDOW_D, "마키", "그렇…네요、도망치면 안되겠어요…/…음、혼자서 천천히 생각해보고 싶으니까 먼저 가주세요…"
					CALL MESSAGE_WINDOW_D, "마키", "……고마워요"
					CALL MESSAGE_WINDOW_D, "", "＞그렇게 말하고、마키는 현실세계로 돌아갔다"
					CFLAG:LOCAL:이자리에없는플래그 = 0
				ENDIF
				;의존도が충성도基準で上昇
					CFLAG:LOCAL:의존도 = MAX(CFLAG:LOCAL:의존도, BASE:LOCAL:충성도 / 4)
				던전플래그:(FLAG:현던전):1 = 1
			ELSEIF FLAG:현X == 12 && FLAG:현Y == 1
				IF 던전플래그:10:10 == 2 && FINDCHARA_B(4514,0) == 2 &&  CFLAG:(RESULT:1):201 == 0
					LOCAL = RESULT:1
					;에리和解ルートで仲間入りさせ、かつパーティに入っていて、しかもまだ조교した事が無い
					CALL MESSAGE_WINDOW_D, "", "에리와 완전히 같은 모습을 한 소녀가 서 있다……"
					CALL MESSAGE_WINDOW_D, "에리", "ㄴ……나？/double……이건 도플갱어라고 말하는……？"
					CALL MESSAGE_WINDOW_D, "", "＞그 녀석은 에리과 같은 품위있는 얼굴에 업신여기는 것 같은 미소를 지으며, 노래하는 것처럼 입을 열었다"
					CALL MESSAGE_WINDOW_D, "에리？", "아름다운‥‥미인‥‥상등품‥‥모두, 나를 그렇게 말하지/물론, 칭찬 받을 수 있으면 기쁜 것은 누구라도 같은‥‥/그러니까 나는 이렇게도 열심히!"
					CALL MESSAGE_WINDOW_D, "에리？", "……그렇지만, 그게 진정한 자신이야？/그런 일을 생각하면 멈출 수가 없어!/왜냐하면、그것 밖에 없으니까！/어찌나 내가 불쌍한지！！"
					CALL MESSAGE_WINDOW_D, "", "＞그렇게 말하며 조소하던 소녀의 모습이 천천히 이형의 것으로 변해간다……/＞업마　앨러건스가 1명 나타났다！"
					FLAG:도주불가플래그 = 1
					FLAG:회화불능플래그 = 1
					FLAG:선제기습취소 = 1
					CALL SET_ENEMY,9,1025,55,0,0
					CALL BATTLE_START
					CALL SHOW_PICTURE, "D"
					CALL MESSAGE_WINDOW_D, "업마 앨러건스", "크크‥‥으으으‥‥윽‥‥"
					CALL MESSAGE_WINDOW_D, "", "＞끝까지 비웃음을 지우지 않은 채、이형의 마물은 힘이 다했다"
					FLAG:도주불가플래그 = 0
					FLAG:회화불능플래그 = 0
					FLAG:선제기습취소 = 0
					CALL MESSAGE_WINDOW_D, "", "＞그것을 멍하니보고 있었던 에리는, 작은 목소리로 중얼거렸다"
					CALL MESSAGE_WINDOW_D, "에리", "그렇, 그렇더라도……/＞기대를 받고 거기에 응하는, 그것도 싫지는 않았었고……/……만약, 있는 그대로 행동한다면, 당신은 어떻다고 생각해"
					CALL MESSAGE_WINDOW_D, "에리", "(그리고, 이 사람이 숨기고 있는 것을 본다면、나는？)"
					;의존도が충성도基準で上昇
					CFLAG:LOCAL:의존도 = MAX(CFLAG:LOCAL:의존도, BASE:LOCAL:충성도 / 4)
					던전플래그:(FLAG:현던전):2 = 1
				ENDIF
			ELSEIF FLAG:현X == 6 && FLAG:현Y == 1
				IF 던전플래그:10:10 == 1 && FINDCHARA_B(4515,0) == 2 &&  CFLAG:(RESULT:1):201 == 0
					LOCAL = RESULT:1
					;아야세和解ルートで仲間入りさせ、かつパーティに入っていて、しかもまだ조교した事が無い
					CALL MESSAGE_WINDOW_D, "", "＞아야세와 완전히 같은 모습을 한 소녀가 서 있다……"
					CALL MESSAGE_WINDOW_D, "아야세", "어라……나？/＞……가、가짜！？"
					CALL MESSAGE_WINDOW_D, "아야세？", "……어째서、아야세가 이런 곳에 있는 거야～/상관없잖아, 가만히 내버려두면/아야세는 관계 없잖아"
					CALL MESSAGE_WINDOW_D, "아야세？", "아픈 것도 힘든 것도 싫고 ~/지금만 좋다면 좋지요？/……그러면 안된다고 그러지만/어려운 일은 몰라！　생각하고 싶지도 않아！！"
					FLAG:도주불가플래그 = 1
					FLAG:회화불능플래그 = 1
					FLAG:선제기습취소 = 1
					CALL MESSAGE_WINDOW_D, "", "＞그렇게 말하며 조소하던 소녀의 모습이 천천히 이형의 것으로 변해간다……/＞업마　옵신이 1명 나타났다！"
					CALL SET_ENEMY,9,1020,45,0,0
					CALL BATTLE_START
					CALL SHOW_PICTURE, "D"
					CALL MESSAGE_WINDOW_D, "업마 옵신", "크크‥‥으으으‥‥윽‥‥"
					CALL MESSAGE_WINDOW_D, "", "＞끝까지 비웃음을 지우지 않은 채、이형의 마물은 힘이 다했다"
					FLAG:도주불가플래그 = 0
					FLAG:회화불능플래그 = 0
					FLAG:선제기습취소 = 0
					CALL MESSAGE_WINDOW_D, "", "＞그것을 멍하니보고 있었던 에리는, 작은 목소리로 중얼거렸다"
					CALL MESSAGE_WINDOW_D, "아야세", "……아야세라도 앞의 일 정도는 생각하고 있어/그렇지만, 이런 상태로는 잘할지 어떨지 몰라서……"
					IF TALENT:MASTER:140 == 1
						CALL MESSAGE_WINDOW_D, "아야세", "가까운 곳에 후보가 없는 것도 아니지만……"
						CALL MESSAGE_WINDOW_D, "", "＞아야세는 왜인지 힐끗 이쪽을 보았다"
						;PRINTFORMW 。
					ENDIF
					CFLAG:LOCAL:의존도 = MAX(CFLAG:LOCAL:의존도, BASE:LOCAL:충성도 / 4)
					던전플래그:(FLAG:현던전):3 = 1
				ENDIF
			ENDIF
	ENDSELECT
	
	CALL MAKE_FLOOR, FLAG:현던전
	CALL AUTOMAP
	
	RETURN 1
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 9
	SELECTCASE FLAG:현M
		CASE 0
			IF FLAG:현X == 18
				FLAG:현M = 1
				FLAG:현X = 0
			ELSE
				FLAG:현M = 2
				FLAG:현Y = 0
			ENDIF
		CASE 1
			IF FLAG:현X == 0
				FLAG:현M = 0
				FLAG:현X = 18
			ENDIF
		CASE 2
			IF FLAG:현Y == 0
				FLAG:현M = 0
				FLAG:현Y = 18
			ELSE
				FLAG:현M = 1
				FLAG:현Y = 15
			ENDIF
		CASE 3
			IF FLAG:현X == 0
				FLAG:현M = 2
				FLAG:현X = 15
			ELSE
				FLAG:현M = 0
				FLAG:현Y = 15
			ENDIF
	ENDSELECT
		
	CALL MAKE_FLOOR, FLAG:현던전
	CALL AUTOMAP
	
	

;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1
ENDIF
RETURN 0


;=======================================================================
;인카운트율
;=======================================================================
@ENCOUNT_RATE_12
	FLAG:인카운트율 = FLAG:미조우걸음수*5

;=======================================================================
;필요레벨눈安
;권장LV 雑魚최저LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_12
RETURN 38, 35, 42, 45
;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_12
IF RAND:100 < FLAG:인카운트율
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:미조우걸음수 += 1
	RETURN 0
ENDIF
;=======================================================================
;적の종족の종류数
;=======================================================================
@CLASS_NUM_12
RETURN CLASS_NUM_COMMON("上級")
;=======================================================================
;적の総数
;ARGは出現する적の종류です
;=======================================================================
@ENEMY_NUM_12, ARG
RETURN ENEMY_NUM_COMMON("上級", ARG)
;=======================================================================
;적の종류
;=======================================================================
@ENEMY_12, ARG
IF FLAG:현M == 0 || FLAG:현M == 1
	SELECTCASE RAND:100
		CASE IS < 20
			;20%で오시치
			RETURN 1008, 35
		CASE IS < 40
			;20%で오세
			RETURN 1704, 36
		CASE IS < 60
			;20%で슈텐도지
			RETURN 655, 38
		CASE IS < 80
			;20%で네반
			RETURN 906, 37
		CASE IS < 100
			;20%で타마모
			RETURN 807, 38
	ENDSELECT
ELSE
	SELECTCASE RAND:100
		CASE IS < 20
			;20%で비비안
			RETURN 509, 42
		CASE IS < 40
			;20%で스퀼라
			RETURN 808, 43
		CASE IS < 60
			;20%でクチサケ
			RETURN 1009, 42
		CASE IS < 80
			;20%で서큐버스
			RETURN 407, 44
		CASE IS < 100
			;20%で마하
			RETURN 907, 42
	ENDSELECT
ENDIF
;=======================================================================
;윤간참가악마
;=======================================================================
@DUNGEON_RAPE_DEVIL_12
;RETURN CSV번호 , 人数 , 레벨
RETURN [[キャラ:슈텐도지]] , 3 + RAND:10 , CSVBASE([[キャラ:슈텐도지]] , GETNUM(BASE , "LV") , 0) + RAND:3

