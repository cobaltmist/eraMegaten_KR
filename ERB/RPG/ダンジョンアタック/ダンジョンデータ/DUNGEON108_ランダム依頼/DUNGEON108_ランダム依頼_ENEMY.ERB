
;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_108
;ランダム依頼：ソドモラ
IF 의뢰플래그:108:1 == 61
	CALL ENCOUNT_RATE_SODOMORRAH

;ランダム依頼：その他
ELSE
	FLAG:인카운트율 = FLAG:미조우걸음수
	SIF FLAG:미조우걸음수 < 8
		FLAG:인카운트율 = 0
	SIF 의뢰플래그:108:1 == 71 && FLAG:현M == 0
		FLAG:인카운트율 = 0
ENDIF

;=======================================================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_108
IF 던전플래그:108:11 == 0
	RETURN 6, 3, 17, 19
ELSE
	RETURN 50, 50,50, 50
ENDIF

;=======================================================================
;エンカウント処理（共通）
;=======================================================================
@CHECK_ENCOUNT_108
IF RAND:100 < FLAG:인카운트율
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:미조우걸음수 += 1
	RETURN 0
ENDIF


;=======================================================================
;敵の種族の種類数
;=======================================================================
@CLASS_NUM_108
;ランダム依頼のダンジョンの敵は一種類
RETURN 1

;=======================================================================
;敵の総数
;ARGは出現する敵の種類です
;RAND(X, Y)はX～Y-1のうちのどれかになるRANDです
;なお、ここで帰った数字は下限を種族数、上限を10として自動的に調整されます
;
;軽子坂高校も初期のダンジョンなので数は抑え目
;=======================================================================
@ENEMY_NUM_108, ARG
IF 의뢰플래그:108:1 == 29 && 던전플래그:108:20 == 1
	RETURN ENEMY_NUM_COMMON("上級", ARG)
ELSEIF 의뢰플래그:108:1 == 59
	RETURN ENEMY_NUM_COMMON("中級", ARG)
ELSE
	RETURN ENEMY_NUM_COMMON("初級", ARG)
ENDIF

;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_108, ARG
SELECTCASE 의뢰플래그:108:1
	CASE 0
		RETURN [[キャラ:잭프로스트]], 15
	CASE 3
		RETURN [[キャラ:슬라임]], 2
	CASE 5, 34
		RETURN [[キャラ:오크]], 11
	CASE 6, 12, 30, 39, 46
		RETURN [[キャラ:템플나이트]], 22
	CASE 7, 55
		RETURN [[キャラ:아귀]], 4
	CASE 9
		RETURN [[キャラ:라비]], 35
	CASE 15
		RETURN [[キャラ:고블린]], 9
	CASE 23
		RETURN [[キャラ:안주]], 26
	CASE 29
		RETURN [[キャラ:다크엘프]], 15
	CASE 32
		RETURN [[キャラ:유키죠로]], 21
	CASE 40
		RETURN [[キャラ:좀비캅]], 9
	CASE 48
		RETURN [[キャラ:비트볼]], 7
	CASE 57
		RETURN [[キャラ:드라이어드]], 12
	CASE 59
		RETURN [[キャラ:좀비독]], 5
	CASE 60
		RETURN [[キャラ:살라만더]], 25
	CASE 61
		;CALL ENEMY_SODOMORRAH
		;抗争の鎮圧
		IF (던전플래그:108:36 == 13 && 던전플래그:108:39 <= 9 && FLAG:현M == 2) || (던전플래그:108:36 == 14 && 던전플래그:108:39 <= 9 && FLAG:현M == 3) || (던전플래그:108:36 == 15 && 던전플래그:108:39 <= 9 && FLAG:현M == 7)
			IF RAND:2 == 0
				RETURN [[キャラ:템플나이트]], 15+RAND:5
			ELSE
				RETURN [[キャラ:하이웨이스타]], 15+RAND:5
			ENDIF
		;秘密文書配達
		ELSEIF (던전플래그:108:38 == 8 && 던전플래그:108:39 == 1 && FLAG:현M == 0) || (던전플래그:108:38 == 9 && 던전플래그:108:39 == 1 && FLAG:현M == 3) || (던전플래그:108:38 == 10 && 던전플래그:108:39 == 1 && FLAG:현M == 2)
			RETURN [[キャラ:하이웨이스타]], 15+RAND:5
		;用心棒特訓
		ELSEIF 던전플래그:108:36 == 1 && 던전플래그:108:39 <= 50 && FLAG:현M == 7
			RETURN [[キャラ:블랙맨]], 15+RAND:5
		;テンプルナイト斬り
		ELSEIF 던전플래그:108:37 == 1 && 던전플래그:108:39 <= 50 && FLAG:현M == 7
			RETURN [[キャラ:템플나이트]], 15+RAND:5
		;ガイア教徒斬り
		ELSEIF 던전플래그:108:38 == 1 && 던전플래그:108:39 <= 50 && FLAG:현M == 7
			RETURN [[キャラ:하이웨이스타]], 15+RAND:5
		ENDIF
	CASE 62
		RETURN [[キャラ:서큐버스]], 25 + RAND:10 + (10 - FLAG:현M)
	CASE 66
		RETURN [[キャラ:드베르가]], 18 + RAND:5
	CASE 67
		RETURN [[キャラ:하이웨이스타]], 10 + RAND:5
	CASE 68
		RETURN [[キャラ:나가]], 28 + RAND:5
	CASE 70
		RETURN [[キャラ:엘프]], 18 + RAND:5
	CASE 71
		RETURN [[キャラ:체르노보그]], 25 + RAND:5
ENDSELECT


;=======================================================================
;特殊調整
;=======================================================================
@SET_ENEMY_STATUS_108, ARG
;ソドモラ
IF 의뢰플래그:108:1 == 61
	IF NO:ARG == [[キャラ:하이웨이스타]]
		NAME:ARG = ガイア教徒
		CALLNAME:ARG = ガイア教徒
	ELSEIF NO:ARG == [[キャラ:블랙맨]]
		NAME:ARG = 用心棒
		CALLNAME:ARG = 用心棒
	ENDIF
ELSEIF 의뢰플래그:108:1 == 66
	IF NO:ARG == [[キャラ:드베르가]]
		NAME:ARG = ドワーフ
		CALLNAME:ARG = ドワーフ
	ENDIF
ELSEIF 의뢰플래그:108:1 == 71
	IF NO:ARG == [[キャラ:체르노보그]]
		NAME:ARG = チェルノボグ(分身）
		CALLNAME:ARG = チェルノボグ(分身）
	ENDIF
ENDIF
