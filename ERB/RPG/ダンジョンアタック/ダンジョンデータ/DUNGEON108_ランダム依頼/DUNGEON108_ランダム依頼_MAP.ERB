;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_108
;=======
;1.ビル
;=======
IF 의뢰플래그:108:3 == 1
	SELECTCASE 의뢰플래그:108:1
		CASE 5, 6
			PRINT ダム内部Ｂ
			SELECTCASE FLAG:현M
				CASE 0
					PRINTL ６Ｆ
				CASE 1
					PRINTL ５Ｆ
				CASE 2
					PRINTL ４Ｆ
				CASE 3
					PRINTL ３Ｆ
				CASE 4
					PRINTL ２Ｆ
				CASE 5
					PRINTL １Ｆ
				CASEELSE
					PRINTL
			ENDSELECT
		CASE 12,40,46,48
			SELECTCASE 의뢰플래그:108:1
				CASE 12
					PRINT 拠点ビル
				CASE 40
					PRINT 異界化したビル
				CASE 46
					PRINT 研究所
				CASE 48
					PRINT 細菌兵器 研究所
			ENDSELECT
			SELECTCASE FLAG:현M
				CASE 0
					PRINTL １Ｆ
				CASE 1
					PRINTL ２Ｆ
				CASE 2
					PRINTL ３Ｆ
				CASE 3
					PRINTL ４Ｆ
				CASE 4
					PRINTL ５Ｆ
				CASE 5
					PRINTL ６Ｆ
				CASEELSE
					PRINTL
			ENDSELECT
	ENDSELECT
;============
;2.下水道
;============
ELSEIF 의뢰플래그:108:3 == 2
	SELECTCASE 의뢰플래그:108:1
		CASE 3, 7, 55
			PRINTL 下水道
		CASE 68
			PRINTL 地下水道（龍の巣）
	ENDSELECT

;============
;3.工場
;============
ELSEIF 의뢰플래그:108:3 == 3
	SELECTCASE 의뢰플래그:108:1
		CASE 0
			PRINTL 製菓工場
		CASE 9
			PRINTL 兵器工場
		CASE 23
			PRINTL 山中の秘密基地
		CASE 30
			PRINTL 廃工場
		CASE 67
			PRINTL 廃倉庫
	ENDSELECT



;============
;4.森
;============
ELSEIF 의뢰플래그:108:3 == 4
	SELECTCASE 의뢰플래그:108:1
		CASE 15
			PRINTL 妖精の森
		CASE 29
			PRINTL 森林地帯
	ENDSELECT

;======
;5.山
;======
ELSEIF 의뢰플래그:108:3 == 5
	SELECTCASE 의뢰플래그:108:1
		CASE 32
			PRINTL 雪山
		CASE 34
			PRINTL 山中
		CASE 39
			PRINTL 山岳防衛網
	ENDSELECT
;======
;6.地下迷宮
;======
ELSEIF 의뢰플래그:108:3 == 6
	SELECTCASE 의뢰플래그:108:1
		CASE 59
			SELECTCASE FLAG:현M
				CASE 9
					PRINTFORML 地下洞窟　入口
				CASE 0
					PRINTFORML 地下洞窟　最深部
				CASEELSE
					PRINTFORML 地下洞窟　B{(9-(FLAG:현M))}F
			ENDSELECT
		CASE 60
			SELECTCASE FLAG:현M
				CASE 9
					PRINTFORML 火山洞窟　入口
				CASE 0
					PRINTFORML 火山洞窟　最深部
				CASEELSE
					PRINTFORML 火山洞窟　B{(9-(FLAG:현M))}F
			ENDSELECT
		CASE 62
			SELECTCASE FLAG:현M
				CASE 9
					PRINTFORML サキュバスダンジョン　入口
				CASE 0
					PRINTFORML サキュバスダンジョン　最深部
				CASEELSE
					PRINTFORML サキュバスダンジョン　B{(9-(FLAG:현M))}F
			ENDSELECT
		CASE 66
			SELECTCASE FLAG:현M
				CASE 9
					PRINTFORML ナラク　入口
				CASE 0
					PRINTFORML ナラク　最深部
				CASEELSE
					PRINTFORML ナラク　B{(9-(FLAG:현M))}F
			ENDSELECT
	ENDSELECT
;======
;7.ソドモラ
;======
ELSEIF 의뢰플래그:108:3 == 7
	SELECTCASE FLAG:현M
		CASE 0
			PRINT 大手門
		CASE 1
			PRINT 住宅街
		CASE 2
			PRINT 飲食街
		CASE 3
			PRINT 道場街
		CASE 4
			PRINT 歓楽街
		CASE 5
			PRINT 職人街
		CASE 6
			PRINT ガイア教拠点
		CASE 7
			PRINT 広場
		CASE 8
			PRINT メシア教拠点
		CASE 9
			PRINT 町外れ
		CASE 10
			PRINT 診療所裏手
	ENDSELECT
	CALL TIME_CAL_RANDOM_REQUEST_61
	PRINTFORML {(던전플래그:108:23 / 5 + 1)}日目、%RESULTS%

;======
;8.ランダム迷宮
;======
ELSEIF 의뢰플래그:108:3 == 8
	SELECTCASE 의뢰플래그:108:1
		CASE 71
			SELECTCASE FLAG:현M
				CASE 9
					PRINTFORML 戦場と化した街　入口
				CASE 0
					PRINTFORML 前線基地
				CASEELSE
					PRINTFORML 戦場と化した街-{(9-(FLAG:현M))}
			ENDSELECT
	ENDSELECT




ENDIF
;=======================================================================
;フロアデータ
;=======================================================================

;0：壁　1：床もしくは通路　2：扉　3：宝箱　4：イベント　5：罠　6：上り階段 7：下り階段　8：出口　9：その他特殊処理等
;これらの内、3～9が代入されているマスに、個別の番号を与える処理を実行します。
;この処理を行った後、これらのマスには 「マップ番号*1000 + そのマスがそのマップで左上から数えて何個目のARGタイプのマスか*10 + ARG」が代入されます
;例えば 2003 であった場合、「2番のマップ＝３Ｆで、左上から数えて0個目の3＝宝箱である」という事を意味しています
;尚、0～2はどのマップにも大量に設置されるので、ここでは基本的に個別の番号を与えません。


@MAKE_FLOOR_108
;===================
;1.ビル
;===================
IF 의뢰플래그:108:3 == 1
	SELECTCASE FLAG:현M
		CASE 0
		;１階
			FLAG:최대X = 11 
			FLAG:최대Y = 16 
								;	  01234567890
			CALL MAKE_FLOOR_LINE, 0, "           "
			CALL MAKE_FLOOR_LINE, 1, "           "
			CALL MAKE_FLOOR_LINE, 2, "  1111111  "
			CALL MAKE_FLOOR_LINE, 3, "  2     2  "
			CALL MAKE_FLOOR_LINE, 4, "  6 1 111  "
			CALL MAKE_FLOOR_LINE, 5, "    1 2    "
			CALL MAKE_FLOOR_LINE, 6, "  12111 1  "
			CALL MAKE_FLOOR_LINE, 7, "  1     1  "
			CALL MAKE_FLOOR_LINE, 8, "  1111111  "
			CALL MAKE_FLOOR_LINE, 9, "        2  "
			CALL MAKE_FLOOR_LINE,10, "  1111121  "
			CALL MAKE_FLOOR_LINE,11, "  1     1  "
			CALL MAKE_FLOOR_LINE,12, "  12E   1  "
			CALL MAKE_FLOOR_LINE,13, "        8  "
			CALL MAKE_FLOOR_LINE,14, "           "
			
			;依頼12ではX == 8,Y == 12にイベントを設置
			SIF 던전플래그:108:20 == 3
				CALL SET_TILE, 8 , 12 , 의뢰플래그:108:1 , 12 , 4
			
		CASE 1
		;２階
			FLAG:최대X = 11 
			FLAG:최대Y = 16 
								;	  01234567890
			CALL MAKE_FLOOR_LINE, 0, "           "
			CALL MAKE_FLOOR_LINE, 1, "           "
			CALL MAKE_FLOOR_LINE, 2, "  111 621  "
			CALL MAKE_FLOOR_LINE, 3, "    1   1  "
			CALL MAKE_FLOOR_LINE, 4, "  7 111 1  "
			CALL MAKE_FLOOR_LINE, 5, "  2 1 2 1  "
			CALL MAKE_FLOOR_LINE, 6, "  1 1 1 1  "
			CALL MAKE_FLOOR_LINE, 7, "  1 2 1 2  "
			CALL MAKE_FLOOR_LINE, 8, "  111 111  "
			CALL MAKE_FLOOR_LINE, 9, "        2  "
			CALL MAKE_FLOOR_LINE,10, "  1111111  "
			CALL MAKE_FLOOR_LINE,11, "  1     1  "
			CALL MAKE_FLOOR_LINE,12, "  12E 121  "
			CALL MAKE_FLOOR_LINE,13, "           "
			
			;依頼12ではX == 2,Y == 2にイベントを設置
			SIF 던전플래그:108:20 == 0
				CALL SET_TILE, 2 , 2 , 의뢰플래그:108:1 , 12 , 4
			
		CASE 2
		;３階
			FLAG:최대X = 11 
			FLAG:최대Y = 14 
								;	  01234567890
			CALL MAKE_FLOOR_LINE, 0, "           "
			CALL MAKE_FLOOR_LINE, 1, "           "
			CALL MAKE_FLOOR_LINE, 2, "  11127 1  "
			CALL MAKE_FLOOR_LINE, 3, "  1     1  "
			CALL MAKE_FLOOR_LINE, 4, "  12111 1  "
			CALL MAKE_FLOOR_LINE, 5, "      1 1  "
			CALL MAKE_FLOOR_LINE, 6, "  126 121  "
			CALL MAKE_FLOOR_LINE, 7, "  1     1  "
			CALL MAKE_FLOOR_LINE, 8, "  1111111  "
			CALL MAKE_FLOOR_LINE, 9, "  2     2  "
			CALL MAKE_FLOOR_LINE,10, "  1211111  "
			CALL MAKE_FLOOR_LINE,11, "  1   2 2  "
			CALL MAKE_FLOOR_LINE,12, "  12E 1 1  "
			CALL MAKE_FLOOR_LINE,13, "           "
			
			;依頼12ではX == 8,Y == 2にイベントを設置
			SIF 던전플래그:108:20 == 1
				CALL SET_TILE, 8 , 2 , 의뢰플래그:108:1 , 12 , 4
			
		CASE 3
		;４階
			FLAG:최대X = 11 
			FLAG:최대Y = 16 
								;	  01234567890
			CALL MAKE_FLOOR_LINE, 0, "           "
			CALL MAKE_FLOOR_LINE, 1, "           "
			CALL MAKE_FLOOR_LINE, 2, "  121 621  "
			CALL MAKE_FLOOR_LINE, 3, "    1   1  "
			CALL MAKE_FLOOR_LINE, 4, "  1111111  "
			CALL MAKE_FLOOR_LINE, 5, "  2     1  "
			CALL MAKE_FLOOR_LINE, 6, "  1 7 111  "
			CALL MAKE_FLOOR_LINE, 7, "  1 2 2 1  "
			CALL MAKE_FLOOR_LINE, 8, "  111 1 1  "
			CALL MAKE_FLOOR_LINE, 9, "  2 1   2  "
			CALL MAKE_FLOOR_LINE,10, "  1 12111  "
			CALL MAKE_FLOOR_LINE,11, "  1   1 2  "
			CALL MAKE_FLOOR_LINE,12, "  12E 1 1  "
			CALL MAKE_FLOOR_LINE,13, "           "
			
			;依頼12ではX == 2,Y == 2にイベントを設置
			SIF 던전플래그:108:20 == 2
				CALL SET_TILE, 2 , 2 , 의뢰플래그:108:1 , 12 , 4
			
		CASE 4
		;５階
			FLAG:최대X = 11 
			FLAG:최대Y = 16 
								;	  01234567890
			CALL MAKE_FLOOR_LINE, 0, "           "
			CALL MAKE_FLOOR_LINE, 1, "           "
			CALL MAKE_FLOOR_LINE, 2, "  11127 1  "
			CALL MAKE_FLOOR_LINE, 3, "  2 1   2  "
			CALL MAKE_FLOOR_LINE, 4, "  6 11111  "
			CALL MAKE_FLOOR_LINE, 5, "      2    "
			CALL MAKE_FLOOR_LINE, 6, "  121 111  "
			CALL MAKE_FLOOR_LINE, 7, "  1   1 2  "
			CALL MAKE_FLOOR_LINE, 8, "  1111111  "
			CALL MAKE_FLOOR_LINE, 9, "  2     2  "
			CALL MAKE_FLOOR_LINE,10, "  1 11111  "
			CALL MAKE_FLOOR_LINE,11, "  1   1    "
			CALL MAKE_FLOOR_LINE,12, "  12E 121  "
			CALL MAKE_FLOOR_LINE,13, "           "
			
			
		CASE 5
		;６階
			FLAG:최대X = 11 
			FLAG:최대Y = 16 
								;	  01234567890
			CALL MAKE_FLOOR_LINE, 0, "           "
			CALL MAKE_FLOOR_LINE, 1, "           "
			CALL MAKE_FLOOR_LINE, 2, "  12111 1  "
			CALL MAKE_FLOOR_LINE, 3, "      111  "
			CALL MAKE_FLOOR_LINE, 4, "  7 121 1  "
			CALL MAKE_FLOOR_LINE, 5, "  2 1   1  "
			CALL MAKE_FLOOR_LINE, 6, "  1 1 121  "
			CALL MAKE_FLOOR_LINE, 7, "  111   1  "
			CALL MAKE_FLOOR_LINE, 8, "  1 12111  "
			CALL MAKE_FLOOR_LINE, 9, "  2    2   "
			CALL MAKE_FLOOR_LINE,10, "  1211111  "
			CALL MAKE_FLOOR_LINE,11, "  1     1  "
			CALL MAKE_FLOOR_LINE,12, "  12E 121  "
			CALL MAKE_FLOOR_LINE,13, "           "
			
			
			;依頼5ではX == 2,Y == 2にイベントを設置
			CALL SET_TILE, 2 , 2 , 의뢰플래그:108:1 , 5 , 4
			;依頼6ではX == 2,Y == 2にイベントを設置
			CALL SET_TILE, 2 , 2 , 의뢰플래그:108:1 , 6 , 4
			;依頼12ではX == 6,Y == 6にイベントを設置
			SIF 던전플래그:108:20 == 4
				CALL SET_TILE, 6 , 6 , 의뢰플래그:108:1 , 12 , 4
			;依頼40ではX == 6,Y == 12にイベントを設置
			CALL SET_TILE, 6 , 12 , 의뢰플래그:108:1 , 40 , 4
			;依頼46ではX == 8,Y == 2にイベントを設置
			CALL SET_TILE, 8 , 2 , 의뢰플래그:108:1 , 46 , 4
			;依頼48ではX == 8,Y == 2にイベントを設置
			CALL SET_TILE, 8 , 2 , 의뢰플래그:108:1 , 48 , 4
			
			
	ENDSELECT




;===========================
;2下水道（地下水道）
;===========================
ELSEIF 의뢰플래그:108:3 == 2
	SELECTCASE FLAG:현M
		CASE 0
			FLAG:최대X = 30 
			FLAG:최대Y = 30 
			;									1		  2			
			;						  012345678901234567890123456789
			CALL MAKE_FLOOR_LINE, 0, "             8998             "
			CALL MAKE_FLOOR_LINE, 1, "             1991             "
			CALL MAKE_FLOOR_LINE, 2, "             1991             "
			CALL MAKE_FLOOR_LINE, 3, "             1991             "
			CALL MAKE_FLOOR_LINE, 4, "             1991 1111111111  "
			CALL MAKE_FLOOR_LINE, 5, "             1991 19999999999 "
			CALL MAKE_FLOOR_LINE, 6, "             1991 1911111111  "
			CALL MAKE_FLOOR_LINE, 7, "             1991 191         "
			CALL MAKE_FLOOR_LINE, 8, "             1991 191         "
			CALL MAKE_FLOOR_LINE, 9, "          11119911191         "
			CALL MAKE_FLOOR_LINE,10, "         1199999999911        "
			CALL MAKE_FLOOR_LINE,11, "       1199111111119911       "
			CALL MAKE_FLOOR_LINE,12, " 111111119911     11991       "
			CALL MAKE_FLOOR_LINE,13, " 19999999911       1191       "
			CALL MAKE_FLOOR_LINE,14, " 1911111191         191       "
			CALL MAKE_FLOOR_LINE,15, " 191    191   999   191       "
			CALL MAKE_FLOOR_LINE,16, " 191  11191   999   191111111 "
			CALL MAKE_FLOOR_LINE,17, "  9  9999911  191  1199999991 "
			CALL MAKE_FLOOR_LINE,18, "      1119911 191 11991111191 "
			CALL MAKE_FLOOR_LINE,19, "        119911191119911   191 "
			CALL MAKE_FLOOR_LINE,20, "         1199999999911     9  "
			CALL MAKE_FLOOR_LINE,21, "          19111111191         "
			CALL MAKE_FLOOR_LINE,22, "          191     191         "
			CALL MAKE_FLOOR_LINE,23, "          191     191         "
			CALL MAKE_FLOOR_LINE,24, "          191     191         "
			CALL MAKE_FLOOR_LINE,25, "          191     191         "
			CALL MAKE_FLOOR_LINE,26, "          191     191         "
			CALL MAKE_FLOOR_LINE,27, "          191     191         "
			CALL MAKE_FLOOR_LINE,28, "           9       9          "
			CALL MAKE_FLOOR_LINE,29, "                              "
			;						  012345678901234567890123456789
			;						  0			1		  2			
			;						 
			;依頼3ではX == 13～16,Y == 8にイベント(=4)を設置
			FOR LOCAL, 13, 17
				CALL SET_TILE, LOCAL , 8 , 의뢰플래그:108:1 , 3 , 4
			NEXT
			;依頼7ではX == 15,Y == 21にイベント(=4)を設置
			CALL SET_TILE, 15 , 21 , 의뢰플래그:108:1 , 7 , 4
			;依頼55ではX == 15,Y == 16にイベント(=4)を設置
			CALL SET_TILE, 15 , 16 , 의뢰플래그:108:1 , 55 , 4
			;依頼68ではX == 15,Y == 16にイベント(=4)を設置
			CALL SET_TILE, 15 , 16 , 의뢰플래그:108:1 , 68 , 4
	ENDSELECT

;===================
;3.工場（無人発電所）
;===================
ELSEIF 의뢰플래그:108:3 == 3
	SELECTCASE FLAG:현M
		CASE 0
			FLAG:최대X = 41 
			FLAG:최대Y = 31 
			;									1		  2			3		  4
			;						  01234567890123456789012345678901234567890
			CALL MAKE_FLOOR_LINE, 0, "                                         "
			CALL MAKE_FLOOR_LINE, 1, " 11111111111111 11111111111111 111111111 "
			CALL MAKE_FLOOR_LINE, 2, " 11111111111111 11111111111111 111111111 "
			CALL MAKE_FLOOR_LINE, 3, " 11111111111111 11111111111111 111111111 "
			CALL MAKE_FLOOR_LINE, 4, " 11111111111111    111    1111 111111111 "
			CALL MAKE_FLOOR_LINE, 5, " 11111111111111 111111111 1111 111111111 "
			CALL MAKE_FLOOR_LINE, 6, " 11111111111111 111111111 1111 111111111 "
			CALL MAKE_FLOOR_LINE, 7, " 11111111111111 111111111 1111 111111111 "
			CALL MAKE_FLOOR_LINE, 8, "8111111111111112111111111 1111 111111111 "
			CALL MAKE_FLOOR_LINE, 9, "8111111111111112111111111 1111 111111111 "
			CALL MAKE_FLOOR_LINE,10, " 111111111111112111111111 1111 111111111 "
			CALL MAKE_FLOOR_LINE,11, "                  1111111 1111  222      "
			CALL MAKE_FLOOR_LINE,12, " 11111111111111 1111111   11111111111111 "
			CALL MAKE_FLOOR_LINE,13, " 11111111111111 111111111 11111111111111 "
			CALL MAKE_FLOOR_LINE,14, " 11111111111111 111111111 11111111111111 "
			CALL MAKE_FLOOR_LINE,15, " 111111 1111111                       11 "
			CALL MAKE_FLOOR_LINE,16, "    111 1111111 111111111111111111111 11 "
			CALL MAKE_FLOOR_LINE,17, " 111111 11111112111111111111111111111 11 "
			CALL MAKE_FLOOR_LINE,18, " 111111 11111112111111111111111111111 11 "
			CALL MAKE_FLOOR_LINE,19, " 111111 11111112111111111111111111111 11 "
			CALL MAKE_FLOOR_LINE,20, " 111    111     111111111111111111111 11 "
			CALL MAKE_FLOOR_LINE,21, " 111111 1111111                       11 "
			CALL MAKE_FLOOR_LINE,22, " 111111 1111111 111111111111111111111111 "
			CALL MAKE_FLOOR_LINE,23, " 111111 1111111   1111111111111111111111 "
			CALL MAKE_FLOOR_LINE,24, "   2    11111111111111111111111111111111 "
			CALL MAKE_FLOOR_LINE,25, " 111111 11111111111111111111111111111111 "
			CALL MAKE_FLOOR_LINE,26, " 111111   222                222         "
			CALL MAKE_FLOOR_LINE,27, " 111111 11111111111111111 11111111111111 "
			CALL MAKE_FLOOR_LINE,28, " 111111 11111111111111111 11111111111111 "
			CALL MAKE_FLOOR_LINE,29, " 111111 11111111111111111 11111111111111 "
			CALL MAKE_FLOOR_LINE,30, "   88                                    "
			;						  01234567890123456789012345678901234567890 
			;						  0			1		  2			3		  4
			;						 
			;依頼0ではX == 7,Y == 2にイベント(=4)を設置
			CALL SET_TILE, 7 , 2 , 의뢰플래그:108:1 , 0 , 4
			;依頼9ではX == 34,Y == 2にイベント(=4)を設置
			CALL SET_TILE, 34 , 2 , 의뢰플래그:108:1 , 9 , 4
			;依頼23ではX == 34,Y == 2にイベント(=4)を設置
			CALL SET_TILE, 34 , 2 , 의뢰플래그:108:1 , 23 , 4
			;依頼30では(X,Y) == (8,2)(31,18)(24,14)(39,1)(39,28)(24,29)にイベント(=4)を設置
			CALL SET_TILE,  8 ,  2 , 의뢰플래그:108:1 , 30 , 4
			CALL SET_TILE, 31 , 18 , 의뢰플래그:108:1 , 30 , 4
			CALL SET_TILE, 24 , 14 , 의뢰플래그:108:1 , 30 , 4
			CALL SET_TILE, 39 ,  1 , 의뢰플래그:108:1 , 30 , 4
			CALL SET_TILE, 39 , 28 , 의뢰플래그:108:1 , 30 , 4
			CALL SET_TILE, 24 , 29 , 의뢰플래그:108:1 , 30 , 4
			;依頼67ではX == 24,Y == 18にイベント(=4)を設置
			CALL SET_TILE, 24 , 18 , 의뢰플래그:108:1 , 67 , 4
			
			
	ENDSELECT



;===================
;4.森(トキワ)
;===================
ELSEIF 의뢰플래그:108:3 == 4
	SELECTCASE FLAG:현M
		CASE 0
			FLAG:최대X = 25 
			FLAG:최대Y = 28 
			;									1		  2
			;						  0123456789012345678901234
			CALL MAKE_FLOOR_LINE, 0, "  8                     "
			CALL MAKE_FLOOR_LINE, 1, " 111  111    111111     "
			CALL MAKE_FLOOR_LINE, 2, " 111 11 11  11    111   "
			CALL MAKE_FLOOR_LINE, 3, " 111 11 11 111      1   "
			CALL MAKE_FLOOR_LINE, 4, " 111 11 11 11111111 1   "
			CALL MAKE_FLOOR_LINE, 5, " 111 11 11 1111111111   "
			CALL MAKE_FLOOR_LINE, 6, " 111 11 11 1111111111   "
			CALL MAKE_FLOOR_LINE, 7, " 111111 11 111   11     "
			CALL MAKE_FLOOR_LINE, 8, "    111  1111    11     "
			CALL MAKE_FLOOR_LINE, 9, "    111          11     "
			CALL MAKE_FLOOR_LINE,10, "    1            11111  "
			CALL MAKE_FLOOR_LINE,11, "    1111         11111  "
			CALL MAKE_FLOOR_LINE,12, "       11        11 11  "
			CALL MAKE_FLOOR_LINE,13, "                 11 11  "
			CALL MAKE_FLOOR_LINE,14, " 111             11 11  "
			CALL MAKE_FLOOR_LINE,15, " 1 1             11 11  "
			CALL MAKE_FLOOR_LINE,16, " 111             11 11  "
			CALL MAKE_FLOOR_LINE,17, " 111111          11 11  "
			CALL MAKE_FLOOR_LINE,18, " 111111           111   "
			CALL MAKE_FLOOR_LINE,19, " 111111           111   "
			CALL MAKE_FLOOR_LINE,20, "   1111111111111  11111 "
			CALL MAKE_FLOOR_LINE,21, "    11    11111  1 1111 "
			CALL MAKE_FLOOR_LINE,22, "    11    11111  1 11 1 "
			CALL MAKE_FLOOR_LINE,23, " 11 1111111  11  1 11 1 "
			CALL MAKE_FLOOR_LINE,24, " 1111111111  11111 1111 "
			CALL MAKE_FLOOR_LINE,25, " 11111111111111111 1111 "
			CALL MAKE_FLOOR_LINE,26, " 11     111111  1111111 "
			CALL MAKE_FLOOR_LINE,27, "          8             "
			;						  0123456789012345678901234
			;						  0			1		  2	
			
			;依頼15ではX == 21,Y == 10にイベント(=4)を設置
			CALL SET_TILE, 21 , 10 , 의뢰플래그:108:1 , 15 , 4
			;依頼29ではX == 18,Y == 8にイベント(=4)を設置
			CALL SET_TILE, 18 ,  8 , 의뢰플래그:108:1 , 29 , 4
			;依頼57ではX ==  8,Y == 12にイベント(=4)を設置
			CALL SET_TILE,  8 , 12 , 의뢰플래그:108:1 , 57 , 4
			;依頼70ではイベントを4回を設置
			IF 의뢰플래그:108:1 == 70
				CALL SET_TILE, 10, 25, 의뢰플래그:108:4, 0 , 4
				CALL SET_TILE,  4, 18, 의뢰플래그:108:4, 1 , 4
				CALL SET_TILE, 16,  5, 의뢰플래그:108:4, 2 , 4
				CALL SET_TILE,  5,  8, 의뢰플래그:108:4, 3 , 4
			ENDIF
	ENDSELECT
;============
;5山(カラコル)
;============
ELSEIF 의뢰플래그:108:3 == 5
	SELECTCASE FLAG:현M
		CASE 0
			FLAG:최대X = 30
			FLAG:최대Y = 30
			;								   1		 2	
			;						 01234567890123456789012345678
			CALL MAKE_FLOOR_LINE, 0,"                             "
			CALL MAKE_FLOOR_LINE, 1,"       11                1   "
			CALL MAKE_FLOOR_LINE, 2,"        11 1      1     111  "
			CALL MAKE_FLOOR_LINE, 3,"     1 11  111    111   111  "
			CALL MAKE_FLOOR_LINE, 4,"     11111111      111  11   "
			CALL MAKE_FLOOR_LINE, 5,"    11     1 1      111111   "
			CALL MAKE_FLOOR_LINE, 6,"   11      111   1     111   "
			CALL MAKE_FLOOR_LINE, 7,"   11       11  11    11     "
			CALL MAKE_FLOOR_LINE, 8,"   1        11991111 111     "
			CALL MAKE_FLOOR_LINE, 9,"  11111     11   111 11      "
			CALL MAKE_FLOOR_LINE,10,"  11  1   1 11     111111    "
			CALL MAKE_FLOOR_LINE,11,"   11   11111      111  1111 "
			CALL MAKE_FLOOR_LINE,12,"    11111 111     111     1  "
			CALL MAKE_FLOOR_LINE,13,"    11     1     1111        "
			CALL MAKE_FLOOR_LINE,14,"     1           1  9        "
			CALL MAKE_FLOOR_LINE,15,"    11              9        "
			CALL MAKE_FLOOR_LINE,16,"    1          1    111   1  "
			CALL MAKE_FLOOR_LINE,17,"   111         11    111 11  "
			CALL MAKE_FLOOR_LINE,18,"   1111        111 111 111   "
			CALL MAKE_FLOOR_LINE,19,"    1           11111  111   "
			CALL MAKE_FLOOR_LINE,20,"    1            111   1     "
			CALL MAKE_FLOOR_LINE,21,"   11            1 1   11    "
			CALL MAKE_FLOOR_LINE,22,"   1111  119911   11   11    "
			CALL MAKE_FLOOR_LINE,23,"  111111111  1111111  11     "
			CALL MAKE_FLOOR_LINE,24," 1111              111111    "
			CALL MAKE_FLOOR_LINE,25," 111              111   111  "
			CALL MAKE_FLOOR_LINE,26,"  8              11      11  "
			CALL MAKE_FLOOR_LINE,27,"                11           "
			CALL MAKE_FLOOR_LINE,28,"                             "
			;						 01234567890123456789012345678
			;						 		   1		 2
			
			;依頼32ではX == 20,Y == 10にイベント(=4)を設置
			CALL SET_TILE, 20 , 10 , 의뢰플래그:108:1 , 32 , 4
			;依頼34ではX == 21,Y == 10にイベント(=4)を設置
			CALL SET_TILE, 21 , 10 , 의뢰플래그:108:1 , 34 , 4
			;依頼39では(X,Y) == (25,26)(11,13)(26,12)にイベント(=4)を設置
			CALL SET_TILE, 25 , 26 , 의뢰플래그:108:1 , 39 , 4
			CALL SET_TILE, 11 , 13 , 의뢰플래그:108:1 , 39 , 4
			CALL SET_TILE, 26 , 12 , 의뢰플래그:108:1 , 39 , 4
	ENDSELECT

;===========================
;6地下迷宮(タルタロスを参考に作成）
;===========================
ELSEIF 의뢰플래그:108:3 == 6
	CALL RAND_DANGEON_MAKE


;=====================================================================
;7ソドモラ
;=====================================================================
ELSEIF 의뢰플래그:108:3 == 7
	CALL SODOMORRAH_DANGEON_MAKE

;===========================
;8ランダム迷路
;===========================
ELSEIF 의뢰플래그:108:3 == 8
	CALL RAND_DANGEON_MAKE


ENDIF

;=======================================================================
;タイル定義
;=======================================================================
@DEFINE_TILES_108, ARGS, ARG, ARG:1
SELECTCASE ARGS
	CASE "m";人物
		RETURN 14
	CASE "U";↑
		RETURN 24
	CASE "d";↓	*D:はダークゾーン
		RETURN 34
	CASE "r";→	*Rは罠
		RETURN 44
	CASE "L";←
		RETURN 54
	CASE "w";水
		RETURN 10
	CASE "t";木(Tはターミナル）
		RETURN 20
ENDSELECT
IF 의뢰플래그:108:3 == 7
	SELECTCASE ARGS
		CASE "a";食い処
			RETURN 12
		CASE "b";茶屋
			RETURN 22
		CASE "c";宿屋
			RETURN 32
		CASE "e";呑み処(dは使用済み）
			RETURN 42
		CASE "f";薬屋
			RETURN 52
		CASE "g";自宅
			RETURN 62
		CASE "h";料亭
			RETURN 72
		CASE "i";道場、かすみ家など
			RETURN 82
	ENDSELECT
ENDIF






;=======================================================================
;特殊タイル表示
;TILE_MARK_COMMON_{FLAG:현던전}_X
;=======================================================================
;ARG:1、ARG:2がX座標とY座標
;ARGはそこに代入されている値
;RESULTSに代入した文字がマップに表示されますが、必ず全角1文字分になるようにしてください。そうでないと表示が崩れます。
; は0
@TILE_MARK_COMMON_108_0,ARG,ARG:1,ARG:2
SELECTCASE ARG
	CASE 0
		IF 의뢰플래그:108:3 == 4
			SETCOLOR 0, 255, 0
			RESULTS = 木
		ELSE
			RESULTS = ■
		ENDIF
	CASE 10
		SETCOLOR 0, 0, 255
		RESULTS = ミ
	CASE 20
		SETCOLOR 0, 255, 0
		RESULTS = 木
	CASEELSE
		RETURN 1
ENDSELECT

@TILE_MARK_COMMON_108_2,ARG,ARG:1,ARG:2
;ソドモラ
IF 의뢰플래그:108:3 == 7
	SELECTCASE ARG
		CASE 12
			RESULTS = 食
		CASE 22
			RESULTS = 茶
		CASE 32
			RESULTS = 宿
		CASE 42
			RESULTS = 酒
		CASE 52
			RESULTS = 薬
		CASE 62
			RESULTS = 自
		CASEELSE
			RESULTS = 扉
	ENDSELECT
ELSE
	RETURN 1
ENDIF

@TILE_MARK_COMMON_108_4, ARG, ARG:1, ARG:2
SELECTCASE ARG
	;メッセンジャーを緑色で"ｍ"と表示
	CASE 14
		SETCOLOR 0x00ff00
		RESULTS = ｍ
	;↑
	CASE 24
		;火山洞窟
		IF 의뢰플래그:108:1 == 60
			SETCOLOR 255, 0, 0
			RESULTS = 岩
		ELSE
			RESULTS = ↑
		ENDIF
	;↓
	CASE 34
		RESULTS = ↓
	;↓
	CASE 44
		RESULTS = →
	CASE 54
		RESULTS = ←
	CASEELSE
		RETURN 1
ENDSELECT


@TILE_MARK_COMMON_108_9,ARG,ARG:1,ARG:2
;下水道
IF 의뢰플래그:108:3 == 2
	SETCOLOR 0, 0, 255
	RESULTS = ミ
;山
ELSEIF 의뢰플래그:108:3 == 5
	SETCOLOR 178, 102, 64
	RESULTS = 橋
;ソドモラ
ELSEIF 의뢰플래그:108:3 == 7
	IF FLAG:현M == 10
		SETCOLOR 255, 0, 0
		RESULTS = 花
	ELSEIF FLAG:현M == 0 || (FLAG:현M == 7 &&던전플래그:108:23 == 29)
		RESULTS = 屋
	ELSE
		SETCOLOR 178, 102, 64
		RESULTS = 橋
	ENDIF
ELSE
	RETURN 1
ENDIF


;=======================================================================
;タイル調整
;=======================================================================
;ARG:1、ARG:2はそれぞれX座標とY座標
;ARGはその座標に現在代入されている番号
;0：壁　1：床もしくは通路　2：扉　3：宝箱　4：イベント　5：罠　6：上り階段 7：下り階段　8：出口　9：その他特殊処理等
;これらの内、3～9が代入されているマスに、個別の番号を与える処理を実行します。
;この処理を行った後、これらのマスには 「マップ番号*1000 + そのマスがそのマップで左上から数えて何個目のARGタイプのマスか*10 + ARG」が代入されます
;例えば 2003 であった場合、「2番のマップ＝３Ｆで、左上から数えて0個目の3＝宝箱である」という事を意味しています
;尚、0～2はどのマップにも大量に設置されるので、ここでは基本的に個別の番号を与えません。
;
;何個目のARGタイプのマスなのか、は左上の(0,0)から右まで探し、その後一つ下の段に移って同様に探す、というのを右下まで繰り返して数えます。
@MAKE_FLOOR_LINE_COMMON_108, ARG, ARG:1, ARG:2
#LOCALSIZE 10
;LOCAL:0～9 それぞれ何個目のARGなのかを記憶しています
;LOCAL:3 宝箱→使用フラグは던전플래그:70~74
;指定された座標が(0,0)である場合、つまり新しいマップを読み込んだ時はLOCALを消去します
SIF ARG:1 == 0 && ARG:2 == 0
	VARSET LOCAL
SELECTCASE ARG
	CASE 3
		;宝箱は多くの場合、開けたフラグが立っていると床を代入します
		;そのマップでどの宝箱を開けたかは、そのマップの宝箱管理用変数の何番目のビットが立っているかで判断します
		IF GETBIT(던전플래그:(FLAG:현던전):(FLAG:현M+70), LOCAL:3)
			DA:(ARG:1):(ARG:2) = 1
		ELSE
			DA:(ARG:1):(ARG:2) = FLAG:현M *1000 + LOCAL:3 * 10 + 3
		ENDIF
		LOCAL:3++
	CASE 4 TO 9
		DA:(ARG:1):(ARG:2) = FLAG:현M *1000 + LOCAL:ARG * 10 + ARG
		LOCAL:ARG++
	
ENDSELECT
;

;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_108,ARG
;ビル
IF 의뢰플래그:108:3 == 1
	FLAG:현M = 0
	FLAG:현X = 8
	FLAG:현Y = 13
;下水道
ELSEIF 의뢰플래그:108:3 == 2
	FLAG:현M = 0
	FLAG:현X = 13
	FLAG:현Y = 1
;工場
ELSEIF 의뢰플래그:108:3 == 3
	FLAG:현M = 0
	FLAG:현X = 3
	FLAG:현Y = 29
;森
ELSEIF 의뢰플래그:108:3 == 4
	FLAG:현M = 0
	FLAG:현X = 10
	FLAG:현Y = 26
;山
ELSEIF 의뢰플래그:108:3 == 5
	FLAG:현M = 0
	FLAG:현X = 2
	FLAG:현Y = 25
;地下迷宮
ELSEIF 의뢰플래그:108:3 == 6
	FLAG:현M = 9
	FLAG:현X = 15
	FLAG:현Y = 8
;ソドモラ
ELSEIF 의뢰플래그:108:3 == 7
	FLAG:현M = 0
	FLAG:현X = 6
	FLAG:현Y = 28
;ランダム迷宮
ELSEIF 의뢰플래그:108:3 == 8
	FLAG:현M = 9
	FLAG:현X = 15
	FLAG:현Y = 3
ENDIF
RETURN 0




;=======================================================================
;階段（ランダム依頼）
;=======================================================================
;上り階段処理
@UPSTAIRS_108
;1つのマップに上り階段が１つ、下り階段が１つ、上の階に進む＝マップ番号が一つ増えるという構造になっています
;なので、下記のように書けば一行で済みます
CALL DUNGEON_UPSTAIRS_ID, FLAG:현M+1 , (FLAG:현M + 1) * 1000 + 7
RETURN 1

;下り階段処理
@DOWNSTAIRS_108
;上り階段同様に、エコービルは下り階段も1行で記述できます
CALL DUNGEON_DOWNSTAIRS_ID, FLAG:현M-1 , (FLAG:현M - 1) * 1000 + 6
RETURN 1


;=======================================================================
;エレベーター定義
;=======================================================================
;ARGはそのエレベーターのタイル
;RESULT:1～5にエレベータが存在するマップの番号を代入し、RESULTS:1～5には表示したい名称を代入、RETURNでは-1を返しておく
@DUNGEON_ELEVATOR_108, ARG
;ビル
IF 의뢰플래그:108:3 == 1
	RESULT:1 = 0
	RESULT:2 = 1
	RESULT:3 = 2
	RESULT:4 = 3
	RESULT:5 = 4
	RESULT:6 = 5
	SELECTCASE 의뢰플래그:108:1
		CASE 5, 6
			RESULTS:1 = Ｂ６Ｆ
			RESULTS:2 = Ｂ５Ｆ
			RESULTS:3 = Ｂ４Ｆ
			RESULTS:4 = Ｂ３Ｆ
			RESULTS:5 = Ｂ２Ｆ
			RESULTS:6 = Ｂ１Ｆ
			RETURN -1
		CASEELSE
			RESULTS:1 = １Ｆ
			RESULTS:2 = ２Ｆ
			RESULTS:3 = ３Ｆ
			RESULTS:4 = ４Ｆ
			RESULTS:5 = ５Ｆ
			RESULTS:6 = ６Ｆ
			RETURN -1
	ENDSELECT
ENDIF

