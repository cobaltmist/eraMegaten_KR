
;=======================================================================
;키타야마 대학
;
;던전플래그:49:0 = 1.ボス
;던전플래그:49:1 = 1.初回마요네
;던전플래그:49:2 = 1.ボス前扉
;던전플래그:49:3 = 1.ルー大柴 2.四階扉
;던전플래그:49:4 = 1.三階ショートカット扉
;던전플래그:49:5 = 1.ジャンク

;던전ナンバー49
;=======================================================================

;=======================================================================
;攻略조건
;=======================================================================
@PREREQUISITE_DUNGEON_49
;教授宅襲撃クリア
SIF 의뢰플래그:5:0 == 1 && !던전플래그:49:0 && BASE:MASTER:LV >= 12
	RETURN 1
RETURN 0

;=======================================================================
;던전名
;=======================================================================
@GET_DUNGEON_NAME_49
RESULTS = 키타야마 대학


;=======================================================================
;플로어名표시
;=======================================================================
@FLOORNAME_49
PRINT 키타야마 대학
SELECTCASE FLAG:현M
	CASE 0
		PRINTL   교외
	CASE 1
		PRINTL   １Ｆ
	CASE 2
		PRINTL   ２Ｆ
	CASE 3
		PRINTL   ３Ｆ
	CASE 4
		PRINTL   ４Ｆ
	CASE 5
		PRINTL   １Ｆ
	CASE 6
		PRINTL   ２Ｆ
ENDSELECT


;=======================================================================
;特殊タイル표시
;=======================================================================


@TILE_MARK_COMMON_49_4, ARG , ARG:1 , ARG:2
SELECTCASE ARG / 10
	CASE 100
		RESULTS = \@던전플래그:49:5 == 0 ?Dr#□\@
	CASE 200
		RESULTS = \@던전플래그:49:3 == 0 ?！#□\@
	CASE 300
		RESULTS = \@던전플래그:49:0 == 0 ?Ｓ#□\@
ENDSELECT

@TILE_MARK_COMMON_49_5, ARG , ARG:1 , ARG:2
	RESULTS = \@GET_FLOOR_ANALYZE_T(ARG:1, ARG:2) == 2 ?덫#□\@

@TILE_MARK_COMMON_49_9, ARG , ARG:1 , ARG:2
	RESULTS = 문


;=======================================================================
;スタート地点입력
;=======================================================================
@START_DUNGEON_49,ARG
IF 던전플래그:49:0 == 0
	FLAG:현M = 0
	FLAG:현X = 9
	FLAG:현Y = 0
ELSEIF 던전플래그:49:0 == 1
	FLAG:현M = 5
	FLAG:현X = 7
	FLAG:현Y = 0
ENDIF
;CALL AUTOMAP

;=======================================================================
;スタート時이벤트
;=======================================================================
@EVENT_DUNGEON_START_49
IF 던전플래그:49:1 == 0
	CUSTOMDRAWLINE =
	PRINTFORMW 키타야마 대학의 이계화 조사 의뢰를 가져온 마요네는 %CALLNAME:MASTER%에게 이렇게 말했다
	PRINTFORMW 「도서관, 키타야마 대학, 일련의 이계화에는 우리 조직의 인물이 관계되었답니다」
	PRINTFORMW 「당신의 임무는 가급적 신속하게 그와 접촉해, 본건에 야타가라스가 개입했으므로 조직은 손을 뗀다는 것을 전달하는 일」
	PRINTFORMW 「그의 이름은 시드 데이비스……  흑인 신부와 만나면 싸우려는 생각하지 말아요」
	던전플래그:49:1 = 1
ENDIF

;=======================================================================
;指定したマスに進入가능か
;=======================================================================
@CAN_ENTER_49,ARG,ARG:1
SELECTCASE DA:ARG:(ARG:1)
CASE 12
	IF 던전플래그:49:4 == 0 && ARG > FLAG:현X
		CALL MESSAGE_WINDOW_D, "", "＞반대쪽에서 열쇠가 잠겨있는 것 같다"
		RETURN 0
	ELSE
		IF 던전플래그:49:4 == 0 
		CALL MESSAGE_WINDOW_D, "", "＞열쇠를 풀었다"
		던전플래그:49:4 = 2
		ENDIF
		RETURN 1
	ENDIF
CASE 22
	IF 던전플래그:49:3 == 1
		CALL MESSAGE_WINDOW_D, "", "＞문 너머에서 사기가 느껴진다/＞문을 열겠는가?"
		CALL INPUT_YN_D
		SIF RESULT == 1
			RETURN 0
	ELSE
		CALL MESSAGE_WINDOW_D, "", "＞문은 강한 마력으로 닫혀있다"
		RETURN 0
	ENDIF
ENDSELECT
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT

;=======================================================================
;플로어데이터
;=======================================================================
@MAKE_FLOOR_49
SELECTCASE FLAG:현M
	CASE 0
	;키타야마대학校外
		FLAG:최대X = 22
		FLAG:최대Y = 18
							    ;012345678901234567890123
		CALL MAKE_FLOOR_LINE, 0,"         8            "
		CALL MAKE_FLOOR_LINE, 1," 11111   1 3111 11111 "
		CALL MAKE_FLOOR_LINE, 2," 1   11  1    1 1  13 "
		CALL MAKE_FLOOR_LINE, 3," 111  11 1 3  1 1     "
		CALL MAKE_FLOOR_LINE, 4,"  111  1 1 1 11111111 "
		CALL MAKE_FLOOR_LINE, 5,"  111  1 1 111     11 "
		CALL MAKE_FLOOR_LINE, 6,"   1   1 1 1   11111  "
		CALL MAKE_FLOOR_LINE, 7," 11111 1 111  11      "
		CALL MAKE_FLOOR_LINE, 8," 1   H 1    111 11111 "
		CALL MAKE_FLOOR_LINE, 9," 11111 111111   1   1 "
		CALL MAKE_FLOOR_LINE,10,"   1          11111 1 "
		CALL MAKE_FLOOR_LINE,11,"  11111P1     1 2 1 1 "
		CALL MAKE_FLOOR_LINE,12," 11     11    1 W 1 1 "
		CALL MAKE_FLOOR_LINE,13," 1  31  11    1   1 1 "
		CALL MAKE_FLOOR_LINE,14," 1  111 11111 11111 1 "
		CALL MAKE_FLOOR_LINE,15," 11  1111 1 H       1 "
		CALL MAKE_FLOOR_LINE,16,"  1111    11111111111 "
		CALL MAKE_FLOOR_LINE,17,"                      "

;키타야마대학本館
	CASE 1
	;一階西
		FLAG:최대X = 31
		FLAG:최대Y = 8
							    ;012345678901234567890123456789
		CALL MAKE_FLOOR_LINE, 0,"                               "
		CALL MAKE_FLOOR_LINE, 1," 112D           w    13  1121  "
		CALL MAKE_FLOOR_LINE, 2," 1     111 1    2    11  11 11 "
		CALL MAKE_FLOOR_LINE, 3," 1 6   1 2 2  11111  2   2   2 "
		CALL MAKE_FLOOR_LINE, 4," 1 2   1 111  11111  111 126 1 "
		CALL MAKE_FLOOR_LINE, 5," 2 1   1     1111111   2     1 "
		CALL MAKE_FLOOR_LINE, 6," 111   1111111  T  11111111111 "
		CALL MAKE_FLOOR_LINE, 7,"                               "

	CASE 2
	;二階
		FLAG:최대X = 28
		FLAG:최대Y = 8
							    ;012345678901234567890123456789
		CALL MAKE_FLOOR_LINE, 0,"                            "
		CALL MAKE_FLOOR_LINE, 1," 111111 111    1211 1111111 "
		CALL MAKE_FLOOR_LINE, 2," 1    1 1 111111 1121     1 "
		CALL MAKE_FLOOR_LINE, 3," 1 76 1 1           1 127 1 "
		CALL MAKE_FLOOR_LINE, 4," 1 22 1 2   6       1 126 1 "
		CALL MAKE_FLOOR_LINE, 5," 111111 11  2 3  11 1 1   1 "
		CALL MAKE_FLOOR_LINE, 6,"        11211 1121121 11111 "
		CALL MAKE_FLOOR_LINE, 7,"                            "

	CASE 3
	;三階
		FLAG:최대X = 25
		FLAG:최대Y = 14
							    ;012345678901234567890123456789
		CALL MAKE_FLOOR_LINE, 0,"                         "
		CALL MAKE_FLOOR_LINE, 1," 111111 121  121 1121121 "
		CALL MAKE_FLOOR_LINE, 2," 1    1 2 1 11 1 31    1 "
		CALL MAKE_FLOOR_LINE, 3," 1 67 1 R 111  1       1 "
		CALL MAKE_FLOOR_LINE, 4," 1 22 1        111111111 "
		CALL MAKE_FLOOR_LINE, 5," 111111 111111   1 1   1 "
		CALL MAKE_FLOOR_LINE, 6,"        1    111 1 1   1 "
		CALL MAKE_FLOOR_LINE, 7,"        1 67   1 1 1   1 "
		CALL MAKE_FLOOR_LINE, 8,"        1 22 111 11127 1 "
		CALL MAKE_FLOOR_LINE, 9,"        111111   1 1   1 "
		CALL MAKE_FLOOR_LINE,10,"        2        1 1   1 "
		CALL MAKE_FLOOR_LINE,11,"        11       1 1   1 "
		CALL MAKE_FLOOR_LINE,12,"        11211111t1111111 "
		CALL MAKE_FLOOR_LINE,13,"                         "

        
	CASE 4
	;四階
		FLAG:최대X = 21
		FLAG:최대Y = 14
							    ;012345678901234567890123456789
		CALL MAKE_FLOOR_LINE, 0,"                     "
		CALL MAKE_FLOOR_LINE, 1," 111111 112111111111 "
		CALL MAKE_FLOOR_LINE, 2," 1    1 11         1 "
		CALL MAKE_FLOOR_LINE, 3," 1111 1 2  1111211 1 "
		CALL MAKE_FLOOR_LINE, 4,"    1 1 1111  1 11 1 "
		CALL MAKE_FLOOR_LINE, 5,"  7 1 1       1    1 "
		CALL MAKE_FLOOR_LINE, 6,"  2 1 1 11111 1211 1 "
		CALL MAKE_FLOOR_LINE, 7," 11 1 1 1   121 1T 1 "
		CALL MAKE_FLOOR_LINE, 8," 1  1 1 1 7 1      1 "
		CALL MAKE_FLOOR_LINE, 9," 1  1 1 1 2 1      1 "
		CALL MAKE_FLOOR_LINE,10," 1  1 1 11111  11111 "
		CALL MAKE_FLOOR_LINE,11," 1111 1        1   s "
		CALL MAKE_FLOOR_LINE,12,"      1111111111   S "
		CALL MAKE_FLOOR_LINE,13,"                     "

	CASE 5
	;現世一階
		FLAG:최대X = 15
		FLAG:최대Y = 8
		 
							    ;012345678901234
		CALL MAKE_FLOOR_LINE, 0,"       8       "
		CALL MAKE_FLOOR_LINE, 1," 6 11  2     6 "
		CALL MAKE_FLOOR_LINE, 2," 2 11 111    2 "
		CALL MAKE_FLOOR_LINE, 3," 1 1  11111111 "
		CALL MAKE_FLOOR_LINE, 4," 1 2  111  2 1 "
		CALL MAKE_FLOOR_LINE, 5," 111111   11 1 "
		CALL MAKE_FLOOR_LINE, 6,"     2    11 1 " 
		CALL MAKE_FLOOR_LINE, 7,"     8         " 
							    ;012345678901234

	CASE 6
	;現世二階
		FLAG:최대X = 15
		FLAG:최대Y = 7
		 
							    ;012345678901234
		CALL MAKE_FLOOR_LINE, 0,"               "
		CALL MAKE_FLOOR_LINE, 1," 7 1 1211    7 "
		CALL MAKE_FLOOR_LINE, 2," 2 1   11 11 2 "
		CALL MAKE_FLOOR_LINE, 3," 1 112111211 1 "
		CALL MAKE_FLOOR_LINE, 4," 1    111    1 "
		CALL MAKE_FLOOR_LINE, 5," 1111111111111 "
		CALL MAKE_FLOOR_LINE, 6,"               " 
							    ;012345678901234


ENDSELECT


;=======================================================================
;タイル調整
;=======================================================================
@MAKE_FLOOR_LINE_COMMON_49, ARG, ARG:1, ARG:2
#LOCALSIZE 30
;LOCAL:3 宝箱 던전플래그:84~99
;LOCAL:4 이벤트
;(0/0)でLOCALを소거
SIF ARG:1 == 0 && ARG:2 == 0
	VARSET LOCAL
SELECTCASE ARG
	CASE 3
		IF GETBIT(던전플래그:(FLAG:현던전):(FLAG:현M+20), LOCAL:3)
			DA:(ARG:1):(ARG:2) = 1
		ELSE
			DA:(ARG:1):(ARG:2) = FLAG:현M *1000 + LOCAL:3 * 10 + 3
		ENDIF
		LOCAL:3++
	CASE 4 TO 9
		DA:(ARG:1):(ARG:2) = FLAG:현M *1000 + LOCAL:ARG * 10 + ARG
		LOCAL:ARG++
ENDSELECT


;=======================================================================
;タイル定義
;=======================================================================
@DEFINE_TILES_49, ARGS, ARG, ARG:1
SELECTCASE ARGS
	CASE "D"
		RETURN 1004
	CASE "R"
		RETURN 2004
	CASE "S"
		RETURN 3004

	CASE "W"
		RETURN 19
	CASE "w"
		RETURN 29

	CASE "t"
		RETURN 12
	CASE "s"
		RETURN 22

	CASE "H"
		RETURN 15
	CASE "P"
		RETURN 35

ENDSELECT

;=======================================================================
;[5]조사　実行時の이벤트
;=======================================================================
@EVENT_CHECK_DUNGEON_49
CALL EVENT_DUNGEON_49
RETURN RESULT

;=======================================================================
;タイル進入時の이벤트
;=======================================================================
@EVENT_ENTER_DUNGEON_49,ARG
;ARG = 進入時の向き　0の場合ワープなど
CALL EVENT_DUNGEON_49, ARG , 0
RETURN RESULT

@EVENT_DUNGEON_49, ARG, ARG:1
SELECTCASE DA:(FLAG:현X):(FLAG:현Y) % 10

;==========
;出口の処理
;==========
CASE 8
	CALL DUNGEON_EXIT
	RETURN 1
;==========
;扉の処理
;==========
CASE 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1
;========
;宝の処理
;========
CASE 3
	CALL TREASURE_49, DA:(FLAG:현X):(FLAG:현Y) / 10
	RETURN 1
;=======
;階段
;=======
CASE 6
	CALL UPSTAIRS_49
	RETURN RESULT

CASE 7
	CALL DOWNSTAIRS_49
	RETURN RESULT
;========
;罠の処理
;========
CASE 5
SELECTCASE DA:(FLAG:현X):(FLAG:현Y)
	CASE 15
		CALL DUNGEON_TRAP_HP,10
	CASE 35
		CALL DUNGEON_TRAP_POISON,40
ENDSELECT
;==========
;이벤트
;==========
CASE 4
SELECTCASE DA:(FLAG:현X):(FLAG:현Y) / 10
	CASE 100
		IF 던전플래그:49:5 == 0
			IF FLAG:DEBUG
				PRINTFORMW ＞방 안에는 수상한 남자와 검정 일색의 인형이 있었다
				PRINTFORML 「이곳은 천재 닥터 스릴 님의 연구실이다. 마음대로 들어가면 안 된뒤야… 이러면 될까」
				PRINTFORMW 『당신이란 참 연극에 맞질 않네에…』
				PRINTFORMW 「시끄럽긴. 너도 얼른 져서 타르타로스 탐색으로 돌아가라고」
				PRINTFORML 
				PRINTFORMW 「그런 이유로 싸워줘야겠어」
				PRINTFORMW 『정크로 만들어줄게에』
			ELSE
				PRINTFORMW ＞방 안에는 수상한 남자가 있었다
				PRINTFORML 「케케케, 이 몸의 조마가 이제 곧 완성인기라」
				PRINTFORMW 「내는 진짜 천재인기라. 케케케케케」
				PRINTFORML 「응? 누꼬?」
				PRINTFORMW %플레이어를()% 알아챈 남자가 뒤돌아보았다
				PRINTFORML 「이곳은 천재 닥터 스릴 님의 연구실인기다. 마음대로 들어오면 안 되능겨」
				IF FINDCHARA_B(4402, -1) == 2
					PRINTFORML 「뭐꼬? 조마배틀인겨」
					PRINTFORMW 「내가 이기면 니도 실험대 삼아서 마개조해불끼다」
				ELSE
					PRINTFORML 「내는 인간에겐 흥미가 없는기라」
					PRINTFORMW 「그라췌, 내의 귀여운 조마 가르강튀아의 완성 전 테스트해주라」
				ENDIF
				PRINTFORMW 「자, 가그라!!  프로토 가르강튀아!!」
			ENDIF
			PRINTL 조마 프로토 가르강튀아가 １체 나타났다！
			CUSTOMDRAWLINE =
			WAIT
			FLAG:도주불가플래그 = 1
			FLAG:회화불능플래그 = 1
			FLAG:선제기습취소 = 1
			CALL SET_ENEMY,9,4403,25,0,1
			IF FLAG:DEBUG
				ABL:(CHARANUM-1):스킬1 = 512
				ABL:(CHARANUM-1):스킬2 = 2001
				ABL:(CHARANUM-1):스킬3 = 2416
				MAXBASE:(CHARANUM-1):지변 = 0
				MAXBASE:(CHARANUM-1):비구 = 125
				CFLAG:(CHARANUM-1):ＨＰ보정 += 1500
				CFLAG:(CHARANUM-1):ＭＰ보정 += 5000
			ELSE
				CFLAG:(CHARANUM-1):ＨＰ보정 += 500
				CFLAG:(CHARANUM-1):ＭＰ보정 += 500
			ENDIF
			CFLAG:(CHARANUM-1):행동횟수 = 2
			ABL:(CHARANUM-1):아이템1 = 1002
			ABL:(CHARANUM-1):입수확률1 = 100
			CALL SYNC_STATUS,(CHARANUM-1)
			CALL HEALTH_CHARA,(CHARANUM-1)
			CALL BATTLE_START
			FLAG:도주불가플래그 = 0
			FLAG:회화불능플래그 = 0
			FLAG:선제기습취소 = 0
		ENDIF
		IF FLAG:DEBUG
			PRINTFORMW 『흥, 오늘은 이 정도만 해주겠어어』
			PRINTFORMW 『두고 보려무나아』
			PRINTFORMW 「에엑!?」
			PRINTFORMW 「이걸로 차례 끝이잖아. 아니 그 전에 말 좀 들어라」
		ELSE
			PRINTFORML 「아앗, 이런 기막힌 일이」
			PRINTFORMW 「우째서 내 조마 군이 지는기가」
			PRINTFORMW 「만들던 기 다 망가져부럿다」
			PRINTFORML 「이 빚은 언젠가 돌려줄끼여」
			PRINTFORMW 「오늘은 마, 이쯤 해두긋다. 두고 바라!!」
		ENDIF
		IF ITEM:도리・카드몬 == 0
			PRINTFORMW ＞쓰러뜨린 조마 안에서 뭔가 나왔다.
			PRINTFORMW 수수께끼의 인형을 손에 넣었다.
			ITEM:도리・카드몬 += 1
		ENDIF
		던전플래그:49:5 = 1
	CASE 200
		IF 던전플래그:49:3 == 0
			PRINTFORMW ＞방에 들어가자 바닥에 마법진이 그려져 있었다
			PRINTFORMW ＞%플레이어가()% 마법진에 다가가자 입구를 막듯이 한 마리의 사귀가 모습을 드러냈다
			;마요네함락済みでＰＴにいる
			IF FINDCHARA_B(4516,0) == 2
				PRINTFORMW 『시드는 어디에 있다니?』
				PRINTFORMW 「하아? 맛없을 것 같은 할망구인데」
				PRINTFORMW 「애당초 알아도 대답할 리 없잖냐. 후딱 뒈지기나 해라」
			ELSE
				PRINTFORMW 「우햐햐, 맛 좋을 것 같은 인간이 들어왔구만」
			ENDIF
			PRINTL 사귀 락샤사가 １체 나타났다！
			CUSTOMDRAWLINE =
			WAIT
			FLAG:도주불가플래그 = 1
			FLAG:회화불능플래그 = 1
			FLAG:선제기습취소 = 1
			CALL SET_ENEMY,9,[[キャラ:락샤사]],23,0,1
			CFLAG:(CHARANUM-1):행동횟수 = 2
			CFLAG:(CHARANUM-1):ＨＰ보정 += 500
			ABL:(CHARANUM-1):아이템1 = 1003
			ABL:(CHARANUM-1):입수확률1 = 50
			ABL:(CHARANUM-1):아이템2 = 45
			ABL:(CHARANUM-1):입수확률2 = 50
			
			CALL SYNC_STATUS,(CHARANUM-1)
			CALL HEALTH_CHARA,(CHARANUM-1)
				
			CALL BATTLE_START
			FLAG:도주불가플래그 = 0
			FLAG:회화불능플래그 = 0
			FLAG:선제기습취소 = 0
			;마요네함락済みでＰＴにいる
			IF FINDCHARA_B(4516,0) == 2
				PRINTFORMW 「힉, 단칼에 죽여줘…」
				PRINTFORMW 『안 들리는걸』
				PRINTFORML 
			ENDIF
			PRINTFORMW 사귀 락샤샤를 격파하고 마법진을 파괴했다
			던전플래그:49:3 = 1
		ENDIF
	CASE 300
		IF 던전플래그:49:0 == 0
			PRINTFORMW ＞방에 들어가자 흑인 신부가 그곳에 있었다
			;마요네함락済みでＰＴにいる
			IF FINDCHARA_B(4516,0) == 2
				PRINTFORMW 『상부의 결정을 전해드리겠어요, 시드』
				PRINTFORMW 『조직은 이 건에서 손을 떼려나 봐요. 물론 명령을 거역한다면… 알고 있겠죠?』
				PRINTFORMW 마요네는 놀리듯이 즐겁게 웃으며 눈을 가늘게 떴다
				PRINTFORML 
			ENDIF
			PRINTFORMW 「과연… 알겠습니다」
			PRINTFORMW 「하지만 제 계획에 변함은 없습니다」
			PRINTFORMW %CALLNAME:MASTER%에게 설명을 들은 시드는 즉답하고 품속에서 꺼낸 종이를 던졌다
			PRINTFORMW 「여기선, 일단 물러나지요」
			PRINTFORMW 「당신 상대는 사귀 시키오지가 해줄 겁니다」
			PRINTFORMW 「부탁했습니다. 시키오지」
			PRINTFORMW 「그럼, 작별입니다」
			
			PRINTL 사귀 시키오지가 １체 나타났다！
			CUSTOMDRAWLINE =
			WAIT
			FLAG:도주불가플래그 = 1
			FLAG:회화불능플래그 = 1
			FLAG:선제기습취소 = 1
			CALL SET_ENEMY,9,661,27,0,1
			CSTR:(CHARANUM-1):사고패턴 = 661
			CFLAG:(CHARANUM-1):행동횟수 = 2
			CFLAG:(CHARANUM-1):ＨＰ보정 += 1000
			CFLAG:(CHARANUM-1):ＭＰ보정 += 500
			ABL:(CHARANUM-1):아이템1 = 1002
			ABL:(CHARANUM-1):입수확률1 = 100
			ABL:(CHARANUM-1):아이템2 = 3012
			ABL:(CHARANUM-1):입수확률2 = 5
			
			CALL SYNC_STATUS,(CHARANUM-1)
			CALL HEALTH_CHARA,(CHARANUM-1)
				
			CALL BATTLE_START
			FLAG:도주불가플래그 = 0
			FLAG:회화불능플래그 = 0
			FLAG:선제기습취소 = 0
			PRINTL 
			PRINTW 시키오지를 쓰러뜨림으로써 시드의 마력이 주던 영향이 끊어졌는지 키타야마 대학의 이계화는 원래대로 돌아왔다
			PRINTW 조직의 메시지를 전달하고 이계화를 해제한 보수로 계좌에 ￥50000이 지불되었다
			CALL MESSAGE_WINDOW_D, "", "의뢰 【교수의 은신처】가 출현！"
			CUSTOMDRAWLINE =
			MONEY += 50000
			던전플래그:49:0 = 1
			DA:(FLAG:현X):(FLAG:현Y) = 1
			FLAG:탈출 = 1
			RETURN 1
		ENDIF
	ENDSELECT
CASE 9
	SELECTCASE DA:(FLAG:현X):(FLAG:현Y)  / 10
	CASE 1
		CALL DUNGEON_WORP,1,16,3
	CASE 2
		CALL DUNGEON_WORP,0,16,10
	ENDSELECT
	RETURN 1
RETURN 0
ENDSELECT
;=======================================================================
;階段置き場
;=======================================================================
@UPSTAIRS_49
;位置とIDが対応しているはずなので、同IDの階段を探させれば良い
	CALL DUNGEON_UPSTAIRS_ID, FLAG:현M+1 , DA:(FLAG:현X):(FLAG:현Y) + 1000 + 1
RETURN 1

@DOWNSTAIRS_49
	CALL DUNGEON_DOWNSTAIRS_ID, FLAG:현M-1 , DA:(FLAG:현X):(FLAG:현Y) - 1000 - 1
RETURN 1

;=======================================================================
;인카운트율
;=======================================================================
@ENCOUNT_RATE_49
	SELECTCASE FLAG:현M
	CASE 0
		FLAG:인카운트율 = FLAG:미조우걸음수*5
	;本館
	CASE 1 TO 4
		IF FLAG:현X >= 7
			FLAG:인카운트율 = FLAG:미조우걸음수*5 + 5
		ELSE
			FLAG:인카운트율 = FLAG:미조우걸음수*5 + 10
		ENDIF
	CASEELSE
		FLAG:인카운트율 = 0
ENDSELECT

;=======================================================================
;필요레벨눈安
;권장LV 雑魚최저LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_49
RETURN 15,15, 22, 27

;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_49
IF RAND:100 < FLAG:인카운트율
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:미조우걸음수 += 1
	RETURN 0
ENDIF
;=======================================================================
;적の종족の종류数
;=======================================================================
@CLASS_NUM_49
RETURN CLASS_NUM_COMMON("中級")
;=======================================================================
;적の総数
;ARGは出現する적の종류です
;=======================================================================
@ENEMY_NUM_49, ARG
RETURN ENEMY_NUM_COMMON("中級", ARG)
;=======================================================================
;적の종류
;=======================================================================
@ENEMY_49, ARG
	SELECTCASE FLAG:현M
	CASE 0
	;校外
		SELECTCASE RAND:100
			CASE IS < 15
				RETURN [[キャラ:오바리욘]], 15
			CASE IS < 35
				RETURN [[キャラ:츠치구모]], 17
			CASE IS < 50
				RETURN [[キャラ:니케]], 17
			CASE IS < 60
				RETURN [[キャラ:야마치치]], 17
			CASE IS < 70
				RETURN [[キャラ:모・쇼보]], 17
			CASE IS < 85
				RETURN [[キャラ:잭프로스트]], 17
			CASE IS < 100
				RETURN [[キャラ:카타키라우와]], 17
		ENDSELECT
	;本館
	CASE 1 TO 4
		IF FLAG:현X >= 7
			SELECTCASE RAND:100
				CASE IS < 18
					RETURN [[キャラ:오키쿠무시]], 19
				CASE IS < 31
					RETURN [[キャラ:미샤구지님]], 19
				CASE IS < 44
					RETURN [[キャラ:야마치치]], 20
				CASE IS < 57
					RETURN [[キャラ:토뵤]], 19
				CASE IS < 70
					RETURN [[キャラ:멜콤]], 22
				CASE IS < 83
					RETURN [[キャラ:야마치치]], 20
				CASE IS < 100
					RETURN [[キャラ:야츠후사]], 21
			ENDSELECT
		ELSE
			SELECTCASE RAND:100
				CASE IS < 15
					RETURN [[キャラ:야츠후사]], 21
				CASE IS < 35
					RETURN [[キャラ:로어]], 21
				CASE IS < 50
					RETURN [[キャラ:미샤구지님]], 21
				CASE IS < 60
					RETURN [[キャラ:토뵤]], 21
				CASE IS < 70
					RETURN [[キャラ:야마치치]], 21
				CASE IS < 85
					RETURN [[キャラ:칼라드리우스]], 22
				CASE IS < 100
					RETURN [[キャラ:야토노카미]], 22
			ENDSELECT
		ENDIF
ENDSELECT

;========
;宝箱
;========
@TREASURE_49, ARG
SELECTCASE DA:(FLAG:현X):(FLAG:현Y) / 10
	CASE 0
		LOCALS = 힘의향
		LOCAL = 1
	CASE 1
		LOCALS = 십자형창
		LOCAL = 1
	CASE 2
		LOCALS = 에어로젯트
		LOCAL = 1
	CASE 3
		LOCALS = 반혼향
		LOCAL = 1
	CASE 100
		LOCALS = 홍련의채찍
		LOCAL = 1
	CASE 200
		LOCALS = 코어실드
		LOCAL = 1
	CASE 300
		LOCALS = 격뢰의곡옥
		LOCAL = 1
	CASE 400
			LOCALS = 안경
			LOCAL = 1
ENDSELECT
IF LOCALS == "￥"
	CALL GET_TREASURE_MONEY, 0, LOCAL, FLAG:현던전, FLAG:현M + 20, (DA:(FLAG:현X):(FLAG:현Y) / 10) % 100
ELSEIF LOCALS == "마카"
	CALL GET_TREASURE_MONEY, 1, LOCAL, FLAG:현던전, FLAG:현M + 20, (DA:(FLAG:현X):(FLAG:현Y) / 10) % 100
ELSEIF LOCALS == "MAG" || LOCALS == "ＭＡＧ"
	CALL GET_TREASURE_MAG, 0, LOCAL, FLAG:현던전, FLAG:현M + 20, (DA:(FLAG:현X):(FLAG:현Y) / 10) % 100
ELSEIF LOCALS == "ＨＰ" || LOCALS == "ＭＰ" || LOCALS == "POISON" || LOCALS == "PALYZE"
	;LOCALS:1 종류 LOCAL 데미지量or확률 LOCALS:2 相性
	CALL GET_TREASURE_TRAP, LOCALS, LOCAL, GET_TYPE_NUM(LOCALS:1), FLAG:현던전, FLAG:현M + 20, (DA:(FLAG:현X):(FLAG:현Y) / 10) % 100
ELSE
	IF GETNUM(ITEM, LOCALS) == -1
		PRINTL ＞아이템 이름의 지정이 잘못되었습니다
		PRINTW ＞아이템의 취득 처리를 중단합니다
	ELSE
		CALL GET_TREASURE, GETNUM(ITEM, LOCALS), LOCAL , FLAG:현던전, FLAG:현M + 20, (DA:(FLAG:현X):(FLAG:현Y) / 10) % 100
	ENDIF
ENDIF
RETURN 1

;시키오지の사고패턴
@SET_ACTION_661,ARG
;1more
IF CFLAG:ARG:１more플래그 > 0
	IF RAND:2 == 0
		;마구날뛰기
		CALL CHECK_ACTIONABLE,ARG,203
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:입력행동 = 203
		CALL RANDOM_TARGET,ARG,203
		RETURN 1
	ELSE
		;아라크노와이어
		CALL CHECK_ACTIONABLE,ARG,1124
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:입력행동 = 1124
		CALL RANDOM_TARGET,ARG,1124
		RETURN 1
	ENDIF
ELSEIF CFLAG:ARG:행동한횟수 == 0
	IF RAND:2 == 0
		CALL CHECK_ACTIONABLE,ARG,1124
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:입력행동 = 1124
		CALL RANDOM_TARGET,ARG,1124
		RETURN 1
	ELSE
		CALL CHECK_ACTIONABLE,ARG,602
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:입력행동 = 602
		CALL RANDOM_TARGET,ARG,602
		RETURN 1
	ENDIF
ELSEIF CFLAG:ARG:행동한횟수 == 1
	SELECTCASE RAND:100
		;50％で아라크노와이어
		CASE IS < 50
			CALL CHECK_ACTIONABLE,ARG,1124
			SIF RESULT == 0
				GOTO DECIDE_ACTION
			CFLAG:ARG:입력행동 = 1124
			CALL RANDOM_TARGET,ARG,1124
			RETURN 1
		;30％で마구날뛰기
		CASE IS < 80
			CALL CHECK_ACTIONABLE,ARG,203
			SIF RESULT == 0
				GOTO DECIDE_ACTION
			CFLAG:ARG:입력행동 = 203
			CALL RANDOM_TARGET,ARG,203
			RETURN 1
		;20％で통상공격
		CASE IS < 100
			CALL CHECK_ACTIONABLE,ARG,0
			SIF RESULT == 0
				GOTO DECIDE_ACTION
			CFLAG:ARG:입력행동 = 0
			CALL RANDOM_TARGET,ARG,0
			RETURN 1
	ENDSELECT
ELSE
	$DECIDE_ACTION
	CALL SEARCH_ACT,ARG
	IF RESULT == 0
		CFLAG:ARG:입력행동 = -1
		CFLAG:ARG:방어플래그 = 1
		RETURN 0
	ENDIF
;	PRINTFORML [{CFLAG:ARG:5}]の스킬数 {RESULT}
	LOCAL = RAND:RESULT
	;스킬번호決定
	LOCAL:1 = S:(10+LOCAL)
;	PRINTFORML [{CFLAG:ARG:5}]の스킬 {LOCAL:1}
	$TARGET_LOOP
	
	CALL RANDOM_TARGET,ARG,LOCAL:1
	CALLFORM SKILL_TARGET_{LOCAL:1}
	;ここで스킬번호を行動内容に代入
	SIF CFLAG:ARG:타겟 == -1
		GOTO DECIDE_ACTION
	CFLAG:ARG:입력행동 = LOCAL:1
ENDIF

;=======================================================================
;윤간참가악마
;=======================================================================
@DUNGEON_RAPE_DEVIL_49
;랜덤にどの악마が出るか決定
;RETURN CSV번호 , 人数 , 레벨
SELECTCASE RAND:4
	CASE 0
		RETURN [[キャラ:오바리욘]] , 3 + RAND:10 , CSVBASE([[キャラ:오바리욘]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 1
		RETURN [[キャラ:토뵤]] , 3 + RAND:10 , CSVBASE([[キャラ:토뵤]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 2
		RETURN [[キャラ:카타키라우와]] , 3 + RAND:10 , CSVBASE([[キャラ:카타키라우와]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 3
		RETURN [[キャラ:야토노카미]] , 3 + RAND:10 , CSVBASE([[キャラ:야토노카미]] , GETNUM(BASE , "LV") , 0) + RAND:3
ENDSELECT
