;롯폰기대환락가
;창관関連

;ファイルが肥大化してきていると思われたため、分割
;また、共通化できそうな部分を一本化
;全員身受けしちゃうとちょっと寂しくなるため、明日以降は代理キャラが働くように対応
;裏娼館にランダムキャラ捕獲の窓口を追加
;20/05/08　身受けキャラの能力を底上げ。即娼館に突っ込んで即戦力として使えるレベルに
;21/03/07　娼婦キャラの追加サンプルを作成。及びキャラ追加の手順等を記載
;21/06/24　魔乳に対応
;21/10/29　娼婦追加をもっとお手軽にできるようにソース改修。娼婦をキャラ別にファイル分割。던전플래그ではなくERHのフラグに移設

;フラグ管理メモ
;59　ランダムキャラ捕獲窓口用　0:利用してない　1:利用経験あり　2:今日利用した

;(21/10/29　ERH化して使用しなくなるフラグ)(参考用に残すけど、削除してもおｋ)
;10　娼館(女)利用経験フラグ　0:利用してない　1:利用した　2:クーポンあり
;10番台　娼館(女)用フラグ　0:その子を指名したことない　1:指名したことある　2:指名経験あり、あなたの技術が十分で身請け可能に　3:あなたに身請けされた　4:代理キャラ出勤　5:代理キャラ指名経験あり
;50　裏路地娼館利用フラグ　0:利用してない　1:利用した　2:クーポンあり
;50番台　娼館(女)用フラグ　0:その子を指名したことない　1:指名したことある　2:指名経験あり、あなたの技術が十分で身請け可能に　3:あなたに身請けされた
;80番台　娼館用フラグ2　

;=======================================================================
;娼婦キャラの追加の手順
;=======================================================================
;1,SORP_PLAY_XXX_(テンプレート用).ERB をコピーして、新規キャラ用のファイルを用意
;2,SORP31.ERHなどから、使用していない娼婦フラグの番号を採番し、1.のファイルのXXを娼婦フラグ番号で置換
;(出来たら、使用する娼婦フラグ番号と名前をERHに追記しておくと、後の人が作りやすいかも？)
;3,ファイルのコメントアウト等を削除し、各項目を埋めて娼婦キャラを作成
;4,完成した3のファイルを娼婦フォルダに移動
;5,キャラ追加完了！
;
;(娼婦キャラ作成メモ)
;ベースとなる悪魔にユニークなカスタマイズを施してキャラを水増しするシステムです。上手い事使ってください
;娼館用なので、基本的に非処女系のキャラと考えておく
;また、出現条件や身受け条件は各自でよく考えること。最低限、初日で身受けできない程度は必須！
;プレイ金額は200～2000ぐらい、身受け金額は2000～5000ぐらい、のフィーリングで
;体力気力や能力は、自宅の娼館や便器にそのまま投入できる程度には盛って良い(と個人的には思っている)
;でも5以上の値はなるべく自重すること

;=======================================================================
;창관(基本部分は一本化)
;=======================================================================
@SORP_BASE_31, ARG

CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"어서 오십시오"
IF (롯폰기앞창관플래그:0 == 0 && ARG == 10) || (롯폰기뒷창관플래그:0 == 0 && ARG == 50)
	$SORP_INFO
	IF (롯폰기앞창관플래그:0 == 0 && ARG == 10) || (롯폰기뒷창관플래그:0 == 0 && ARG == 50)
		;初回時
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"이런、고객님은 당 점포의 이용은 처음이시군요/그렇가면, 당 점포의 룰을 설명하겠습니다"
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"고객님은 다행히 악마에 대해 알고 계신 듯하군요/감출 것 없이 말씀드리겠습니다"
	ELSE
		;설명時
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"알겠습니다、그럼 설명하겠습니다"
	ENDIF
	CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"당 점포는 악마 계집들과 하룻밤의 꿈을 지내기 위한 가게입니다/그리고 모두、인간으로는 맛보기 힘든 다양한 경험을 쌓고 있습니다/그러기에、고객님도 귀중한 경험을 얻을 수 있겠지요"
	IF ARG == 50
		;창관(裏)の場合
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"또한 당점의 아가씨들은 모두 아주 특수합니다/그 일을 아무쪼록 잘 부탁드립니다."
	ENDIF
	CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"물론、그걸 위해서는 마카가 필요합니다/일본 돈은 받지 않고 있기에/아무쪼록 잘 부탁드립니다"
	CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"또한 덧붙여 당 점포에는、일부의 계집에 대해 「낙적」도 허가하고 있습니다/이 경우、그 계집은 당신의 것이 됩니다/말할 것도 없습니다만、그 계집이랑은 당 점포에선 더 놀 수 없게 되므로 주의해주십시오"
	;Ｎｏ.１なら現状が一番でしょ…という技術힘的問題の隠蔽
	IF ARG == 50
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"또한、본 서비스는 고객님과 계집이 동의한 경우에만 가능합니다/따라서、낙적을 거부한 계집은 낙적할 수 없습니다/당 점포에선、현 Ｎｏ.１인 피아는 낙적을 거부하고 있습니다"
	ELSE
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"또한、본 서비스는 고객님과 계집이 동의한 경우에만 가능합니다/따라서、낙적을 거부한 계집은 낙적할 수 없습니다/당 점포에선、현 Ｎｏ.１인 조세피느는 낙적을 거부하고 있습니다"
	ENDIF
	CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"그럼 마지막으로、당 점포의 룰을 정리해드리겠습니다"
	CALL MESSAGE_WINDOW_D, "", @"１：여기선 마카를 지불해서 당신의 구슬과 경험을 얻는 게 가능합니다/２：계집에 따라 얻을 수 있는 구슬과 경험은 다릅니다/３：고액의 마카를 지불하면 낙적할 수도 있습니다/４：당연히、낙적한 게집은 여기서는 더는 놀 수 없습니다"
	CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"이와 같습니다"
	SIF 롯폰기앞창관플래그:0 == 0 && ARG == 10
		롯폰기앞창관플래그:0 = 1
	SIF 롯폰기뒷창관플래그:0 == 0 && ARG == 50
		롯폰기뒷창관플래그:0 = 1
ENDIF

$BRO_LOOP_W
CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"그럼、오늘은 어쩌시겠습니까？"
IF ARG == 50 && 이벤트플래그:88:0 > 2
	CALL INPUT_SELECT_D, "[1]계집과 논다/[2]계집을 낙적한다/[3]설명을 듣는다/[4]암구호를 댄다///[0]그만둔다" , , , 2
ELSE
	CALL INPUT_SELECT_D, "[1]계집과 논다/[2]계집을 낙적한다/[3]설명을 듣는다く////[0]그만둔다" , , , 2
ENDIF

IF RESULT == 3
	;설명を聞く
	GOTO SORP_INFO
ELSEIF RESULT == 1
	;プレイ
	CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"그러면、이쪽 계집들 중에서 골라 주십시오…"
	$BRO_LOOP_W_1
	IF ARG == 10
		CALLFORM SORP_LIST_NEW, 1, 1
	ELSE
		CALLFORM SORP_LIST_NEW, 1, 2
	ENDIF
	IF RESULT == 0
		;やめる
		GOTO BRO_LOOP_W
	ELSE
		LOCAL = 0
		LOCAL = RESULT
	ENDIF
	CALLFORM SORP_PLAY_{LOCAL}, "紹介文"
	CALLFORM SORP_PLAY_{LOCAL}, "確認"
	IF ARG == 10 && 롯폰기앞창관플래그:0 == 2 && LOCAL != 1001
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"어라、손님。쿠폰을 가지고 계셨습니까/그럼 ２００마카로 놀 수 있습니다"
		RESULT = 200
	ELSEIF ARG == 50 && 롯폰기뒷창관플래그:0 == 2 && LOCAL != 1008
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"어라、손님。쿠폰을 가지고 계셨습니까/그럼 ２００마카로 놀 수 있습니다."
		RESULT = 200
	ELSE
		CALLFORM SORP_PLAY_{LOCAL}, "価格"
	ENDIF
	IF MONEY:1 < RESULT
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"…손님, 아무래도 마카가 모자라는 모양이군요/아무래도 안되겠습니다/다른 계집을 선택해주십시오…"
		GOTO BRO_LOOP_W_1
	ENDIF
	CALLFORM SORP_PLAY_{LOCAL}, "キャラ名"
	CALL MESSAGE_WINDOW_D, "", @"%조사처리(RESULTS,"과")% 노시겠습니까？"
	CALL INPUT_YN_D , "논다" , "다른 계집으로 한다"
	IF RESULT == 0
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"그럼 저쪽 방에서 잠시 기다려주십시오…"
		IF ARG == 10 && 롯폰기앞창관플래그:0 == 2 && LOCAL != 3111
			RESULT = 200
			롯폰기앞창관플래그:0 = 1
		ELSEIF ARG == 50 && 롯폰기뒷창관플래그:0 == 2 && LOCAL != 3151
			RESULT = 200
			롯폰기뒷창관플래그:0 = 1
		ELSE
			CALLFORM SORP_PLAY_{LOCAL}, "価格"
		ENDIF
		MONEY:1 -= RESULT
		CALLFORM SORP_PLAY_{LOCAL}, "プレイ口上"
		IF ARG == 10
			CALL DUNGEON_WORP,1,15,6
		ELSE
			CALL DUNGEON_WORP,2,6,14
		ENDIF
	ELSE
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"그렇습니까/그럼 다른 계집을 선택하세요/물론 다른 계집의 이야기를 듣고나서 %RESULTS% 양을 선택하셔도 괜찮습니다."
		GOTO BRO_LOOP_W_1
	ENDIF
ELSEIF RESULT == 2
	;身受け
	CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"호오、계집을 낙적하시는 겁니까/손님、계집에게 반하셨나봐요"
	LOCAL = 0
	IF ARG == 10
		CALLFORM SORP_CAN_BUY_COUNT_NEW, 1
		LOCAL = RESULT
	ELSE
		CALLFORM SORP_CAN_BUY_COUNT_NEW, 2
		LOCAL = RESULT
	ENDIF
	IF LOCAL == 0
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"하지만 지금 현재, 손님에게 낙적해도 좋다는 계집이 없습니다/저희 가게에서 잠시 친분을 쌓는 편이 좋을것 같습니다"
		GOTO BRO_LOOP_W
	ENDIF
	$BRO_LOOP_W_2
	CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"현재 손님에게 낙적해도 좋다고 말하는 계집은 이쪽입니다/아무쪼록 납득이 갈 때까지 선택해 주십시오"
	IF ARG == 10
		CALLFORM SORP_LIST_NEW, 2, 1
	ELSE
		CALLFORM SORP_LIST_NEW, 2, 2
	ENDIF
	IF RESULT == 0
		;やめる
		GOTO BRO_LOOP_W
	ELSE
		LOCAL = 0
		LOCAL = RESULT
	ENDIF
	CALLFORM SORP_PLAY_{LOCAL}, "身受け確認"
	CALLFORM SORP_PLAY_{LOCAL}, "身受け価格"
	IF MONEY:1 < RESULT
		CALLFORM SORP_PLAY_{LOCAL}, "キャラ名"
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"…고객님、마카가 부족합니다/그렇다면 %조사처리(RESULTS,"를")% 낙적하실 수는 없습니다"
		GOTO BRO_LOOP_W_2
	ENDIF
	CALL INPUT_YN_D , "身受けする" , "他の娘にする"
	IF RESULT == 0
		CALLFORM SORP_PLAY_{LOCAL}, "キャラ名"
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"알겠습니다/그러면 조금만 기다려 주십시오/%RESULTS% 본인을 불러 이야기를 전해야 하기에…"
		CALL MESSAGE_WINDOW_D, "", @"………………/…………/……"
		CALLFORM SORP_PLAY_{LOCAL}, "身受け口上"
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"그러면…/차후 우리들은 일절 관여하지 않겠습니다/부디 행복하시길…"
		IF ARG == 10
			CALL DUNGEON_WORP,1,15,6
		ELSE
			CALL DUNGEON_WORP,2,6,14
		ENDIF
	ELSE
		GOTO BRO_LOOP_W_2
	ENDIF
ELSEIF RESULT == 4 && ARG == 50
	;ここから추가랜덤キャラ
	IF 던전플래그:31:59 == 0
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"…손님, 어디서 그것을？"
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"아니, 꼬치꼬치 캐묻는 것은 실례지요/이 업계는 입단속이 단단해야 오래 살 수 있으니까요."
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"이쪽으로 오십시오…"
	ELSE
		CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"알겠습니다. 이쪽으로 오십시오…"
	ENDIF
	CALL SHOW_PICTURE, "EMPTY"
	CALLFORM PRISON_BASE_31
	CALL SHOW_PICTURE, "EMPTY"
	CALL DUNGEON_WORP,2,6,14
ELSE
	CALL MESSAGE_WINDOW_D, "접수 보는 남자", @"그럼 다음 번 이용하실 것을 기다리고 있겠습니다."
	IF ARG == 10
		CALL DUNGEON_WORP,1,15,6
	ELSE
		CALL DUNGEON_WORP,2,6,14
	ENDIF
ENDIF

;=======================================================================
;地下留置所の処理
;=======================================================================
@PRISON_BASE_31
#DIM CHARA, 1

IF 던전플래그:31:59 == 0
	CALL MESSAGE_WINDOW_D, "", @"＞창관은 지하 감옥과 연결된 것 같다！"
	CALL MESSAGE_WINDOW_D, "남자 간수", @"어서와 동업자/가능하면 만나고 싶지 않았지만…"
	CALL MESSAGE_WINDOW_D, "남자 간수", @"그러나 룰은 룰이다. 이 곳에 대해서 설명하겠다"
	$RANCHAR_INFO
	;설명も兼ねる
	IF 던전플래그:31:59 == 1
		CALL MESSAGE_WINDOW_D, "남자 간수", @"오케이 더 자세히 설명하겠다"
	ENDIF
	CALL MESSAGE_WINDOW_D, "남자 간수", @"이곳은 빚을 갚을 수 없는 자나 권력자의 노여움을 사는 등/겉으로 드러낼 수 없게 된 말썽쟁이들의 감옥이다."
	CALL MESSAGE_WINDOW_D, "남자 간수", @"하지만 말썽쟁이라도 가끔 소중한 물건이 있기도 해/그러니까 우리끼리 유효활용하자!/라는 노예조합이란 뜻이야.…"
	CALL MESSAGE_WINDOW_D, "남자 간수", @"입장료만 내면 게스트는 1일 1명까지 데리고 나오는 것이 허용된다/이런, 얕은 생각은 하지마/그러면 네가 다음 놈이 될 뿐이야"
	CALL MESSAGE_WINDOW_D, "남자 간수", @"입장료는 1만 마카다/덧붙여서 일정기간 팔리지 않고 남은 놈이나 희망이 없는 놈은/…뭐 동업이고 알고 있지？"
	SIF 던전플래그:31:59 == 0 
		던전플래그:31:59 = 1
ELSE
	CALL MESSAGE_WINDOW_D, "", @"＞창관의 지하 감옥에 왔다"
	CALL MESSAGE_WINDOW_D, "남자 간수", @"어서와 동업자"
ENDIF
IF 던전플래그:31:59 == 2
	CALL MESSAGE_WINDOW_D, "남자 간수", @"입장할 수 있는 것은 1일 1회까지다/얌전히 있다 가라고！"
	RETURN
ENDIF

$RANCHAR_LOOP_W
CALL INPUT_SELECT_D, "[1]입장료를 낸다/[2]설명을 듣는다/[3]잡담한다////[0]그만둔다" , , , 2
IF RESULT == 2
	;설명を聞く
	GOTO RANCHAR_INFO
ELSEIF RESULT == 3
	;雑談
	SELECTCASE RAND:3
		CASE 0
			CALL MESSAGE_WINDOW_D, "남자 간수", @"오 뇌물 상담이야?/…뭐지, 아닌가"
			CALL MESSAGE_WINDOW_D, "남자 간수", @"참고로 여기는 게스트 전문입장이고/여기서는 매입은 안한다구/가끔씩은 있어, 쓰레기를 데려와서 돈주고 지나가려고 하는 녀석이"
		CASE 1
			CALL MESSAGE_WINDOW_D, "남자 간수", @"表通りにプチデモSHOPってあるだろ？/この街の女どもの間じゃあそこで악마ごっこするのが流行ってるらしいぜ"
			CALL MESSAGE_WINDOW_D, "남자 간수", @"実はうちの組織にもあそこと同じ装置があってな/あれ인간の악마化にも使えるんだぜ？知ってたか？"
			IF BASE:MASTER:LV >= 60
				CALL MESSAGE_WINDOW_D, "남자 간수", @"この前、処分品のクズに複数のデビル소스を突っ込んで遊んだんだが/あれは中々愉快だったぜ？/同業も気になるんなら店主に相談してみたらどうだ？"
			ENDIF
		CASE 2
			CALL MESSAGE_WINDOW_D, "남자 간수", @"こんなところに一人は大変じゃないかって？/普段は奥の控室でダラダラしてるからそうでもないぜ"
			CALL MESSAGE_WINDOW_D, "남자 간수", @"ここに誰か来る時は表から連絡来るから/その時だけ出てるって寸法よ/ちなみに言えば俺は夜間担当で他にも人員はいるからな"
	ENDSELECT
	GOTO RANCHAR_LOOP_W
ELSEIF RESULT == 1
	IF MONEY:1 < 10000
		CALL MESSAGE_WINDOW_D, "남자 간수", @"…돈 없으면 여길 못 지나가/가난한 사람은 가라！"
		CALL MESSAGE_WINDOW_D, "", @"＞강제적으로 쫓겨났다"
		RETURN
	ENDIF
	CALL MESSAGE_WINDOW_D, "남자 간수", @"へへっ！毎度/あと出来れば売れ残り品のゴミみたいな奴を持ち帰ってくれ/処分の손間が省けて楽ができる"
	CALLFORM SELECT_RANDOM_CHARA
	CALL MESSAGE_WINDOW_D, "남자 간수", @"そのクズを持って帰るのか/言っておくが返品は一切受け付けないからそのつもりでな"
	MONEY:1 -= 10000
	던전플래그:31:59 = 2
	RETURN
ELSE
	CALL MESSAGE_WINDOW_D, "남자 간수", @"そうかい/もう来るなよ同業"
	RETURN
ENDIF

;=======================================================================
;창관(表)の処理
;=======================================================================
;창부リスト
@SORP_LIST_NEW, ARG:0, ARG:1
#DIMS VALUES

$BRO_LOOP_S
IF ARG:0 == 2
	VALUES = ＞누구로 할까？/
ELSE
	VALUES = [0]やめる/
ENDIF
LOCAL = 0
FOR LOCAL,1001, (1000 + MAX_DUNGEON_31_FLAGS)
	;所属している娼館
	TRYCALLFORM SORP_PLAY_{LOCAL}, "所属"
	SIF RESULT != ARG:1
		CONTINUE
	;指名できるか
	TRYCALLFORM SORP_PLAY_{LOCAL}, "判定"
	SIF RESULT == 1
		CONTINUE
	;身受けの場合、身請け可能フラグがあるもののみ
	SIF ARG:0 == 2 && 롯폰기창관플래그:(LOCAL-1000) != 2
		CONTINUE
	
	CALLFORM SORP_PLAY_{LOCAL}, "娼婦番号"
	VALUES += @"/%RESULTS%"
	CALLFORM SORP_PLAY_{LOCAL}, "指名名"
	VALUES += @"%RESULTS%"
NEXT
IF ARG:0 == 2
	VALUES += "/[0]やめる"
ENDIF
CALL INPUT_SELECT_D , VALUES , , , 2

LOCAL = 0
SELECTCASE RESULT
	CASE 0
		RETURN 0
	CASEELSE
		LOCAL = RESULT
		;所属している娼館
		TRYCALLFORM SORP_PLAY_{LOCAL}, "所属"
		SIF RESULT != ARG:1
			GOTO BRO_LOOP_S
		;指名できるか
		TRYCALLFORM SORP_PLAY_{LOCAL}, "判定"
		SIF RESULT == 1
			GOTO BRO_LOOP_S
		;身受けの場合、身請け可能フラグがあるもののみ
		SIF ARG:0 == 2 && 롯폰기창관플래그:(LOCAL-1000) != 2
			GOTO BRO_LOOP_S
		RETURN LOCAL
ENDSELECT

;身受け可能数
@SORP_CAN_BUY_COUNT_NEW, ARG
#DIM LCOUNT
LCOUNT = 0
LOCAL = 0
FOR LOCAL,1001, (1000 + MAX_DUNGEON_31_FLAGS)
	;所属している娼館
	TRYCALLFORM SORP_PLAY_{LOCAL}, "所属"
	SIF RESULT != ARG
		CONTINUE
	;指名できるか
	TRYCALLFORM SORP_PLAY_{LOCAL}, "判定"
	SIF RESULT == 1
		CONTINUE
	;身受けの場合、身請け可能フラグがあるもののみ
	SIF 롯폰기창관플래그:(LOCAL-1000) != 2
		CONTINUE
	LCOUNT++
NEXT
RETURN LCOUNT
