
;=======================================================================
;皇居
;ボス・イザナミ
;見るたびに陛下どんな所に住んでいらっしゃったんすかｗｗｗｗと突っ込みたくなる。
;ダンジョンナンバー6　ＭＡＰ0~2
;던전플래그:(FLAG:현던전):0 イベント進行状況 1 イザナミ撃破

;東京駅追加
;使用フラグ
;투기장플래그:1:0							攻略条件			0:未発生	1:皇居			2:東京駅
;던전플래그:25:10	ダンジョン進行		0:初期状態	1:キョウジ戦後	2:サイガ撃破
;던전플래그:25:11	強行突破			0:初期状態	1:強行突破した
;던전플래그:25:12	タヱ確保イベント	0:初期状態	1:イベント終了
;던전플래그:25:13	キョウジ戦			0:初期状態	1:戦闘開始		2:戦闘終了
;던전플래그:25:14	サイガ戦			0:初期状態	1:戦闘開始		2:戦闘終了



;=======================================================================
;攻略条件
;=======================================================================
@PREREQUISITE_DUNGEON_25
;旧皇居(四天王の試練クリア済み)
IF 투기장플래그:1:0 == 1
	RETURN 1
;東京驛
ELSEIF 투기장플래그:1:0 == 2 && 던전플래그:25:10 != 2
	RETURN 1
ELSE
	RETURN 0
ENDIF



;=======================================================================
;ダンジョン名
;=======================================================================
@GET_DUNGEON_NAME_25
IF 투기장플래그:1:0 == 1
	RESULTS = 구황궁
ELSEIF 투기장플래그:1:0 == 2 && 던전플래그:25:10 < 2
	RESULTS = 도쿄역
ELSE
	RESULTS = 구황궁
ENDIF

;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_25
SELECTCASE FLAG:현M
	CASE 10 TO 14
		CALL FLOORNAME_TOKYO_STATION
	CASEELSE
		PRINT 구황궁　
		SELECTCASE FLAG:현M
			CASE 0
				PRINT Ｂ１Ｆ
			CASE 1
				PRINT Ｂ２Ｆ
			CASE 2
				PRINT Ｂ３Ｆ
			CASE 3
				PRINT Ｂ４Ｆ
			CASE 4
				PRINT Ｂ５Ｆ
		ENDSELECT
ENDSELECT

;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_25,ARG
;東京駅
IF 투기장플래그:1:0 == 2 && 던전플래그:25:10 < 2
	FLAG:현M = 10
	FLAG:현X = 1
	FLAG:현Y = 1
;旧皇居
ELSE
	FLAG:현M = 0
	FLAG:현X = 7
	FLAG:현Y = 7
ENDIF
;CALL AUTOMAP


;=======================================================================
;スタート時イベント
;=======================================================================
@EVENT_DUNGEON_START_25
IF 투기장플래그:1:0 == 2 && 던전플래그:25:10 == 0
	CALL EVENT_TOKYO_STATION_START
ENDIF

;=======================================================================
;指定したマスに進入可能か
;=======================================================================
@CAN_ENTER_25,ARG,ARG:1
;ARG、ARG:1はそれぞれ対象のマスのX、Y座標を表しています。
;このダンジョンではエレベーターの扉が一度右側から開けておかないと左側からは開けられない、という処理に使用しています。
;どのマップでも座標(2,7)がエレベータの扉。던전플래그:(FLAG:현던전):13 ＝ エレベータフラグ管理用変数の{現在のマップ番号}番目のビットが立っていなければ開けられるかどうかの判定を行う
;偵察部隊(東京駅）
IF DA:ARG:(ARG:1) == 12
	CALL RECONAISSANCE_UNIT_DUNGERN_25
	RETURN RESULT
;龍のアギト
ELSEIF DA:ARG:(ARG:1) == 22
	CALL MESSAGE_WINDOW_D, "", "＞龍のアギトが行く手を塞いでいる/＞通過するのは難しそうだ。"
	RETURN 0
;ボス前
ELSEIF DA:ARG:(ARG:1) == 52
	CALL MESSAGE_WINDOW_D, "", "＞扉の向こうから恐ろしい気配がする…/　進みますか？"
	CALL INPUT_YN,"はい","いいえ"
	IF RESULT == 0
		CALL CAN_ENTER,ARG,ARG:1
		RETURN RESULT
	ELSE
		RETURN 0
	ENDIF
;水場
ELSEIF DA:ARG:(ARG:1) == 10
	RETURN 0
;空中
ELSEIF DA:ARG:(ARG:1) == 20
	RETURN 0
ELSE
	CALL CAN_ENTER,ARG,ARG:1
	RETURN RESULT
ENDIF


;=======================================================================

;フロアデータ

;=======================================================================
@MAKE_FLOOR_25
SELECTCASE FLAG:현M
	CASE 0 TO 4
		CALL MAKE_FLOOR_OLD_IMPERIAL_PALACE
	CASE 10 TO 14
		CALL MAKE_FLOOR_TOKYO_STATION
ENDSELECT

;=======================================================================
;タイル定義
;=======================================================================
@DEFINE_TILES_25, ARGS, ARG, ARG:1
SELECTCASE ARGS
	CASE "m";人物
		RETURN 14
	CASE "L";ループのためのワープ
		RETURN 24
	CASE "H";ヒルコ床
		RETURN 34
	CASE "U";↑
		RETURN 44
	CASE "d";↓	*D:はダークゾーン
		RETURN 54
	CASE "r";→
		RETURN 64
	CASE "W";←
		RETURN 74
	CASE "A";偵察部隊
		RETURN 12
	CASE "R";龍のアギト
		RETURN 22
	CASE "B";封印された扉
		RETURN 32
	CASE "C";シキミの壁
		RETURN 42
	CASE "F";ボス前(Eはエレベーター）
		RETURN 52
	CASE "w";水
		RETURN 10
	CASE "a";空中
		RETURN 20
ENDSELECT
;=======================================================================
;特殊タイル表示
;TILE_MARK_COMMON_{FLAG:현던전}_X
;=======================================================================
;ARG:1、ARG:2がX座標とY座標
;ARGはそこに代入されている値
;RESULTSに代入した文字がマップに表示されますが、必ず全角1文字分になるようにしてください。そうでないと表示が崩れます。
@TILE_MARK_COMMON_25_0, ARG, ARG:1, ARG:2
SELECTCASE ARG
	CASE 10
		SETCOLOR 0, 0, 255
		RESULTS = ミ
	CASE 20
		GETBGCOLOR
		SETCOLOR RESULT
		RESULTS = ■
	CASEELSE
		IF FLAG:현M >= 39 && FLAG:현M <= 52
			GETBGCOLOR
			SETCOLOR RESULT
		ENDIF
		RESULTS = ■
ENDSELECT

@TILE_MARK_COMMON_25_1, ARG, ARG:1, ARG:2
IF FLAG:현M >= 32 && FLAG:현M <= 35
	SETCOLOR 200, 50, 50
	RESULTS = ■
ELSEIF FLAG:현M >= 39 && FLAG:현M <= 52
	SETCOLOR 200, 200, 200
	RESULTS = ■
ELSE
	RESULTS = □
ENDIF

@TILE_MARK_COMMON_25_2, ARG, ARG:1, ARG:2
SELECTCASE ARG
	CASE 12
		SETCOLOR 255, 0, 0
		RESULTS = 偵
	CASE 22
		SETCOLOR 0, 255, 0
		RESULTS = 壁
	CASE 32
		SETCOLOR 255, 0, 0
		RESULTS = 封
	CASEELSE
		RETURN 1
ENDSELECT

@TILE_MARK_COMMON_25_4, ARG, ARG:1, ARG:2
SELECTCASE ARG
	;メッセンジャーを緑色で"ｍ"と表示
	CASE 14
		SETCOLOR 0x00ff00
		RESULTS = ｍ
	;ループポイント
	CASE 24
		;アカラナ回廊の時空の歪み
		IF 이벤트플래그:86:1 == 23
			SETCOLOR 0, 0, 255
			RESULTS = ■
		ELSE
			RESULTS = □
		ENDIF
	;ヒルコ床
	CASE 34
		;アカラナ回廊の砂時計
		IF 이벤트플래그:86:1 == 23
			RESULTS = %UNICODE(0x231B)%
		ELSE
			SETCOLOR 255, 100, 100
			RESULTS = ◆ 
		ENDIF
	;↑
	CASE 44
		RESULTS = ↑
	;↓
	CASE 54
		RESULTS = ↓
	;↓
	CASE 64
		RESULTS = →
	CASE 74
		RESULTS = ←
	CASEELSE
		RETURN 1
ENDSELECT




;=======================================================================

;[5]調べる　実行時のイベント

;=======================================================================
@EVENT_CHECK_DUNGEON_25

;==========
;出口の処理
;==========
IF DA:(FLAG:현X):(FLAG:현Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
	
;========
;宝の処理
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 3
	CALL TREASURE_25
	RETURN RESULT

;========
;イベント
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 4
	SELECTCASE FLAG:현M
		CASE 10 TO 14
			CALL EVENT_TOKYO_STATION
	ENDSELECT
;=======
;階段
;=======
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 6
	SELECTCASE FLAG:현M
		CASE 10, 11, 12, 13, 14
			CALL UPSTAIRS_TOKYO_STATION
		CASEELSE
			CALL UPSTAIRS_25
	ENDSELECT
	RETURN RESULT
		
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 7
	SELECTCASE FLAG:현M
		CASE 10, 11, 12, 13, 14
			CALL DOWNSTAIRS_TOKYO_STATION
		CASEELSE
			CALL DOWNSTAIRS_25
	ENDSELECT
	RETURN RESULT
	
;======
;特殊処理
;======
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 9
	CALL MESSAGE_WINDOW_D, "", "＞아무것도 없는 것 같다"
	RETURN 0
ENDIF

;=======================================================================

;タイル進入時のイベント

;=======================================================================
@EVENT_ENTER_DUNGEON_25,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出口の処理
;==========
IF DA:(FLAG:현X):(FLAG:현Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 3
	CALL TREASURE_25
	RETURN RESULT
;========
;罠の処理
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 5
	CALL TRAP_25
	RETURN RESULT
;=======
;階段
;=======
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 6
	SELECTCASE FLAG:현M
		CASE 10, 11, 12, 13, 14
			CALL UPSTAIRS_TOKYO_STATION
		CASEELSE
			CALL UPSTAIRS_25
	ENDSELECT
	RETURN RESULT
	
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 7
	SELECTCASE FLAG:현M
		CASE 10, 11, 12, 13, 14
			CALL DOWNSTAIRS_TOKYO_STATION
		CASEELSE
			CALL DOWNSTAIRS_25
	ENDSELECT
	RETURN RESULT
	
;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1


;========================
;イベント
;========================
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 4
	SELECTCASE FLAG:현M
		CASE 4
			CALL EVENT_OLD_IMPERIAL_PALACE
		CASE 10, 11, 12, 13, 14
			CALL EVENT_TOKYO_STATION
	ENDSELECT

;========================
;その他、特殊処理
;========================
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 9
ENDIF
RETURN 0


;===========
;宝箱の中身
;===========
@TREASURE_25
SELECTCASE FLAG:현M
	CASE 0
ENDSELECT
RETURN 1


;========
;罠置き場
;========
@TRAP_25
SELECTCASE FLAG:현M
	CASE 0
		IF FLAG:현X == 5 && FLAG:현Y == 1
			CALL DUNGEON_TRAP_MP,10
		ELSE
			CALL DUNGEON_TRAP_HP,10
		ENDIF
	CASE 1
		IF FLAG:현X == 7 && FLAG:현Y == 1
			CALL DUNGEON_TRAP_HP,10
		ELSE
			CALL DUNGEON_TRAP_MP,10
		ENDIF
	CASE 2
		IF FLAG:현X == 7 && FLAG:현Y == 7
			CALL DUNGEON_TRAP_MP,10
		ELSE
			CALL DUNGEON_TRAP_HP,10
		ENDIF
	CASE 3
		IF FLAG:현X == 1 && FLAG:현Y == 3
			CALL DUNGEON_TRAP_MP,10
		ELSEIF FLAG:현X == 1 && FLAG:현Y == 7
			CALL DUNGEON_TRAP_POISON,40
		ELSE
			CALL DUNGEON_TRAP_HP,10
		ENDIF
	CASE 29
		CALL DUNGEON_TRAP_HP,10
ENDSELECT
RETURN 0

;=======================================================================

;エンカウント率

;=======================================================================
@ENCOUNT_RATE_25
SELECTCASE FLAG:현M
	CASE 10 TO 14
		IF 던전플래그:25:11 == 0
			FLAG:인카운트율 = FLAG:미조우걸음수
		ELSE
			FLAG:인카운트율 = FLAG:미조우걸음수*10 + 10
		ENDIF
	CASE 15
		FLAG:인카운트율 = FLAG:미조우걸음수
	CASE 16, 17, 18, 20, 22, 29
		FLAG:인카운트율 = 0
	CASEELSE
		FLAG:인카운트율 = FLAG:미조우걸음수*5 + 5
ENDSELECT
SIF FLAG:미조우걸음수 < 5
	FLAG:인카운트율 = 0

;====================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=====================================
@GET_DUNGEON_LV_25
IF 투기장플래그:1:0 == 2
	RETURN 40, 35, 44, 55
ELSE
	RETURN 33, 30, 39, 50
ENDIF
;=================
;エンカウント処理
;=================
@CHECK_ENCOUNT_25
IF RAND:100 < FLAG:인카운트율
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:미조우걸음수 += 1
	RETURN 0
ENDIF
;================
;敵の種族の種類数
;================
@CLASS_NUM_25
SELECTCASE FLAG:현M
	CASE 10 TO 14
		IF 던전플래그:25:11 == 0
			RETURN 1
		ELSE
			RETURN 3
		ENDIF
	CASE 15 TO 40
		RETURN 1
	CASEELSE
		RETURN CLASS_NUM_COMMON("上級")
ENDSELECT
;=========================
;敵の総数
;ARGは出現する敵の種類です
;=========================
@ENEMY_NUM_25, ARG
SELECTCASE FLAG:현M
	CASE 10 TO 14
		IF 던전플래그:25:11 == 0
			RETURN ENEMY_NUM_COMMON("中級", ARG)
		ELSE
			RETURN 10
		ENDIF
	CASE 15 TO 60 
			RETURN ENEMY_NUM_COMMON("中級", ARG)
	CASEELSE
		RETURN ENEMY_NUM_COMMON("上級", ARG)
ENDSELECT
;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_25, ARG
SELECTCASE FLAG:현M
	CASE IS < 4
		SELECTCASE RAND:100
			CASE IS < 30
				;30%でサラシナヒメ
				RETURN 605, 30+RAND:(1+(FLAG:현M))
			CASE IS < 55
				;25%でフジムスメ
				RETURN 507, 31+RAND:(1+(FLAG:현M))
			CASE IS < 70
				;15%でセンリ
				RETURN 806, 33+RAND:(1+(FLAG:현M))
			CASE IS < 85
				;15%でハクジョウシ
				RETURN 705, 34+RAND:(1+(FLAG:현M))
			CASE IS < 100
				;10%でおしち
				RETURN 1008, 35+RAND:(1+(FLAG:현M))
		ENDSELECT
	CASE 4, 28
		SELECTCASE RAND:100
			CASE IS < 30
				;30%でサラシナヒメ
				RETURN 605, 34
			CASE IS < 55
				;25%でフジムスメ
				RETURN 507, 35
			CASE IS < 70
				;15%でセンリ
				RETURN 806, 37
			CASE IS < 85
				;15%でハクジョウシ
				RETURN 705, 38
			CASE IS < 100
				;10%でおしち
				RETURN 1008, 39
		ENDSELECT
	;東京驛
	CASE 10 TO 14
		SELECTCASE RAND:100
			CASE IS < 40
				RETURN [[キャラ:카라스텐구]], 30 + RAND:5
			CASE IS < 70
				RETURN [[キャラ:가샤도쿠로]], 35 + RAND:5
			CASE IS < 100
				RETURN [[キャラ:네비로스]], 40 + RAND:5
		ENDSELECT
ENDSELECT

;=======================================================================
;特殊調整
;=======================================================================
@SET_ENEMY_STATUS_25, ARG
SIF NO:ARG == [[キャラ:후지무스메]]
	ABL:ARG:스킬4 = 106
IF NO:ARG == 1014
	NAME:ARG = オボログルマ
	CALLNAME:ARG = オボログルマ
ELSEIF NO:ARG == [[キャラ:좀비아미]]
	NAME:ARG = トゥルダク
	CALLNAME:ARG = トゥルダク
ELSEIF NO:ARG == [[キャラ:카라스텐구]]
	NAME:ARG = クラマテング
	CALLNAME:ARG = クラマテング
ELSEIF NO:ARG == [[キャラ:블랙맨]]
	NAME:ARG = 赤マント
	CALLNAME:ARG = 赤マント
ELSEIF NO:ARG == [[キャラ:슬라임]]
	NAME:ARG = ヒルコ
	CALLNAME:ARG = ヒルコ
ENDIF	


;========================================

;戦闘開始時の処理

;========================================
@EVENT_BATTLE_START_DUNGEON25

;========================================

;行動終了時の処理

;========================================
@EVENT_BATTLE_ACTEND_DUNGEON25

;========================================

;ターン終了時の処理

;========================================
@EVENT_BATTLE_TURNEND_DUNGEON25


;=================================
;思考パターン
;=================================
;ゾンビーサムライの思考パターン
@SET_ACTION_1091,ARG
;後衛に居るなら前衛に出る
IF CPOS(ARG) > 11
	CFLAG:ARG:입력행동 = 2301
	CFLAG:ARG:입력행동 = CPOS(ARG)-5
;それ以外は通常
ELSE
		$DECIDE_ACTION
		CALL SEARCH_ACT,ARG
		IF RESULT == 0
			CFLAG:ARG:입력행동 = -1
			CFLAG:ARG:방어플래그 = 1
			RETURN 0
		ENDIF
;		PRINTFORML [{CFLAG:ARG:5}]のスキル数 {RESULT}
		LOCAL = RAND:RESULT
		;スキル番号決定
		LOCAL:1 = S:(10+LOCAL)
;		PRINTFORML [{CFLAG:ARG:5}]のスキル {LOCAL:1}
		$TARGET_LOOP
		
		CALL RANDOM_TARGET,ARG,LOCAL:1
		CALLFORM SKILL_TARGET_{LOCAL:1}
		;ここで스킬番号を行動内容に代入
		SIF CFLAG:ARG:타겟 == -1
			GOTO DECIDE_ACTION
		CFLAG:ARG:입력행동 = LOCAL:1
ENDIF

;ゾンビーケンペイの思考パターン
@SET_ACTION_1092,ARG
;前衛に居るなら自爆
IF CPOS(ARG) < 12
	CFLAG:ARG:입력행동 = 2005
	CALL CHECK_ACTIONABLE_AND_SET , ARG , CFLAG:ARG:입력행동
;それ以外は通常
ELSE
		$DECIDE_ACTION
		CALL SEARCH_ACT,ARG
		IF RESULT == 0
			CFLAG:ARG:입력행동 = -1
			CFLAG:ARG:방어플래그 = 1
			RETURN 0
		ENDIF
;		PRINTFORML [{CFLAG:ARG:5}]のスキル数 {RESULT}
		LOCAL = RAND:RESULT
		;スキル番号決定
		LOCAL:1 = S:(10+LOCAL)
;		PRINTFORML [{CFLAG:ARG:5}]のスキル {LOCAL:1}
		$TARGET_LOOP
		
		CALL RANDOM_TARGET,ARG,LOCAL:1
		CALLFORM SKILL_TARGET_{LOCAL:1}
		;ここで스킬番号を行動内容に代入
		SIF CFLAG:ARG:타겟 == -1
			GOTO DECIDE_ACTION
		CFLAG:ARG:입력행동 = LOCAL:1
ENDIF

;イザナミの思考パターン
@SET_ACTION_7,ARG
IF CFLAG:ARG:행동한횟수 == 4
	;５回目の行動は闇の審判
	CALL CHECK_ACTIONABLE,ARG,1406
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:입력행동 = 1406
	CALL RANDOM_TARGET,ARG,1406
ELSEIF CFLAG:ARG:행동한횟수 < 4
	;ポジション9のゾンビかポジション14のゾンビが死んでたら優先サマリカーム
	;それ以外に死んでるキャラが居たらサマリカーム
	;居なかったらコンセントレイト(既に貯まってたらメギド)or子守唄（一人でも眠ってたら誘い）
	IF CFLAG:POS(9):상태이상 == GET_STATE_NUM("DYING")
		CALL CHECK_ACTIONABLE,ARG,418
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:입력행동 = 418
		CFLAG:ARG:타겟 = POS(9)
		RETURN 1
	ELSEIF CFLAG:POS(14):상태이상 == GET_STATE_NUM("DYING")
		CALL CHECK_ACTIONABLE,ARG,418
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:입력행동 = 418
		CFLAG:ARG:타겟 = POS(14)
		RETURN 1
	ENDIF
	;前衛に居るならスウェイバック
	IF CPOS(ARG) == 9
		CFLAG:ARG:입력행동 = 2301
		CFLAG:ARG:타겟 = 14
		RETURN 1
	ENDIF
	
	LOCAL:10 = 0

	FOR LOCAL,16,6,-1
		IF POS(LOCAL) > -1 && CFLAG:(POS(LOCAL)):상태이상 == GET_STATE_NUM("DYING")
			CALL CHECK_ACTIONABLE,ARG,418
			SIF RESULT == 0
			GOTO DECIDE_ACTION
			CFLAG:ARG:입력행동 = 418
			CFLAG:ARG:타겟 = POS(LOCAL)
			RETURN 1
		ENDIF
	NEXT
	
	IF RAND:2 == 0
	
		IF CFLAG:ARG:집중플래그
			CALL CHECK_ACTIONABLE,ARG,2001
			SIF RESULT == 0
				GOTO DECIDE_ACTION
			CFLAG:ARG:입력행동 = 2001
			CALL RANDOM_TARGET,ARG,2001
			RETURN 1
		ELSE
			CALL CHECK_ACTIONABLE,ARG,512
			SIF RESULT == 0
				GOTO DECIDE_ACTION
			CFLAG:ARG:입력행동 = 512
			CALL RANDOM_TARGET,ARG,512
			RETURN 1
		ENDIF
	ELSE
		FOR LOCAL,1,7
			IF POS(LOCAL) > -1 && CFLAG:(POS(LOCAL)):상태이상 == GET_STATE_NUM("SLEEP")
				CALL CHECK_ACTIONABLE,ARG,1225
				SIF RESULT == 0
					GOTO DECIDE_ACTION
				CFLAG:ARG:입력행동 = 1225
				CALL RANDOM_TARGET,ARG,1225
				RETURN 1
			ENDIF
		NEXT
		CALL CHECK_ACTIONABLE,ARG,1223
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:입력행동 = 1223
		CALL RANDOM_TARGET,ARG,1223
		RETURN 1
	ENDIF
	
ELSE
		$DECIDE_ACTION
		CALL SEARCH_ACT,ARG
		IF RESULT == 0
			CFLAG:ARG:입력행동 = -1
			CFLAG:ARG:방어플래그 = 1
			RETURN 0
		ENDIF
;		PRINTFORML [{CFLAG:ARG:5}]のスキル数 {RESULT}
		LOCAL = RAND:RESULT
		;スキル番号決定
		LOCAL:1 = S:(10+LOCAL)
;		PRINTFORML [{CFLAG:ARG:5}]のスキル {LOCAL:1}
		$TARGET_LOOP
		
		CALL RANDOM_TARGET,ARG,LOCAL:1
		CALLFORM SKILL_TARGET_{LOCAL:1}
		;ここでスキル番号を行動内容に代入
		SIF CFLAG:ARG:타겟 == -1
			GOTO DECIDE_ACTION
		CFLAG:ARG:입력행동 = LOCAL:1
ENDIF

;=======================================================================
;マップ(皇居）
;=======================================================================
@MAKE_FLOOR_OLD_IMPERIAL_PALACE
SELECTCASE FLAG:현M
		CASE 0
			FLAG:최대X = 9
			FLAG:최대Y = 9
			CALL MAKE_FLOOR_LINE, 0,"000000000"
			CALL MAKE_FLOOR_LINE, 1,"011115110"
			CALL MAKE_FLOOR_LINE, 2,"01     10"
			CALL MAKE_FLOOR_LINE, 3,"0521 1250"
			CALL MAKE_FLOOR_LINE, 4,"01     10"
			CALL MAKE_FLOOR_LINE, 5,"0121 1210"
			CALL MAKE_FLOOR_LINE, 6,"01     20"
			CALL MAKE_FLOOR_LINE, 7,"0127   80"
			CALL MAKE_FLOOR_LINE, 8,"000000000"

		CASE 1
			FLAG:최대X = 9
			FLAG:최대Y = 9
			CALL MAKE_FLOOR_LINE, 0,"000000000"
			CALL MAKE_FLOOR_LINE, 1,"011511150"
			CALL MAKE_FLOOR_LINE, 2,"02  2  10"
			CALL MAKE_FLOOR_LINE, 3,"07  1  10"
			CALL MAKE_FLOOR_LINE, 4,"0 1121210"
			CALL MAKE_FLOOR_LINE, 5,"0 1 1  50"
			CALL MAKE_FLOOR_LINE, 6,"0      10"
			CALL MAKE_FLOOR_LINE, 7,"0  621110"
			CALL MAKE_FLOOR_LINE, 8,"000000000"
		
		CASE 2
			FLAG:최대X = 9
			FLAG:최대Y = 9
			CALL MAKE_FLOOR_LINE, 0,"000000000"
			CALL MAKE_FLOOR_LINE, 1,"0    7210"
			CALL MAKE_FLOOR_LINE, 2,"0   1  10"
			CALL MAKE_FLOOR_LINE, 3,"06 111250"
			CALL MAKE_FLOOR_LINE, 4,"01  1  10"
			CALL MAKE_FLOOR_LINE, 5,"01  7  10"
			CALL MAKE_FLOOR_LINE, 6,"01     10"
			CALL MAKE_FLOOR_LINE, 7,"011511150"
			CALL MAKE_FLOOR_LINE, 8,"000000000"
		
		CASE 3
			FLAG:최대X = 9
			FLAG:최대Y = 9
			CALL MAKE_FLOOR_LINE, 0,"000000000"
			CALL MAKE_FLOOR_LINE, 1,"011126  0"
			CALL MAKE_FLOOR_LINE, 2,"01      0"
			CALL MAKE_FLOOR_LINE, 3,"05 111  0"
			CALL MAKE_FLOOR_LINE, 4,"01 111  0"
			CALL MAKE_FLOOR_LINE, 5,"01 161 70"
			CALL MAKE_FLOOR_LINE, 6,"01     20"
			CALL MAKE_FLOOR_LINE, 7,"051115110"
			CALL MAKE_FLOOR_LINE, 8,"000000000"
		
		
		CASE 4
			FLAG:최대X = 17
			FLAG:최대Y = 20
			CALL MAKE_FLOOR_LINE, 0,"00000000000000000"
			CALL MAKE_FLOOR_LINE, 1,"0        11111110"
			CALL MAKE_FLOOR_LINE, 2,"0        1     20"
			CALL MAKE_FLOOR_LINE, 3,"0        111   60"
			CALL MAKE_FLOOR_LINE, 4,"0          2    0"
			CALL MAKE_FLOOR_LINE, 5,"01111111111111110"
			CALL MAKE_FLOOR_LINE, 6,"01             10"
			CALL MAKE_FLOOR_LINE, 7,"01 11111 11111 10"
			CALL MAKE_FLOOR_LINE, 8,"01 1 2       1 10"
			CALL MAKE_FLOOR_LINE, 9,"01 1 11111 1 1 10"
			CALL MAKE_FLOOR_LINE,10,"01 1 1   2 1 2 10"
			CALL MAKE_FLOOR_LINE,11,"01 1 1 1 1 1 1 10"
			CALL MAKE_FLOOR_LINE,12,"02 1 1 1 1 1 1 20"
			CALL MAKE_FLOOR_LINE,13,"01 1 1 2 2 1 1 10"
			CALL MAKE_FLOOR_LINE,14,"01 1 111 111 1 10"
			CALL MAKE_FLOOR_LINE,15,"01 1         1 10"
			CALL MAKE_FLOOR_LINE,16,"01 11111111111 10"
			CALL MAKE_FLOOR_LINE,17,"01       2     10"
			CALL MAKE_FLOOR_LINE,18,"01111121 11111110"
			CALL MAKE_FLOOR_LINE,19,"00000000000000000"
			
			
			;イベントの設置
			CALL SET_TILE,7,11,던전플래그:(FLAG:현던전):0,0,4
			;階段の設置
			CALL SET_TILE,7,11,이벤트플래그:86:1,11,7
			
		CASEELSE
	ENDSELECT

;=======================================================================
;階段置き場(旧皇居)
;=======================================================================
@UPSTAIRS_25
SELECTCASE FLAG:현M
	CASE IS < 4
			CALL DUNGEON_DOWNSTAIRS,FLAG:현M - 1,FLAG:현X,FLAG:현Y
	CASE 4
		CALL DUNGEON_UPSTAIRS,3,7,5
	CASEELSE
ENDSELECT
RETURN 1

@DOWNSTAIRS_25
SELECTCASE FLAG:현M
	CASE IS < 3
			CALL DUNGEON_DOWNSTAIRS,FLAG:현M + 1,FLAG:현X,FLAG:현Y
	CASE 3
			CALL DUNGEON_DOWNSTAIRS,4,15,3
	CASE 4
			CALL DUNGEON_DOWNSTAIRS,28, 1,29
	CASEELSE
ENDSELECT
RETURN 1


;=======================================================================
;イベント（旧皇居)
;=======================================================================
@EVENT_OLD_IMPERIAL_PALACE
IF FLAG:현X == 7 && FLAG:현Y == 11
	FLAG:도주불가플래그 = 1
	FLAG:회화불능플래그 = 1
	FLAG:선제기습취소 = 1
	CALL SHOW_PICTURE, "D"
	CALL MESSAGE_WINDOW_D, "", "＞침실 같다/＞발(御簾)을 넘어 여성의 모습이 보인다……"
	CALL MESSAGE_WINDOW_D, "지모신 이자나미", "여러분、여길 떠나십시오/이곳은 우리들의 피를 이은 자만이 들어올 수 있는 장소/당신들도 내가 낳은 자식들이라고는 하지만、/함부로 들어와서는 안 됩니다"
	CALL MESSAGE_WINDOW_D, "지모신 이자나미", "……비사문천들의 허가를 받았다고？/그럼、당신은……/이곳을 지키라고 명한 지상의 아이들은/우리들의 말보다 저 신입들의 말을 우선시했다고！？"
	CALL MESSAGE_WINDOW_D, "지모신 이자나미", "……불쾌하구나/정말로 불쾌하구나！/첩의 노여움을 사려는 눈치다！"
	CALL MESSAGE_WINDOW_D, "지모신 이자나미", "좋을 것이다、네가 원하는 대로 해 주지 않겠는가/부디 이 어머니의 팔에 안겨 재워주마、이 후레자식들아！"
	CALL MESSAGE_WINDOW_D, "", "＞지모신 이자나미가 1체 나타났다！"
	CALL SET_ENEMY,14,7,50,1,1
	CFLAG:(CHARANUM-1):ＨＰ보정 = 2000
	CFLAG:(CHARANUM-1):ＭＰ보정 = 4000
	CFLAG:(CHARANUM-1):속도보정 = 15
	CSTR:(CHARANUM-1):사고패턴 = 7
	CFLAG:(CHARANUM-1):행동횟수 = 5
	ABL:(CHARANUM-1):스킬3 = 2001
	ABL:(CHARANUM-1):스킬4 = 1223
	ABL:(CHARANUM-1):스킬5 = 1225
	CALL SYNC_STATUS,(CHARANUM-1)
	CALL HEALTH_CHARA,(CHARANUM-1)

	FLAG:21091 += 1
	FLAG:21092 += 1

	;ゾンビーサムライを前衛
	CALL SET_ENEMY,8,1091,40,1
	CSTR:(CHARANUM-1):사고패턴 = 1091
	CFLAG:(CHARANUM-1):ＨＰ보정 = 200
	CALL SYNC_STATUS,(CHARANUM-1)
	CALL HEALTH_CHARA,(CHARANUM-1)

	CALL SET_ENEMY,9,1091,40,1
	CSTR:(CHARANUM-1):사고패턴 = 1091
	CFLAG:(CHARANUM-1):ＨＰ보정 = 200
	CALL SYNC_STATUS,(CHARANUM-1)
	CALL HEALTH_CHARA,(CHARANUM-1)
	CALL SET_ENEMY,10,1091,40,1
	CSTR:(CHARANUM-1):사고패턴 = 1091
	CFLAG:(CHARANUM-1):ＨＰ보정 = 200
	CALL SYNC_STATUS,(CHARANUM-1)
	CALL HEALTH_CHARA,(CHARANUM-1)

	;ゾンビーケンペイ自爆なし
	CALL SET_ENEMY,13,1092,40,1
	CSTR:(CHARANUM-1):사고패턴 = 1092
	CFLAG:(CHARANUM-1):ＨＰ보정 = 100
	CALL SYNC_STATUS,(CHARANUM-1)
	CALL HEALTH_CHARA,(CHARANUM-1)
	ABL:(CHARANUM-1):스킬3 = 0
	CALL SET_ENEMY,15,1092,40,1
	ABL:(CHARANUM-1):스킬3 = 0
	CSTR:(CHARANUM-1):사고패턴 = 1092
	CFLAG:(CHARANUM-1):ＨＰ보정 = 100
	CALL SYNC_STATUS,(CHARANUM-1)
	CALL HEALTH_CHARA,(CHARANUM-1)

	CALL BATTLE_START

	CALL MESSAGE_WINDOW_D, "지모신 이자나미", "아아아……"
	CALL MESSAGE_WINDOW_D, "", @"＞이자나미는 그 자리에서 쓰러졌다/＞살갗이 찬 옷에서 들여다보이는 피부는 %CALLNAME:MASTER%의 정욕을 돋우기에는 충분했다/＞전설의 국모도 이렇게 되면 한낱 여자에 지나지 않는다"
	CALL MESSAGE_WINDOW_D, "", "＞지모신 이자나미을 손에 넣었다"
	;FLAG:10007 = 1
	CALL LIFTING_A_BAN([[キャラ:이자나미]])
	CALL ADD_NEW_COMPANION,7,0,1
	SIF FLAG:20007 < 100
		FLAG:20007 = 100
	IF RESULT == 1
		CFLAG:(CHARANUM-1):전투참가불가능 = 1
		BASE:(CHARANUM-1):LV -= 28
		BASE:(CHARANUM-1):힘 -= 7
		BASE:(CHARANUM-1):지혜 -= 7
		BASE:(CHARANUM-1):마력 -= 7
		BASE:(CHARANUM-1):인내력 -= 7
		BASE:(CHARANUM-1):속도 -= 7
		BASE:(CHARANUM-1):운 -= 7
		BASE:(CHARANUM-1):ＥＸＰ = 5*(31)*(32)*(33)/3
		CALL SYNC_STATUS,(RESULT:1)
		CALL HEALTH_CHARA,(RESULT:1)
	ENDIF
	FLAG:도주불가플래그 = 0
	FLAG:회화불능플래그 = 0
	FLAG:선제기습취소 = 0
	던전플래그:(FLAG:현던전):0 = 1
	DA:7:11 = 1
	RETURN 1
ENDIF

;=======================================================================
;エレベーター定義
;=======================================================================
;ARGはそのエレベーターのタイル
;RESULT:1～5にエレベータが存在するマップの番号を代入し、RESULTS:1～5には表示したい名称を代入、RETURNでは-1を返しておく
@DUNGEON_ELEVATOR_25, ARG
;臨海公園
IF 이벤트플래그:86:1 == 9
	RESULT:1 = 26
	RESULT:2 = 27
	RESULTS:1 = Ｂ３Ｆ
	RESULTS:2 = 最下層
ENDIF
RETURN -1


