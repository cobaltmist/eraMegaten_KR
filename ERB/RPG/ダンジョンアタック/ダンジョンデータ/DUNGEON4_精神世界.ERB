;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:DUNGEON_4.ERB
;	Facility	:ダンジョン精神世界 アマネ・イザベル・レミエルを獲得可能
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX								新規作成
;	002		20XX/XX/XX			P					デビサバルートにおけるダンジョンとしてイベント再構築
;	003		2011/01/11			P					レミエルが仲魔になるように改訂・敵配置を変更
;	004		2011/01/15			P					直哉ルート版を作成
;	005		2011/01/16			P					バグ修正
;	006		2011/01/28			P					バグ修正
;	007		2011/02/02			P					バグ修正及び一部仕様変更
;	008		2011/02/04			P					バグ修正及び一部仕様変更
;	009		2011/02/06			P					上記の修正の修正
;	010		2011/02/08			P					一部選択肢に属性の変動を追加
;	011		2011/05/03			P					敵テーブルを変更
;	012		2013/01/06			TR					アマネの調教を1箇所1回から回数制に変更
;	013		2020/09/28			JK好き				ボス２体の合体解禁処理を統一＆イザ・ベル加入時に一人称二人称の設定追加
;	014		2021/03/17			名無し				EXイベントを追加、ファイル内容を関数化して分割
;	013		2021/05/28			JK好き				@EVENT_ENTER_DUNGEON_4の最後でRETURN1しているせいで@CHECK_ENCOUNT_4まで処理が走らない問題を修正
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;=======================================================================
;精神世界
;ダンジョンナンバー4
;=======================================================================
;1 イベント1
;2 イベント2
;3 イベント3
;4 イベントクリア数
;5 レイプフラグ
;6 レミエルレベル
;7 クリア方法の分岐取得
;8 アマネ調教回数

;10:六本木ヒルズ開始時イベント
;11:六本木ヒルズイベント1
;12:六本木ヒルズイベント2
;13:六本木ヒルズイベント3
;14:六本木ヒルズイベント4

;21:新宿地下街イベント1
;22:新宿地下街イベント2
;23:新宿地下街イベント3
;24:新宿地下街イベント4

;30:監視塔イベント1
;31:監視塔イベント2
;32:監視塔イベント3

;90 TO 99: 宝箱
;=======================================================================
;攻略조건
;=======================================================================
@PREREQUISITE_DUNGEON_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	RETURN 1
ELSE
	RETURN 0
ENDIF

;=======================================================================
;던전名
;=======================================================================
@GET_DUNGEON_NAME_4
RESULTS = 정신세계


;=======================================================================
;플로어名표시
;=======================================================================
@FLOORNAME_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	CALL FLOORNAME_4_MINDWORLD
ENDIF


;=======================================================================
;スタート地点입력
;=======================================================================
@START_DUNGEON_4,ARG
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	FLAG:현M = 6
	FLAG:현X = 2
	FLAG:현Y = 3
ENDIF


;=======================================================================
;ダンジョン開始時イベント
;=======================================================================
@EVENT_DUNGEON_START_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	CALL EVENT_DUNGEON_START_4_MINDWORLD
ENDIF


;=======================================================================
;ダンジョン脱出時のイベント
;=======================================================================
@EVENT_DUNGEON_END_4

;=======================================================================
;指定したマスに進入가능か
;=======================================================================
@CAN_ENTER_4,ARG,ARG:1
;精神世界
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	IF FLAG:현M == 0 && ARG == 12 && ARG:1 == 12
		IF FLAG:백합의꽃루트
			CALL MESSAGE_WINDOW_D, "", "＞레미엘의 목소리가 들려온다"
			LOCALS = 이 문 너머가 아마네의 정신의 가장 안쪽입니다/
			IF 던전플래그:(FLAG:현던전):4 < 3
				LOCALS += "아마네의 정신은 해방되지 않았습니다/험난한 싸움이 되겠지만, 그런데도 진행하겠습니까？"
			ELSE
				LOCALS +=  "이자·벨은 강력한 악마…, 험난한 싸움이 되겠지요/준비되셨습니까？"
			ENDIF
			CALL MESSAGE_WINDOW_D, "레미엘", LOCALS
			CALL INPUT_YN_D
			IF RESULT == 0
				CALL MESSAGE_WINDOW_D, "레미엘", "당신에게 신의 가호가 있기를…!"
				RETURN 1
			ELSE
				CALL MESSAGE_WINDOW_D, "레미엘", "그러면 준비를 하십시오"
				RETURN 0
			ENDIF
		ELSE
			CALL MESSAGE_WINDOW_D, "", "이자·벨의 목소리가 들린다"
			LOCALS = 이 문 너머가 아마네의 정신의 가장 안쪽입니다/
			IF 던전플래그:(FLAG:현던전):4 < 3
				LOCALS += "아직, 아마네의 마음은 기운이 넘치네요/이러면 귀찮은 일이 될테지만, 그런데도 진행할건가요?"
			ELSE
				LOCALS += "레미엘은 강해요。/험난한 싸움이 될 거에요/준비는 되었나요?"
			ENDIF
			CALL MESSAGE_WINDOW_D, "이자·벨", LOCALS
			CALL INPUT_YN_D
			IF RESULT == 0
				CALL MESSAGE_WINDOW_D, "이자·벨", "그러면 열심히 해보세요"
				RETURN 1
			ELSE
				CALL MESSAGE_WINDOW_D, "이자·벨", "그럼、서둘러 준비를 마치세요"
				RETURN 0
			ENDIF
		ENDIF
	ENDIF
ENDIF
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT


;=======================================================================
;플로어데이터
;=======================================================================
@MAKE_FLOOR_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	CALL MAKE_FLOOR_4_MINDWORLD
ENDIF

;=======================================================================
;タイル定義
;=======================================================================
@DEFINE_TILES_4, ARGS, ARG, ARG:1
SELECTCASE ARGS
	CASE "m"
		;メッセンジャー
		RETURN 14
	CASE "W"
		;ワープトラップ
		RETURN 15
ENDSELECT

;=======================================================================
;特殊タイル表示
;TILE_MARK_COMMON_{FLAG:현던전}_X
;=======================================================================
;ARG:1、ARG:2がX座標とY座標
;ARGはそこに代入されている値
;RESULTSに代入した文字がマップに表示されますが、必ず全角1文字分になるようにしてください。そうでないと表示が崩れます。
;=======================================================================
@TILE_MARK_COMMON_4_4, ARG, ARG:1, ARG:2
SELECTCASE ARG
	CASE 14
		;メッセンジャーを緑色で"ｍ"と表示
		SETCOLOR 0x00ff00
		RESULTS = ｍ
	CASEELSE
		IF FLAG:현M == 21 && ARG:1== 12 && ARG:2 == 13
			RESULTS = 薬
		ELSEIF FLAG:현M == 21 && ARG:1 == 14 && ARG:2 == 13
			SETCOLOR COLOR("RED")
			RESULTS = Ｇ
		ELSEIF FLAG:현M == 21 && ARG:1 == 14 && ARG:2 == 11
			RESULTS = 防
		ELSE
			RESULTS = ！
		ENDIF
ENDSELECT

@TILE_MARK_COMMON_4_5, ARG, ARG:1, ARG:2
SELECTCASE ARG
	CASE 15
		RESULTS = 穴
	CASEELSE
		RETURN 1
ENDSELECT

;ワープゾーンのタイル調整
@TILE_MARK_COMMON_4_6, ARG, ARG:1, ARG:2
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	RESULTS = ！
ELSE
	RESULTS = 상
ENDIF

@TILE_MARK_COMMON_4_7, ARG, ARG:1, ARG:2
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	RESULTS = ！
ELSE
	RESULTS = 하
ENDIF


@TILE_MARK_COMMON_4_9, ARG, ARG:1, ARG:2
IF FLAG:백합의꽃루트 == 9
	IF FLAG:현M == 20 && EQUIP:MASTER:게이트서치 == 0
		RESULTS = ■
	ELSE
		RESULTS = □
	ENDIF
ELSE
	RESULTS = □
ENDIF


;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_4
;==========
;出口の処理
;==========
IF DA:(FLAG:현X):(FLAG:현Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1

;==========
;宝箱の処理
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 3
;==========
;イベントの処理
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 4
	IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
		CALL EVENT_4
		RETURN 1
	ENDIF
;=======
;階段
;=======
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 6

ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 7

;=======
;그외
;=======
ELSE
	CALL MESSAGE_WINDOW_D, "SYSTTEM", "아무 것도 없는 것 같다"
	RETURN 0
ENDIF


;=======================================================================
;タイル進入時の이벤트
;=======================================================================
@EVENT_ENTER_DUNGEON_4,ARG
#LOCALSIZE 10
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出口の処理
;==========
IF DA:(FLAG:현X):(FLAG:현Y) == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1
	;==========
;宝箱の処理
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 3

;========
;이벤트の処理
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 4
	IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
		CALL EVENT_4
		RETURN 1
	ENDIF
;========
;罠の処理
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 5
	CALL DUNGEON_TRAP_HP,10
;=======
;階段
;=======
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 6
	IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
		CALL WARP_4
	ENDIF
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 7
	IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
		CALL WARP_4
	ENDIF
;==========
;出口の処理
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;ワープの処理
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 15

ENDIF
CALL MAKE_FLOOR, FLAG:현던전
CALL AUTOMAP
;RETURN 1
;=======================================================================
;エレベーター定義
;=======================================================================
;ARGはそのエレベーターのタイル
;RESULT:1～5にエレベータが存在するマップの番号を代入し、RESULTS:1～5には表示したい名称を代入、RETURNでは-1を返しておく
@DUNGEON_ELEVATOR_4, ARG

;=======================================================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_4
IF FLAG:백합의꽃루트
	RETURN 24, 27, 29, 35
ELSE
	RETURN 29, 32, 34, 45
ENDIF


;=======================================================================
;인카운트율
;=======================================================================
@ENCOUNT_RATE_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	FLAG:인카운트율 = FLAG:미조우걸음수*3
	SIF FLAG:미조우걸음수 <= 6
		FLAG:인카운트율 = 0
ENDIF

;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_4
IF RAND:100 < FLAG:인카운트율
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:미조우걸음수 += 1
	RETURN 0
ENDIF


;=======================================================================
;적の종족の종류数
;=======================================================================
@CLASS_NUM_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	RETURN CLASS_NUM_COMMON("上級")
ELSE
	RETURN 1
ENDIF


;=======================================================================
;적の総数
;=======================================================================
@ENEMY_NUM_4, ARG
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	RETURN CLASS_NUM_COMMON("上級")
ENDIF


;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_4, ARG
IF FLAG:백합의꽃루트 == 5
	SELECTCASE RAND:100
		CASE IS < 20
			RETURN [[キャラ:맨이터]], 27
		CASE IS < 40
			RETURN [[キャラ:밴시]], 27
		CASE IS < 60
			RETURN [[キャラ:고르곤]], 29
		CASE IS < 80
			RETURN [[キャラ:나가]], 28
		CASE IS < 100
			RETURN [[キャラ:피닉스]], 28
	ENDSELECT
ELSEIF FLAG:나오야루트 == 5
	SELECTCASE RAND:100
		CASE IS < 30
			RETURN [[キャラ:엔젤]], 32
		CASE IS < 60
			RETURN [[キャラ:니케]], 33
		CASE IS < 80
			RETURN [[キャラ:아크엔젤]], 33
		CASE IS < 100
			RETURN [[キャラ:프린시펄리티]], 34
	ENDSELECT
ENDIF


;=======================================================================
;特殊調整
;=======================================================================
@SET_ENEMY_STATUS_4, ARG
;精神世界の天使はTALKできない
SIF FLAG:나오야루트 == 5
	ABL:ARG:회화타입 = 331
