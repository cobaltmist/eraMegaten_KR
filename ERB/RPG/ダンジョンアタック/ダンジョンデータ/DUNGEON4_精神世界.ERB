;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:DUNGEON_4.ERB
;	Facility	:던전정신세계 아마네・イザベル・레미엘を획득가능
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX								新規作成
;	002		20XX/XX/XX			P					デビサバルートにおける던전として이벤트再構築
;	003		2011/01/11			P					레미엘が仲魔になるように改訂・적配置を변경
;	004		2011/01/15			P					나오야루트版を作成
;	005		2011/01/16			P					バグ수정
;	006		2011/01/28			P					バグ수정
;	007		2011/02/02			P					バグ수정及び一部仕様변경
;	008		2011/02/04			P					バグ수정及び一部仕様변경
;	009		2011/02/06			P					上記の수정の수정
;	010		2011/02/08			P					一部選択肢に속성の変動を추가
;	011		2011/05/03			P					적テーブルを변경
;	012		2013/01/06			TR					アマネの調教を1箇所1回から回数制に変更
;	013		2020/09/28			JK好き				ボス２体の合体解禁処理を統一＆イザ・ベル加入時に일인칭이인칭の設定追加
;	014		2021/03/17			名無し				EXイベントを追加、ファイル内容を関数化して分割
;	013		2021/05/28			JK好き				@EVENT_ENTER_DUNGEON_4の最後でRETURN1しているせいで@CHECK_ENCOUNT_4まで処理が走らない問題を修正
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;=======================================================================
;정신세계
;던전ナンバー4
;=======================================================================
;1 이벤트1
;2 이벤트2
;3 이벤트3
;4 이벤트クリア数
;5 레이프플래그
;6 레미엘레벨
;7 クリア方法の分岐취득
;8 아마네조교횟수

;10:六本木ヒルズ開始時イベント
;11:六本木ヒルズイベント1
;12:六本木ヒルズイベント2
;13:六本木ヒルズイベント3
;14:六本木ヒルズイベント4

;21:新宿地下街イベント1
;22:新宿地下街イベント2
;23:新宿地下街イベント3
;24:新宿地下街イベント4

;30:監視塔イベント1
;31:監視塔イベント2
;32:監視塔イベント3

;90 TO 99: 宝箱
;=======================================================================
;攻略조건
;=======================================================================
@PREREQUISITE_DUNGEON_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	RETURN 1
ELSEIF (FLAG:백합의꽃루트 == 7  || FLAG:백합의꽃루트 == 9)&& FINDCHARA_B([[キャラ:아마네]]) == 2
	RETURN 1
ELSEIF FLAG:백합의꽃루트 == 11&& FINDCHARA_B([[キャラ:아마네]]) != 2
	RETURN 1
ELSEIF FLAG:백합의꽃루트 == 13&& FINDCHARA_B([[キャラ:아마네]]) == 2
	RETURN 1
ELSE
	RETURN 0
ENDIF
;---- EDIT 004 ADD END ---------------------------
;=======================================================================
;던전名
;=======================================================================
@GET_DUNGEON_NAME_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	RESULTS = 정신세계
ELSEIF FLAG:백합의꽃루트 == 7
	RESULTS = 롯폰기힐즈
ELSEIF FLAG:백합의꽃루트 == 9
	RESULTS = 신주쿠（지하상가）
ELSEIF FLAG:백합의꽃루트 == 11
	RESULTS = 감시탑
ELSEIF FLAG:백합의꽃루트 == 13
	RESULTS = 대성당（신주쿠）
ENDIF


;=======================================================================
;フ로어名표시
;=======================================================================
@FLOORNAME_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	CALL FLOORNAME_4_MINDWORLD
ELSEIF FLAG:백합의꽃루트 == 7
	CALL FLOORNAME_4_ROPPONGIHILLS
ELSEIF FLAG:백합의꽃루트 == 9
	CALL FLOORNAME_4_SHINJUKU_UNDER_GROUND
ELSEIF FLAG:백합의꽃루트 == 11
	CALL FLOORNAME_4_WATCHTOWER
ELSEIF FLAG:백합의꽃루트 == 13
	CALL FLOORNAME_4_SHINJUKUCATHEDRAL
ENDIF

;=======================================================================
;スタート地点입력
;=======================================================================
@START_DUNGEON_4,ARG
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	FLAG:현M = 6
	FLAG:현X = 2
	FLAG:현Y = 3
ELSEIF FLAG:백합의꽃루트 == 7
	FLAG:현M = 10
	FLAG:현X = 29
	FLAG:현Y = 18
ELSEIF FLAG:백합의꽃루트 == 9
	FLAG:현M = 20
	FLAG:현X = 28
	FLAG:현Y = 16
ELSEIF FLAG:백합의꽃루트 == 11
	FLAG:현M = 25
	FLAG:현X = 1
	FLAG:현Y = 6
ELSEIF FLAG:백합의꽃루트 == 13
	FLAG:현M = 40
	FLAG:현X = 9
	FLAG:현Y = 28
ENDIF


;=======================================================================
;ダンジョン開始時イベント
;=======================================================================
@EVENT_DUNGEON_START_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	CALL EVENT_DUNGEON_START_4_MINDWORLD
ELSEIF FLAG:백합의꽃루트 == 7
	CALL EVENT_DUNGEON_START_4_ROPPONGIHILLS
ELSEIF FLAG:백합의꽃루트 == 9
	CALL EVENT_DUNGEON_START_4_SHINJUKU_UNDER_GROUND
ELSEIF FLAG:백합의꽃루트 == 11
	CALL EVENT_DUNGEON_START_4_WATCHTOWER
ELSEIF FLAG:백합의꽃루트 == 13
	CALL EVENT_DUNGEON_START_4_SHINJUKUCATHEDRAL
ENDIF


;=======================================================================
;ダンジョン脱出時のイベント
;=======================================================================
@EVENT_DUNGEON_END_4
IF FLAG:백합의꽃루트 == 11 && 던전フラグ:4:33 == 1
	CALL EVENT_DUNGEON_END_4_WATCHTOWER
ELSEIF FLAG:백합의꽃루트 == 13 && 던전フラグ:4:40 == 4
	CALL EVENT_DUNGEON_END_4_SHINJUKUCATHEDRAL
ENDIF

;=======================================================================
;指定したマスに進入가능か
;=======================================================================
@CAN_ENTER_4,ARG,ARG:1
;精神世界
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	IF FLAG:현M == 0 && ARG == 12 && ARG:1 == 12
		IF FLAG:백합의꽃루트
			CALL MESSAGE_WINDOW_D, "", "＞레미엘의 목소리가 들려온다"
			LOCALS = 이 문 너머가 아마네의 정신의 가장 안쪽입니다/
			IF 던전フラグ:(FLAG:현던전):4 < 3
				LOCALS += "아마네의 정신은 해방되지 않았습니다/험난한 싸움이 되겠지만, 그런데도 진행하겠습니까？"
			ELSE
				LOCALS +=  "이자·벨은 강력한 악마…, 험난한 싸움이 되겠지요/준비되셨습니까？"
			ENDIF
			CALL MESSAGE_WINDOW_D, "레미엘", LOCALS
			CALL INPUT_YN_D
			IF RESULT == 0
				CALL MESSAGE_WINDOW_D, "레미엘", "당신에게 신의 가호가 있기를…!"
				RETURN 1
			ELSE
				CALL MESSAGE_WINDOW_D, "레미엘", "그러면 준비를 하십시오"
				RETURN 0
			ENDIF
		ELSE
			CALL MESSAGE_WINDOW_D, "", "이자·벨의 목소리가 들린다"
			LOCALS = 이 문 너머가 아마네의 정신의 가장 안쪽입니다/
			IF 던전フラグ:(FLAG:현던전):4 < 3
				LOCALS += "아직, 아마네의 마음은 기운이 넘치네요/이러면 귀찮은 일이 될테지만, 그런데도 진행할건가요?"
			ELSE
				LOCALS += "레미엘은 강해요。/험난한 싸움이 될 거에요/준비는 되었나요?"
			ENDIF
			CALL MESSAGE_WINDOW_D, "이자·벨", LOCALS
			CALL INPUT_YN_D
			IF RESULT == 0
				CALL MESSAGE_WINDOW_D, "이자·벨", "그러면 열심히 해보세요"
				RETURN 1
			ELSE
				CALL MESSAGE_WINDOW_D, "이자·벨", "그럼、서둘러 준비를 마치세요"
				RETURN 0
			ENDIF
		ENDIF
	ENDIF
;六本木ヒルズ
ELSEIF FLAG:백합의꽃루트 == 7
	IF FLAG:현M == 13 && ARG == 3 && ARG:1 == 11
		CALL MESSAGE_WINDOW_D, "", "＞鍵がかかっていて通れない"
		RETURN 0
	ENDIF
;監視塔
ELSEIF FLAG:백합의꽃루트 == 11
	IF FLAG:현M == 37 && ARG == 16 && ARG:1 == 12
		IF FLAG:현Y == 11
			CALL MESSAGE_WINDOW_D, "", "＞扉の向こうに強敵のを存在を感じる/＞扉をあけますか？"
			CALL INPUT_YN_D
			SIF RESULT
				RETURN 0
		ELSE
			RETURN 1
		ENDIF
	ENDIF
ENDIF
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT
;=======================================================================
;フ로어데이터
;=======================================================================
@MAKE_FLOOR_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	CALL MAKE_FLOOR_4_MINDWORLD
ELSEIF FLAG:백합의꽃루트 == 7
	CALL MAKE_FLOOR_4_ROPPONGIHILLS
ELSEIF FLAG:백합의꽃루트 == 9
	CALL MAKE_FLOOR_4_SHINJUKU_UNDER_GROUND
ELSEIF FLAG:백합의꽃루트 == 11
	CALL MAKE_FLOOR_4_WATCHTOWER
ELSEIF FLAG:백합의꽃루트 == 13
	CALL MAKE_FLOOR_4_SHINJUKUCATHEDRAL
ENDIF

;=======================================================================
;タイル定義
;=======================================================================
@DEFINE_TILES_4, ARGS, ARG, ARG:1
SELECTCASE ARGS
	CASE "m"
		;メッセンジャー
		RETURN 14
	CASE "W"
		;ワープトラップ
		RETURN 15
ENDSELECT

;=======================================================================
;特殊タイル表示
;TILE_MARK_COMMON_{FLAG:현던전}_X
;=======================================================================
;ARG:1、ARG:2がX座標とY座標
;ARGはそこに代入されている値
;RESULTSに代入した文字がマップに表示されますが、必ず全角1文字分になるようにしてください。そうでないと表示が崩れます。
;=======================================================================
@TILE_MARK_COMMON_4_4, ARG, ARG:1, ARG:2
SELECTCASE ARG
	CASE 14
		;メッセンジャーを緑色で"ｍ"と表示
		SETCOLOR 0x00ff00
		RESULTS = ｍ
	CASEELSE
		IF FLAG:현M == 21 && ARG:1== 12 && ARG:2 == 13
			RESULTS = 薬
		ELSEIF FLAG:현M == 21 && ARG:1 == 14 && ARG:2 == 13
			SETCOLOR COLOR("RED")
			RESULTS = Ｇ
		ELSEIF FLAG:현M == 21 && ARG:1 == 14 && ARG:2 == 11
			RESULTS = 防
		ELSE
			RESULTS = ！
		ENDIF
ENDSELECT

@TILE_MARK_COMMON_4_5, ARG, ARG:1, ARG:2
SELECTCASE ARG
	CASE 15
		RESULTS = 穴
	CASEELSE
		RETURN 1
ENDSELECT

;ワープゾーンのタイル調整
@TILE_MARK_COMMON_4_6, ARG, ARG:1, ARG:2
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	RESULTS = ！
ELSE
	RESULTS = 上
ENDIF

@TILE_MARK_COMMON_4_7, ARG, ARG:1, ARG:2
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	RESULTS = ！
ELSE
	RESULTS = 下
ENDIF


@TILE_MARK_COMMON_4_9, ARG, ARG:1, ARG:2
IF FLAG:백합의꽃루트 == 9
	IF FLAG:현M == 20 && EQUIP:MASTER:게이트서치 == 0
		RESULTS = ■
	ELSE
		RESULTS = □
	ENDIF
ELSE
	RESULTS = □
ENDIF


;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_4
;==========
;出口の処理
;==========
IF DA:(FLAG:현X):(FLAG:현Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1

;==========
;宝箱の処理
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 3
	IF FLAG:백합의꽃루트 == 7
		CALL TREASURE_DUNGEON_4_ROPPONGIHILLS
		RETURN RESULT
	ELSEIF FLAG:백합의꽃루트 == 9
		CALL TREASURE_DUNGEON_4_SHINJUKU_UNDER_GROUND
		RETURN RESULT
	ELSEIF FLAG:백합의꽃루트 == 11
		CALL TREASURE_DUNGEON_4_WATCHTOWER
		RETURN RESULT
	ELSEIF FLAG:백합의꽃루트 == 13
		CALL TREASURE_DUNGEON_4_SHINJUKUCATHEDRAL
		RETURN RESULT
	ENDIF
;==========
;イベントの処理
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 4
	IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
		CALL EVENT_4
		RETURN 1
	ELSEIF FLAG:백합의꽃루트 == 7
		CALL EVENT_4_ROPPONGIHILLS
		RETURN 1
	ELSEIF FLAG:백합의꽃루트 == 9
		CALL EVENT_4_SHINJUKU_UNDER_GROUND
		RETURN RESULT
	ELSEIF FLAG:백합의꽃루트 == 11
		CALL EVENT_4_WATCHTOWER
		RETURN RESULT
	ELSEIF FLAG:백합의꽃루트 == 13
		CALL EVENT_4_SHINJUKUCATHEDRAL
		RETURN RESULT
	ENDIF
;=======
;階段
;=======
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 6
	IF FLAG:백합의꽃루트 == 7
		CALL UPSTAIRS_ROPPONGIHILLS
	ELSEIF FLAG:백합의꽃루트 == 9
		CALL UPSTAIRS_SHINJUKU_UNDER_GROUND
	ELSEIF FLAG:백합의꽃루트 == 11
		CALL UPSTAIRS_WATCHTOWER
	ELSEIF FLAG:백합의꽃루트 == 13
		CALL UPSTAIRS_SHINJUKUCATHEDRAL
	ENDIF
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 7
	IF FLAG:백합의꽃루트 == 7
		CALL DOWNSTAIRS_ROPPONGIHILLS
	ELSEIF FLAG:백합의꽃루트 == 9
		CALL DOWNSTAIRS_SHINJUKU_UNDER_GROUND
	ELSEIF FLAG:백합의꽃루트 == 11
		CALL DOWNSTAIRS_WATCHTOWER
	ELSEIF FLAG:백합의꽃루트 == 13
		CALL DOWNSTAIRS_SHINJUKUCATHEDRAL
	ENDIF
;=======
;그외
;=======
ELSE
	CALL MESSAGE_WINDOW_D, "SYSTTEM", "아무 것도 없는 것 같다"
	RETURN 0
ENDIF


;=======================================================================
;タイル進入時の이벤트
;=======================================================================
@EVENT_ENTER_DUNGEON_4,ARG
#LOCALSIZE 10
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出입の処理
;==========
IF DA:(FLAG:현X):(FLAG:현Y) == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1
	;==========
;宝箱の処理
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 3
	IF FLAG:백합의꽃루트 == 7
		CALL TREASURE_DUNGEON_4_ROPPONGIHILLS
		RETURN RESULT
	ELSEIF FLAG:백합의꽃루트 == 9
		CALL TREASURE_DUNGEON_4_SHINJUKU_UNDER_GROUND
		RETURN RESULT
	ELSEIF FLAG:백합의꽃루트 == 11
		CALL TREASURE_DUNGEON_4_WATCHTOWER
		RETURN RESULT
	ELSEIF FLAG:백합의꽃루트 == 13
		CALL TREASURE_DUNGEON_4_SHINJUKUCATHEDRAL
		RETURN RESULT
	ENDIF
;========
;이벤트の処理
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 4
	IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
		CALL EVENT_4
		RETURN 1
	ELSEIF FLAG:백합의꽃루트 == 7
		CALL EVENT_4_ROPPONGIHILLS
		RETURN 1
	ELSEIF FLAG:백합의꽃루트 == 9
		CALL EVENT_4_SHINJUKU_UNDER_GROUND
		RETURN RESULT
	ELSEIF FLAG:백합의꽃루트 == 11
		CALL EVENT_4_WATCHTOWER
		RETURN RESULT
	ELSEIF FLAG:백합의꽃루트 == 13
		CALL EVENT_4_SHINJUKUCATHEDRAL
		RETURN RESULT
	ENDIF
;========
;罠の処理
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 5
	CALL DUNGEON_TRAP_HP,10
;=======
;階段
;=======
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 6
	IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
		CALL WARP_4
	ELSEIF FLAG:백합의꽃루트 == 7
		CALL UPSTAIRS_ROPPONGIHILLS
	ELSEIF FLAG:백합의꽃루트 == 9
		CALL UPSTAIRS_SHINJUKU_UNDER_GROUND
	ELSEIF FLAG:백합의꽃루트 == 11
		CALL UPSTAIRS_WATCHTOWER
	ELSEIF FLAG:백합의꽃루트 == 13
		CALL UPSTAIRS_SHINJUKUCATHEDRAL
	ENDIF
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 7
	IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
		CALL WARP_4
	ELSEIF FLAG:백합의꽃루트 == 7
		CALL DOWNSTAIRS_ROPPONGIHILLS
	ELSEIF FLAG:백합의꽃루트 == 9
		CALL DOWNSTAIRS_SHINJUKU_UNDER_GROUND
	ELSEIF FLAG:백합의꽃루트 == 11
		CALL DOWNSTAIRS_WATCHTOWER
	ELSEIF FLAG:백합의꽃루트 == 13
		CALL DOWNSTAIRS_SHINJUKUCATHEDRAL
	ENDIF
;==========
;出口の処理
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;ワープの処理
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) == 15
	CALL WORP_WATCHTOWER
ENDIF
CALL MAKE_FLOOR, FLAG:현던전
CALL AUTOMAP

;=======================================================================
;エレベーター定義
;=======================================================================
;ARGはそのエレベーターのタイル
;RESULT:1～5にエレベータが存在するマップの番号を代入し、RESULTS:1～5には表示したい名称を代入、RETURNでは-1を返しておく
@DUNGEON_ELEVATOR_4, ARG
;六本木ヒルズ
IF FLAG:백합의꽃루트 == 7
	RESULT:1 = 12
	RESULTS:1 = 　３Ｆ
	RESULT:2 = 13
	RESULTS:2 = 最上階
	RETURN -1
;新宿大聖堂
ELSEIF FLAG:백합의꽃루트 == 13
	RESULT:1 = 40
	RESULTS:1 = 　１Ｆ
	RESULT:2 = 41
	RESULTS:2 = 　２Ｆ
	RESULT:3 = 42
	RESULTS:3 = 　３Ｆ
	RETURN -1
ENDIF

;=======================================================================
;필요레벨눈安
;권장LV 雑물고기최저LV 雑물고기最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_4
IF FLAG:백합의꽃루트
	RETURN 24, 27, 29, 35
ELSE
	RETURN 29, 32, 34, 45
ENDIF

;=======================================================================
;인카운트율
;=======================================================================
@ENCOUNT_RATE_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	FLAG:인카운트율 = FLAG:미조우걸음수*3
	SIF FLAG:미조우걸음수 <= 6
		FLAG:인카운트율 = 0
ELSEIF FLAG:백합의꽃루트 == 7
	IF (FLAG:현M == 10 || FLAG:현M == 11 || FLAG:현M == 12)
		FLAG:인카운트율 = FLAG:미조우걸음수*3
		SIF FLAG:미조우걸음수 <= 6
			FLAG:인카운트율 = 0
	ELSEIF FLAG:현M == 13
		FLAG:인카운트율 = FLAG:미조우걸음수*10+10
	ENDIF
ELSEIF FLAG:백합의꽃루트 == 9
	IF FLAG:현M == 20
		FLAG:인카운트율 = 0
	ELSE
		FLAG:인카운트율 = FLAG:미조우걸음수*3
		SIF FLAG:미조우걸음수 <= 5
			FLAG:인카운트율 = 0
	ENDIF
ELSEIF FLAG:백합의꽃루트 == 11
	FLAG:인카운트율 = FLAG:미조우걸음수*3
	SIF FLAG:미조우걸음수 <= 5
		FLAG:인카운트율 = 0
ELSEIF FLAG:백합의꽃루트 == 13
	IF 던전フラグ:4:40 >= 1
		FLAG:인카운트율 = FLAG:미조우걸음수*5
		SIF FLAG:미조우걸음수 <= 5
			FLAG:인카운트율 = 0
	ELSE
		FLAG:인카운트율 = FLAG:미조우걸음수*3
		SIF FLAG:미조우걸음수 <= 5
			FLAG:인카운트율 = 0
	ENDIF
ENDIF

;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_4
IF RAND:100 < FLAG:인카운트율
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:미조우걸음수 += 1
	RETURN 0
ENDIF
;=======================================================================
;적の종족の종류数
;=======================================================================
@CLASS_NUM_4
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	RETURN CLASS_NUM_COMMON("上級")
ELSE
	RETURN 1
ENDIF


;=======================================================================
;적の総数
;=======================================================================
@ENEMY_NUM_4, ARG
IF FLAG:백합의꽃루트 == 5 || FLAG:나오야루트 == 5
	RETURN CLASS_NUM_COMMON("上級")
ELSE
	RETURN CLASS_NUM_COMMON("中級")
ENDIF


;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_4, ARG
IF FLAG:백합의꽃루트 == 5
	SELECTCASE RAND:100
		CASE IS < 20
			RETURN [[キャラ:맨이터]], 27
		CASE IS < 40
			RETURN [[キャラ:밴시]], 27
		CASE IS < 60
			RETURN [[キャラ:고르곤]], 29
		CASE IS < 80
			RETURN [[キャラ:나가]], 28
		CASE IS < 100
			RETURN [[キャラ:피닉스]], 28
	ENDSELECT
ELSEIF FLAG:백합의꽃루트 == 7
	RETURN [[キャラ:템플나이트]], 25+RAND:5
ELSEIF FLAG:백합의꽃루트 == 9
	SELECTCASE FLAG:현M
		CASE 21, 22
			SELECTCASE RAND:100
				CASE IS < 20
					RETURN [[キャラ:하이픽시]], 7+RAND:5
				CASE IS < 30
					RETURN [[キャラ:케트시]], 12+RAND:5
				CASE IS < 40
					RETURN [[キャラ:잭프로스트]], 12+RAND:5
				CASE IS < 50
					RETURN [[キャラ:잭랜턴]], 13+RAND:5
				CASE IS < 60
					RETURN [[キャラ:커시]], 14+RAND:5
				CASE IS < 70
					RETURN [[キャラ:다크엘프]], 18+RAND:5
				CASE IS < 90
					RETURN [[キャラ:엘프]], 20+RAND:5
				CASE IS < 100
					RETURN [[キャラ:밴시]], 28+RAND:5
			ENDSELECT
		CASE 23, 24
			SELECTCASE RAND:100
				CASE IS < 50
					RETURN [[キャラ:템플나이트]], 25+RAND:5
				CASE IS < 100
					RETURN [[キャラ:메이거스]], 34+RAND:5
			ENDSELECT
	ENDSELECT
ELSEIF FLAG:백합의꽃루트 == 11
	SELECTCASE RAND:100
		CASE IS < 50
			RETURN [[キャラ:템플나이트]], 25+RAND:5
		CASE IS < 100
			RETURN [[キャラ:메이거스]], 34+RAND:5
	ENDSELECT
ELSEIF FLAG:백합의꽃루트 == 13
	IF 던전フラグ:4:40 == 0
		SELECTCASE RAND:100
			CASE IS < 50
				RETURN [[キャラ:템플나이트]], 25+RAND:5
			CASE IS < 100
				RETURN [[キャラ:메이거스]], 34+RAND:5
		ENDSELECT
	ELSE
		SELECTCASE RAND:100
			CASE IS < 25
				RETURN [[キャラ:템플나이트]], 25+RAND:5
			CASE IS < 50
				RETURN [[キャラ:도미니온]], 43+RAND:5
			CASE IS < 75
				RETURN [[キャラ:스로네]], 52+RAND:5
			CASE IS <100
				RETURN [[キャラ:메이거스]], 34+RAND:5
		ENDSELECT
	ENDIF
ELSEIF FLAG:나오야루트 == 5
	SELECTCASE RAND:100
		CASE IS < 30
			RETURN [[キャラ:엔젤]], 32
		CASE IS < 60
			RETURN [[キャラ:니케]], 33
		CASE IS < 80
			RETURN [[キャラ:아크엔젤]], 33
		CASE IS < 100
			RETURN [[キャラ:프린시펄리티]], 34
	ENDSELECT
ENDIF


;=======================================================================
;特殊調整
;=======================================================================
@SET_ENEMY_STATUS_4, ARG
;精神世界の天使はTALKできない
SIF FLAG:나오야루트 == 5
	ABL:ARG:회화타입 = 331
