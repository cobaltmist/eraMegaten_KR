
;=======================================================================
;레온자동차공장
;ボス・무위스
;던전ナンバー88
;
;
;
;フラグ사용状況
;던전플래그:88:1		レオン自動車工場クリアフラグ（0:クリア前、1:クリア後）
;던전플래그:88:2		レオン自動車工場突入フラグ（0:入ってない、1:入ったことある、2:네밋사と入ったことある）
;던전플래그:88:3		무위스撃破フラグ（0:倒してない、1:倒した）
;
;
;던전플래그:88:5		非常用電源（0:キー입수前、1:キー입수済み、2:非常用電源起動済み）


;時事系列的に天体博物館＞闘技場이벤트ってことにして、クリア前に闘技場で네밋사を仲間にすると의뢰が消え던전ボスは他の서머너に倒される。
;우라베クエストに関係なくカツオ達は네밋사入りのＣＯＭＰを손に入れて서머너として活動している感じ。
;=======================================================================

;=======================================================================
;攻略조건
;=======================================================================
@PREREQUISITE_DUNGEON_88
;무위스の襲撃이벤트종료済みでレオン自動車工場クリア前、か、レオン自動車工場クリア済み
IF (이벤트플래그:27:0 == 1 && 던전플래그:88:1 == 0) || 던전플래그:88:1 == 1
	RETURN 1
ENDIF
RETURN 0


;=======================================================================
;던전名
;=======================================================================
@GET_DUNGEON_NAME_88
RESULTS = 레온자동차공장

;=======================================================================
;フ로어名표시
;=======================================================================
@FLOORNAME_88
PRINT 레온자동차공장
SELECTCASE FLAG:현M
	CASE 0
		PRINTL １Ｆ
	CASE 1
		PRINTL ２Ｆ
	CASE 2
		PRINTL ３Ｆ
	CASE 3
		PRINTL ４Ｆ
	CASEELSE
		PRINTL
ENDSELECT

;=======================================================================
;スタート地点입력
;=======================================================================
@START_DUNGEON_88, ARG
FLAG:현M = 0
FLAG:현X = 9
FLAG:현Y = 24
;CALL AUTOMAP
RETURN 0

;=======================================================================
;攻略開始時の이벤트
;=======================================================================
@EVENT_DUNGEON_START_88
;クリア前で네밋사と入ったことがない
IF 던전플래그:88:1 == 0 && 던전플래그:88:2 != 2
	;네밋사がパーティ内にいる
	IF (FINDCHARA_B([[キャラ:네밋사(밝음)]]) == 2 || FINDCHARA_B([[キャラ:네밋사(쿨)]]) == 2 || FINDCHARA_B([[キャラ:네밋사(섹시)]]) == 2)
		PRINTFORMW 레온 자동차 공장앞으로 왔다……
		PRINTFORML 　
		;네밋사と入ったことない
		IF 던전플래그:88:2 != 2
			PRINTFORMW 「여기에 무위스가 있는거네……。」
			PRINTFORMW 「내 기억의 단서……이번엔 놓치지 않을 거니까!」
			PRINTFORMW 단단히 마음먹은 네밋사와 함께 공장 내부로 발을 내밀었다.
			
			;네밋사と入ったことあるへ
			던전플래그:88:2 = 2
		ENDIF
	ELSE
		;入ったことない
		IF 던전플래그:88:2 == 0
			PRINTFORMW 이 공장 어딘가에 무위스가 있을 터.
			PRINTFORMW 하지만, 표적이 된 네밋사가 없는 상태로는, 모습을 드러내지 않을지도 모른다.
			PRINTFORML 　
			PRINTFORMW 무위스와 싸우기 위해선 네밋사를 데려올 필요가 있을 것 같다……
			
			;入ったことあるへ
			던전플래그:88:2 = 1
		ENDIF
	ENDIF
ENDIF


;=======================================================================
;탈출時の이벤트
;=======================================================================
@EVENT_DUNGEON_END_88
PRINTFORML 　
;무위스撃破済みでレオン自動車工場クリアしてない
IF 던전플래그:88:3 == 1 && 던전플래그:88:1 == 0
	PRINTFORMW 무위스를 쓰러트려 레온 자동차 공장으로부터 사례비를 받았다
	PRINTFORML 　
	PRINTFORMW 계좌에 40000엔이 들어왔고, 명성 30을 얻었다.
	
	MONEY += 40000
	FLAG:명성 += 30
	
	;レオン自動車工場クリアフラグ
	던전플래그:88:1 = 1
ENDIF

;=======================================================================
;指定したマスに進入가능か
;=======================================================================
@CAN_ENTER_88, ARG, ARG:1
;SELECTCASE DA:ARG:(ARG:1)
;ENDSELECT

;非常用電源起動してない
IF FLAG:현M == 0 && ARG == 16 && ARG:1 == 18
	IF FLAG:현X < ARG
		PRINTFORML 오토록이 걸려있기 때문에
		PRINTFORMW 여기서는 열 수 없을 것 같다.
		RETURN 0
	ENDIF
ELSEIF FLAG:현M == 1 && ARG == 21 && ARG:1 == 23 && 던전플래그:88:5 != 2
	PRINTFORMW 엘레베이터 같지만 전원이 끊긴 모양이다……
	RETURN 0
;カギ持ってる、扉開けてない
;ELSEIF FLAG:현M == 1 && ARG == 21 && ARG:1 == 23 && 던전플래그:88:2 == 4 && 던전플래그:88:6 == 0
;	PRINTFORMW 가지고 있는 열쇠로 문을 열었다……
;	;扉開錠フラグ開けたへ
;	던전플래그:88:6 = 1
;	RETURN 0
ENDIF

CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT

;=======================================================================
;フ로어데이터
;=======================================================================

;0：壁　1：床또는通路　2：扉　3：宝箱　4：이벤트　5：罠　6：上り階段 7：下り階段　8：出입　9：그외特殊処理等
;これらの内、3～9が代入されているマスに、個別の번호を与える処理を実行します。
;この処理を行った後、これらのマスには 「マップ번호*1000 + そのマスがそのマップで左上から数えて何個눈のARG타입のマスか*10 + ARG」が代入されます
;例えば 2003 であった場合、「2番のマップ＝３Ｆで、左上から数えて0個눈の3＝宝箱である」という事を意味しています
;尚、0～2はどのマップにも大量に設置されるので、ここでは기본的に個別の번호を与えません。


@MAKE_FLOOR_88
SELECTCASE FLAG:현M
	CASE 0
	;１階
		FLAG:최대X = 29 
		FLAG:최대Y = 26 
							;	  01234567890123456789012345678
		CALL MAKE_FLOOR_LINE, 0, "                             "
		CALL MAKE_FLOOR_LINE, 1, " 1       A     1   1   1  13 "
		CALL MAKE_FLOOR_LINE, 2, " 1       2     1 11111111111 "
		CALL MAKE_FLOOR_LINE, 3, " 1    1111111111   1     1   "
		CALL MAKE_FLOOR_LINE, 4, " 1    1111     1 111    111  "
		CALL MAKE_FLOOR_LINE, 5, " 1   11111     1 11111111113 "
		CALL MAKE_FLOOR_LINE, 6, " 1   1 111     1   1    111  "
		CALL MAKE_FLOOR_LINE, 7, " 1   1 1111    121 1     1   "
		CALL MAKE_FLOOR_LINE, 8, " 1   1 4111    1   1     1   "
		CALL MAKE_FLOOR_LINE, 9, " 11111 1111    1 111     1   "
		CALL MAKE_FLOOR_LINE,10, " 1111111111    1 11111111111 "
		CALL MAKE_FLOOR_LINE,11, "         2                2  "
		CALL MAKE_FLOOR_LINE,12, "   1111111111111       11111 "
		CALL MAKE_FLOOR_LINE,13, "   1           1    3  11111 "
		CALL MAKE_FLOOR_LINE,14, "   1           1   111 11111 "
		CALL MAKE_FLOOR_LINE,15, "   1           1   111 11111 "
		CALL MAKE_FLOOR_LINE,16, "   1  1111111  1  11111111   "
		CALL MAKE_FLOOR_LINE,17, "   1  1     1  1   1111  1   "
		CALL MAKE_FLOOR_LINE,18, "   1111     111121 1111  123 "
		CALL MAKE_FLOOR_LINE,19, "   11         11 1       1   "
		CALL MAKE_FLOOR_LINE,20, "   1  1111111  1 1       1   "
		CALL MAKE_FLOOR_LINE,21, "   1  1411111  1 1      1113 "
		CALL MAKE_FLOOR_LINE,22, "   1111111111111 111111111   "
		CALL MAKE_FLOOR_LINE,23, "         2                   "
		CALL MAKE_FLOOR_LINE,24, "         8                   "
		CALL MAKE_FLOOR_LINE,25, "                             "
		
		;作業員２
		;作業員１
		
	CASE 1
	;２階
		FLAG:최대X = 29 
		FLAG:최대Y = 26 
							;	  01234567890123456789012345678
		CALL MAKE_FLOOR_LINE, 0, "                             "
		CALL MAKE_FLOOR_LINE, 1, " 1111111 A 11111111111111111 "
		CALL MAKE_FLOOR_LINE, 2, " 1    11 2 11              1 "
		CALL MAKE_FLOOR_LINE, 3, " 1    1111111              1 "
		CALL MAKE_FLOOR_LINE, 4, " 1    1111111  1111111111111 "
		CALL MAKE_FLOOR_LINE, 5, " 1       1     111111111 4 1 "
		CALL MAKE_FLOOR_LINE, 6, " 1       1     1         1 1 "
		CALL MAKE_FLOOR_LINE, 7, " 1       1     4         1 1 "
		CALL MAKE_FLOOR_LINE, 8, " 1       1     1         1 1 "
		CALL MAKE_FLOOR_LINE, 9, " 1     14111   1         1 1 "
		CALL MAKE_FLOOR_LINE,10, " 1             1         1 1 "
		CALL MAKE_FLOOR_LINE,11, " 1             1         1 1 "
		CALL MAKE_FLOOR_LINE,12, " 1             1         1 1 "
		CALL MAKE_FLOOR_LINE,13, " 1   11       13        11 1 "
		CALL MAKE_FLOOR_LINE,14, " 1111111114111131114111111 1 "
		CALL MAKE_FLOOR_LINE,15, " 1   11       1         33 1 "
		CALL MAKE_FLOOR_LINE,16, " 1            1            1 "
		CALL MAKE_FLOOR_LINE,17, " 1            4            1 "
		CALL MAKE_FLOOR_LINE,18, " 1            1            1 "
		CALL MAKE_FLOOR_LINE,19, " 1            1            1 "
		CALL MAKE_FLOOR_LINE,20, " 1        11  1            1 "
		CALL MAKE_FLOOR_LINE,21, " 1        1111111114111111 1 "
		CALL MAKE_FLOOR_LINE,22, " 1               111111111 1 "
		CALL MAKE_FLOOR_LINE,23, " 1               111 2 111 1 "
		CALL MAKE_FLOOR_LINE,24, " 1111111111111111111 a 11111 "
		CALL MAKE_FLOOR_LINE,25, "                             "
		
		;落とし穴？４
		;							CALL DUNGEON_WORP,0,25,5
		;	CALL DUNGEON_WORP,0,15,7
		;악마
		;CALL DUNGEON_WORP,0,10,12	CALL DUNGEON_WORP,0,19,14
		;CALL DUNGEON_WORP,0,14,18
		
		;成長する서머너
		
	CASE 2
	;３階
		FLAG:최대X = 29 
		FLAG:최대Y = 26 
							;	  01234567890123456789012345678
		CALL MAKE_FLOOR_LINE, 0, "                             "
		CALL MAKE_FLOOR_LINE, 1, " 1111111 A 11111111111111111 "
		CALL MAKE_FLOOR_LINE, 2, " 1     1 2 11    11        1 "
		CALL MAKE_FLOOR_LINE, 3, " 1     111111    11  11111 1 "
		CALL MAKE_FLOOR_LINE, 4, "        11111  4111111   1 1 "
		CALL MAKE_FLOOR_LINE, 5, "        11111      111   1 1 "
		CALL MAKE_FLOOR_LINE, 6, " 111111     1       1    1 1 "
		CALL MAKE_FLOOR_LINE, 7, "   1  11111 411111  1      1 "
		CALL MAKE_FLOOR_LINE, 8, " T21  1   1   1  1  111111 1 "
		CALL MAKE_FLOOR_LINE, 9, "   1  1   1         1      1 "
		CALL MAKE_FLOOR_LINE,10, " 12111111 111111 1111      1 "
		CALL MAKE_FLOOR_LINE,11, "   11 2 1 11 2 1           1 "
		CALL MAKE_FLOOR_LINE,12, " 1111 4 1 11 1 1111111111111 "
		CALL MAKE_FLOOR_LINE,13, "                             "
		CALL MAKE_FLOOR_LINE,14, " 111113   3113 113  3 1  3 1 "
		CALL MAKE_FLOOR_LINE,15, "   1      1  1   1  1 1  2 1 "
		CALL MAKE_FLOOR_LINE,16, "   1  1   1  1   1  1 111111 "
		CALL MAKE_FLOOR_LINE,17, "   1  11111  11111  12111111 "
		CALL MAKE_FLOOR_LINE,18, "   1  1   1  1   1  1   1  1 "
		CALL MAKE_FLOOR_LINE,19, "   1111   11111111111   1111 "
		CALL MAKE_FLOOR_LINE,20, "   1  1   1  1   1      1  1 "
		CALL MAKE_FLOOR_LINE,21, "   1  1   1  1   1  1   1  1 "
		CALL MAKE_FLOOR_LINE,22, "   111111111111111 111111111 "
		CALL MAKE_FLOOR_LINE,23, "                   1 2 1     "
		CALL MAKE_FLOOR_LINE,24, "                   1 a 1     "
		CALL MAKE_FLOOR_LINE,25, "                             "
		
		;フリーの서머너
		;メモ、電源キー、電源室
		
	CASE 3
	;４階
		FLAG:최대X = 29 
		FLAG:최대Y = 26 
							;	  01234567890123456789012345678
		CALL MAKE_FLOOR_LINE, 0, "                             "
		CALL MAKE_FLOOR_LINE, 1, "       1 A 1     11111111111 "
		CALL MAKE_FLOOR_LINE, 2, "       1 2 1         1     1 "
		CALL MAKE_FLOOR_LINE, 3, "     1111111         1     1 "
		CALL MAKE_FLOOR_LINE, 4, " 1     14111111      1     1 "
		CALL MAKE_FLOOR_LINE, 5, " 1       1    1      1     1 "
		CALL MAKE_FLOOR_LINE, 6, " 1       1    1      1     1 "
		CALL MAKE_FLOOR_LINE, 7, " 1       4    4      1     1 "
		CALL MAKE_FLOOR_LINE, 8, " 1       1           4     1 "
		CALL MAKE_FLOOR_LINE, 9, " 1       1                 1 "
		CALL MAKE_FLOOR_LINE,10, " 1       1                 1 "
		CALL MAKE_FLOOR_LINE,11, " 1     11111               1 "
		CALL MAKE_FLOOR_LINE,12, " 1     1 2 1               1 "
		CALL MAKE_FLOOR_LINE,13, " 1     1 4 11141111111111111 "
		CALL MAKE_FLOOR_LINE,14, " 1     1   1         1   4 1 "
		CALL MAKE_FLOOR_LINE,15, " 1     11111         1   1 1 "
		CALL MAKE_FLOOR_LINE,16, " 1       1           1   1 1 "
		CALL MAKE_FLOOR_LINE,17, " 11111111111         1   1 1 "
		CALL MAKE_FLOOR_LINE,18, " 1         1         1   1 1 "
		CALL MAKE_FLOOR_LINE,19, " 1         111111111111111 1 "
		CALL MAKE_FLOOR_LINE,20, " 1         1         1     1 "
		CALL MAKE_FLOOR_LINE,21, " 1         1     111111111 1 "
		CALL MAKE_FLOOR_LINE,22, " 1         1     111111111 1 "
		CALL MAKE_FLOOR_LINE,23, " 1         1     111 2 111 1 "
		CALL MAKE_FLOOR_LINE,24, " 1111111111111111111 a 11111 "
		CALL MAKE_FLOOR_LINE,25, "                             "
		
		
		;作業員3
		;落とし穴？４
		;CALL DUNGEON_WORP,2,9,7	CALL DUNGEON_WORP,2,14,7
		;									CALL DUNGEON_WORP,2,21,8
		;モニタールーム		CALL DUNGEON_WORP,1,14,13
		;										CALL DUNGEON_WORP,1,25,14
		
	CASE 10
	CASEELSE
ENDSELECT

;=======================================================================
;タイル調整
;=======================================================================
;ARG:1、ARG:2はそれぞれX座標とY座標
;ARGはその座標に現在代入されている번호
;0：壁　1：床또는通路　2：扉　3：宝箱　4：이벤트　5：罠　6：上り階段 7：下り階段　8：出입　9：그외特殊処理等
;これらの内、3～9が代入されているマスに、個別の번호を与える処理を実行します。
;この処理を行った後、これらのマスには 「マップ번호*1000 + そのマスがそのマップで左上から数えて何個눈のARG타입のマスか*10 + ARG」が代入されます
;例えば 2003 であった場合、「2番のマップ＝３Ｆで、左上から数えて0個눈の3＝宝箱である」という事を意味しています
;尚、0～2はどのマップにも大量に設置されるので、ここでは기본的に個別の번호を与えません。
;
;何個눈のARG타입のマスなのか、は左上の(0,0)から右まで探し、その後一つ下の段に移って同様に探す、というのを右下まで繰り返して数えます。
@MAKE_FLOOR_LINE_COMMON_88, ARG, ARG:1, ARG:2
#LOCALSIZE 10
;LOCAL:0～9 それぞれ何個눈のARGなのかを記憶しています
;LOCAL:3 宝箱→사용フラグは던전플래그:70~74
;指定された座標が(0,0)である場合、つまり新しいマップを読み込んだ時はLOCALを소거します
SIF ARG:1 == 0 && ARG:2 == 0
	VARSET LOCAL
SELECTCASE ARG
	CASE 3
		;宝箱は多くの場合、開けたフラグが立っていると床を代入します
		;そのマップでどの宝箱を開けたかは、そのマップの宝箱管理用変数の何番눈のビットが立っているかで判断します
		IF GETBIT(던전플래그:(FLAG:현던전):(FLAG:현M+70), LOCAL:3)
			DA:(ARG:1):(ARG:2) = 1
		ELSE
			DA:(ARG:1):(ARG:2) = FLAG:현M *1000 + LOCAL:3 * 10 + 3
		ENDIF
		LOCAL:3++
	CASE 4 TO 8
		DA:(ARG:1):(ARG:2) = FLAG:현M *1000 + LOCAL:ARG * 10 + ARG
		LOCAL:ARG++
	CASE 9
		DA:(ARG:1):(ARG:2) = FLAG:현M *1000 + LOCAL:9 * 10 + 9
		LOCAL:ARG++	
	
ENDSELECT

;=======================================================================
;タイル定義
;=======================================================================
@DEFINE_TILES_88, ARGS, ARG, ARG:1
SELECTCASE ARGS
	CASE "A"
		RETURN 29
	CASE "a"
		RETURN 39
ENDSELECT


;=======================================================================
;特殊タイル표시
;TILE_MARK_COMMON_{FLAG:현던전}_X
;=======================================================================
;ARG:1、ARG:2がX座標とY座標
;ARGはそこに代入されている値
;RESULTSに代入した文字がマップに표시されますが、必ず全角1文字分になるようにしてください。そうでないと표시が崩れます。


@TILE_MARK_COMMON_88_4, ARG , ARG:1 , ARG:2
SELECTCASE ARG / 10
	;落とし穴を"●"と표시
	CASE 100 , 101 , 103 , 104 , 105 , 301 , 302 , 303 , 305 , 306
		RESULTS = ●
	;メッセンジャーを緑色で"ｍ"と표시
	CASE 0 , 1 , 2 , 106 , 200 , 300
		SETCOLOR 0x00ff00
		RESULTS = ｍ
	CASE 102
		;신주쿠でオザワが倒されている
		IF 던전플래그:42:신주쿠フラグ("オザワ") > 1
			SETCOLOR 0x00ff00
			RESULTS = ｍ
		ELSE
			RESULTS = ！
		ENDIF
	CASEELSE
		;特に설정しない場合、"！"と표시されます
		RETURN 1
ENDSELECT


;이벤트タイル特殊표시
;末꼬리の_4は이벤트であることを示す4
;@TILE_MARK_COMMON_88_4, ARG , ARG:1 , ARG:2
;SELECTCASE ARG / 10
	;メッセンジャーを緑色で"ｍ"と표시
	;CASE 0 TO 2 , 100 , 101 , 300
	;	SETCOLOR 0x00ff00
	;	RESULTS = ｍ
;	CASEELSE
		;特に설정しない場合、"！"と표시されます
;		RETURN 1
;ENDSELECT

;特殊タイルの표시
;末꼬리の_9は特殊タイルであることを示す9
@TILE_MARK_COMMON_88_9, ARG , ARG:1 , ARG:2
SELECTCASE ARG / 10
	CASE 2 , 3
		RESULTS = Ｅ
	CASEELSE
		;特に설정しない場合、"■"が표시されます
		RETURN 1
ENDSELECT

;=======================================================================
;[5]조사　実行時の이벤트
;=======================================================================
@EVENT_CHECK_DUNGEON_88

;==========
;出입の処理
;==========
IF DA:(FLAG:현X):(FLAG:현Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
;% 10は10で割ったあまり、つまり下一桁がでてきます
;그리고、下一桁はタイルの종류を表す
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 3
	CALL TREASURE_88
	RETURN RESULT
;=======
;階段
;=======
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 6
	CALL UPSTAIRS_88
	RETURN RESULT

ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 7
	CALL DOWNSTAIRS_88
	RETURN RESULT
;========
;이벤트
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 4
	SELECTCASE DA:(FLAG:현X):(FLAG:현Y) / 10
		;１Ｆ이벤트
		;作業員２
		CASE 0
			CALL DUNGEON_88_W02
		;作業員１
		CASE 1
			CALL DUNGEON_88_W01
		;２Ｆ이벤트
		;落とし穴
		CASE 100
			CALL DUNGEON_WORP,0,25,5
		;落とし穴
		CASE 101
			CALL DUNGEON_WORP,0,15,7
		;악마
		CASE 102
			CALL DUNGEON_88_D01
		;落とし穴
		CASE 103
			CALL DUNGEON_WORP,0,10,12
		;落とし穴
		CASE 104
			CALL DUNGEON_WORP,0,19,14
		;落とし穴
		CASE 105
			CALL DUNGEON_WORP,0,14,18
		;成長する서머너
		CASE 106
			CALL DUNGEON_88_S00
		;３Ｆ이벤트
		;フリーの서머너
		CASE 200
			CALL DUNGEON_88_S01
		;非常用電源室キー
		CASE 201
			CALL DUNGEON_88_KEY00
		;非常用電源室
		CASE 202
			CALL DUNGEON_88_KEY01
		;４Ｆ이벤트
		;作業員3
		CASE 300
			CALL DUNGEON_88_W03
		;落とし穴
		CASE 301
			CALL DUNGEON_WORP,2,9,7
		;落とし穴
		CASE 302
			CALL DUNGEON_WORP,2,14,7
		;落とし穴
		CASE 303
			CALL DUNGEON_WORP,2,21,8
		;モニタールーム、무위스戦
		CASE 304
			CALL DUNGEON_88_BOSS
		;落とし穴
		CASE 305
			CALL DUNGEON_WORP,1,14,13
		;落とし穴
		CASE 306
			CALL DUNGEON_WORP,1,25,14
	ENDSELECT
ELSE
	PRINTFORMW 何も無いようだ
	RETURN 0
ENDIF

;=======================================================================
;タイル進入時の이벤트
;=======================================================================
@EVENT_ENTER_DUNGEON_88,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出입の処理
;==========
IF DA:(FLAG:현X):(FLAG:현Y) % 10 == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 3
	CALL TREASURE_88
	RETURN RESULT
;=======
;階段
;=======
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 6
	CALL UPSTAIRS_88
	RETURN RESULT

ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 7
	CALL DOWNSTAIRS_88
	RETURN RESULT

;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1

;========
;이벤트
;========
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 4
	SELECTCASE DA:(FLAG:현X):(FLAG:현Y) / 10
		;１Ｆ이벤트
		;作業員２
		CASE 0
			CALL DUNGEON_88_W02
			RETURN 0
		;作業員１
		CASE 1
			CALL DUNGEON_88_W01
			RETURN 0
		;２Ｆ이벤트
		;落とし穴
		CASE 100
			CALL DUNGEON_WORP,0,25,5
			RETURN 0
		;落とし穴
		CASE 101
			CALL DUNGEON_WORP,0,15,7
			RETURN 0
		;악마
		CASE 102
			CALL DUNGEON_88_D01
			RETURN 0
		;落とし穴
		CASE 103
			CALL DUNGEON_WORP,0,10,12
			RETURN 0
		;落とし穴
		CASE 104
			CALL DUNGEON_WORP,0,19,14
			RETURN 0
		;落とし穴
		CASE 105
			CALL DUNGEON_WORP,0,14,18
			RETURN 0
		;成長する서머너
		CASE 106
			CALL DUNGEON_88_S00
			RETURN 0
		;３Ｆ이벤트
		;フリーの서머너
		CASE 200
			CALL DUNGEON_88_S01
			RETURN 0
		;非常用電源室キー
		CASE 201
			CALL DUNGEON_88_KEY00
			RETURN 0
		;非常用電源室
		CASE 202
			CALL DUNGEON_88_KEY01
			RETURN 0
		;４Ｆ이벤트
		;作業員3
		CASE 300
			CALL DUNGEON_88_W03
			RETURN 0
		;落とし穴
		CASE 301
			CALL DUNGEON_WORP,2,9,7
			RETURN 0
		;落とし穴
		CASE 302
			CALL DUNGEON_WORP,2,14,7
			RETURN 0
		;落とし穴
		CASE 303
			CALL DUNGEON_WORP,2,21,8
			RETURN 0
		;モニタールーム、무위스戦
		CASE 304
			CALL DUNGEON_88_BOSS
			RETURN 0
		;落とし穴
		CASE 305
			CALL DUNGEON_WORP,1,14,13
			RETURN 0
		;落とし穴
		CASE 306
			CALL DUNGEON_WORP,1,25,14
			RETURN 0
	ENDSELECT
;ENDIF
;RETURN 0

;========================
;그외
;========================
ELSEIF DA:(FLAG:현X):(FLAG:현Y) % 10 == 9
		SELECTCASE DA:(FLAG:현X):(FLAG:현Y) / 10
			;エレベーター
			CASE 2
				CALL DUNGEON_ELEVATOR, 29
			CASE 3
				CALL DUNGEON_ELEVATOR, 39
		ENDSELECT
		RETURN 1
ENDIF
RETURN 0
;=======================================================================
;エレベーター定義
;=======================================================================
;ARGはそのエレベーターのタイル
@DUNGEON_ELEVATOR_88, ARG
VARSET RESULT, -1
VARSET RESULTS
SELECTCASE ARG
	CASE 29
		RESULT:1 = 0
		RESULT:2 = 1
		RESULT:3 = 2
		RESULT:4 = 3
		RETURN -1
	CASE 39
		RESULT:1 = 1
		RESULT:2 = 2
		RESULT:3 = 3
		RETURN -1
ENDSELECT

;=======================================================================
;인카운트율
;=======================================================================
@ENCOUNT_RATE_88
SELECTCASE FLAG:현M
	CASE 0 , 1 , 2 , 3 , 4 , 5
		FLAG:인카운트율 = FLAG:미조우걸음수 * 5
	CASEELSE
		FLAG:인카운트율 = FLAG:미조우걸음수 * 5
ENDSELECT
SIF FLAG:미조우걸음수 < 5
	FLAG:인카운트율 = 0
;=======================================================================
;필요레벨눈安
;권장LV 雑魚최저LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_88
RETURN 20, 20, 25, 35
;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_88
IF RAND:100 < FLAG:인카운트율
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:미조우걸음수 += 1
	RETURN 0
ENDIF
;=======================================================================
;적の종족の종류数
;RETURN X でXが종류の数になります
;あまりいろんなのが混ざっているとCOOPがとりにくくなったり
;적の공격속성が多くなる傾向にあるので、
;종류の数は1・2종류を主体に時々3종류くらいを기본にするのが望ましいと思われます
;=======================================================================
@CLASS_NUM_88
SELECTCASE FLAG:현M
	CASE 0
		SELECTCASE RAND:100
			CASE IS < 50
				RETURN 1
			CASEELSE
				RETURN 2
		ENDSELECT
	CASE 1
		SELECTCASE RAND:100
			CASE IS < 50
				RETURN 1
			CASEELSE
				RETURN 2
		ENDSELECT
	CASE 2 , 3
		SELECTCASE RAND:100
			CASE IS < 35
				RETURN 1
			CASEELSE
				RETURN 2
		ENDSELECT
ENDSELECT
;=======================================================================
;적の総数
;ARGは出現する적の종류です
;RAND(X, Y)はX～Y-1のうち중 하나になるRANDです
;なお、ここで帰った数字は下限を종족数、上限を10として自動的に調整されます
;=======================================================================
@ENEMY_NUM_88, ARG
SELECTCASE FLAG:현M
	CASE 0
		RETURN RAND(ARG, ARG*2+1)
	CASE 1
		RETURN RAND(ARG, ARG*2+2)
	CASE 2 , 3
		RETURN RAND(ARG, ARG*2+3)
ENDSELECT
;=======================================================================
;적の종류
;=======================================================================
@ENEMY_88, ARG
SELECTCASE FLAG:현M
	;１Ｆ
	CASE 0
		SELECTCASE RAND:100
			CASE IS < 10
				RETURN [[キャラ:웨어캣]],19
			CASE IS < 25
				RETURN [[キャラ:랴난시]],19
			CASE IS < 35
				RETURN [[キャラ:목없는라이더]],19
			CASE IS < 55
				RETURN [[キャラ:그르르]],19
			CASE IS < 70
				RETURN [[キャラ:야토노카미]],22
			CASE IS < 80
				RETURN [[キャラ:아크엔젤]],24
			CASE IS < 90
				RETURN [[キャラ:잔트만]],21
			CASE IS < 100
				RETURN [[キャラ:카타키라우와]],20
		ENDSELECT
	;２Ｆ
	CASE 1
		SELECTCASE RAND:100
			CASE IS < 10
				RETURN [[キャラ:랴난시]],19
			CASE IS < 20
				RETURN [[キャラ:목없는라이더]],19
			CASE IS < 30
				RETURN [[キャラ:그르르]],19
			CASE IS < 45
				RETURN [[キャラ:야토노카미]],22
			CASE IS < 60
				RETURN [[キャラ:아크엔젤]],24
			CASE IS < 70
				RETURN [[キャラ:매드가서]],26
			CASE IS < 80
				RETURN [[キャラ:니스록]],28
			CASE IS < 90
				RETURN [[キャラ:잔트만]],22
			CASE IS < 100
				RETURN [[キャラ:카타키라우와]],21
		ENDSELECT
	;３、４Ｆ
	CASE 2 , 3
		SELECTCASE RAND:100
			CASE IS < 10
				RETURN [[キャラ:목없는라이더]],19
			CASE IS < 20
				RETURN [[キャラ:그르르]],20
			CASE IS < 35
				RETURN [[キャラ:야토노카미]],23
			CASE IS < 40
				RETURN [[キャラ:매드가서]],26
			CASE IS < 50
				RETURN [[キャラ:어반테러]],30
			CASE IS < 60
				RETURN [[キャラ:니스록]],28
			CASE IS < 65
				RETURN [[キャラ:니스록]],29
			CASE IS < 70
				RETURN [[キャラ:잔트만]],22
			CASE IS < 80
				RETURN [[キャラ:잔트만]],23
			CASE IS < 90
				RETURN [[キャラ:카타키라우와]],22
			CASE IS < 100
				RETURN [[キャラ:카타키라우와]],22
		ENDSELECT
ENDSELECT

;=======================================================================
;宝箱の中身
;=======================================================================
@TREASURE_88
#LOCALSIZE 3
;宝箱のフラグ管理用変数の번호
LOCAL = FLAG:현M + 70
;そのマップの何番눈の宝箱か
LOCAL:1 = (DA:(FLAG:현X):(FLAG:현Y) / 10) % 100

;/10はIDを10で割る。つまり、下一桁をなくして、二桁눈と三桁눈を二桁の数字として取り出している
SELECTCASE DA:(FLAG:현X):(FLAG:현Y) / 10
	;マップ번호*100+そのマップで何番눈の宝箱かを示す数値でどの宝箱かを判別します。
	;例えば0の場合、0版のマップの0番눈の宝箱であることを表しています。
	;１Ｆ
	CASE 0
		CALL GET_TREASURE, [[아이템:전망구슬]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	CASE 1
		CALL GET_TREASURE, [[아이템:반혼향]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	CASE 2
		;適当
		CALL GET_TREASURE, [[아이템:스킬카드【지온가】]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	CASE 3
		CALL GET_TREASURE, [[아이템:디스포이즌]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	CASE 4
		CALL GET_TREASURE, [[아이템:운의향]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	;２Ｆ
	CASE 100
		CALL GET_TREASURE, [[아이템:풀페이스]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	CASE 101
		CALL GET_TREASURE, [[아이템:챠크라포트]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	CASE 102
		CALL GET_TREASURE, [[아이템:반혼향]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	CASE 103
		;適当
		CALL GET_TREASURE, [[아이템:힘의향]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	;３Ｆ
	CASE 200
		CALL GET_TREASURE, [[아이템:지혜의향]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	CASE 201
		;適当
		CALL GET_TREASURE, [[아이템:속도의향]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	CASE 202
		CALL GET_TREASURE, [[아이템:마반경]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	CASE 203
		CALL GET_TREASURE, [[아이템:전망구슬]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	CASE 204
		CALL GET_TREASURE, [[아이템:도반옥]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
	CASE 205
		CALL GET_TREASURE, [[아이템:에어로젯트]] , 1 , FLAG:현던전, LOCAL , LOCAL:1
ENDSELECT
RETURN 1
;=======================================================================
;階段置き場
;=======================================================================
;上り階段処理
@UPSTAIRS_88
;SELECTCASE DA:(FLAG:현X):(FLAG:현Y) /10
;	;IDで分岐
;	;１Ｆ上り階段
;	CASE 0
;			;@DUNGEON_UPSTAIRS_ID , 階段を進んだ先のマップ번호 , 移動先のタイルのID
;			;２Ｆ＝マップ번호1番の0番눈の下り階段 = 1 * 1000 + 0 * 10 + 7 = 1007へと飛ぶ
;			CALL DUNGEON_UPSTAIRS_ID, 1 , 1007
;	CASE 100
;			CALL DUNGEON_UPSTAIRS_ID, 2 , 2007
;	CASE 200
;			CALL DUNGEON_UPSTAIRS_ID, 3 , 3007
;	CASE 300
;			CALL DUNGEON_UPSTAIRS_ID, 4 , 4007
;	CASEELSE
;ENDSELECT

;入り組んだ던전で階段がたくさん入り乱れている상태であれば、上記のように階段毎に指定してあげる필요があります。
;しかしこの에코빌딩は1つのマップに上り階段が１つ、下り階段が１つ、上の階に進む＝マップ번호が一つ増えるという구조になっています
;なので、下記のように書けば一行で済みます
CALL DUNGEON_UPSTAIRS_ID, FLAG:현M+1 , (FLAG:현M + 1) * 1000 + 7
RETURN 1

;下り階段処理
@DOWNSTAIRS_88
;上り階段同様に、에코빌딩は下り階段も1行で記述できます
CALL DUNGEON_DOWNSTAIRS_ID, FLAG:현M-1 , (FLAG:현M - 1) * 1000 + 6
RETURN 1

;=======================================================================
;윤간참가악마
;=======================================================================
@DUNGEON_RAPE_DEVIL_88
;랜덤にどの악마が出るか決定
;RETURN CSV번호 , 人数 , 레벨
SELECTCASE RAND:3
	CASE 0 , 1 , 2 , 3
		RETURN [[キャラ:그르르]] , 3 + RAND:10 , CSVBASE([[キャラ:그르르]] , GETNUM(BASE , "LV") , 0) + RAND:3
ENDSELECT








;=======================================================================
;이벤트
;=======================================================================
;非常用電源室キー
@DUNGEON_88_KEY00
;非常用電源キー손に入れてない
IF 던전플래그:88:5 == 0
	;네밋사がパーティ内にいる
	IF (FINDCHARA_B([[キャラ:네밋사(밝음)]]) == 2 || FINDCHARA_B([[キャラ:네밋사(쿨)]]) == 2 || FINDCHARA_B([[キャラ:네밋사(섹시)]]) == 2)
		PRINTFORMW 「응? 지금 뭔가 빛난것 같아…….」
		PRINTFORMW 「열쇠네. 열쇠가 떨어져 있어.」
		PRINTFORMW 갑자기 멈춰 선 네밋사가 가리키는 방향을 보니, 거기에 열쇠가 떨어져 있었다.
		PRINTFORML 　
		PRINTFORMW 이 공장 열쇠일까?
		PRINTFORML 　
		PRINTFORMW 「비상용 전원 키」를 입수했다.
	ELSE
		PRINTFORMW 통로를 걷는 %CALLNAME:MASTER%의 시계 끝에 무언가 빛나는 것이 보였다.
		PRINTFORMW 다가가 보니, 그것은 열쇠였다.
		PRINTFORML 　
		PRINTFORMW 이 공장 열쇠일까?
		PRINTFORML 　
		PRINTFORMW 「비상용 전원 키」를 입수했다.
	ENDIF
	;非常用電源キー손に入れたへ
	던전플래그:88:5 = 1
;非常用電源キー손に入れた
ELSE
	PRINTFORMW 아무 것도 없다……
ENDIF
;----------------------------------------------------
;非常用電源室
@DUNGEON_88_KEY01
;非常用電源キー손に入れてない
IF 던전플래그:88:5 == 0
	PRINTFORMW 비상용 전원실인 모양이다……
	PRINTFORML 　
	PRINTFORMW 배전함에 열쇠가 걸려 있어서 조작할 수 없다.
	PRINTFORMW 이곳의 전원을 넣으면 제2 엘레베이터를 움직일 수 있는 것 같다……
;非常用電源キー손に入れた
ELSEIF 던전플래그:88:5 == 1
	PRINTFORMW 비상용 전원실인 모양이다……
	PRINTFORML 　
	PRINTFORMW 여기서, 제2 엘레베이터 조작이 가능 할 것 같다……
	PRINTFORML 　
	PRINTFORML 『………제2 엘레베이터를 기동합니다.』
	PRINTFORMW 주어온 열쇠를 써서 배선함을 조작하자 엘레베이터 기동 메세지가 흘러나왔다.
	PRINTFORML 　
	;네밋사がパーティ内にいる
	IF (FINDCHARA_B([[キャラ:네밋사(밝음)]]) == 2 || FINDCHARA_B([[キャラ:네밋사(쿨)]]) == 2 || FINDCHARA_B([[キャラ:네밋사(섹시)]]) == 2)
		PRINTFORMW 「진짜, 귀찮네.」
		PRINTFORMW 「하지만, 이제 앞으로 나아갈 수 있어. 서두르자, %CALLNAME:MASTER%。」
	ELSE
		PRINTFORMW 이걸로 앞으로 나아갈 수 있을터……
	ENDIF
	;非常用電源起動済みへ
	던전플래그:88:5 = 2
;非常用電源起動済み
ELSEIF 던전플래그:88:5 == 2
	PRINTFORMW 여긴 비상용 전원실이다
	PRINTFORMW 더이상 용무는 없다……
ENDIF
;----------------------------------------------------
;従業員１
@DUNGEON_88_W01
PRINTFORMW 従業員の男がいる……
PRINTFORML 　
;レオン自動車工場クリア前
IF 던전플래그:88:1 == 0
	;아마미 공항에서 레이･레이를 노예로 삼았지만 조직에 넘겼다
	IF 던전플래그:93:6 == 1 || 던전플래그:93:6 == 2
		PRINTFORMW 「이 앞에서 말이야……여기서 로봇을 봤어.」
		PRINTFORMW 「끼기긱 끼기긱하고, 소리를 내면서 걸어 다니던데…….」
		PRINTFORMW 「……나, 피곤한걸까……。」
	ELSE
		;레이･레이가 팬텀을 찾으러 온 이미지
		PRINTFORMW 「이 앞에서 말이야……여기서 로봇을 봤어.」
		PRINTFORMW 「끼기긱 끼기긱하고, 소리를 내면서 걸어 다니던데…….」
		PRINTFORML 　
		PRINTFORMW 「그 전에는, 하얀 인영을 봤고…….」
		PRINTFORMW 「……나, 뭔가에 씌인거 아닐까…….」
	ENDIF
	PRINTFORMW 먼산을 바라보는 남자를 두고 서둘렀다……
;レオン自動車工場クリア後
ELSEIF 던전플래그:88:1 == 1
	PRINTFORMW 「예전에 말이야……여기서 사고가 있던 모양이야…….」
	PRINTFORMW 「위에서 엄청난 소리가 나서……무서웠어……。」
	PRINTFORMW 「그 전에는 로봇을 봤고……무슨 일이 일어나고 있는걸까…….」
	PRINTFORMW 불안한 듯한 남자를 두고 걸어나갔다……
ENDIF
;----------------------------------------------------
;従業員２
@DUNGEON_88_W02
PRINTFORMW 종업원이 있다……
PRINTFORML 　
;レオン自動車工場クリア前
IF 던전플래그:88:1 == 0
	PRINTFORMW 「아-, 거기있는 너. 여기 관계자 아니지?」
	PRINTFORMW 「부외자가 들어오면 곤란하다구-. 진짜, 저번에도 전원에 누가 장난쳐둬서…….」
	PRINTFORMW 중얼중얼 거리는 남자를 무시하고 걸었다……
;レオン自動車工場クリア後
ELSEIF 던전플래그:88:1 == 1
	PRINTFORMW 「아-, 거기있는 너. 여기 관계자 아니지?」
	PRINTFORMW 「부외자가 들어오면 곤란하다구-. 진짜……。」
	PRINTFORMW 중얼중얼 거리는 남자를 무시하고 걸었다……
ENDIF
;----------------------------------------------------
;従業員３
@DUNGEON_88_W03
PRINTFORMW 종업원이 있다……
PRINTFORML 　
;非常用電源キー손に入れてない
IF 던전플래그:88:5 == 0
	PRINTFORMW 「너, 어디서 열쇠 못봤어?」
	PRINTFORMW 「３Ｆ에 있는, 비상용 전원실의 열쇠인데.」
	PRINTFORMW 열쇠를 찾는 남자를 두고 걸어나갔다……
;非常用電源キー손に入れた
ELSEIF 던전플래그:88:5 == 1
	PRINTFORMW 「너, 어디서 열쇠 못봤어?」
	PRINTFORMW 「３Ｆ에 있는, 비상용 전원실의 열쇠인데.」
	PRINTFORML 　
	PRINTFORMW 「그거 없으면, 제2 엘레베이터가 기동시킬 수 없어서……어쩌지……。」
	PRINTFORMW 열쇠를 찾는 남자를 두고 걸어나갔다……
;非常用電源起動済み
ELSEIF 던전플래그:88:5 == 2
	PRINTFORMW 「너, 어디서 열쇠 못봤어?」
	PRINTFORMW 「３Ｆ에 있는, 비상용 전원실의 열쇠인데.」
	PRINTFORML 　
	PRINTFORMW 「그거 없으면, 제2 엘레베이터가 기동시킬 수 없……는데, 엘레베이터 움직이고 있어?」
	PRINTFORMW 「그런가……, 그렇다면, 괜찮으려나……？」
	PRINTFORMW 혼자서 끄덕이는 남자를 두고 걸어나갔다……
ENDIF
;----------------------------------------------------
;악마
;真４ネタを入れておく
@DUNGEON_88_D01
;신주쿠でオザワが倒されている
IF 던전플래그:42:신주쿠フラグ("オザワ") > 1
	PRINTFORMW 악마가 있다……
	PRINTFORML 　
	PRINTFORMW 「하아……. 뭐야, 서머너인가…….」
	PRINTFORMW 「난 좌절중이야……절루 가줘…….」
	PRINTFORML 　
	PRINTFORMW 「아카다마(赤玉)만 없었다면……。」
	PRINTFORMW 요리사 복장을 한 악마를 두고 걸어나갔다……
ELSE
	PRINTFORMW 아무도 없다……
ENDIF
;----------------------------------------------------
;フリーの서머너
@DUNGEON_88_S01
;レオン自動車工場クリア前
PRINTFORMW 나른해보이는 서머너가 있다……
PRINTFORML 　
IF 던전플래그:88:1 == 0
	;非常用電源起動前
	IF 던전플래그:88:5 != 2
		PRINTFORMW 「여기에 악마가 나왔다는 소릴 듣고 돈 벌러 왔는데…….」
		PRINTFORMW 「엘레베이터 멈춰있거나 하고, 귀찮아…….」
		PRINTFORMW 나른한 듯 COMP를 조작하는 서머너와 걸어나갔다……
	;非常用電源起動済み
	ELSEIF 던전플래그:88:5 == 2
		PRINTFORMW 「여기에 악마가 나왔다는 소릴 듣고 돈 벌러 왔는데…….」
		PRINTFORMW 「엘레베이터 멈춰있거나 하고, 귀찮아…….」
		PRINTFORMW 「엑? 움직이게 됬다고? 그렇담 나중에 가볼까.」
		PRINTFORMW 나른한 듯 COMP를 조작하는 서머너와 걸어나갔다……
	ENDIF
;レオン自動車工場クリア後
ELSEIF 던전플래그:88:1 == 1
	PRINTFORMW 「이곳 보스가 쓰러졌다고 듣긴 했는데, 잡다한 악마는 없어지지 않네.」
	PRINTFORMW 「한동안, 여기서 돈이나 벌고 있을까…….」
	PRINTFORMW 나른한 듯 COMP를 조작하는 서머너와 걸어나갔다……
ENDIF
;----------------------------------------------------
;成長する서머너
;天体博物館のクリア状況で分岐
@DUNGEON_88_S00
PRINTFORMW 組織の서머너がいる……
PRINTFORML 　
;レオン自動車工場クリア前
IF 던전플래그:88:1 == 0
	;天体博物館クリア상태당신がクリア
	IF 던전플래그:89:1 == 1 || 던전플래그:89:1 == 2 || 던전플래그:89:1 == 3
		PRINTFORMW 「넌……캐롤J를 쓰러트렸다는…….」
		PRINTFORMW 「여기 있다는 건, 너도 노리고 있다는 건가.」
		PRINTFORMW 「젠장! 박물관에선 선수를 빼앗겼지만, 이번엔 지지 않아!」
		PRINTFORMW 남자는 혼자 말을 하더니, 탐색하러 갔다……
	;天体博物館クリア상태他人がクリア
	ELSEIF 던전플래그:89:1 == 4
		;당신の레벨が３５이상
		IF BASE:MASTER:LV >= 35
			PRINTFORMW 「엇! 다, 당신은!?」
			PRINTFORMW 「설마……, 당신도, 여기 나온다는 악마를 쓰러트리기 위해서?」
			PRINTFORML 　
			PRINTFORMW 「……망했다! 피네간하고도 대적하는 녀석과 경쟁하게 될 줄이야…….」
			PRINTFORMW 「하지만, 질 순 없어!」
		ELSE
			PRINTFORMW 「당신은……？」
			PRINTFORMW 「그런가, 같은 사냥감을 노리고 있는 라이벌이라는 건가.」
			PRINTFORMW 「피네간정도라면 또 몰라도, 너한테 질수는 없겠네…….」
			PRINTFORMW 「미안하지만, 이 사냥감도 내가 먼저 쓰러트리지!」
		ENDIF
		PRINTFORMW 남자는 혼자 말을 하더니, 탐색하러 갔다……
	ENDIF
;レオン自動車工場クリア後
ELSEIF 던전플래그:88:1 == 1
	;天体博物館クリア상태당신がクリア
	IF 던전플래그:89:1 == 1 || 던전플래그:89:1 == 2 || 던전플래그:89:1 == 3
		PRINTFORMW 「선수를 빼앗겼나…….」
		PRINTFORMW 「젠장! 이래선, 아무리 해봤자…….」
		PRINTFORMW 분개하는 남자를 두고 걸어나갔다……
	;天体博物館クリア상태他人がクリア
	ELSEIF 던전플래그:89:1 == 4
		PRINTFORMW 「이번엔 내 패배인가…….」
		PRINTFORMW 「좀 더, 강해지고 싶다…….」
		PRINTFORMW 기운을 잃은 남자를 두고 걸어나갔다……
	ENDIF
ENDIF
;----------------------------------------------------
;무위스戦
@DUNGEON_88_BOSS

;무위스撃破前
IF 던전플래그:88:3 == 0
	;네밋사がパーティ内にいる
	IF (FINDCHARA_B([[キャラ:네밋사(밝음)]]) == 2 || FINDCHARA_B([[キャラ:네밋사(쿨)]]) == 2 || FINDCHARA_B([[キャラ:네밋사(섹시)]]) == 2)
		PRINTFORMW 여긴 모니터실인 모양이다……
		PRINTFORML 　
		PRINTFORMW 그 중앙에, 공장 내부 부품을 써서 전보다 흉악한 모습이 된 무위스가 있었다.
		PRINTFORML 　
		PRINTFORMW 「네밋사……네녀석의 존재는, 우리 주에 있어 위협이 된다.」
		PRINTFORMW 「이번에야 말로, 네녀석을 지워주마.」
		PRINTFORML 　
		PRINTFORMW 「위협, 위협 거리기나 하고 실례네. 네밋사가 위협이 될지 어떨지는 너 하기 나름이라고.」
		PRINTFORMW 「무위스, 너 어째서 네밋사에 대해서 알고 있는거야?」
		PRINTFORMW 「네밋사를 가둔 녀석을 알고 있지?」
		PRINTFORMW 「그 주라는 녀석이 네밋사를 가둔거야? 말좀 해봐.」
		PRINTFORML 　
		PRINTFORMW 「응? 무슨 소리를 하는거냐? 우리의 주를 잊어버렸다는 건가?」
		PRINTFORMW 네밋사의 잇다른 물음에 무위스의 입에서 곤혹스러움이 섞인 말이 흘러나왔다.
		PRINTFORML 　
		PRINTFORMW 「"우리들의" 란게 뭔 소리야? 나랑도 관계있단 말이야?」
		PRINTFORMW 「아- 진짜! 빙 돌리는 건 이제 지겨워!」
		PRINTFORMW 「전부 실토하게 해주겠어. 네가 누구고 네밋사의 뭘 알고 있는가!」
		PRINTFORML 　
		PRINTFORMW 「기억을 잃은 건가……. 아니면, 언젠가 눈 뜨는 것인가……。」
		PRINTFORMW 「네녀석이 소멸하면, 그 대답은 하나가 된다. 멸해라 네밋사……그것이 내 대답이다.」
		PRINTFORMW 그 말만을 남기고, 무위스는 수많은 파츠로 만들어진 거대한 팔을 들어올리며 공격해왔다.
		PRINTFORML 　
		
		;전투
		FLAG:도주불가플래그 = 1
		FLAG:회화불능플래그 = 1
		FLAG:선제기습취소 = 1
		
		;他の이벤트との調整で무위스の레벨を40に（原作は36）
		CALL SET_ENEMY,9,1115,40,0,1
		CFLAG:(CHARANUM-1):ＨＰ보정 += 1500
		CFLAG:(CHARANUM-1):행동횟수 = 2
		ABL:(CHARANUM-1):스킬1 = [[스킬:아기]]
		ABL:(CHARANUM-1):스킬2 = [[스킬:아기라오]]
		ABL:(CHARANUM-1):스킬3 = [[스킬:마하・라기온]]
		ABL:(CHARANUM-1):스킬4 = [[스킬:메기도]]
		ABL:(CHARANUM-1):스킬5 = [[스킬:메기도라]]
		ABL:(CHARANUM-1):스킬6 = [[스킬:베어넘기기]]
		
		ABL:(CHARANUM-1):스킬7 = 0
		ABL:(CHARANUM-1):스킬8 = 0
		
		CALL SYNC_STATUS,(CHARANUM-1)
		CALL HEALTH_CHARA,(CHARANUM-1)
		
		CALL BATTLE_START
		FLAG:도주불가플래그 = 0
		FLAG:회화불능플래그 = 0
		FLAG:선제기습취소 = 0
		
		PRINTFORMW 「카가…그……그……」
		PRINTFORMW 「이, 이 몸을 가지고서도……네녀석을 멸할 수 없는건가…….」
		PRINTFORML 　
		PRINTFORMW 「조…좀 더……좀 더……거대한 힘을……손…손에 넣으면…….」
		PRINTFORML 　
		PRINTFORMW 「큭, 또 도망칠 샘?」
		;캐롤Ｊがパーティ内にいる
		IF FINDCHARA_B([[キャラ:캐롤Ｊ]]) == 2
			PRINTFORML 　
			PRINTFORMW 「엇차, 도망치게 두지 않는다구!」
		ENDIF
		PRINTFORML 　
		PRINTFORMW 「그그극, 기긱………그그……。」
		PRINTFORML %CALLNAME:MASTER%와의 전투에서 패해, 너덜너덜해진 몸을 끌고 도망치려던 무위스가,
		PRINTFORM 네밋사
		;캐롤Ｊがパーティ内にいる
		IF FINDCHARA_B([[キャラ:캐롤Ｊ]]) == 2
			PRINTFORM 와 캐롤Ｊ
		ENDIF
		PRINTFORMW 에게 저지당해 초조한 듯 소리를 냈다.
		PRINTFORML 　
		PRINTFORML 　
		PRINTFORMW 「무위스！ 네밋사에 대해서, 다 토해내!」
		PRINTFORML 　
		PRINTFORMW 「네밋사……역시 네녀석에게서 도망칠 순 없는건가……」
		PRINTFORMW 「멸하라 관장하는 자……네녀석만 없었더라면……마니투(マニトゥ)는…….」
		PRINTFORMW 점점 몸을 구성하고 있던 파츠가 떨어지는 속에서, 무위스는 네밋사를 노려보며 중얼거렸다.
		PRINTFORML 　
		PRINTFORMW 「멸하라고? 멸하라니 무슨 소리야? 거기에…마니투라니!?」
		PRINTFORML 　
		PRINTFORMW 「후후후……허나…나는 혼자가 아니다…….」
		PRINTFORMW 「제2…제3의 내가…반드시…네녀석을……그, 그리고 마니투는……」
		PRINTFORMW 「마니투…는…….」
		PRINTFORML 　
		PRINTFORMW 「무, 무위스! 아직 묻고 싶은 게 있다고! 무위스! 무위스!!」
		PRINTFORMW 추궁하는 네밋사에게 대답하지 않은채, 무위스의 몸은 무너져내려 움직이지 않게 되었다.
		PRINTFORML 　
		PRINTFORMW 「진짜아아아!! 어째서! 드디어 단서를 잡았는데!」
		PRINTFORMW 고철덩어리가 된 무위스의 잔해 앞에서 네밋사가 신경질적으로 외쳤다.
		PRINTFORML 　
		PRINTFORMW 무위스를 쓰러트렸으니 네밋사가 노려질 일은 없겠지.
		PRINTFORMW 마니투라 불린 존재도 신경쓰이지만, 지금은 돌아가도록 하자……
		
		CUSTOMDRAWLINE -
		;무위스撃破フラグ
		던전플래그:88:3 = 1
		
		FLAG:탈출 = 1
		
	ELSE
		PRINTFORMW 여긴 모니터실인 모양이다……
		PRINTFORML 　
		PRINTFORMW “무언가”가 있던 기척이 있지만, 지금은 없는 모양이다……
		PRINTFORML 　
		;캐롤Ｊがパーティ内にいる
		IF FINDCHARA_B([[キャラ:캐롤Ｊ]]) == 2
			PRINTFORMW 「이건……,무위스의 기척이 드는데……。」
			PRINTFORMW 「하지만……도망쳤는지, 숨었는지……. “무언가”로 꾀어낼 필요가 있겠네…….」
			PRINTFORML 　
			PRINTFORMW 캐롤Ｊ의 말대로, 무위스와 싸우기 위해선, 어떻게든 꾀어낼 필요가 있는 모양이다……
		ELSE
			PRINTFORMW 무위스와 싸우기 위해선, 어떻게든 꾀어낼 필요가 있는 모양이다……
		ENDIF
	ENDIF
;무위스撃破後
ELSEIF 던전플래그:88:3 == 1
	PRINTFORMW 여긴 모니터실인 모양이다……
	PRINTFORML 　
	PRINTFORMW 아무도 없다……
	;네밋사がパーティ内にいる
	IF (FINDCHARA_B([[キャラ:네밋사(밝음)]]) == 2 || FINDCHARA_B([[キャラ:네밋사(쿨)]]) == 2 || FINDCHARA_B([[キャラ:네밋사(섹시)]]) == 2)
		PRINTFORML 　
		PRINTFORMW 「무위스……. 나는…….」
	ENDIF
ENDIF
;----------------------------------------------------
