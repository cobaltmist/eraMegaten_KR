
;
;ランダム依頼modで使用するフラグ
;의뢰플래그:2:1		依頼の分岐に使用
;의뢰플래그:2:2		依頼の受諾判定に使用
;의뢰플래그:2:3		ダンジョン依頼の場所判定に使用
;					（1、ビル、３、無人工場）
;의뢰플래그:2:4		依頼中の状況判定に使用
;=================================================
;	EVENT_BATTLE_ACTEND_REQUEST2
;=================================================
;	戦闘中行動終了時処理
;-------------------------------------------------
@EVENT_BATTLE_ACTEND_REQUEST2
SELECTCASE 의뢰플래그:2:1
	;28では行動終了時に화염데미지
	CASE 28
		LOCAL = FLAG:행동순1
		SIF CFLAG:LOCAL:PT플래그 == 0
			RETURN 0
		SIF LOCAL < 0
			RETURN 0
		SIF CFLAG:LOCAL:상태이상 == GET_STATE_NUM("DYING")
			RETURN 0
		IF MAXBASE:LOCAL:화염 == 999
			LOCAL:1 = 0
		ELSE
			LOCAL:1 = RAND:20 + 50
			IF CFLAG:LOCAL:화염무효화횟수 > 0
				LOCAL:1 = 0
				CFLAG:LOCAL:화염무효화횟수 -= 1
			ELSE
				LOCAL:1 = LOCAL:1 * MAXBASE:LOCAL:화염 / 100
			ENDIF
		ENDIF
		IF LOCAL:1 < 0
			LOCALS:1 = {-LOCAL:1}회복
		ELSEIF LOCAL:1 == 0
			LOCALS:1 = BLOCK
		ELSE
			LOCALS:1 = {LOCAL:1}데미지
		ENDIF
		;表示テキスト
		SELECTCASE 의뢰플래그:2:1
			CASE 28
				LOCALS = 폭발이 %조사처리(CALLNAME:LOCAL,"를")% 위협해온다!
		ENDSELECT
		CALL MESSAGE_B(1, LOCALS)
		PRINTFORMW TARGET:[{LOCAL,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　%LOCALS:1%
		CALL VAR_HP, LOCAL, -(LOCAL:1), 0
	;依頼14では行動終了時に비구데미지
	CASE 14
		LOCAL = FLAG:행동순1
		SIF CFLAG:LOCAL:PT플래그 == 0
			RETURN 0
		SIF LOCAL < 0
			RETURN 0
		SIF CFLAG:LOCAL:상태이상 == GET_STATE_NUM("DYING")
			RETURN 0
		IF MAXBASE:LOCAL:비구 == 999
			LOCAL:1 = 0
		ELSE
			LOCAL:1 = RAND:20 + 50
			IF CFLAG:LOCAL:비구무효화횟수 > 0
				LOCAL:1 = 0
				CFLAG:LOCAL:비구무효화횟수 -= 1
			ELSE
				LOCAL:1 = LOCAL:1 * MAXBASE:LOCAL:비구 / 100
			ENDIF
		ENDIF
		IF LOCAL:1 < 0
			LOCALS:1 = {-LOCAL:1}회복
		ELSEIF LOCAL:1 == 0
			LOCALS:1 = BLOCK
		ELSE
			LOCALS:1 = {LOCAL:1}데미지
		ENDIF
		;表示テキスト
		SELECTCASE 의뢰플래그:2:1
			CASE 14
				LOCALS = 헬기의 기총 소사가 %조사처리(CALLNAME:LOCAL,"를")% 위협해온다!
			ENDSELECT
		CALL MESSAGE_B(1, LOCALS)
		PRINTFORMW TARGET:[{LOCAL,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　%LOCALS:1%
		CALL VAR_HP, LOCAL, -(LOCAL:1), 0
	;依頼35では行動終了時に신경데미지
	CASE 35
		LOCAL = FLAG:행동순1
		SIF CFLAG:LOCAL:PT플래그 == 0
			RETURN 0
		SIF LOCAL < 0
			RETURN 0
		SIF CFLAG:LOCAL:상태이상 == GET_STATE_NUM("DYING")
			RETURN 0
		IF MAXBASE:LOCAL:신경 == 999
			LOCAL:1 = 0
		ELSE
			LOCAL:1 = RAND:20 + 50
			IF CFLAG:LOCAL:신경무효화횟수 > 0
				LOCAL:1 = 0
				CFLAG:LOCAL:신경무효화횟수 -= 1
			ELSE
				LOCAL:1 = LOCAL:1 * MAXBASE:LOCAL:신경 / 100
			ENDIF
		ENDIF
		IF LOCAL:1 < 0
			LOCALS:1 = {-LOCAL:1}회복
		ELSEIF LOCAL:1 == 0
			LOCALS:1 = BLOCK
		ELSE
			LOCALS:1 = {LOCAL:1}데미지
		ENDIF
		;表示テキスト
		SELECTCASE 의뢰플래그:2:1
			CASE 35
				LOCALS = 유독 가스가 %조사처리(CALLNAME:LOCAL,"를")% 위협해온다!
		ENDSELECT
		CALL MESSAGE_B(1, LOCALS)
		PRINTFORMW TARGET:[{LOCAL,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　%LOCALS:1%
		CALL VAR_HP, LOCAL, -(LOCAL:1), 0
ENDSELECT








;=================================================
;EVENT_BATTLE_TURNEND_REQUEST_2
;=================================================
;戦闘中・ターン終了時処理
;-------------------------------------------------
;FLAG:승리플래그に1をセットすると、戦闘を強制終了出来る
;FLAG:패배플래그に1をセットすると、ゲームオーバーに出来る
;-------------------------------------------------
@EVENT_BATTLE_TURNEND_REQUEST2
SELECTCASE 의뢰플래그:2:1
	CASE 43
		;3ターン経過でゲームオーバー
		SELECTCASE FLAG:경과턴수
			CASE 3
				CUSTOMDRAWLINE =
				CALL MESSAGE_WINDOW, "" , 	@"차량이 통과해 버렸다…", , , , , 4
				FLAG:승리플래그 = 1
				의뢰플래그:2:4 = 1
		ENDSELECT
	CASE 47, 50
	;경과턴수をカウント
		의뢰플래그:2:4 += 1
ENDSELECT







;===================================================================================
;報酬判定
;의뢰플래그:2:1		依頼の分岐に使用
;의뢰플래그:2:2		依頼の受諾判定に使用
;의뢰플래그:2:5		依頼の難易度判定に使用
;의뢰플래그:2:6		報酬の種類（0:日本円、1マッカ）の判定に使用
;의뢰플래그:2:7		依頼主（1:メシア教、2:ガイア教、0:그외）の判定に使用
;의뢰플래그:2:11	メシア教の戦力値
;의뢰플래그:2:12	ガイア教の戦力値
;
;===================================================================================
@PRIZE_REQUEST_2
;ダンジョンの依頼の場合：通常の4倍の報酬
IF 의뢰플래그:2:2 == 1
	;報酬がマッカ
	IF 의뢰플래그:2:6 == 1
		;メシア教、ガイア教の依頼は戦力値によって報酬変動
		IF 의뢰플래그:2:7 == 1
			LOCAL:1 = (RAND:5 + 의뢰플래그:2:5) * 4 * 100 / (의뢰플래그:2:11 * 2 + 10)
		ELSEIF 의뢰플래그:2:7 == 2
			LOCAL:1 = (RAND:5 + 의뢰플래그:2:5) * 4 * 100 / (의뢰플래그:2:12 * 2 + 10)
		ELSE
			LOCAL:1 = (RAND:5 + 의뢰플래그:2:5) * 4
		ENDIF
		;獲得名声の算出
		LOCAL:2 = (RAND:5 + 의뢰플래그:2:5) * 4
		CALL MESSAGE_WINDOW, "" , @"＞%조사처리(CALLNAME:MASTER,"는")% 보수로서 {LOCAL:1}00마카와 {LOCAL:2}의 명성을 얻었다。", , , , , 4
		MONEY:1 += 100*LOCAL:1
		FLAG:명성 += LOCAL:2
	;日本円
	ELSE
		SELECTCASE 의뢰플래그:2:1
			CASE 30;物資破壊に応じて報酬増加
				LOCAL:1 = (RAND:5 + 의뢰플래그:2:5 + 던전플래그:2:20 )*4* 100 / (의뢰플래그:2:11 * 2 + 10)
			CASEELSE
				;メシア教、ガイア教の依頼は戦力値によって報酬変動
				IF 의뢰플래그:2:7 == 1
					LOCAL:1 = (RAND:5 + 의뢰플래그:2:5) * 4 * 100 / (의뢰플래그:2:11 * 2 + 10)
				ELSEIF 의뢰플래그:2:7 == 2
					LOCAL:1 = (RAND:5 + 의뢰플래그:2:5) * 4 * 100 / (의뢰플래그:2:12 * 2 + 10)
				ELSE
					LOCAL:1 = (RAND:5 + 의뢰플래그:2:5) * 4
				ENDIF
		ENDSELECT
		;獲得名声の算出
		LOCAL:2 = (RAND:5 + 의뢰플래그:2:5) * 4
		CALL MESSAGE_WINDOW, "" , @"＞%조사처리(CALLNAME:MASTER,"는")% 보수로서 {LOCAL:1},000엔과 {LOCAL:2}의 명성을 얻었다。", , , , , 4
		MONEY:0 += 1000*LOCAL:1
		FLAG:명성 += LOCAL:2
	ENDIF
;ダンジョンの無い依頼
ELSE
	;報酬がマッカ
	IF 의뢰플래그:2:6 == 1
		;メシア教、ガイア教の依頼は戦力値によって報酬変動
		IF 의뢰플래그:2:7 == 1
			LOCAL:1 = (RAND:5 + 의뢰플래그:2:5) * 100 / (의뢰플래그:2:11 * 2 + 10)
		ELSEIF 의뢰플래그:2:7 == 2
			LOCAL:1 = (RAND:5 + 의뢰플래그:2:5) * 100 / (의뢰플래그:2:12 * 2 + 10)
		ELSE
			LOCAL:1 = RAND:5 + 의뢰플래그:2:5
		ENDIF
		;獲得名声の算出
		LOCAL:2 = RAND:5 + 의뢰플래그:2:5
		PRINTFORML ＞%조사처리(CALLNAME:MASTER,"는")% 보수로서 {LOCAL:1}00마카와 {LOCAL:2}의 명성을 얻었다。
		CUSTOMDRAWLINE =
		WAIT
		MONEY:1 += 100*LOCAL:1
		FLAG:명성 += LOCAL:2
	;日本円
	ELSE
		SELECTCASE 의뢰플래그:2:1
			CASE 2, 17, 43;輸送襲撃系は報酬の幅を大きく
				;メシア教、ガイア教の依頼は戦力値によって報酬変動
				IF 의뢰플래그:2:7 == 1
					LOCAL:1 = (RAND:10 + 의뢰플래그:2:5) * 100 / (의뢰플래그:2:11 * 2 + 10)
				ELSEIF 의뢰플래그:2:7 == 2
					LOCAL:1 = (RAND:10 + 의뢰플래그:2:5) * 100 / (의뢰플래그:2:12 * 2 + 10)
				ELSE
					LOCAL:1 = RAND:10 + 의뢰플래그:2:5
				ENDIF 
			CASEELSE
				;メシア教、ガイア教の依頼は戦力値によって報酬変動
				IF 의뢰플래그:2:7 == 1
					LOCAL:1 = (RAND:5 + 의뢰플래그:2:5) * 100 / (의뢰플래그:2:11 * 2 + 10)
				ELSEIF 의뢰플래그:2:7 == 2
					LOCAL:1 = (RAND:5 + 의뢰플래그:2:5) * 100 / (의뢰플래그:2:12 * 2 + 10)
				ELSE
					LOCAL:1 = RAND:5 + 의뢰플래그:2:5
				ENDIF
		ENDSELECT
		;獲得名声の算出
		LOCAL:2 = RAND:5 + 의뢰플래그:2:5
		PRINTFORML ＞%조사처리(CALLNAME:MASTER,"는")% 보수로서 {LOCAL:1},000엔과 {LOCAL:2}의 명성을 얻었다。
		CUSTOMDRAWLINE =
		WAIT
		MONEY:0 += 1000*LOCAL:1
		FLAG:명성 += LOCAL:2
	ENDIF
ENDIF

;アライメントの変化
IF 의뢰플래그:2:7 == 1;メシア教からの依頼はアライメントがLaw側に変化
	CALL INCREASE_LC, 10
ELSEIF 의뢰플래그:2:7 == 2;ガイア教からの依頼はアライメントがChaos側に変化
	CALL INCREASE_LC, -10
ENDIF

;戦力値の変化
IF 의뢰플래그:2:7 == 1;メシア教からの依頼
	의뢰플래그:2:11 += (RAND:3 + 1)
	IF 의뢰플래그:2:12 <= 3
		의뢰플래그:2:12 = 0
	ELSE
		의뢰플래그:2:12 -= (RAND:3 + 1)
	ENDIF
ELSEIF 의뢰플래그:2:7 == 2;ガイア教からの依頼
	IF 의뢰플래그:2:11 <= 3
		의뢰플래그:2:11 = 0
	ELSE
		의뢰플래그:2:11 -= (RAND:3 + 1)
	ENDIF
	의뢰플래그:2:12 += (RAND:3 + 1)
ELSE
ENDIF


;===================================================================================
;排日の戦力値辺土イベント
;의뢰플래그:2:11	メシア教の戦力値
;의뢰플래그:2:12	ガイア教の戦力値
;===================================================================================
@DAIRY_EVENT_REQUEST_2
CUSTOMDRAWLINE -
IF RAND:2 == 0
	PRINTFORMW 메시아교 활동이 활발해지고 있는 것 같습니다。
	의뢰플래그:2:11 += 10 + RAND:10
ENDIF
IF RAND:2 == 0 && 의뢰플래그:2:11 <= 20
	PRINTFORMW 새로운 로우 히어로가 나타나、활약하고 있는것 같습니다。
	의뢰플래그:2:11 += 50 + RAND:10
ENDIF
IF RAND:2 == 0
	PRINTFORMW 가이아교의 활동이 활발해지고 있는 것 같습니다。
	의뢰플래그:2:12 += 10 + RAND:10
ENDIF
IF RAND:2 == 0 && 의뢰플래그:2:12 <= 20
	PRINTFORMW 새로운 카오스 영웅이 나타나、활약하고 있는것 같습니다。
	의뢰플래그:2:12 += 50 + RAND:10
ENDIF
IF RAND:2 == 0 && (의뢰플래그:2:11 >= 80 && 의뢰플래그:2:12 >= 80)
	PRINTFORMW 야타가라스가 적극적으로 활동하고 있는 것 같습니다。
	의뢰플래그:2:11 -= 30 +RAND:10
	의뢰플래그:2:12 -= 30 +RAND:10
ENDIF
IF RAND:2 == 0 && (의뢰플래그:2:11 >= 40 && 의뢰플래그:2:12 >= 40)
	PRINTFORMW 메시아교와 가이아교의 충돌이 발생했습니다！
	SELECTCASE RAND:3
		CASE 0
			PRINTFORMW 메시아교가 우세한 것 같습니다。
			의뢰플래그:2:11 -= 10 +RAND:10
			의뢰플래그:2:12 -= 10 +RAND:20
		CASE 1
			PRINTFORMW 가이아교가 우세한 것 같습니다。
			의뢰플래그:2:11 -= 10 +RAND:20
			의뢰플래그:2:12 -= 10 +RAND:10
		CASE 2
			PRINTFORMW 양측이 팽팽한 것 같습니다。
			의뢰플래그:2:11 -= 15 +RAND:10
			의뢰플래그:2:12 -= 15 +RAND:10
	ENDSELECT
ENDIF