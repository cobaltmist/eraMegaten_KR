;コロシアム
;とりあえず今の所전투のテスト用
;クリアフラグに던전フラグ:0:Xを사용


@SHOPCOMABLE_104
#DIM LCOUNT
;パーティに一人이상いるなら
RESULT:1 = 2
RESULTS = 의뢰청부
LOCAL = 0
FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:PTフラグ == 2
		LOCAL += 1
NEXT
IF LOCAL > 0
	RETURN 1
ELSE
	RETURN 0
ENDIF


@SHOW_REQUEST
CUSTOMDRAWLINE =

FOR LOCAL, 0, 999
	TFLAG:LOCAL = 0
	RESULTS = 
	TRYCCALLFORM PLAYABLE_REQUEST_{LOCAL}
		IF RESULT
			TFLAG:LOCAL = 1
			IF RESULTS != ""
				PRINTFORML [{LOCAL}] - %RESULTS%
			ENDIF
		ENDIF
	CATCH
		
	ENDCATCH
NEXT

@SHOP_COM_104
CALL SHOW_REQUEST
PRINTL [1000] 돌아간다

DRAWLINE
PRINTL 어느 의뢰를 받습니까？

$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= VARSIZE("TFLAG")
	GOTO INPUT_LOOP
ELSEIF TFLAG:RESULT || FLAG:DEBUG
	LOCAL = RESULT
	DRAWLINE
	TRYCALLFORM EXPLAIN_REQUEST_{RESULT}
	DRAWLINE
	SELECTCASE RESULT
		;통상
		CASE 0
			PRINTL 이 의뢰를 받습니까？
			CALL INPUT_YN,"Yes","No"

			IF RESULT == 0
				FLAG:進行中의뢰 = LOCAL
				TRYCALLFORM REQUEST_{LOCAL}
				FLAG:進行中의뢰 = 0
				FOR LOCAL,0,CHARANUM
					SIF CFLAG:LOCAL:악마변신 == 1
						CALL ACTION_2310 , LOCAL , 0
				NEXT
				IF RESULT == 1
					VARSET TFLAG,-1
					BEGIN TURNEND
				ENDIF
			ELSEIF RESULT == 1
				RESTART
			ENDIF
		;설명だけを見る패턴
		CASE 1
			WAIT
			RESTART
	ENDSELECT
ENDIF

VARSET TFLAG,-1
RETURN 0


