;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:REQUEST_108_ランダム依頼.ERB
;	Facility	:
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;	002		2021/12/31		軽木					REQUEST_108_軽子坂高校体育館1.ERBから処理を分離、連続で受けられないように変更
;	003		2022/05/08		ロッド＠カヤノソト		ランダム依頼で使用するダンジョンを「DUNGEON_2_軽子坂高校」→「DUNGEON108_ランダム依頼」に変更　※既存のダンジョンに相乗りするとソースがごちゃごちゃになってしまうことを懸念
;	004		2022/05/14		ロッド＠カヤノソト		イベントの発生条件に『EXTRAフィルターがOFFの場合は発生しない』を追加。　※当初はヤマタイカ(EXイベントの一種)フラグONが必須であったが、上記「003」の変更で撤廃。その後、最初の作者さんがEXイベントにしていた意向を重視してEXTRAフィルターを付けるべきと考え直しました。
;	005		2022/05/		やぶへび				依頼の削除。整備。
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;
;ランダム依頼modで使用するフラグ
;의뢰플래그:108:1		依頼の番号。主にダンジョンの方で見る。

;의뢰플래그:108:2		依頼の受諾判定に使用
;의뢰플래그:108:3		ダンジョン依頼の場所判定に使用
;						（1、ビル　２、下水道　３.工場　４.森　５.山　６.ランダムダンジョン）
;의뢰플래그:108:4		依頼中の状況判定に使用
;의뢰플래그:108:5		依頼の難易度判定に使用
;의뢰플래그:108:7		依頼主（1:メシア教、2:ガイア教、0:その他）の判定に使用

;의뢰플래그:108:8		前回依頼を受けた日
;의뢰플래그:108:9		前回依頼を受けた時間帯

;의뢰플래그:108:11		メシア教の戦力値
;의뢰플래그:108:12		ガイア教の戦力値
;의뢰플래그:108:13		依頼の達成状況によるボーナス値
;의뢰플래그:108:14		固定報酬フラグ（0:変動報酬、1：固定報酬）の判定に使用　※固定報酬の場合、「의뢰플래그:108:13」の依頼達成状況によるボーナス値のみで報酬を算出する
;
;의뢰플래그:108:19		依頼60の非表示化


;依頼難易度はボスの最大レベルによって変動
; 1～10　☆
;11～20　☆☆
;21～30　☆☆☆
;31～40　☆☆☆☆
;41～50　☆☆☆☆☆
;51～60　☆☆☆☆☆☆
;61～70　☆☆☆☆☆☆☆
;71～80　☆☆☆☆☆☆☆☆
;81～90　☆☆☆☆☆☆☆☆☆
;91～100 ☆☆☆☆☆☆☆☆☆☆

;ダンジョンの場合は☆は★になる。
;Qなんでボスなの？　A道中の難易度で印象変えやすくするため。


;====================
;条件
;====================
@PLAYABLE_REQUEST_108
;ランダム依頼を受注できる条件、および受注できる場合はどの種類の依頼が表示されるかを設定する

;ランダム依頼開始イベントをやっていないと出現しない。
IF 이벤트플래그:108:0 == 0
	RETURN 0
;EXTRAイベントがOFFの場合は発生しない　※以前はヤマタイカフィルターでしたが、EXTRA全般に変更
ELSEIF GETBIT(FLAG:EXTRA이벤트설정, 0) == 0
	RETURN 0

;既にランダム依頼を実行中の場合、受注不可
ELSEIF DAY == 의뢰플래그:108:8 && TIME == 의뢰플래그:108:9
	의뢰플래그:108:2 = 1
	RETURN 0

;この日のこの時間に既に依頼を受けていれば表示しない
ELSEIF DAY == 의뢰플래그:108:8 && TIME == 의뢰플래그:108:9
	RETURN 0

;その他の場合
ELSE
	;最初に、フラグなどをリセットする　※個別依頼でのフラグリセットし忘れによる報酬計算ミスの対応
	CALL REQUEST_108_FLAG_CLEAR

	;ランダムな依頼を表示する
	SELECTCASE RAND(0,33)
		CASE 0
			RESULTS = 얼음천지 공장（난이도 ★★）
			의뢰플래그:108:1 = 0
		CASE 1
			RESULTS = 교회 시설을 방위（난이도 ☆☆☆）
			의뢰플래그:108:1 = 1
		CASE 2
			RESULTS = 수송 부대를 습격（난이도 ☆☆☆☆）
			의뢰플래그:108:1 = 2
		CASE 3
			RESULTS = 하수도의 괴음（난이도 ★）
			의뢰플래그:108:1 = 3
		CASE 4
			RESULTS = 정의의 서머너의 요격（난이도 ☆☆）
			의뢰플래그:108:1 = 4
		CASE 5
			RESULTS = 무장집단 제거（난이도 ★★★）
			의뢰플래그:108:1 = 5
		CASE 6
			RESULTS = 기밀정보 탈환（난이도 ★★★）
			의뢰플래그:108:1 = 6
		CASE 7
			RESULTS = 강탈부대 추격（난이도 ★★★★）
			의뢰플래그:108:1 = 7
		CASE 8
			RESULTS = 파괴공작 저지（난이도 ☆☆☆☆☆）
			의뢰플래그:108:1 = 8
		CASE 9
			RESULTS = 방어시스템 파괴（난이도 ★★★★★）
			의뢰플래그:108:1 = 9
		CASE 10
			RESULTS = 처리장 점거자 배제（난이도 ☆☆☆）
			의뢰플래그:108:1 = 10
		CASE 11
			RESULTS = 서머너 평가 시험（난이도 ☆☆☆☆☆）
			의뢰플래그:108:1 = 11
		CASE 12
			RESULTS = 거점 빌딩 습격（난이도 ★★★★）
			의뢰플래그:108:1 = 12
		CASE 13
			RESULTS = 물자 방위（난이도 ☆☆☆☆☆）
			의뢰플래그:108:1 = 13
		CASE 14
			RESULTS = 강습 부대 요격（난이도 ☆☆☆）
			의뢰플래그:108:1 = 14
		CASE 15
			RESULTS = 고블린 배제（난이도 ★★）
			의뢰플래그:108:1 = 15
		CASE 16
			RESULTS = 요정 포획작전（난이도 ☆☆）
			의뢰플래그:108:1 = 16
		CASE 17
			RESULTS = 메시아교 습격（난이도 ☆☆☆）
			의뢰플래그:108:1 = 17
		CASE 18
			RESULTS = 가이가교 습격（난이도 ☆☆☆）
			의뢰플래그:108:1 = 18
		CASE 19
			RESULTS = 도둑을 쫓아버려줘（난이도 ☆☆）
			의뢰플래그:108:1 = 19
		CASE 20
			RESULTS = 대 서머너 연습（난이도 ☆☆☆☆）
			의뢰플래그:108:1 = 20
		CASE 21
			RESULTS = 원인 모를 화재 조사（난이도 ☆☆☆）
			의뢰플래그:108:1 = 21
		CASE 22
			RESULTS = 경호원 의뢰（난이도 ☆☆☆）
			의뢰플래그:108:1 = 22
		CASE 23
			RESULTS = 산을 타는 수상한 그림자（난이도 ★★★★）
			의뢰플래그:108:1 = 23
		CASE 24
			RESULTS = 폐공장 침입（난이도 ★★★）
			의뢰플래그:108:1 = 24
		CASE 25
			RESULTS = 연구 시설 해방（난이도 ☆☆☆）
			의뢰플래그:108:1 = 25
		CASE 26
			RESULTS = 이계화된 빌딩의 해방（난이도 ★★★★）
			의뢰플래그:108:1 = 26
		CASE 27
			RESULTS = 아이들의 수색（난이도 ☆☆☆☆☆）
			의뢰플래그:108:1 = 27
		CASE 28
			RESULTS = 변이된 뱀장어의 처리（난이도 ★★）
			의뢰플래그:108:1 = 28
		CASE 29
			RESULTS = 수상한 그림자 퇴치（난이도 ☆☆☆☆☆）
			의뢰플래그:108:1 = 29
		CASE 30
			RESULTS = 드라이어드를 솎아 주게（난이도 ★★）
			의뢰플래그:108:1 = 30
		CASE 31
			RESULTS = 억지 권유의 저지（난이도 ☆☆☆☆）
			의뢰플래그:108:1 = 31
		CASE 32
			RESULTS = 지하 동굴 수사（난이도 ★★★）
			의뢰플래그:108:1 = 32
		CASE 33
			RESULTS = 침공 세력 조사（난이도 ☆☆）
			의뢰플래그:108:1 = 33
	ENDSELECT
	RETURN 1
ENDIF
RETURN 0

;====================
;説明
;====================
@EXPLAIN_REQUEST_108
;-------------------
;変数定義
;-------------------
#DIM REWARD_PERCENTAGE_L		;メシア教側の報酬変動率(%)
#DIM REWARD_PERCENTAGE_C		;ガイア教側の報酬変動率(%)

;-------------------
;変数の初期化
;-------------------
REWARD_PERCENTAGE_L = 100		;メシア教側の報酬変動率(%)
REWARD_PERCENTAGE_C = 100		;ガイア教側の報酬変動率(%)

;-------------------
;ここから本処理
;-------------------
;メシア・ガイアの報酬変動率の算出　※劣勢なほど報酬は良くなる
REWARD_PERCENTAGE_L = (100 * MAX(의뢰플래그:108:12, 1) + 20) / (MAX(의뢰플래그:108:11, 1) + 20)
REWARD_PERCENTAGE_C = (100 * MAX(의뢰플래그:108:11, 1) + 20) / (MAX(의뢰플래그:108:12, 1) + 20)

;倍率は50%～200%とする
REWARD_PERCENTAGE_L = MAX(REWARD_PERCENTAGE_L, 50)		;倍率の最小値を50%に設定
REWARD_PERCENTAGE_L = MIN(REWARD_PERCENTAGE_L, 200)		;倍率の最大値を200%に設定
REWARD_PERCENTAGE_C = MAX(REWARD_PERCENTAGE_C, 50)		;倍率の最小値を50%に設定
REWARD_PERCENTAGE_C = MIN(REWARD_PERCENTAGE_C, 200)		;倍率の最大値を200%に設定

;メシア・ガイアの勢力状況および報酬への影響を表示する
PRINTFORML メシア教　報酬：{REWARD_PERCENTAGE_L}\%
IF REWARD_PERCENTAGE_L == 50 && REWARD_PERCENTAGE_C == 50
	PRINTL (拮抗しているようです。報酬に期待は持てません。）
ELSEIF REWARD_PERCENTAGE_L >= 120
	PRINTL (劣勢のようです。いつもより高額の報酬が期待できます。）
ELSEIF REWARD_PERCENTAGE_L <= 80
	PRINTL (優勢のようです。報酬に期待は持てません。）
ELSE
	PRINTL 
ENDIF
PRINTFORML ガイア教　報酬：{REWARD_PERCENTAGE_C}\%
IF REWARD_PERCENTAGE_C == 50 && REWARD_PERCENTAGE_L == 50
	PRINTL (拮抗しているようです。報酬に期待は持てません。）
ELSEIF REWARD_PERCENTAGE_C >= 120
	PRINTL (劣勢のようです。いつもより高額の報酬が期待できます。）
ELSEIF REWARD_PERCENTAGE_C <= 80
	PRINTL (優勢のようです。報酬に期待は持てません。）
ELSE
	PRINTL 
ENDIF

;個別の依頼の説明を表示する(「氷漬けの工場」「教会施設を防衛」など各依頼の説明はここから)
CALLFORM REQUEST_EXPLAIN_108_{의뢰플래그:108:1}

;====================
;内容
;====================
@REQUEST_108
;ランダム依頼を受けるのが初回である場合、メシア・ガイアの勢力を互角にする(両方50ずつ)
IF 의뢰플래그:108:8 == 0 && 의뢰플래그:108:9 == 0
	의뢰플래그:108:11 = 50
	의뢰플래그:108:12 = 50
ENDIF

;個別の依頼を開始する(「氷漬けの工場」「教会施設を防衛」など各依頼の処理はここから)
CALLFORM REQUEST_108_{의뢰플래그:108:1}

;ランダム依頼実行時の日時を記録
의뢰플래그:108:8 = DAY
의뢰플래그:108:9 = TIME

;====================
;その他
;====================
;各種フラグの初期化を行う。(個別の依頼における初期化忘れによって報酬計算が変になるケースがあるため、共通処理でリセットをかける)
@REQUEST_108_FLAG_CLEAR
;ダンジョンの有無(デフォルトは「0：なし」とする。)
의뢰플래그:108:2 = 0

;依頼の難易度(デフォルトは「1」とする)
의뢰플래그:108:5 = 1			

;依頼主(デフォルトは「0：その他」とする)
의뢰플래그:108:7 = 0			

;依頼の達成状況によるボーナス値(デフォルトは「0」とする)
의뢰플래그:108:13 = 0			

;固定報酬フラグ（デフォルトは「0:変動報酬」とする）
의뢰플래그:108:14 = 0			

;ダンジョン系依頼の進行状況フラグ。(デフォルトは0とする)
던전플래그:108:20 = 0		
