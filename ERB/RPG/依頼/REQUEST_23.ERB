
;====================
;조건
;====================
@PLAYABLE_REQUEST_23
;10F突破が조건、幾月がいない(最終指令を受け取る)と発生しない
IF 던전플래그:[[던전:타르타로스]]:1 >= 2 && 이벤트플래그:9:0 < 4 || FLAG:DEBUG == 1
	RESULTS = 연구자료 수집
	RETURN 1
ENDIF
RETURN 0

;====================
;설명
;====================
@EXPLAIN_REQUEST_23
IF !STRFLAG_REQ("초회", 0, 23)
	PRINTL 
	PRINTFORML 이쿠츠키로부터 연락이 와있다
	PRINTFORML %CALLNAME:MASTER%에게 부탁하고 싶은 것이 있는 것 같다
	PRINTL 
ELSE
	PRINTL 
	PRINTFORML 이쿠츠키로부터의 의뢰
	PRINTFORML 쉐도우의 연구에 도움이 될 것 같은 것을 가져다주자
	PRINTL 
ENDIF
;====================
;内容
;====================
@REQUEST_23
#DIM LSELECTABLS_CHARA_COUNT
IF !STRFLAG_REQ("초회", 1, 23)
	PRINTL ＞이쿠츠키에게 만나자는 뜻을 전하자, 즉시 왔다.
	PRINTL ＞변함없이 이상한 모습을 하고 있다…
	PRINTL 
	PRINTL 이쿠츠키:
	PRINTW 「야아, 타르타로스의 공략은 순조로운 것 같군」
	PRINTW 「덕분에 나도 편해지고 있어」
	PRINTW 「정말 너를 소개준 마요네씨 さまさま 네요」 (※역자주 : さまさま [様様] 자기에게 고마운 사람이나 물건의 이름에 붙이는 말)
	PRINTW 「아, 물론 너에게도 감사하고 있어」
	PRINTW 「아니 정말, 우수한 서머너님인 거다」
	PRINTW 「그래서, 아무튼, 오늘의 용건이지만…」
	PRINTW 「조금, 쉐도우 연구가 안개 속을 헤매고 있는 것처럼 되서」
	PRINTW 「그 때문에 재료를 모으는 심부름을 해줄 수 없을까」
	PRINTW 「쉐도우 녀석들은, 대체 어디에 쓰는 건가 싶은 이상한 쓰레기를 가지고 있을 때가 있어서 말이지」
	PRINTW 「그것을 가져와주면 좋겠는데」
	PRINTW 「그렇게 하면, 거기에 응해 포인트를 주기 때문에」
	PRINTW 「그 포인트에 맞춰 경품과 교환을 할까, 하는데」
	PRINTW 「경품은 전에도 조금 이야기했었지만, 노예야」
	PRINTW 「탐색보다 우선시 하지 않아도 좋으니, 그런 것을 찾아내면 부디 부탁하지」
	PRINTL 
	PRINTW ＞이쿠츠키의 의뢰를 받았다
	PRINTW ＞무언가 그럴 듯한 것을 손에 넣으면 이쿠츠키에게 건네주자
ELSE
	IF STRFLAG_NUM_REQ("보고") > 0 && STRFLAG_NUM_REQ("보고") + 3 <= DAY && !STRFLAG_REQ("P4", 1)
		PRINTL 이쿠츠키:
		PRINTW 「예의 패러다임(paradigm) X의 그림자이지만」
		PRINTW 「상층부가 꽤 이 연구에 자금을 내 주어서」
		PRINTW 「나의 연구 쪽도, 비교적 자유롭게 할 수 있게 되었어」
		PRINTW 「그래서, 그것도 이것도 너의 덕분이고, 나 나름대로 너에게 환원할까 생각해(かんげん) 본 것이지만」 (※역자주 : 환원(かんげん)과 생각하다(かんがえる)의 발음이 비슷한것을 이용한 말장난. 이쿠츠키 개새끼 해봐)
		PRINTW 「인간이 사라진다고 하는 이나바시쪽을 조사하는 김에, 사람들을 납치해봤어.」
		PRINTW 「그러니까 이제부터 이나바시의 인간도 노예로서 포인트로 교환할수 있어.」
		PRINTW 「상품의 종류는 많아서 나쁠 게 없겠지?」
		PRINTW 「응응, 충실한 교환 라인업으로, 너의 나에 대한 호감도도 올라가는 것이다」
		PRINTW 「좋아, 그러면, 오늘은 무슨 일인가?」
	;ちょっと메티스이벤트はさみますね(타르타로스内に登場させたのでオミット)
;	ELSEIF STRFLAG_NUM_REQ("누계점수") > 200 && 던전플래그:70:21 == 0 && 던전플래그:70:(1) >= 4
;		PRINTL 이쿠츠키:
;		PRINTFORMW 「안녕 안녕 %CALLNAME:MASTER%군」
;		PRINTW 「그런데 이야기가 있는데 말이야, 자네는 로봇 같은 것이라도 조교할 수 있어？」
;		PRINTW 「아니, 조금 연구소에 어처구니 없는 것이 있어서……」
;		PRINTW 「조사해보려 해도 꽤 저항이 심해서, 솔직히 성가시다고」
;		PRINTW 「그러니 갖고 싶다면 포인트로 교환할 수 있으니까, 말해줘」
;		던전플래그:70:21 = 1
	ELSE
		IF STRFLAG_NUM_REQ("누계점수") > 1000 && !ITEM:뮤테이션기어
				PRINTL 이쿠츠키:
				PRINTW 「야아、좀 이것을 받아 주게나」
				PRINTL 
				PRINTW ＞이쿠츠키는 그렇게 말하면서 디스크를 건넸다.
				PRINTL 
				PRINTL 이쿠츠키:
				PRINTW 「이것은 뮤테이션기어、페르소나의 변이를 촉진하는 인스톨 소프트다」
				PRINTW 「쉐도의 연구의 부산물 태어난 기념품이야」
				PRINTW 「응、포인트？　아아、그거라면 됐어」
				PRINTL 「약간 ＣＯＭＰ용으로 재작업한 것 뿐이고
				PRINTW 　순수한 선물로 받아 줘」
				PRINTL 
				PRINTW ＞인스톨 소프트【뮤테이션기어】를 손에 넣었다
				ITEM:뮤테이션기어 = 1
		ELSEIF STRFLAG_NUM_REQ("누계점수") > 750 && STRFLAG_REQ("마이너스카드판매") && !STRFLAG_REQ("EXP카드판매", 1)
			PRINTL 이쿠츠키:
			PRINTW 「적당히、친숙한 일이라고 생각하지만、이것을 받아 주게」
			PRINTL 
			PRINTW ＞이쿠츠키에게 카드를 건네받았다.
			PRINTL 
			PRINTL 이쿠츠키:
			PRINTW 「이번 카드는 쉐도의 힘을 페르소나에 옮길 수 있어」
			PRINTW 「즉、지금까지의 카드와 달리、순수하게 페르소나를 강화할 수 있다」
			PRINTW 「이 강화가 가능해진 것으로、오늘부터、전혀 다른 페르소나 라이프가 시작하지 않을까？」
			PRINTW 「뭐어…만들기가 귀찮으니까、그에 상응하는 포인트가 없으면 건네주기 힘들지만…」
			PRINTL 
			PRINTW >명화의카드를 손에 넣었다
			PRINTL 
			ITEM:명화의카드 += 1
		ELSEIF STRFLAG_NUM_REQ("누계점수") > 500 && STRFLAG_REQ("인센스카드판매") && !STRFLAG_REQ("마이너스카드판매", 1)
			PRINTL 이쿠츠키:
			PRINTW 「본론에 들어가기 전에 이것을 받아 주게」
			PRINTL 
			PRINTW ＞이쿠츠키에게 카드를 건네받았다.
			PRINTL 
			PRINTL 이쿠츠키:
			PRINTW 「그것도 전에 건네준 카드와 같이、페르소나를 변질시킬 수 있다」
			PRINTW 「연구의 결과、전과는 다른 힘을 쉐도로부터 꺼낼 수 있었다」
			PRINTW 「대충、주의점은 전의 카드와 같아」
			PRINTW 「지금부터는 이 카드도 교환해서、사용해 줘」
			PRINTL 
			PRINTW >ALL마이너스카드를 손에 넣었다
			PRINTL 
			ITEM:ALL마이너스카드 += 1
		;벨벳룸にいったことがあるのも조건
		ELSEIF STRFLAG_NUM_REQ("누계점수") > 250 && 이벤트플래그:5:0 && !STRFLAG_REQ("인센스카드판매", 1)
			PRINTL 이쿠츠키:
			PRINTW 「아, 왔군. 우선 이것을 받아 주게」
			PRINTL 
			PRINTW ＞이쿠츠키가 카드를 건넸다
			PRINTL 
			PRINTL 이쿠츠키:
			PRINTW 「네 덕분에 쉐도의 연구가 많이 진행되어서」
			PRINTW 「덕분에 그런 것을 얻었다」
			PRINTL 「그것은 쉐도의 힘을 추출한 것으로서、페르소나에 사용하는 것으로
			PRINTW 　그 페르소나를 변질시킬 수 있어」
			PRINTW 「뭐、알맞은 장치가 없으면、그래…조금도 사용 할 수 없지만」
			PRINTW 「이것은 억측이지만、자네는 아무래도 페르소나개조의 커넥션을 가지고 있는 것 같고」
			PRINTW 「아아、아니 자세하게 말할 필요는 없어」
			PRINTW 「나는 쉐도의 연구와、타르타로스의 파괴에 손을 빌릴 수만 있으면 그것을 충분하다」
			PRINTW 「자네가 다른 곳에서 무엇을 하든 신경쓰지 않네」
			PRINTW 「그것을 건네준 것도, 자네에게 건네주면 나의 일이 도움이 된다고 생각했기 때문이다」
			PRINTW 「필요없으면 그것으로 좋고, 더 필요하면 말해 줘」
			PRINTW 「다만, 그것을 만드는 것은 조금 귀찮아」
			PRINTW 「다음부터는 노예처럼, 포인트와 교환이다」
			PRINTL 
			PRINTW >STR인센스카드를 손에 넣었다
			PRINTL 
			ITEM:STR인센스카드 += 1
		ENDIF
		$START
		PRINTL 이쿠츠키:
		PRINTW 「그럼、무슨 용무지？」
	ENDIF
	PRINTL 
	PRINTL [0] 쉐도로부터의 전리품을 넘겨준다
	PRINTL [1] 노예 구입
	SIF STRFLAG_REQ("인센스카드판매")
		PRINTL [2] 카드 구입
	SIF 던전플래그:[[던전:유키코공주의성]]:8 && STRFLAG_NUM_REQ("보고",,,,23) == 0
		PRINTL [3] 유키코 공주에 대해 말한다
;	SIF 던전플래그:70:21 > 0
;		PRINTL [4] 로봇을 산다
	PRINTL [9] 그만둔다
	CALL INPUTINT(0, 1, STRFLAG_REQ("인센스카드판매")*2, !STRFLAG_NUM_REQ("보고",,,,23)*3,!STRFLAG_NUM_REQ("메티스",,,,23)*4, 9)
	IF RESULT == 0
		PRINTL [0]메달 외의 물건을 넘긴다
		PRINTL [1]전부 넘겨준다
		INPUT
		IF RESULT == 0
			LOCAL:2 = 305
		ELSE
			LOCAL:2 = 300
		ENDIF
		IF SUMARRAY(ITEM, LOCAL:2, 322)
			PRINTL 이쿠츠키:
			PRINTW 「그거라면、조금 보여주게나」
			PRINTW 「보는것은、안된다…는 일은 없겠지？」
			PRINTW 「그러면、으ー음……」
			LOCAL:1 = 0
			;ちょっと메시지の変化用
			LOCAL:99 = 0
			FOR LOCAL, LOCAL:2 , 322
				IF ITEM:LOCAL > 0
					IF ITEMPRICE:LOCAL >= 50 && STRFLAG_REQ(ITEMNAME:LOCAL, 1)
						IF LOCAL:99 == 0
							PRINTFORML 「%ITEMNAME:LOCAL%인가…. 이것은 전에도 받았던 거니까 포인트가 줄어들게 될거야」
							LOCAL:99 = 1
						ELSE
							PRINTFORML 「%ITEMNAME:LOCAL%(이)군….이것은 전에도 받았던 거니까 포인트가 줄어들게 될거야」
						ENDIF
						PRINTFORMW 「%ITEMNAME:LOCAL%(이)가 {ITEM:LOCAL}개、{20*ITEM:LOCAL}점…」
						LOCAL:1 += 20*ITEM:LOCAL
					ELSE
						IF ITEMPRICE:LOCAL >= 50 && ITEM:LOCAL > 1
							PRINTFORML 「%ITEMNAME:LOCAL%(이)군…잔뜩 있지만、
							PRINTFORML 　첫번째 것만 {ITEMPRICE:LOCAL}점으로、나머지는 20점으로 해 주겠네」
							PRINTFORMW 　그런 것으로、{ITEMPRICE:LOCAL+20*(ITEM:LOCAL-1)}점…」
							LOCAL:1 += ITEMPRICE:LOCAL+20*(ITEM:LOCAL-1)
						ELSE
							LOCAL:1 += ITEMPRICE:LOCAL*ITEM:LOCAL
							PRINTFORMW 「%ITEMNAME:LOCAL%(이)가 {ITEM:LOCAL}개、{ITEMPRICE:LOCAL*ITEM:LOCAL}점…」
						ENDIF
					ENDIF
					ITEM:LOCAL = 0
				ENDIF
			NEXT
			PRINTFORMW 「전부 합쳐서、{LOCAL:1}점이네」
			의뢰플래그:(FLAG:진행중의뢰):0 += LOCAL:1
			CALLF STRFLAG_NUM_REQ("누계점수", "+=", , LOCAL:1)
			PRINTFORMW 「이걸로, 자네의 포인트는{의뢰플래그:(FLAG:진행중의뢰):0}점이다」
			PRINTFORMW 「앞으로도 아무쪼록 부탁하네」
		ELSE
			PRINTFORMW ＞이쿠츠키에게 넘겨줄 것이 없다……
		ENDIF
		GOTO START
	ELSEIF RESULT == 1
		PRINTL 이쿠츠키:
		PRINTFORMW 「지금 자네의 포인트는{의뢰플래그:(FLAG:진행중의뢰):0}점이다」
		IF 의뢰플래그:(FLAG:진행중의뢰):0 < 100
			PRINTW 「100점마다、노예 한명이지만…부족하군」
			PRINTW 「너무 조급하게 굴진 말라고、신세를 지고 있지만」
			PRINTW 「적어도 인간이니까. 공짜로 건네줄만한 물건은 아닌거야」
		ELSE
			$CHOICE
			PRINTL 「그러면, 어느 아이로 할까 선택해 줘」
			VARSET LOCAL
			LSELECTABLS_CHARA_COUNT = 1
			FOR LOCAL, 4575, 4597
				SELECTCASE LOCAL
					CASE 4576, 4577 , 4578, 4579, 4595
						IF GETCHARA(LOCAL) == -1 && !STRFLAG_REQ(CSVCALLNAME(LOCAL,0)) && STRFLAG_REQ("P4")
							LOCAL:LSELECTABLS_CHARA_COUNT = LOCAL
							PRINTFORM [{LSELECTABLS_CHARA_COUNT,2}] %CSVNAME(LOCAL,0),16, LEFT%
							LSELECTABLS_CHARA_COUNT++
							SIF LSELECTABLS_CHARA_COUNT % 2
								PRINTL
						ENDIF
					CASE 4580, 4581, 4582, 4583, 4589, 4590, 4591, 4592, 4593, 4594
						CONTINUE
					CASEELSE
						IF GETCHARA(LOCAL) == -1 && !STRFLAG_REQ(CSVCALLNAME(LOCAL,0))
							LOCAL:LSELECTABLS_CHARA_COUNT = LOCAL
							PRINTFORM [{LSELECTABLS_CHARA_COUNT,2}] %CSVNAME(LOCAL,0),16, LEFT%
							LSELECTABLS_CHARA_COUNT++
							SIF LSELECTABLS_CHARA_COUNT % 2
								PRINTL
						ENDIF
				ENDSELECT
			NEXT
			PRINTL [99] 돌아간다
			$INPUT_LOOP
			INPUT
			IF RESULT == 99
				PRINTL 이쿠츠키:
				PRINTW 「그만두는 건가？」
			ELSEIF RESULT <= 0 || RESULT > 99 || LOCAL:RESULT == 0 
				GOTO INPUT_LOOP
			ELSE
				PRINTL 이쿠츠키:
				PRINTFORMW 「%CSVNAME(LOCAL:RESULT,0)%로 좋은가？」
				LOCAL = LOCAL:RESULT
				CALL INPUT_YN
				PRINTL 이쿠츠키:
				IF RESULT == 0
					PRINTW 「그러면, 사무소에 보내줄게」
					PRINTL 
					PRINTFORMW ＞%조사처리(CSVNAME(LOCAL,0),"를")% 손에 넣었다
					CALLF STRFLAG_REQ(CSVCALLNAME(LOCAL,0), 1)
					의뢰플래그:(FLAG:진행중의뢰):0 -= 100
					CALL ADD_NEW_COMPANION, LOCAL, 200
				ELSE
					PRINTW 「이런, 다른 것인가」
					PRINTW 「그러면, 다시 선택해 줘」
					GOTO CHOICE
				ENDIF
			ENDIF
		ENDIF
	ELSEIF RESULT == 2
		LOCAL:3 = LINECOUNT
		$CARDCHOICE
		CLEARLINE LINECOUNT - LOCAL:3
		PRINTL 
		PRINTL 이쿠츠키:
		PRINTFORML 「지금 자네의 포인트는{의뢰플래그:(FLAG:진행중의뢰):0}점일세」
		PRINTW 「그러면, 카드를 선택해 줘」
		DRAWLINE
		FOR LOCAL, 0, 6
			LOCAL:1 = GETNUM(ITEM, "STR인센스카드") + LOCAL
			PRINTFORM [{LOCAL,2}] %ITEMNAME:(LOCAL:1),20,LEFT% 100P　
			SIF LOCAL % 2 == 1
				PRINTL 
		NEXT
		IF STRFLAG_REQ("마이너스카드판매")
			FOR LOCAL, 0, 6
				LOCAL:1 = GETNUM(ITEM, "STR마이너스카드") + LOCAL
				PRINTFORM [{LOCAL+6,2}] %ITEMNAME:(LOCAL:1),20,LEFT% 100P　
				SIF LOCAL % 2 == 1
					PRINTL 
			NEXT
			PRINTFORM [12] %"ALL마이너스카드",20,LEFT% 200P　
		ENDIF
		SIF STRFLAG_REQ("EXP카드판매")
			PRINTFORM [13] %"명화의카드",20,LEFT% 200P　
		PRINTL 
		PRINTL [99] 돌아간다
		$INPUT_CARD
		INPUT
		IF RESULT == 99
			PRINTL 이쿠츠키:
			PRINTW 「그만두는건가？」
		ELSEIF RESULT < 0 || RESULT > 13 || (RESULT > 5 && !STRFLAG_REQ("마이너스카드판매")) || (RESULT == 13 && !STRFLAG_REQ("EXP카드판매"))
			CLEARLINE 1
			GOTO INPUT_CARD
		ELSE
			LOCAL:1 = RESULT < 12 ? 100 # 200
			SELECTCASE RESULT
				CASE IS < 6
					LOCAL = GETNUM(ITEM, "STR인센스카드") + RESULT
				CASE IS < 13
					LOCAL = GETNUM(ITEM, "STR마이너스카드") + RESULT - 6
				CASE 13
					LOCAL = GETNUM(ITEM, "명화의카드")
			ENDSELECT
			PRINTL 이쿠츠키:
			PRINTFORM 「%ITEMNAME:LOCAL%인가. 그거라면 한장에 {LOCAL:1}포인트
			IF 의뢰플래그:(FLAG:진행중의뢰):0/LOCAL:1 == 0
				PRINTL 지만
				PRINTL 　포인트가 부족하네. 그래서는 교환할 수 없어」
				GOTO START
			ENDIF
			PRINTL 
			PRINTFORML 다. 몇매 교환하겠나？」
			PRINTFORML ({ITEM:LOCAL}매 소지)
			CALL INPUT_MANY(0, 의뢰플래그:(FLAG:진행중의뢰):0/LOCAL:1)
			IF RESULT == 0
				PRINTL 이쿠츠키:
				PRINTW 「그런가, 교환은 그만두는 것인가」
			ELSE
				의뢰플래그:(FLAG:진행중의뢰):0 -= LOCAL:1 * RESULT
				ITEM:LOCAL += RESULT
				PRINTFORMW >%ITEMNAME:LOCAL%를 {RESULT}매 손에 넣었다
				GOTO CARDCHOICE
			ENDIF
		ENDIF
	ELSEIF RESULT == 3 && 던전플래그:[[던전:유키코공주의성]]:8 && STRFLAG_NUM_REQ("보고",,,,23) == 0
		CALLF STRFLAG_NUM_REQ("보고", "=", , DAY)
		PRINTL ＞패러다임X에 대한것과, 유키코 공주의 성에대한것,
		PRINTW ＞또 거기가 쉐도의 소굴이 되어 있다는 것을 이쿠츠키에게 이야기했다
		PRINTL 
		PRINTL 이쿠츠키:
		PRINTW 「으흠…그 타르타로스 이외에도 쉐도가 있을줄이야……」
		PRINTW 「알았어. 이쪽에서도 조사해 보기로 한다」
		PRINTW 「우선, 답례로 이번은 50점 정도 점수를 주겠네.」
		PRINTW 「또 무엇인가 있으면 잘 부탁하네」
		의뢰플래그:(FLAG:진행중의뢰):0 += 50
;	ELSEIF RESULT == 4
;		PRINTL 이쿠츠키:
;		PRINTFORML 「이런, 구입할 거야？」
;		PRINTW 「그러면, 200점이 되는데……」
;		IF 의뢰플래그:(FLAG:진행중의뢰):0 >= 200
;			PRINTL 「어쩔 거야？」
;			PRINTFORML （現在 {의뢰플래그:(FLAG:진행중의뢰):0}点）
;			CALL INPUT_YN
;			PRINTL 이쿠츠키:
;			IF RESULT == 0
;				PRINTW 「그렇다면 사무소로 보내도록 할께……」
;				PRINTW 「꽤나 폭력적이니까, 조심하세요？」
;				PRINTL 
;				PRINTFORMW ＞메티스를 손에 넣었다
;				CALLF STRFLAG_NUM_REQ("메티스", "=", , DAY)
;				의뢰플래그:(FLAG:진행중의뢰):0 -= 200
;				CALL ADD_NEW_COMPANION, 4556, 200
;			ELSE
;				PRINTW 「이런, 그만두는 건가」
;				PRINTW 「그렇다면, 아직 무슨 용무인거야？」
;			ENDIF
;		ELSE
;			PRINTW 「이런, 안타깝지만 포인트가 부족한 것 같은데」
;		ENDIF
	ELSE
		PRINTL 이쿠츠키:
		PRINTW 「돌아가는가. 그럼, 다음번엔 살 수 있도록 노력해 줘.」
		RETURN 0
	ENDIF
ENDIF
DRAWLINE
PRINTL 
GOTO START
