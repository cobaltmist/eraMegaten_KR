;통상공격
;스킬のテン플레이트になっています

;스킬名
@SKILL_NAME_2855,ARG
IF ARG < 0
	RESULTS = 「반혼접」
;突然のラストワードが적を襲う！
ELSEIF SKILLGAGE_H_GET(ARG, 2855) == 100
	RESULTS = 「서행사무여열반」
;80이상で脅威(Lunatic)級
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 79
	RESULTS = 「反魂蝶 -八分사키-」
;50이상で難関(Hard)級
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 49
	RESULTS = 「反魂蝶 -伍分사키-」
;30이상で普通(Normal)級
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 29
	RESULTS = 「反魂蝶 -参分사키-」
;10이상で入門(Easy)級
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 9
	RESULTS = 「反魂蝶 -一分사키-」
;それ이외なら사용불가
ELSE
	RESULTS = 「반혼접」
ENDIF

;　명중
@SKILL_HITRATE_2855, ARG
RETURN 100

;　위력
@SKILL_POWER_2855, ARG
IF ARG < 0
	RETURN 370
;八分사키=370を80%として計算して多少丸めた
;だいたい브라흐마스트라と同じくらい
ELSEIF SKILLGAGE_H_GET(ARG, 2855) == 100
	RETURN 460
;オリジナルの위력
;ダイン系よりはちょっと上
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 79
	RETURN 370
;八分사키=370を80%として以下略
;아기라오とかと同じくらい
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 49
	RETURN 230
;八分사키=370を以下略
;아기とかと同値
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 29
	RETURN 140
;以下略
;포이즈마より弱い　クソザコ
ELSE
	RETURN 50
ENDIF

;　최저공격횟수
@SKILL_MINATTACKNUMBER_2855,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_2855,ARG
RETURN 1

;　ＨＰ2を支払うかＭＰ3を支払うか
@SKILL_COSTTYPE_2855, ARG
RETURN 3
;소비なし＝1

;　コストの量
@SKILL_COST_2855,ARG
;RETURN 35
;どの위력でもコスト40。위력460+파마주살で即死판정で40は安いかもしれんけど전투の度리셋されるゲージ溜めなきゃだし
RETURN 40

;　相性
@SKILL_TYPE_2855,ARG
RETURN GET_TYPE_NUM("만능")

;　1EXTRAか2MAGICか
@SKILL_DECIDE_TYPE_2855, ARG
RETURN 1
;EXTRA

;　1물리데미지で計算するか2마법데미지で計算するか
@SKILL_DAMAGETYPE_2855, ARG
RETURN 2


;　대상が1적か2아군か3場전체か4特殊か
@SKILL_TARGET_2855
RETURN 1

;　사거리
@SKILL_RANGE_2855,ARG
RETURN 3

;　범위　11명　2１열　3전체　4　적아군전체
@SKILL_SPHERE_2855,ARG
RETURN 1

;　 効果の타입　1공격、2회복、3상태변화、4特殊、5상태회복
@SKILL_EFECT_2855
RETURN 1

;　 特殊処理の有無、及び処理内容
@SKILL_SPECIALEFECT_2855,ARG
;ARG = 1だった場合は処理の有無を返します
SIF ARG == 1
	RETURN 0
;以下に特殊な処理を記述します

;계승타입
@SKILL_SUCCESSION_TYPE_2855
RETURN 23
;승계

;속도보정
@SKILL_SPEED_2855, ARG = -1
RETURN 0

;　구상の표시とその타입
@SKILL_MESSAGE_2855,ARG

;전투時以外に사용できるか
@SKILL_ACTIONABLE_FIELD_2855
RETURN 0

;스킬변화？用랭크
@SKILL_RANK_2855
RETURN 15

@継承不能_2855
RETURN 1
;

;テン플레이트用、추가효과シリーズ
;추가효과스테이터스
@SKILL_ADDTIONAL_STATE_2855,ARG
RETURN 0

;추가효과상성
@SKILL_ADDTIONAL_TYPE_2855,ARG
RETURN 0

;추가효과기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_2855,ARG
RETURN 0

;추가효과부여확률上限
@SKILL_ADDTIONAL_MAX_RATE_2855,ARG
RETURN 0

;====================================================
;스킬及び行動の実行
;====================================================
@ACTION_2855,ARG,ARG:1
;コストを支払う
;CALL VAR_MP,ARG,-20
LOCAL = POS(ARG:1)
LOCALS = 포지션{ARG:1}

;すでに倒しているなら飛ばす
SIF LOCAL < 0
	RETURN 0
SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
	RETURN 0

;ラストのアレ표시　どうせ使えるの유유코と조마だけだし問題ないじゃろ……
PRINTFORML 
ALIGNMENT CENTER
SETCOLOR 0xff8ccd
PRINTFORML 身のうさを思ひしらでややみなまし　　　　 
SETCOLOR 0x918cff
PRINTFORML 　　　 そむくならひのなき世な리세ば　　　
RESETCOLOR
ALIGNMENT LEFT
PRINTFORML 

;満開なら내성とか見ないで即死판정
IF SKILLGAGE_H_GET(ARG, 2855) == 100
	;대상がボスか방어してない限り、強制DYING
	IF CFLAG:LOCAL:보스플래그 == 0 && CFLAG:LOCAL:방어플래그 == 0
		PRINTFORM 　　TARGET:[{LOCAL,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
		;直前でCFLAG:LOCAL:방어플래그 == 0と聞いているんだからこの分岐は要らない気がするが一応残す
		IF CFLAG:LOCAL:방어플래그
			PRINTW GUARD
		ELSE
			PRINTFORML DYING
			CALL DEAD_CHARA, LOCAL
		ENDIF
	ELSE
		;대상がボスか방어中なら、공격
		CALL ATTACK_SINGLE, ARG, LOCAL, 2855
	ENDIF
;満開이외なら파마と주살で即死판정(改変前と同じ)
ELSE
	;대상が파마か주살に完全내성がない限り、強制DYING
	IF MAXBASE:(FLAG:LOCALS):파마 > 0 && MAXBASE:(FLAG:LOCALS):파마 < 999 && CFLAG:LOCAL:보스플래그 == 0 && CFLAG:LOCAL:방어플래그 == 0
		PRINTFORM 　　TARGET:[{LOCAL,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
		IF CFLAG:LOCAL:방어플래그
			PRINTW GUARD
		ELSE
			PRINTFORML DYING
			CALL DEAD_CHARA, LOCAL
		ENDIF
	ELSEIF MAXBASE:(FLAG:LOCALS):주살 > 0 && MAXBASE:(FLAG:LOCALS):주살 < 999 && CFLAG:LOCAL:보스플래그 == 0 && CFLAG:LOCAL:방어플래그 == 0
		PRINTFORM 　　TARGET:[{LOCAL,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
		IF CFLAG:LOCAL:방어플래그
			PRINTW GUARD
		ELSE
			PRINTFORML DYING
			CALL DEAD_CHARA, LOCAL
		ENDIF
	ELSE
		;파마と주살に完全내성があれば、공격
		CALL ATTACK_SINGLE, ARG, LOCAL, 2855
	ENDIF
ENDIF

;一発사키いたら枯れる。また春を貯め直してきてください
CALL SKILLGAGE_H_SET, ARG, 2855, 0

;====================================================
;実行内容ここまで
;====================================================
@SKILLGAGE_CHARGE_2855, ARG, ARG:1
#LOCALSIZE 1
;自分の行動の場合
IF ARG == ARG:1
	;反魂蝶을 사용했다らゲージは溜めない
	IF CFLAG:ARG:입력행동 == 2855
		RETURN 0
	ENDIF
	;데미지を受けた적の数に応じて스킬게이지アップ
	;ただし유유코が깨달음える데미지を与える스킬は【마계의조사】のみである
	FOR LOCAL,0, CHARANUM
		SIF CFLAG:LOCAL:PT플래그 == CFLAG:ARG:PT플래그
			CONTINUE
		IF CFLAG:LOCAL:현데미지 > 0
			CALL SKILLGAGE_H_ADD, ARG, 2855, 5
		ELSE
			CONTINUE
		ENDIF
		;会心、약점を出すとさらにアップ
		SIF GETBIT(CFLAG:LOCAL:받음플래그,21)
			CALL SKILLGAGE_H_ADD, ARG, 2855, 5
		SIF GETBIT(CFLAG:LOCAL:받음플래그,22)
			CALL SKILLGAGE_H_ADD, ARG, 2855, 5
	NEXT
	;集中しても上がる（ただし유유코は깨달음えない）
	SIF CFLAG:ARG:입력행동 == 512
		CALL SKILLGAGE_H_ADD, ARG, 2855, 30
	;확산탄막사출で上がる（ただし유유코は以下略
	SIF CFLAG:ARG:입력행동 == 2601
		CALL SKILLGAGE_H_ADD, ARG, 2855, 25
	;집중탄막사출で上がる（ただし以下略
	SIF CFLAG:ARG:입력행동 == 2602
		CALL SKILLGAGE_H_ADD, ARG, 2855, 25
ENDIF
;데미지を受けた又はカウンター、トリガー이외の공격を受けたらゲージが増える
;避ける方がお得にしたいのでクライマックスレヴューとか데빌트리거から上昇値を半分に
IF CFLAG:ARG:현데미지 > 0 || GETBIT(CFLAG:ARG:받음플래그,18) || GETBIT(CFLAG:ARG:받음플래그,19)
	LOCAL = 30 * CFLAG:ARG:현데미지 / MAXBASE:ARG:ＨＰ
	LOCAL = LOCAL < 2 ? 2 # LOCAL
	CALL SKILLGAGE_H_ADD, ARG, 2855, LOCAL
ENDIF
;공격を회피したらゲージが増える（グ레이ズ的なアレ）
;元は弾幕STGなので当てるより避ける方がお得にしたいのだ
IF (GETBIT(CFLAG:ARG:받음플래그,18) || GETBIT(CFLAG:ARG:받음플래그,19)) && !CFLAG:ARG:회피실패
	CALL SKILLGAGE_H_ADD, ARG, 2855, 15
ENDIF
;스킬게이지が100이상だと100にする
SIF SKILLGAGE_H_GET(ARG, 2855) > 100
	CALL SKILLGAGE_H_SET, ARG, 2855, 100
RETURN 0

;====================================================
;전투중以外での実行
;====================================================
@SKILLGAGE_DISPLAY_2855, ARG
#LOCALSIZE 2
#DIM ループ用
IF SKILLGAGE_H_GET(ARG, 2855) == 100
	SETCOLOR 0xff8ccd
	PRINTL Last Word 　>>>　[||||||||||]　発動可(POWER:MAX)
	RESETCOLOR
ELSE
	LOCAL = SKILLGAGE_H_GET(ARG, 2855) / 10
	LOCAL:1 = 10 - LOCAL
	PRINT Spell Card　>>>　[
	SETCOLOR 0xff8ccd
	FOR ループ用, 0, LOCAL
		PRINT |
	NEXT
	SETCOLOR 0x918cff
	FOR ループ用, 0, LOCAL:1
		PRINT .
	NEXT
	RESETCOLOR
	PRINT ]　
	IF SKILLGAGE_H_GET(ARG, 2855) > 9
		PRINT 発動可(POWER:
		SETCOLOR 0xff8ccd
		IF SKILLGAGE_H_GET(ARG, 2855) > 79
			PRINT 80%
		ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 49
			PRINT 50%
		ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 29
			PRINT 30%
		ELSE
			PRINT 10%
		ENDIF
		RESETCOLOR
		PRINTL )
	ELSE
		SETCOLOR 0x666666
		PRINTL 発動불가
		RESETCOLOR
	ENDIF
ENDIF

;====================================================
;実行内容ここまで
;====================================================


;　 설명文の표시
@SKILL_EXPLAIN_2855
;스테이터스화면からはこっち표시
IF CFLAG:(CHARANUM-1):PT플래그 || U < 0 || U >= CHARANUM
	PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
	PRINTL スペル카드ゲージが付く。
	PRINTL ・여데미지、피데미지、회피성공で上昇
	PRINTL ・컨센트레이트、弾幕射出系스킬で大幅上昇
	PRINTL ゲージの上昇に応じて西行妖が힘を取り戻し、위력が増す。
	PRINTL 위령　사이교우지 유유코의 스펠카드
;전투중はこっち표시
ELSE
	;満開
	IF SKILLGAGE_H_GET(U, 2855) == 100
		PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
		PRINTL 西行妖は満開の時を迎えた。
		PRINTL 대상がガードしていなければ即死させ、
		PRINTL 방어하는 상대에겐 만능대칭의 특대 데미지。
		PRINTL 威霊　西行寺유유코のラストワード
	;八分사키
	ELSEIF SKILLGAGE_H_GET(U, 2855) > 79
		PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
		PRINTL 西行妖は満開まで秒読みの단계に入った。
		PRINTL 대상が파마・주살の両方に完全な내성を持っていなければ即死させ、
		PRINTL 완전한 내성을 가지고 있는 상대、방어하는 상대에겐 만능대칭의 대 데미지。
		PRINTL 위령　사이교우지 유유코의 스펠카드
	;伍分사키
	ELSEIF SKILLGAGE_H_GET(U, 2855) > 49
		PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
		PRINTL 西行妖は既に半分이상が花開いている。
		PRINTL 대상が파마・주살の両方に完全な내성を持っていなければ即死させ、
		PRINTL 완전한 내성을 가지고 있는 상대、방어하는 상대에겐 만능대칭의 대 데미지。
		PRINTL 위령　사이교우지 유유코의 스펠카드
	;参分사키
	ELSEIF SKILLGAGE_H_GET(U, 2855) > 29
		PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
		PRINTL 西行妖は着実に힘を取り戻しつつある。
		PRINTL 대상が파마・주살の両方に完全な내성を持っていなければ即死させ、
		PRINTL 완전한 내성을 가지고 있는 상대、방어하는 상대에겐 만능대칭의 대 데미지。
		PRINTL 위령　사이교우지 유유코의 스펠카드
	;一分사키
	ELSEIF SKILLGAGE_H_GET(U, 2855) > 9
		PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
		PRINTL 西行妖は満발に힘を振るえない상태だが、死に誘うには十分だ。
		PRINTL 대상が파마・주살の両方に完全な내성を持っていなければ即死させ、
		PRINTL 완전한 내성을 가지고 있는 상대、방어하는 상대에겐 만능대칭의 대 데미지。
		PRINTL 위령　사이교우지 유유코의 스펠카드
	;使えないから書く意味はない
	ELSE
		PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
		PRINTL 西行妖は花開く兆しさえも見えない。発動불가。
		PRINTL 위령　사이교우지 유유코의 스펠카드
	ENDIF
ENDIF
;コスパを悪く調整

;継承に필요한部位
@継承部位_2855,ARG
RETURN 1

;사용조건
@SKILL_SPECIAL_ACTIONABLE_2855,ARG
SIF SKILLGAGE_H_GET(ARG, 2855) > 9
	RETURN 1
RETURN 0
;ゲージを10まで溜めてから。

