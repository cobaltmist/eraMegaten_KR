;通常攻撃
;スキルのテンプレートになっています
;スキルゲージを導入して대폭 性能変更。八分咲で改変前と同性能になるようにした

;스킬명
@SKILL_NAME_2855,ARG
IF ARG < 0
	RESULTS = 「반혼접」
;突然のラストワードが敵を襲う！
ELSEIF SKILLGAGE_H_GET(ARG, 2855) == 100
	RESULTS = 「사이교우지 무여열반」
;80以上で脅威(Lunatic)級
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 79
	RESULTS = 「반혼접 -팔분소-」
;50以上で難関(Hard)級
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 49
	RESULTS = 「반혼접 -오분소-」
;30以上で普通(Normal)級
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 29
	RESULTS = 「반혼접 -삼분소-」
;10以上で入門(Easy)級
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 9
	RESULTS = 「반혼접 -일분소-」
;それ以外なら使用不可
ELSE
	RESULTS = 「반혼접」
ENDIF

;　명중
@SKILL_HITRATE_2855, ARG
RETURN 100

;　위력
@SKILL_POWER_2855, ARG
IF ARG < 0
	RETURN 370
;八分咲=370を80%として計算して多少丸めた
;だいたいブラフマーストラと同じくらい
ELSEIF SKILLGAGE_H_GET(ARG, 2855) == 100
	RETURN 460
;オリジナルの威力
;ダイン系よりはちょっと上
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 79
	RETURN 370
;八分咲=370を80%として以下略
;アギラオとかと同じくらい
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 49
	RETURN 230
;八分咲=370を以下略
;アギとかと同値
ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 29
	RETURN 140
;以下略
;ポイズマより弱い　クソザコ
ELSE
	RETURN 50
ENDIF

;　최저공격횟수
@SKILL_MINATTACKNUMBER_2855,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_2855,ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_2855, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_2855,ARG
;RETURN 35
;どの威力でもコスト40。威力460+破魔呪殺で即死判定で40は安いかもしれんけど戦闘の度リセットされるゲージ溜めなきゃだし
RETURN 40

;　상성
@SKILL_TYPE_2855,ARG
RETURN GET_TYPE_NUM("만능")

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_2855, ARG
RETURN 1
;EXTRA

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_2855, ARG
RETURN 2


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_2855
RETURN 1

;　사거리
@SKILL_RANGE_2855,ARG
RETURN 3

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_2855,ARG
RETURN 1

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_2855
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_2855,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_2855
RETURN 23
;승계

;속도보정
@SKILL_SPEED_2855, ARG = -1
RETURN 0

;　구상의 표시와 그 타입
@SKILL_MESSAGE_2855,ARG

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_2855
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_2855
RETURN 15

@계승불능_2855
RETURN 1
;

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_2855,ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_2855,ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_2855,ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_2855,ARG
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_2855,ARG,ARG:1
;コストを支払う
;CALL VAR_MP,ARG,-20
LOCAL = POS(ARG:1)
LOCALS = 포지션{ARG:1}

;すでに倒しているなら飛ばす
SIF LOCAL < 0
	RETURN 0
SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
	RETURN 0

;ラストのアレ表示　どうせ使えるの幽々子と造魔だけだし問題ないじゃろ……
PRINTFORML 
ALIGNMENT CENTER
SETCOLOR 0xff8ccd
PRINTFORML 혹여 구도의 길이 존재하지 않는 세상이었다면　　　　 
SETCOLOR 0x918cff
PRINTFORML 　　　 자신의 업조차 알 길 없이 그대로 세상을 떴으리라　　　
RESETCOLOR
ALIGNMENT LEFT
PRINTFORML 

;満開なら耐性とか見ないで即死判定
IF SKILLGAGE_H_GET(ARG, 2855) == 100
	;対象がボスか防御してない限り、強制DYING
	IF CFLAG:LOCAL:보스플래그 == 0 && CFLAG:LOCAL:방어플래그 == 0
		PRINTFORM 　　TARGET:[{LOCAL,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
		;直前でCFLAG:LOCAL:방어플래그 == 0と聞いているんだからこの分岐は要らない気がするが一応残す
		IF CFLAG:LOCAL:방어플래그
			PRINTW GUARD
		ELSE
			PRINTFORML DYING
			CALL DEAD_CHARA, LOCAL
		ENDIF
	ELSE
		;対象がボスか防御中なら、攻撃
		CALL ATTACK_SINGLE, ARG, LOCAL, 2855
	ENDIF
;満開以外なら破魔と呪殺で即死判定(改変前と同じ)
ELSE
	;対象が破魔か呪殺に完全耐性がない限り、強制DYING
	IF MAXBASE:(FLAG:LOCALS):파마 > 0 && MAXBASE:(FLAG:LOCALS):파마 < 999 && CFLAG:LOCAL:보스플래그 == 0 && CFLAG:LOCAL:방어플래그 == 0
		PRINTFORM 　　TARGET:[{LOCAL,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
		IF CFLAG:LOCAL:방어플래그
			PRINTW GUARD
		ELSE
			PRINTFORML DYING
			CALL DEAD_CHARA, LOCAL
		ENDIF
	ELSEIF MAXBASE:(FLAG:LOCALS):주살 > 0 && MAXBASE:(FLAG:LOCALS):주살 < 999 && CFLAG:LOCAL:보스플래그 == 0 && CFLAG:LOCAL:방어플래그 == 0
		PRINTFORM 　　TARGET:[{LOCAL,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
		IF CFLAG:LOCAL:방어플래그
			PRINTW GUARD
		ELSE
			PRINTFORML DYING
			CALL DEAD_CHARA, LOCAL
		ENDIF
	ELSE
		;破魔と呪殺に完全耐性があれば、攻撃
		CALL ATTACK_SINGLE, ARG, LOCAL, 2855
	ENDIF
ENDIF

;一発咲いたら枯れる。また春を貯め直してきてください
CALL SKILLGAGE_H_SET, ARG, 2855, 0

;====================================================
;실행내용 여기까지
;====================================================
@SKILLGAGE_CHARGE_2855, ARG, ARG:1
#LOCALSIZE 1
;自分の行動の場合
IF ARG == ARG:1
	;反魂蝶を使ったらゲージは溜めない
	IF CFLAG:ARG:입력행동 == 2855
		RETURN 0
	ENDIF
	;ダメージを受けた敵の数に応じてスキルゲージアップ
	;ただし幽々子が覚える데미지를 준다スキルは【魔界の調べ】のみである
	FOR LOCAL,0, CHARANUM
		SIF CFLAG:LOCAL:PT플래그 == CFLAG:ARG:PT플래그
			CONTINUE
		IF CFLAG:LOCAL:현데미지 > 0
			CALL SKILLGAGE_H_ADD, ARG, 2855, 5
		ELSE
			CONTINUE
		ENDIF
		;会心、弱点を出すとさらにアップ
		SIF GETBIT(CFLAG:LOCAL:받음플래그,21)
			CALL SKILLGAGE_H_ADD, ARG, 2855, 5
		SIF GETBIT(CFLAG:LOCAL:받음플래그,22)
			CALL SKILLGAGE_H_ADD, ARG, 2855, 5
	NEXT
	;集中しても上がる（ただし幽々子は覚えない）
	SIF CFLAG:ARG:입력행동 == 512
		CALL SKILLGAGE_H_ADD, ARG, 2855, 30
	;拡散弾幕射出で上がる（ただし幽々子は以下略
	SIF CFLAG:ARG:입력행동 == 2601
		CALL SKILLGAGE_H_ADD, ARG, 2855, 25
	;集中弾幕射出で上がる（ただし以下略
	SIF CFLAG:ARG:입력행동 == 2602
		CALL SKILLGAGE_H_ADD, ARG, 2855, 25
ENDIF
;ダメージを受けた又はカウンター、トリガー以外の攻撃を受けたらゲージが増える
;避ける方がお得にしたいのでクライマックスレヴューとかデビルトリガーから上昇値を半分に
IF CFLAG:ARG:현데미지 > 0 || GETBIT(CFLAG:ARG:받음플래그,18) || GETBIT(CFLAG:ARG:받음플래그,19)
	LOCAL = 30 * CFLAG:ARG:현데미지 / MAXBASE:ARG:ＨＰ
	LOCAL = LOCAL < 2 ? 2 # LOCAL
	CALL SKILLGAGE_H_ADD, ARG, 2855, LOCAL
ENDIF
;攻撃を回避したらゲージが増える（グレイズ的なアレ）
;元は弾幕STGなので当てるより避ける方がお得にしたいのだ
IF (GETBIT(CFLAG:ARG:받음플래그,18) || GETBIT(CFLAG:ARG:받음플래그,19)) && !CFLAG:ARG:회피실패
	CALL SKILLGAGE_H_ADD, ARG, 2855, 15
ENDIF
;スキルゲージが100以上だと100にする
SIF SKILLGAGE_H_GET(ARG, 2855) > 100
	CALL SKILLGAGE_H_SET, ARG, 2855, 100
RETURN 0

;====================================================
;전투중 이외에서의 실행
;====================================================
@SKILLGAGE_DISPLAY_2855, ARG
#LOCALSIZE 2
#DIM ループ用
IF SKILLGAGE_H_GET(ARG, 2855) == 100
	SETCOLOR 0xff8ccd
	PRINTL Last Word 　>>>　[||||||||||]　발동가능(POWER:MAX)
	RESETCOLOR
ELSE
	LOCAL = SKILLGAGE_H_GET(ARG, 2855) / 10
	LOCAL:1 = 10 - LOCAL
	PRINT Spell Card　>>>　[
	SETCOLOR 0xff8ccd
	FOR ループ用, 0, LOCAL
		PRINT |
	NEXT
	SETCOLOR 0x918cff
	FOR ループ用, 0, LOCAL:1
		PRINT .
	NEXT
	RESETCOLOR
	PRINT ]　
	IF SKILLGAGE_H_GET(ARG, 2855) > 9
		PRINT 발동가능(POWER:
		SETCOLOR 0xff8ccd
		IF SKILLGAGE_H_GET(ARG, 2855) > 79
			PRINT 80%
		ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 49
			PRINT 50%
		ELSEIF SKILLGAGE_H_GET(ARG, 2855) > 29
			PRINT 30%
		ELSE
			PRINT 10%
		ENDIF
		RESETCOLOR
		PRINTL )
	ELSE
		SETCOLOR 0x666666
		PRINTL 발동불가
		RESETCOLOR
	ENDIF
ENDIF

;====================================================
;실행내용 여기까지
;====================================================

;　 설명문 표시
@SKILL_EXPLAIN_2855
;ステータス画面からはこっち表示
IF CFLAG:(CHARANUM-1):PT플래그 || U < 0 || U >= CHARANUM
	PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
	PRINTL 스펠카드 게이지가 붙는다。
	PRINTL ・여데미지、피데미지、회피성공으로 상승
	PRINTL ・컨센트레이트、탄막사출계 스킬으로 대폭 상승
	PRINTL 게이지의 상승에 따라 사이교우아야카시가 힘을 되찾아、위력이 늘어난다。
	PRINTL 위령　사이교우지 유유코의 스펠카드
;戦闘中はこっち表示
ELSE
	;満開
	IF SKILLGAGE_H_GET(U, 2855) == 100
		PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
		PRINTL 사이교우아야카시는 만개의 때를 맞이했다。
		PRINTL 대상이 가드하고 있지 않으면 즉사시키고、
		PRINTL 가드하고 있는 상대에게는 만능 상성의 특대 데미지。
		PRINTL 위령　사이교우지 유유코의 라스트워드
	;八分咲
	ELSEIF SKILLGAGE_H_GET(U, 2855) > 79
		PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
		PRINTL 사이교우아야카시는 만개까지 초읽기 단계에 들어갔다。
		PRINTL 대상이 파마・주살 양쪽에 완전한 내성을 가지고 있지 않으면 즉사시키고、
		PRINTL 완전한 내성을 가진 상대、가드하고 있는 상대에게는 만능 상성의 대 데미지。
		PRINTL 위령　사이교우지 유유코의 스펠카드
	;伍分咲
	ELSEIF SKILLGAGE_H_GET(U, 2855) > 49
		PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
		PRINTL 사이교우아야카시는 이미 절반 이상 꽃이 피어 있다。
		PRINTL 대상이 파마・주살 양쪽에 완전한 내성을 가지고 있지 않으면 즉사시키고、
		PRINTL 완전한 내성을 가진 상대、가드하고 있는 상대에게는 만능 상성의 대 데미지。
		PRINTL 위령　사이교우지 유유코의 스펠카드
	;参分咲
	ELSEIF SKILLGAGE_H_GET(U, 2855) > 29
		PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
		PRINTL 사이교우아야카시는 착실하게 힘을 되찾고 있다。
		PRINTL 대상이 파마・주살 양쪽에 완전한 내성을 가지고 있지 않으면 즉사시키고、
		PRINTL 완전한 내성을 가진 상대、가드하고 있는 상대에게는 만능 상성의 대 데미지。
		PRINTL 위령　사이교우지 유유코의 스펠카드
	;一分咲
	ELSEIF SKILLGAGE_H_GET(U, 2855) > 9
		PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
		PRINTL 사이교우아야카시는 만족스럽게 힘을 발휘하지는 못하는 상태지만、죽음으로 이끌기에는 충분하다。
		PRINTL 대상이 파마・주살 양쪽에 완전한 내성을 가지고 있지 않으면 즉사시키고、
		PRINTL 완전한 내성을 가진 상대、가드하고 있는 상대에게는 만능 상성의 대 데미지。
		PRINTL 위령　사이교우지 유유코의 스펠카드
	;使えないから書く意味はない
	ELSE
		PRINTL 「죽음을 다루는 정도의 능력」　그 힘의 현현。
		PRINTL 사이교우아야카시는 꽃 필 기미조차 보이지 않는다。발동불가。
		PRINTL 위령　사이교우지 유유코의 스펠카드
	ENDIF
ENDIF
;コスパを悪く調整

;계승에 필요한 부위
@계승부위_2855,ARG
RETURN 1

;사용조건
@SKILL_SPECIAL_ACTIONABLE_2855,ARG
SIF SKILLGAGE_H_GET(ARG, 2855) > 9
	RETURN 1
RETURN 0
;ゲージを10まで溜めてから。

