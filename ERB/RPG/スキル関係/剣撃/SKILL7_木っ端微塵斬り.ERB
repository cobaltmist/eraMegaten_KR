;通常攻撃
;スキルのテンプレートになっています

;스킬명
@SKILL_NAME_7, ARG
IF SKILL_CHANGE(ARG, [[キャラ:암법사]]) && 던전플래그:25:13 == 1
	RESULTS = 칠성검
ELSEIF SKILL_CHANGE(ARG, [[キャラ:블레이드(X)]]) && EQUIP:ARG:악세서리 == 5349
	RESULTS = 산산조각베기・벽뢰
ELSE
	RESULTS = 산산조각베기
ENDIF

;　명중
@SKILL_HITRATE_7, ARG
RETURN 110

;　위력
@SKILL_POWER_7, ARG
IF SKILL_CHANGE(ARG, [[キャラ:블레이드(X)]]) && EQUIP:ARG:악세서리 == 5349
	RETURN 420
ELSE
	RETURN 335
ENDIF

;　최저공격횟수
@SKILL_MINATTACKNUMBER_7, ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_7, ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_7, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_7, ARG
IF SKILL_CHANGE(ARG, [[キャラ:블레이드(X)]]) && EQUIP:ARG:악세서리 == 5349
	RETURN 33
ELSE
	RETURN 26
ENDIF

;　상성
@SKILL_TYPE_7, ARG
#LOCALSSIZE 3
#LOCALSIZE 4
IF SKILL_CHANGE(ARG, [[キャラ:블레이드(X)]]) && EQUIP:ARG:악세서리 == 5349
	SIF ARG < 0 || 스킬속성표시중
		RETURN GET_TYPE_NUM("검격")
	SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
		RETURN GET_TYPE_NUM("검격")
	LOCAL = POS(CFLAG:ARG:타겟)
	SIF LOCAL < 0 || CFLAG:LOCAL:마법반사플래그 || CFLAG:LOCAL:무효플래그
		RETURN GET_TYPE_NUM("검격")
	SIF LOCAL < 0 || CFLAG:LOCAL:물리반사플래그
		RETURN GET_TYPE_NUM("전격")
	LOCAL:2 = -1000
	LOCALS = 검격
	LOCALS:1 = 검격
	LOCALS:2 = 전격
	FOR LOCAL:1, 1, 3
		;判定用に数値を仕舞う。反射は-200扱い
		LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
		LOCAL:3 = !(HAVE_SKILL(ARG, [[스킬:영세라이도우]]) || HAVE_SKILL(ARG,2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
		LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
		SELECTCASE LOCAL:1
			CASE 1
				LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
			CASE 2
				LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
			CASEELSE
		ENDSELECT
		IF LOCAL:3 > LOCAL:2
			LOCAL:2 = LOCAL:3
			LOCALS = %LOCALS:(LOCAL:1)%
		ENDIF
	NEXT
	RETURN GET_TYPE_NUM(LOCALS)
ELSE
	RETURN 0
	;검격
ENDIF

;　1 EXTRA　2 MAGIC ;KR판 임의수정, DECIDE_TYPE ARG 추가 통일
@SKILL_DECIDE_TYPE_7, ARG
RETURN 1
;どちらでもない

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_7, ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_7
RETURN 1

;　사거리
@SKILL_RANGE_7, ARG
RETURN 4

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_7, ARG
RETURN 3

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_7
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_7, ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_7
RETURN 0
;검격

;속도보정
@SKILL_SPEED_7, ARG = -1
IF SKILL_CHANGE(ARG, [[キャラ:블레이드(X)]]) && EQUIP:ARG:악세서리 == 5349
	RETURN 0
ELSE
	RETURN -2
ENDIF

;　구상의 표시와 그 타입
@SKILL_MESSAGE_7, ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_7
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_7
RETURN 11



;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_7, ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_7, ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_7, ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_7, ARG
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_7, ARG, ARG:1
#LOCALSIZE 3

IF SKILL_CHANGE(ARG, [[キャラ:암법사]]) && 던전플래그:25:13 == 1
	SETCOLOR 0xff0033
	IF RAND:2 == 0
		PRINTFORML ┏> 着流しの男　　━━━━━━━━━━━━━━━┓
		PRINTFORML ┃이얍！　　　　　　　　　　　　　　　　　　　　┃
		PRINTFORMW ┗━━━━━━━━━━━━━━━━━━━━━━━┛
	ELSE
		PRINTFORML ┏> 着流しの男　　━━━━━━━━━━━━━━━┓
		PRINTFORML ┃웃기지 마！ 　　　　　　　　　　　　　　　　　┃
		PRINTFORMW ┗━━━━━━━━━━━━━━━━━━━━━━━┛
	ENDIF
	PRINTFORML ┏> MESSAGE 　　　━━━━━━━━━━━━━━━┓
	PRINTFORML ┃칠성검이 날카롭게 베어올려진다！　　　　　　　┃
	PRINTFORMW ┗━━━━━━━━━━━━━━━━━━━━━━━┛
	RESETCOLOR
ENDIF

IF SKILL_CHANGE(ARG, [[キャラ:블레이드(X)]]) && EQUIP:ARG:악세서리 == 5349
	IF CFLAG:ARG:포지션 < 7
		LOCAL:1 = 7
		LOCAL:2 = 17
	ELSE
		LOCAL:1 = 1
		LOCAL:2 = 7
	ENDIF

	FOR LOCAL, LOCAL:1, LOCAL:2
		LOCALS = 포지션{LOCAL}
		SIF FLAG:LOCALS < 0
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
			CONTINUE
		CFLAG:ARG:타겟 = LOCAL
		CALL SKILL_TYPE_7, ARG
		PRINT 상성판정：
		SELECTCASE RESULT
			CASE 0
				PRINT 검격
			CASE 6
				PRINT 전격
		ENDSELECT
		PRINTL ！
		CALL ATTACK_SINGLE, ARG, FLAG:LOCALS, 7
	NEXT

	CFLAG:ARG:타겟 = CFLAG:ARG:포지션 < 7 ? 20 # 17
ELSE
	CALL ATTACK_FIELD, ARG, ARG:1, 7
ENDIF




;====================================================
;실행내용 여기까지
;====================================================

;　 설명문 표시
@SKILL_EXPLAIN_7
PRINTL 적 전체를 마구 베어가른다。
IF U > 0 && U < CHARANUM && NO:U == [[キャラ:블레이드(X)]] && EQUIP:U:악세서리 == 5349
	SETCOLOR 0x14ffd7
	PRINTL 벽색의뇌정 장비중　위력강화／속도보정강화／상성변경：검격＋전격
	RESETCOLOR
ENDIF

;계승에 필요한 부위
@계승부위_7, ARG
SIF TALENT:ARG:무기
	RETURN 1
RETURN 0

;사용조건
@SKILL_SPECIAL_ACTIONABLE_7, ARG
RETURN 1
;とくにない

