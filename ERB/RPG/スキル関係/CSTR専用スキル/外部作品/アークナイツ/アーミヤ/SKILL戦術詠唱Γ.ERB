;스킬명
@SKILL_NAME_戦術詠唱γ,ARG
#DIM SKILL_NUM
RESULTS = 전술영창γ
;SKILL_NUM = PU_SKILLNUM_GET(ARG,"戦術詠唱γ")
;IF FLAG:카운터중 && SKILLGAGE_D_GET(ARG,SKILL_NUM) > 0 && SKILLGAGE_F_GET(ARG,SKILL_NUM) > 0
;	TRYCALLFORM SKILL_NAME_{SKILLGAGE_F_GET(ARG,SKILL_NUM)}
;ENDIF

;　명중
@SKILL_HITRATE_戦術詠唱γ, ARG
RETURN 100

;　위력
@SKILL_POWER_戦術詠唱γ, ARG
RETURN 100

;;クリティカル率
;@SKILL_CRITICAL_RATE_戦術詠唱γ, ARG
;RETURN 5

;;　吸収率
;@SKILL_HP_ABSORBER_戦術詠唱γ, ARG
;RETURN 0

;　최저공격횟수
@SKILL_MINATTACKNUMBER_戦術詠唱γ,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_戦術詠唱γ,ARG
RETURN 1

;;一体辺りの最大ＨＩＴ数
;;※ランダム攻撃系に使う
;@SKILL_MAXATK_PER_戦術詠唱γ,ARG
;RETURN 1

;;一体辺りの最低ＨＩＴ数
;;※ランダム攻撃系に使う
;@SKILL_MINATK_PER_戦術詠唱γ,ARG
;RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_戦術詠唱γ, ARG
RETURN 1
;소비없음＝1

;　코스트량
@SKILL_COST_戦術詠唱γ,ARG
RETURN 0

;　상성
@SKILL_TYPE_戦術詠唱γ,ARG
RETURN 0

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_戦術詠唱γ,ARG
RETURN 0

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_戦術詠唱γ,ARG
RETURN 2


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_戦術詠唱γ,ARG
RETURN 4

;　사거리
@SKILL_RANGE_戦術詠唱γ,ARG
RETURN 3

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_戦術詠唱γ,ARG
RETURN 1

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_戦術詠唱γ,ARG
RETURN 4

;　 특수처리 여부 및 처리내용
;@SKILL_SPECIALEFECT_戦術詠唱γ,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
;SIF ARG == 1
;	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
;@SKILL_SUCCESSION_TYPE_戦術詠唱γ,ARG
;RETURN 23
;剣撃

;속도보정
@SKILL_SPEED_戦術詠唱γ, ARG = -1
RETURN 0

;;　구상의 표시와 그 타입
;@SKILL_MESSAGE_戦術詠唱γ,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_戦術詠唱γ, ARG = -1
RETURN 0

;没
;@SKILL_RANK_戦術詠唱γ
;@SKILL_RANKUP_戦術詠唱γ
;@계승불능_戦術詠唱γ

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_戦術詠唱γ,ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_戦術詠唱γ,ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_戦術詠唱γ,ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_戦術詠唱γ,ARG
RETURN 0

;;拡散攻撃用追加処理
;;ブレスなどの拡散攻撃に使う
;@SKILL_SPREAD_戦術詠唱γ,ARG
;RETURN 0

;====================================================
;トリガー発動条件
;====================================================
@SKILL_TRIGGER_ACTIONABLE_戦術詠唱γ,ARG,ARG:1
#DIM SKILL_NUM
#DIM L_行動
#DIM L_タゲ
SKILL_NUM = PU_SKILLNUM_GET(ARG,"戦術詠唱γ")
SIF ARG != ARG:1 || SKILLGAGE_H_GET(ARG,SKILL_NUM) < 1
	RETURN 0
L_行動 = SKILLGAGE_H_GET(ARG,SKILL_NUM) % 10000
L_タゲ = SKILLGAGE_H_GET(ARG,SKILL_NUM) / 10000
SIF SKILLGAGE_F_GET(ARG,SKILL_NUM) > -1 || L_行動 < 1
	RETURN 0
CALL TRUE_SKILL_COST,ARG,L_行動
SIF BASE:ARG:ＭＰ < RESULT
	RETURN 0
CALLFORM SKILL_EFECT_{L_行動}
IF RESULT == 1 && L_タゲ < 17
	SIF GET_STATE(CFLAG:POS(L_タゲ):상태이상) == "DYING"
		RETURN 0
ENDIF
RETURN 1

;====================================================
;カウンター発動条件
;====================================================
;@SKILL_COUNTER_ACTIONABLE_戦術詠唱γ,ARG,ARG:1
;RETURN RESULT

;====================================================
;ヒット時ダメージ前処理
;====================================================
;@SKILL_HIT_EFFECT_戦術詠唱γ,ARG,ARG:1

;====================================================
;ダメージ発動時
;====================================================
;@SKILL_DAMAGE_EFFECT_戦術詠唱γ,ARG,ARG:1

;====================================================
;行動によるスキルゲージ操作
;====================================================
;@SKILLGAGE_CHARGE_戦術詠唱γ,ARG,ARG:1
;#DIM SKILL_NUM
;SKILL_NUM = PU_SKILLNUM_GET(ARG,"戦術詠唱γ")

;====================================================
;特定のタイミングで行う処理
;====================================================
;戦闘開始時に行う処理
;@SKILL_BATTLE_START_戦術詠唱γ,ARG
;RETURN 0

;戦闘終了時に行う処理
;@SKILL_BATTLE_END_戦術詠唱γ,ARG
;RETURN 0

;ターン開始時に行う処理
;@SKILL_TURNSTART_戦術詠唱γ,ARG
;RETURN 0

;ターン終了時に行う処理
@SKILL_TURNEND_戦術詠唱γ,ARG
#DIM SKILL_NUM
#DIM L_GAGE
#DIM L_MAX_F
SIF ARG < 0 || CSTR:ARG:102 == ""
	RETURN 0
SKILL_NUM = PU_SKILLNUM_GET(ARG,"戦術詠唱γ")
;PRINTFORMW H1 {SKILLGAGE_H_GET(ARG,SKILL_NUM)}
L_GAGE = SKILLGAGE_F_GET(ARG,SKILL_NUM)
IF GET_STATE(CFLAG:ARG:상태이상) == "DYING"
	;死んだ場合はゲージを初期値にリセットする
	L_MAX_F = TOINT(AUTO_SPLIT("0_5_10_15","_",TOINT(CSTR:ARG:102)-1))
	SIF L_GAGE != L_MAX_F
		CALL SKILLGAGE_F_SET(ARG,SKILL_NUM,L_MAX_F)
	CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,0
	RETURN 0
ENDIF
;PRINTFORMW H2 {SKILLGAGE_H_GET(ARG,SKILL_NUM)}
;スキルランクに応じて最大値を設定
L_MAX_F = TOINT(AUTO_SPLIT("40_35_32_30","_",TOINT(CSTR:ARG:102)-1))
;Fゲージが0or正の値（起動していない状態）か、負の値（効果発動中）で処理を変更
L_GAGE = L_GAGE > -1 ? MIN(L_GAGE + 5,L_MAX_F) # MIN(L_GAGE + 5 ,0)
;効果時間が終了した際にメッセージ表示
IF L_GAGE == 0 && SKILLGAGE_F_GET(ARG,SKILL_NUM) < 0
	PRINTFORMW %CALLNAME:ARG%의 전술영창γ의 효과가 종료되었다…。
	CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,0
ENDIF
CALL SKILLGAGE_F_SET(ARG,SKILL_NUM,L_GAGE)
;PRINTFORMW H3 {SKILLGAGE_H_GET(ARG,SKILL_NUM)}
RETURN 0

;SHOP画面を開いた時に行う処理
@SKILL_INITIALIZE_戦術詠唱γ,ARG
#DIM SKILL_NUM
SIF ARG < 0 || CSTR:ARG:102 == ""
	RETURN 0
SKILL_NUM = PU_SKILLNUM_GET(ARG,"戦術詠唱γ")
;スキルランクに応じて初期値を設定
CALL SKILLGAGE_F_SET(ARG,SKILL_NUM,TOINT(AUTO_SPLIT("0_5_10_15","_",TOINT(CSTR:ARG:102)-1)))
RETURN 0


;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_戦術詠唱γ,ARG,ARG:1,SKILL_NUM
#DIM SKILL_NUM
#DIM L_HOLD
#DIM L_行動
#DIM L_タゲ
SKILL_NUM = PU_SKILLNUM_GET(ARG,"戦術詠唱γ")
;Fゲージ一本でSPゲージと効果時間を管理するので、HとDで入力行動とダゲを管理する必要が出てくる
IF SKILLGAGE_F_GET(ARG,SKILL_NUM) > -1
	CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,0
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,-30
	PRINTFORMW %CALLNAME:ARG%의 영창속도가 가속했다！
ELSE
	L_行動 = SKILLGAGE_H_GET(ARG,SKILL_NUM) % 10000
	L_タゲ = SKILLGAGE_H_GET(ARG,SKILL_NUM) / 10000
	;防御しながら攻撃したらバランスが崩れるから防御を維持しちゃダメだ！
	SIF CFLAG:ARG:방어플래그 > 0
		CFLAG:ARG:방어플래그 = 0
	L_HOLD = CFLAG:ARG:입력행동
	CFLAG:ARG:입력행동 = L_行動
	CALL PAY_COST,ARG,L_行動
	SIF CFLAG:ARG:PT플래그 == 0
		SETCOLOR 0xff0033
	SIF CFLAG:ARG:PT플래그 > 0
		SETCOLOR 0x33ffcc
	PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓
	PRINTFORM ┃[{CPOS(ARG),2}] %CALLNAME:ARG,21,LEFT%┃　┃
	CALLFORM SKILL_NAME_{L_行動},ARG
	PRINTFORML %RESULTS,40,LEFT%┃
	PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
	RESETCOLOR
	SWAP TARGET,ARG
	CALLFORM ACTION_{L_行動},TARGET,L_タゲ
	CALL 구상호출 , "BATTLE_MESSAGE" , TARGET , TARGET
	SWAP TARGET,ARG
	CFLAG:ARG:입력행동 = L_HOLD
	;PRINTFORMW H1 {SKILLGAGE_H_GET(ARG,SKILL_NUM)}
	SELECTCASE L_行動
		CASE [[스킬:사바트마]],[[스킬:초래의무도]]
			CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,0
			;PRINTFORMW H2 {SKILLGAGE_H_GET(ARG,SKILL_NUM)}
	ENDSELECT
ENDIF
;CALL ATTACK_SINGLE,ARG,POS(ARG:1),SKILL_NUM;	単体攻撃
;CALL ATTACK_FIELD,ARG,ARG:1,SKILL_NUM;			範囲攻撃(列、全体)
;CALL ATTACK_SPREAD,ARG,ARG:1,SKILL_NUM;		拡散攻撃
;CALL ATTACK_RAND,ARG,ARG:1,SKILL_NUM;			ランダム攻撃


;====================================================
;실행내용 여기까지
;====================================================

;====================================================
;전투중 이외에서의 실행
;====================================================
;@ACTION_FIELD_戦術詠唱γ,ARG,ARG:1,SKILL_NUM
;#DIM SKILL_NUM

;CALL HEAL_SINGLE,ARG,POS(ARG:1),SKILL_NUM	;単体回復
;CALL HEAL_FIELD,ARG,ARG:1,SKILL_NUM		;範囲回復(列、全体)
;CALL ALL_HEAL, POS(ARG:1), 0				;単体前回復

;====================================================
;실행내용 여기까지
;====================================================

;====================================================
;追加コマンド(EXTRAの下)
;====================================================
;@SKILL_SPECIAL_COMMAND_EXTRA_戦術詠唱γ,ARG,LOCAL,SKILL_NUM
;#DIM SKILL_NUM
;#LOCALSIZE 1
;PRINTFORML [{LOCAL}] 剣質変化
;C:(LOCAL++) = 10000 + SKILL_NUM;←必須
;RETURN LOCAL;←必須

;====================================================
;追加コマンド(MAGICの下、魔封だと消える)
;====================================================
;@SKILL_SPECIAL_COMMAND_MAGIC_戦術詠唱γ,ARG,LOCAL,SKILL_NUM
;#DIM SKILL_NUM
;#LOCALSIZE 1
;PRINTFORML [{LOCAL}] 剣質変化
;C:(LOCAL++) = 10000 + SKILL_NUM;←必須
;RETURN LOCAL;←必須

;====================================================
;追加コマンド(一番下)
;====================================================
@SKILL_SPECIAL_COMMAND_戦術詠唱γ,ARG,LOCAL,SKILL_NUM
#LOCALSIZE 1
#DIM SKILL_NUM
#DIM L_MAX_F
#DIM L_行動
#DIM L_タゲ
#DIM T_SKILL_COST
#DIMS TARGETS
SKILL_NUM = PU_SKILLNUM_GET(ARG,"戦術詠唱γ")
L_MAX_F = TOINT(AUTO_SPLIT("40_35_32_30","_",TOINT(CSTR:ARG:102)-1))
IF SKILLGAGE_F_GET(ARG,SKILL_NUM) >= L_MAX_F
	SETCOLOR 0xffa500
	PRINTFORML [{LOCAL}] 『전술영창γ』ＲＥＡＤＹ！
	C:(LOCAL++) = 10000 + SKILL_NUM;←必須
	RESETCOLOR
ELSEIF SKILLGAGE_F_GET(ARG,SKILL_NUM) < 0 && TOINT(CSTR:ARG:102) > 3
	IF SKILLGAGE_H_GET(ARG,SKILL_NUM) > 0
		L_行動 = SKILLGAGE_H_GET(ARG,SKILL_NUM) % 10000
		L_タゲ = SKILLGAGE_H_GET(ARG,SKILL_NUM) / 10000
	ELSEIF SKILLGAGE_H_GET(ARG,SKILL_NUM) == 0
		VARSET L_行動
		VARSET L_タゲ
	ENDIF
	IF L_行動 > 0
		T_SKILL_COST = 0
		IF CFLAG:ARG:입력행동 > -1
			CALL TRUE_SKILL_COST,ARG,CFLAG:ARG:입력행동
			T_SKILL_COST = RESULT
		ENDIF
		CALL TRUE_SKILL_COST,ARG,L_行動
		IF BASE:ARG:ＭＰ < RESULT + T_SKILL_COST
			SETCOLOR COLOR("GRAY")
			T_SKILL_COST = -1
		ENDIF
	ENDIF
	PRINTFORM [{LOCAL}] %"ＥＸ－ＣＡＳＴ",20,LEFT%
	IF L_行動 > 0
		$YARINAOSI
		CALLFORM SKILL_NAME_{L_行動},ARG
		PRINTFORM %RESULTS + " " * MAX(0, (18 - STRLENS(RESULTS))),LOCAL%
		;SUMMON
		IF L_行動 == 2303 || L_行動 == 529 || L_行動 == 540
			TARGETS =  >>> [{L_タゲ,2}] %CALLNAME:(POS(L_タゲ))%
		;東方ＭＯＤ追加-------------------------------------
		;---- EDIT 025 ADD START
		ELSEIF L_行動 == [[스킬:스리스티]]
			TARGETS =  >>> [--] 아군전체
		;---- EDIT 025 ADD END
		;単体蘇生 道返玉・反魂香・リカーム・サマリカーム・トンリン芳香
		ELSEIF L_行動 == [[스킬:리캄]] || L_行動 == [[스킬:사마리캄]] || L_行動 == [[스킬:통령「통령요시카」]]
			;現ターゲットが蘇生対象か見る
			CALL CHECK_TARGET_RESUSCITATION, ARG
			IF RESULT > -1 
				L_タゲ = RESULT
				CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,(L_タゲ*10000) + L_行動
				TARGETS =  >>> [--] %CALLNAME:(L_タゲ)%
			ELSE
				;対象キャラが戦闘参加していない場合
				CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,0
				GOTO YARINAOSI
			ENDIF
		;東方ＭＯＤ追加ここまで-------------------------------------
		;単体
		ELSEIF L_タゲ <= 16 && L_タゲ > 0
			;記録されているポジションに敵がいない場合
			IF POS(L_タゲ) == -1
				CALL CHECK_ACTIONABLE, ARG, L_行動
				IF RESULT == 0
					CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,0
					GOTO YARINAOSI
				ELSE
					CALL RANDOM_TARGET, ARG, L_行動
				ENDIF
			ENDIF
			TARGETS =  >>> [{L_タゲ,2}] %CALLNAME:(POS(L_タゲ))%
		;列
		ELSEIF L_タゲ == 20 || L_タゲ == 21
			TARGETS =  >>> [--] \@L_タゲ == 20 ? 적전열 # 적후열 \@
			CALLFORM SKILL_TARGET_{L_行動}
			IF RESULT == 1
				CALLFORM SKILL_SPHERE_{L_行動},ARG
				IF RESULT == 2
					CALLFORM SKILL_RANGE_{L_行動},ARG
					;前列のみ攻撃可能なスキルで後列を選択できない様に変更
					IF RESULT > 2
						IF CHECK_BACKROW()
						ENDIF
						
					ENDIF
				ENDIF
			ENDIF
		;その他
		ELSE
			CALLFORM SKILL_TARGET_{L_行動}
			IF RESULT == 1
				TARGETS =  >>> [--] 적전체
			ELSEIF RESULT == 2
				TARGETS =  >>> [--] 아군전체
			ELSEIF RESULT == 3
				TARGETS =  >>> [--] 전체
			ELSE
				TARGETS =  　　　　　　　　　
			ENDIF
		ENDIF
		PRINTFORML %TARGETS , 26 , LEFT%
	ELSE
		PRINTL 
	ENDIF
	C:(LOCAL++) = 10000 + SKILL_NUM;←必須
	RESETCOLOR
ENDIF
RETURN LOCAL;←必須

;====================================================
;コマンド実行
;====================================================
@INPUT_COM_戦術詠唱γ,ARG
#LOCALSIZE 2
#DIM SKILL_NUM
#DIM T_SKILL_COST
SKILL_NUM = PU_SKILLNUM_GET(ARG,"戦術詠唱γ")
IF SKILLGAGE_F_GET(ARG,SKILL_NUM) > -1
	LOCAL = CFLAG:ARG:입력행동
	CFLAG:ARG:입력행동 = SKILL_NUM
	SETCOLOR 0x33ffcc
	PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓
	PRINTFORM ┃[{CPOS(ARG),2}] %CALLNAME:ARG,21,LEFT%┃　┃
	CALLFORM SKILL_NAME_戦術詠唱γ,ARG
	PRINTFORML %RESULTS,40,LEFT%┃
	PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
	RESETCOLOR
	SWAP TARGET,ARG
	CALL ACTION_戦術詠唱γ,TARGET
	CALL 구상호출 , "BATTLE_MESSAGE" , TARGET , TARGET
	SWAP TARGET,ARG
	CFLAG:ARG:입력행동 = LOCAL
ELSEIF SKILLGAGE_F_GET(ARG,SKILL_NUM) < 0 && TOINT(CSTR:ARG:102) > 3
	;スキル一覧を表示して、追加行動で使用するスキルを選択する
	;ただし、表示するスキルはMAGICに該当する物のみに限定する
	;魔法はCLOSEだと使えない
	IF CFLAG:ARG:상태이상 == GET_STATE_NUM("CLOSE")
		CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,0
		RETURN -1
	ENDIF
	$INPUT_SKILL_LOOP1
	CALL SHOW_SKILL_LIST_EXTRAMOVE, ARG, 2
	INPUT
	IF !RESULT
		CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,(SKILLGAGE_H_GET(ARG,SKILL_NUM) / 10000) * 10000
		RETURN -1
	ELSEIF ((TALENT:ARG:이능자 || TALENT:ARG:달인 || TALENT:ARG:인수라) && INRANGE(RESULT, 1, FLAG:이능자스킬수)) ||(TALENT:ARG:이능자 == 0 && TALENT:ARG:달인 == 0 && TALENT:ARG:인수라 == 0 && INRANGE(RESULT, 1, FLAG:스킬수)) || INRANGE(RESULT, 21, 42)
		LOCAL = ABL:ARG:@"\@ RESULT > 20 ? 장비 # \@스킬{RESULT > 20 ? RESULT - 20 # RESULT}"
		LOCAL:1 = !LOCAL	;GOTOフラグ
		RESULT = 1
		TRYCALLFORM SKILL_ACTIONABLE_BATTLE_{LOCAL}
		LOCAL:1 |= !RESULT	;戦闘中に使えないスキルなら不正
		CALL CHECK_ACTIONABLE, ARG, LOCAL
		LOCAL:1 |= !RESULT	;사용조건を満たしていないなら不正
		;----------------------------------------------------
		;正しく動作する保障がないので招来系は弾く
		SIF LOCAL == 2303 || LOCAL == 529 || LOCAL == 540
			LOCAL:1 |= 1
		;通常行動と追加行動の分を合わせたコストを足りているか調べる
		T_SKILL_COST = 0
		CALL TRUE_SKILL_COST,ARG,LOCAL
		T_SKILL_COST = RESULT
		RESULT = 0
		SIF CFLAG:ARG:입력행동 > -1
			CALL TRUE_SKILL_COST,ARG,CFLAG:ARG:입력행동
		SIF BASE:ARG:ＭＰ < RESULT + T_SKILL_COST
			LOCAL:1 |= 1	;コスト不足なら不正
		;----------------------------------------------------
		CALLFORM SKILL_DECIDE_TYPE_{LOCAL}
		SIF LOCAL:1 || (RESULT != 2)	;TYPEが異なっていたら不正
			GOTO INPUT_SKILL_LOOP1
		
		DRAWLINE
		SIF FLAG:스킬속성표시설정
			CALL SKILL_EXPLAIN_PERFORMANCE, LOCAL, ARG
	;スキル使用者を一文字関数Uに入れておく
	U = ARG
		TRYCALLFORM SKILL_EXPLAIN_{LOCAL}
		CALL SELECT_SKILL_TARGET, LOCAL, ARG
		SIF RESULT == -1
			GOTO INPUT_SKILL_LOOP1
		;追加行動分のターゲットと追加行動の内容を設定
		CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,(RESULT*10000) + LOCAL
	ENDIF
ENDIF
RETURN -1
;-1 再びコマンド選択へ
; 1 キャラ選択へ

;====================================================
;戦闘中スキルゲージの可視化
;====================================================
@SKILLGAGE_DISPLAY_戦術詠唱γ,ARG
#DIM SKILL_NUM
#DIM L_GAGE
#DIM GAGE_LIMIT
#DIM L_COLOR
SKILL_NUM = PU_SKILLNUM_GET(ARG,"戦術詠唱γ")
L_GAGE = SKILLGAGE_F_GET(ARG,SKILL_NUM)
;効果未発動なら獲得上限の値を、発動中なら最大効果時間をゲージ上限として代入
GAGE_LIMIT = L_GAGE > -1 ? TOINT(AUTO_SPLIT("40_35_32_30","_",TOINT(CSTR:ARG:102)-1)) # 30
;効果状態に応じてゲージの色を設定
L_COLOR = L_GAGE > -1 ? 0x9acd32 # 0xff8c00
PRINTFORM ＥＸ―ＳＫＩＬＬ|
CALL PRINT_COLORBAR, ABS(L_GAGE), GAGE_LIMIT, 25, UNICODE(0x2585), UNICODE(0x2585), L_COLOR, 0x696969
PRINTL


;　説明文の表示
@SKILL_EXPLAIN_戦術詠唱γ,ARG
PRINTL 스킬게이지 추가。턴 경과로 게이지가 쌓인다。게이지 가득 시 기동 가능
PRINTL 발동 중、마법공격의 여데미지가 상승하고、마법공격의 명중・회피、물리공격에 대한 회피력이 상승한다
IF TOINT(CSTR:ARG:102) == 4
	PRINTL 추가로、공격마법의 스킬을 통상 행동 후에 발동하는「ＥＸ－ＣＡＳＴ」가 사용 가능해진다
ENDIF
PRINTL 스킬랭크가 높을수록 상승률이 높아지고、스킬이 발동하기 쉬워진다

;没
;@계승부위_戦術詠唱γ,ARG

;사용조건
@SKILL_SPECIAL_ACTIONABLE_戦術詠唱γ,ARG
RETURN 0

;타겟조건
;↓のSKILL_T_RESERVEがRETURN 1 の時に使用する
;@SKILL_SPECIAL_TARGET_戦術詠唱γ,USE_CHARA,ARG
;#DIM USE_CHARA
;RETURN 0

;控えに使えるか
;@SKILL_T_RESERVE_戦術詠唱γ,ARG
;RETURN 0

;어떤 배드스테이터스를 고칠수 있는가
;@SKILL_CURE_STATE_戦術詠唱γ,USE_CHARA,ARG
;#DIM USE_CHARA
;SELECTCASE GET_STATE(ARG)
;	CASE "HAPPY","CLOSE"
;		RETURN 1
;ENDSELECT
;RETURN 0

;この技を複数持てるか
;@중복습득수_戦術詠唱γ,ARG
;RETURN 1

;スキル重複で役に立つ関数
;HAVE_SKILL_OVERLAP(ARG,PU_SKILLNUM_GET(ARG,"戦術詠唱γ"))

;スキルゲージで役に立つ関数
;SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"戦術詠唱γ"))
;CALL SKILLGAGE_H_SET,ARG,PU_SKILLNUM_GET(ARG,"戦術詠唱γ"),0

;====================================================
;独自処理
;====================================================
;（予定）と付けられている方は仮置きの物。実際に採用されるかどうかはわからない。
@AUTO_PU_SKILL_戦術詠唱γ,ARG,ARGS,ARG:1,ARG:2
#DIM SKILL_RANK
#DIM SKILL_NUM
#DIM FC_FLAG,2
#DIM L_USER
;ARG = 攻撃者
;ARGS （ダメージ↑、被ダメージ↑、命中↑、回避↑、魔威↑、魔効↑、etc…）といった感じの奴
;ARG:1 = 防御者
;文字列を参考にする都合上、どの文字列を使うか事前に定めておいた方がいいかも
;CSTR:ARG:100 = EXTRA勢出典作品、『アークナイツ』とでも書き込む（予定）
;CSTR:ARG:101 = EXTRA勢出典作品でどのキャラなのか、とりあえず『アーミヤ（アークナイツ）』とでも書き込む（予定）
;CSTR:ARG:102 = 専用スキルその2のスキルランク変数として使用、この場合は『戦術詠唱γ』が対象（予定）
;CSTR:ARG:103 = 専用スキルその1のスキルランク変数として使用、この場合は『レーヴァテイン（ＡＮ）』が対象（予定）
;CSTR:ARG:104 = 専用スキルその3のスキルランク変数として使用、この場合は『ラグナロク（ＡＮ）』が対象（予定）
L_USER = -1
SELECTCASE ARGS
	CASE "ダメージ↑","魔法命中判定↑"
		SIF CSTR:ARG:101 == "アーミヤ"
			L_USER = ARG
	CASE "魔法回避判定↑","物理回避判定↑"
		SIF CSTR:(ARG:1):101 == "アーミヤ"
			L_USER = ARG:1
	;CASE "Hゲージ最大値","Hゲージ初期値","Dゲージ最大値"
	;	L_USER = ARG
ENDSELECT
SIF L_USER == -1
	RETURN 0
SKILL_RANK = TOINT(CSTR:L_USER:102)
SKILL_NUM = PU_SKILLNUM_GET(L_USER,"戦術詠唱γ")
;IF ARGS == "Hゲージ最大値"
;	;スキルHゲージの最大値を決める処理
;	SELECTCASE SKILL_RANK
;		CASE 1
;			RETURN 40
;		CASE 2
;			RETURN 35
;		CASE 3
;			RETURN 32
;		CASE 4
;			RETURN 30
;	ENDSELECT
;ELSEIF ARGS == "Hゲージ初期値"
;	;スキルHゲージの初期値を決める処理
;	SELECTCASE SKILL_RANK
;		CASE 1
;			RETURN 0
;		CASE 2
;			RETURN 5
;		CASE 3
;			RETURN 10
;		CASE 4
;			RETURN 15
;	ENDSELECT
;ELSEIF ARGS == "Dゲージ最大値"
;	;スキルDゲージの最大値を決める処理
;	RETURN 6
;ENDIF
IF SKILLGAGE_F_GET(L_USER,SKILL_NUM) < 0
	SELECTCASE ARGS
		CASE "ダメージ↑"
			;与ダメージとかに挟む場合の処理
			SELECTCASE SKILL_RANK
				CASE 1
					RETURN 10
				CASE IS > 1
					RETURN 20
			ENDSELECT
		CASE "魔法命中判定↑"
			;命中判定に挟む場合の処理
			SELECTCASE SKILL_RANK
				CASE 1,2
					RETURN 4
				CASE IS > 2
					RETURN 8
			ENDSELECT
		CASE "魔法回避判定↑"
			;命中判定に挟む場合の処理
			SELECTCASE SKILL_RANK
				CASE 1,2
					RETURN 4
				CASE IS > 2
					RETURN 8
			ENDSELECT
		CASE "物理回避判定↑"
			;命中判定に挟む場合の処理
			RETURN 4
	ENDSELECT
ENDIF
RETURN 0

@SHOW_SKILL_LIST_EXTRAMOVE,ARG,ARG:1
#LOCALSIZE 6
#DIM SKILL_STOCK,41
#DIM T_SKILL_COST
VARSET SKILL_STOCK
LOCAL:3 = 0	;表示個数
LOCAL:5 = CHARA_SKILLCOUNT(ARG)	;スキル数

;SHOW_SKILL_LISTを改造して借りています

;コマンドの表示
FOR LOCAL, 1, 42
	;IF CHARA_SKILLCOUNT(ARG) == 12
		LOCAL = LOCAL == LOCAL:5 + 1 ? 21 # LOCAL
	;ELSE
	;	LOCAL = LOCAL == FLAG:스킬수 + 1 ? 21 # LOCAL
	;ENDIF
	;スキル番号
	LOCAL:1 = ABL:ARG:@"\@ LOCAL > 20 ? 장비 # \@스킬{LOCAL > 20 ? LOCAL - 20 # LOCAL}"
	;IF CHARA_SKILLCOUNT(ARG) == 12
	;	SIF !LOCAL:1 && LOCAL > 12	;装備スキルの終端まで行ったら抜ける
		SIF !LOCAL:1 && LOCAL > LOCAL:5	;装備スキルの終端まで行ったら抜ける
			BREAK
	;ELSE
	;	SIF !LOCAL:1 && LOCAL > 8	;装備スキルの終端まで行ったら抜ける
	;		BREAK
	;ENDIF
	
	LOCAL:2 = !LOCAL:1	;CONTINUEフラグ
	CALLFORM SKILL_DECIDE_TYPE_{LOCAL:1}	;　1 EXTRA　2 MAGIC
	LOCAL:2 |= RESULT != ARG:1
	RESULT = 1
	TRYCALLFORM SKILL_ACTIONABLE_BATTLE_{LOCAL:1}
	SIF LOCAL:2 || !RESULT
		CONTINUE
		
	SIF MATCH(SKILL_STOCK,LOCAL:1)
		CONTINUE
	SKILL_STOCK:(LOCAL:3) = LOCAL:1
	CALLFORM SKILL_NAME_{LOCAL:1},ARG
	LOCALS = %RESULTS%
	CALLFORM SKILL_COSTTYPE_{LOCAL:1},ARG
	LOCAL:2 = RESULT	;コストタイプ
	CALLFORM SKILL_COST_{LOCAL:1},ARG
	;/////////素質追加項目
	LOCAL:4 = RESULT	;コストの量
	SIF LOCAL:2 == 2
		LOCAL:4 = LOCAL:4 * MAXBASE:ARG:ＨＰ / 100
	CALL COST_CUT, LOCAL:4, LOCAL:1, ARG
	LOCAL:4 = RESULT
	;/////////////////////
	CALLFORM CHECK_ACTIONABLE,ARG,LOCAL:1
	SIF !RESULT
		SETCOLOR 0x404040
	;----------------------------------------------------
	;正しく動作する保障がないので招来系は使用不可
	SIF LOCAL:1 == 2303 || LOCAL:1 == 529 || LOCAL:1 == 540
		SETCOLOR 0x404040
	;通常行動と追加行動の分を合わせたコストを足りているか調べる
	CALL TRUE_SKILL_COST,ARG,LOCAL:1
	T_SKILL_COST = RESULT
	RESULT = 0
	SIF CFLAG:ARG:입력행동 > -1
		CALL TRUE_SKILL_COST,ARG,CFLAG:ARG:입력행동
	SIF (LOCAL:2 == 2 && BASE:ARG:ＭＰ < RESULT + T_SKILL_COST) || (LOCAL:2 == 3 && BASE:ARG:ＨＰ < RESULT + T_SKILL_COST)
		SETCOLOR 0x404040
	;----------------------------------------------------
	IF GETBIT (FLAG:커스텀게임화면,1)
		PRINTFORM [{LOCAL, 2}] %LOCALS, 38, LEFT%{LOCAL:4, 3}\@ LOCAL:2 == 2 ? Ｈ # Ｍ \@Ｐ　　\@ !(++LOCAL:3 & 1) ? \n # \@
		;PRINTFORM [{LOCAL, 2}] %RESULTS, 38, LEFT%{LOCAL:4, 3}\@ LOCAL:2 == 2 ? Ｈ # Ｍ \@Ｐ　　\@ !(++LOCAL:3 & 1) ? \n # \@
		;PRINTFORM [{LOCAL, 2}] %RESULTS, 38, LEFT%{LOCAL:2 == 2 ? RESULT * MAXBASE:ARG:ＨＰ / 100 # RESULT, 3}\@ LOCAL:2 == 2 ? Ｈ # Ｍ \@Ｐ　　\@ !(++LOCAL:3 & 1) ? \n # \@
	ELSE
		PRINTFORM [{LOCAL, 2}] %LOCALS, 20, LEFT%{LOCAL:4, 3}\@ LOCAL:2 == 2 ? Ｈ # Ｍ \@Ｐ　　\@ !(++LOCAL:3 & 1) ? \n # \@
		;PRINTFORM [{LOCAL, 2}] %RESULTS, 20, LEFT%{LOCAL:4, 3}\@ LOCAL:2 == 2 ? Ｈ # Ｍ \@Ｐ　　\@ !(++LOCAL:3 & 1) ? \n # \@
		;PRINTFORM [{LOCAL, 2}] %RESULTS, 20, LEFT%{LOCAL:2 == 2 ? RESULT * MAXBASE:ARG:ＨＰ / 100 # RESULT, 3}\@ LOCAL:2 == 2 ? Ｈ # Ｍ \@Ｐ　　\@ !(++LOCAL:3 & 1) ? \n # \@
	ENDIF
	RESETCOLOR
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
PRINTL [ 0] ＣＡＮＣＥＬ
