;001	2021/05/XX		kamedakeisuke(ﾟдﾟ)			新規作成
;002	2021/06/05		kamedakeisuke(ﾟдﾟ)			エラー避けのおまじない追加
;003	2021/06/21		Collared					おまじない部分のミスを修正

;スキル名
@SKILL_NAME_情愛の奇想曲,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RESULTS = 애프터눈･티타임 B
;それ以外
ELSE
	RESULTS = 애정의 기상곡
ENDIF

;　명중
@SKILL_HITRATE_情愛の奇想曲, ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 999
;通常時
ELSE
	RETURN 100
ENDIF

;　위력
@SKILL_POWER_情愛の奇想曲, ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	;3回攻撃
	RETURN 215
ELSE
	RETURN 220
ENDIF

;クリティカル率
@SKILL_CRITICAL_RATE_情愛の奇想曲, ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 0
;それ以外
ELSE
	RETURN 0
ENDIF

;　최저공격횟수
@SKILL_MINATTACKNUMBER_情愛の奇想曲,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	;3回攻撃
	RETURN 3
;通常時
ELSE
	RETURN 1
ENDIF

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_情愛の奇想曲,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	;3回攻撃
	RETURN 3
;通常時
ELSE
	RETURN 1
ENDIF

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_情愛の奇想曲, ARG
RETURN 3
;消費なし＝1

;　코스트량
@SKILL_COST_情愛の奇想曲,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	;消費無し（代わりにゲージをゼロに）
	RETURN 0
;通常時
ELSE
	RETURN 20
ENDIF

;　상성
@SKILL_TYPE_情愛の奇想曲,ARG
#LOCALSSIZE 5
#LOCALSIZE 4
;CR展開時、複合属性
;타격・정신・万能のうち、最も効果のある相性で攻撃
IF SKILLGAGE_D_GET(ARG,3588)
	SIF ARG < 0 || 스킬속성표시중
		RETURN GET_TYPE_NUM("타격")
	SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
		RETURN GET_TYPE_NUM("정신")
	LOCAL = POS(CFLAG:ARG:타겟)
	SIF LOCAL < 0
		RETURN GET_TYPE_NUM("만능")
		
	LOCAL:2 = -300
	;全部-400とかないとおもうけど、あったらあれなんで、打撃をいれとく
	LOCALS = 타격
	LOCALS:1 = 타격
	LOCALS:2 = 정신
	LOCALS:3 = 만능
	FOR LOCAL:1, 1, 4
		;判定用に数値を仕舞う。反射は-200扱い
		LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
		LOCAL:3 = !(HAVE_SKILL(ARG,[[스킬:영세라이도우]]) || HAVE_SKILL(ARG,2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
		LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
		SELECTCASE LOCAL:1
			CASE 1 TO 2
				LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
			CASE 3
				LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
			CASEELSE
		ENDSELECT
		IF LOCAL:3 > LOCAL:2
			LOCAL:2 = LOCAL:3
			LOCALS = %LOCALS:(LOCAL:1)%
		ENDIF
	NEXT
	PRINT 속성상성：
	PRINTS LOCALS
	PRINTL ！
	RETURN GET_TYPE_NUM(LOCALS)
	
;通常時は精神
ELSE
	LOCALS = 정신
	RETURN GET_TYPE_NUM(LOCALS)
ENDIF

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_情愛の奇想曲,ARG
RETURN 1

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_情愛の奇想曲,ARG
RETURN 2


;　対象が1敵か2味方か3場全体か4特殊か
@SKILL_TARGET_情愛の奇想曲,ARG
RETURN 1

;　사거리
@SKILL_RANGE_情愛の奇想曲,ARG
RETURN 1

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_情愛の奇想曲,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 1
;通常時
ELSE
	RETURN 2
ENDIF

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_情愛の奇想曲,ARG
RETURN 1

;　 특수처리 여부 및 처리내용
;@SKILL_SPECIALEFECT_情愛の奇想曲,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
;SIF ARG == 1
;	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
;@SKILL_SUCCESSION_TYPE_情愛の奇想曲,ARG
;RETURN 23
;검격

;속도보정
@SKILL_SPEED_情愛の奇想曲, ARG = -1
RETURN 0

;;　口上の表示とそのタイプ
;@SKILL_MESSAGE_情愛の奇想曲,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_情愛の奇想曲, ARG = -1
RETURN 0

;没
;@SKILL_RANK_情愛の奇想曲
;@SKILL_RANKUP_情愛の奇想曲
;@계승불능_情愛の奇想曲

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
;@SKILL_ADDTIONAL_STATE_情愛の奇想曲,ARG
;RETURN GET_STATE_NUM("PANIC")

;추가효과 상성
;@SKILL_ADDTIONAL_TYPE_情愛の奇想曲,ARG
;RETURN 9

;추가효과 기본부여확률
;@SKILL_ADDTIONAL_BASE_RATE_情愛の奇想曲,ARG
;RETURN 40

;추가효과 부여확률상한
;@SKILL_ADDTIONAL_MAX_RATE_情愛の奇想曲,ARG
;RETURN 70

;;拡散攻撃用追加処理
;;ブレスなどの拡散攻撃に使う
;@SKILL_SPREAD_情愛の奇想曲,ARG
;RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_情愛の奇想曲,ARG,ARG:1,SKILL_NUM
#DIM SKILL_NUM
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	ALIGNMENT CENTER
	SETCOLOR 0xdc7b8c
	PRINTL 【ＣＬＩＭＡＸ　ＡＣＴ】
	RESETCOLOR
	ALIGNMENT LEFT

	;パワーアップイベント中かつボスが最終形態時のみ、ボスの不死フラグを剥がして攻撃
	IF GETBIT(이벤트플래그:93:16, 5) && (이벤트플래그:93:0 == 12 || 이벤트플래그:93:0 == 13) && (이벤트플래그:93:10 && [[キャラ:카오루코]])
		;HPが1まで削れていたら不死身フラグを外して攻撃
		SIF BASE:(CHARANUM-1):ＨＰ < 2
			CFLAG:(CHARANUM-1):불사신플래그 = 0
	ENDIF
	
	IF ARG:1 > 16
		CALL RANDOM_TARGET,ARG,SKILL_NUM
		ARG:1 = POS(CFLAG:ARG:타겟)
	ENDIF
	
	;ぶん殴る
	CALL ATTACK_SINGLE,ARG,ARG:1,SKILL_NUM
	
	;パワーアップイベント中かつボスが最終形態時のみ、撃破時に敵が喋る
	IF GETBIT(이벤트플래그:93:16, 5) && (이벤트플래그:93:0 == 12 || 이벤트플래그:93:0 == 13) && (이벤트플래그:93:10 && [[キャラ:카오루코]])

		;ボスのHPがぴったり1の場合、『すでに削り切ったけど、攻撃をミスってとどめを刺せていない』状況と見る
		IF BASE:(CHARANUM-1):ＨＰ == 1 && CFLAG:(CHARANUM-1):불사신플래그 < 1
			PRINTL
			SETCOLOR 0xdc7b8c
			PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓
			PRINTFORML ┃%"놓치지 않아！　후타바앗",72,LEFT%┃
			PRINTFORML ┃%"네가 안심하고、너만의 길을 걸을 수 있도록……",72,LEFT%┃
			PRINTFORML ┃%"으으으아아아아아아아아아아아아아아아아아아아아아아아아아아앗ー！！",72,LEFT%┃
			PRINTFORML ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
			PRINTL
			ALIGNMENT CENTER
			PRINTFORML ┏━━━━━━━━━━┓
			PRINTFORML ┃　　 　1more！　　　┃
			PRINTFORMW ┗━━━━━━━━━━┛
			RESETCOLOR
			ALIGNMENT LEFT
			
			;テイクツー
			CFLAG:(CHARANUM-1):불사신플래그 = 0
			CFLAG:ARG:베어내기플래그 = 1
			
			CALL ATTACK_SINGLE,ARG,ARG:1,SKILL_NUM
			
		ENDIF
		
		;ボス撃破
		IF BASE:(CHARANUM-1):ＨＰ < 1
			ALIGNMENT CENTER
			PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━━━━━━━━┓
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　어디까지라도　뒤쫓아갈꺼야　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　나는　샤를＝앙리・상송　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　마리아！　……너의 꽃길 끝에서、언젠가 반드시！　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
			PRINTL 
			ALIGNMENT LEFT
			
			SETCOLOR 0xdc7b8c
			PRINTL 　　　――꽃피워！　단 한 사람만을 위해　언젠가 져버릴　생명을！――
			SETCOLOR 0x815ca1
			PRINTL 　　　――꽃피워！　단 한 사람만을 위해　언젠가 져버릴　생명을！――
		ENDIF
	ENDIF

	ALIGNMENT CENTER
	SETCOLOR 0xdc7b8c
	PRINTW 【ＣＬＩＭＡＸ　ＲＥＶＵＥ／ＦＩＮＩＳＨ】
	PRINTL
	RESETCOLOR
	ALIGNMENT LEFT
	;クライマックスレヴュー終了
	;CALL SKILLGAGE_D_SET,ARG,3588,0

;それ以外
ELSE
	;相手をぶん殴る処理
	CALL ATTACK_FIELD,ARG,ARG:1,SKILL_NUM
	;自分の命中・回避強化を上げる処理
	CFLAG:ARG:명중강화 = MIN(32, CFLAG:ARG:명중강화 + 4)
	CFLAG:ARG:마법효과강화 = MIN(32, CFLAG:ARG:마법효과강화 + 4)
	;自分のみ物理反射
	CFLAG:ARG:물리반사플래그 = 1
	PRINTL ＞물리배리어 전개！
	PRINTW ＞자신의 명중・마법효과가 상승한다！
ENDIF

;====================================================
;실행내용 여기까지
;====================================================

;　説明文の表示
@SKILL_EXPLAIN_情愛の奇想曲,ARG
#LOCALSIZE 1
IF CFLAG:(CHARANUM-1):PT플래그 || U < 0 || U >= CHARANUM
	PRINTL 【통상시】
	PRINTL 적 1열에 정신 상성의 중 데미지＋자신의 명중・회피를 1단계 높이고、1턴 물리반사 배리어를 친다
	PRINTL 
	PRINTL 【클라이맥스・레뷰　발동시】
	PRINTL 적 1체에、마법계산의『타격・정신・만능』초고명중 ３HIT 초특대 데미지
ELSE
	;CR展開時
	IF SKILLGAGE_D_GET(U,3588)
		SETCOLOR 0x8ac4ec
		PRINTL 【ＣＬＩＭＡＸ　ＡＣＴ】
		RESETCOLOR
		PRINTL 오스트리아 마술의 극에 이른 마리아・안토니아의 필살기
		PRINTL 적 1체에、마법계산의『타격・정신・만능』초고명중 ３HIT 초특대 데미지
		;香子パワーアップイベント中かつボスが最終形態時のみ、撃ち放題
		IF GETBIT(이벤트플래그:93:16, 5) && (이벤트플래그:93:0 == 12 || 이벤트플래그:93:0 == 13) && (이벤트플래그:93:10 && [[キャラ:카오루코]])
			SETCOLOR 0x8ac4ec
			PRINTL 혼신의 일격으로 이 레뷰에 결착을！
			RESETCOLOR
		;通常
		ELSE
			PRINTL 사용하면 클라이맥스 레뷰는 종료된다
		ENDIF
	ELSE
		PRINTL 적 1열에 정신 상성의 중 데미지＋자신의 명중・회피를 1단계 높이고、1턴 물리반사 배리어를 친다
		PRINTL 클라이맥스 레뷰 발동 중에는 성능이 변화한다。
	ENDIF
ENDIF

;没
;@계승부위_情愛の奇想曲,ARG

;사용조건
@SKILL_SPECIAL_ACTIONABLE_情愛の奇想曲,ARG
RETURN 1
