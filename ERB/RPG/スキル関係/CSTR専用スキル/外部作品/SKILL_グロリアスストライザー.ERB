;スキル名
@SKILL_NAME_グロリアスストライザー, ARG
RESULTS = GLORIOUS STRIZER

;　명중
@SKILL_HITRATE_グロリアスストライザー, ARG
RETURN 200

;　위력
@SKILL_POWER_グロリアスストライザー, ARG
#DIM RGRN_F

RGRN_F = SKILLGAGE_F_GET(ARG, [[스킬:뇌격린]])

;コメントアウトにテンジアンお兄様降臨　別に参戦させようとして没った訳ではない
;用心堅固。アパシーマンのクードスは1200上限にする
;直前までアパシーで10000溜めれば……という軽慮浅謀は見逃さない
SIF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]] && RGRN_F > 1200
	RGRN_F = 1200

IF RGRN_F > 9999
	;最高値　一念通天、やってやれないことはない
	RETURN 800
ELSEIF RGRN_F > 8999
	;八色雷光
	RETURN 550
ELSEIF RGRN_F > 7999
	;焼灼拳とか風神撃とか　臥薪嘗胆、ここまで溜めれば実用に堪えるか
	RETURN 400
ELSEIF RGRN_F > 6999
	;지오다인
	RETURN 320
ELSEIF RGRN_F > 4999
	;지온가
	RETURN 225
ELSEIF RGRN_F > 2499
	;스파크볼트
	RETURN 180
ELSEIF RGRN_F > 1999
	;지오
	RETURN 130
ELSEIF RGRN_F > 1499
	;코우하/에이하
	RETURN 95
;クードス1000以上→
ELSEIF RGRN_F > 999
	;프린파　威力75。電子の謡精が歌い出してすぐでは大した効果にはなり得ない。残念無念
	RETURN 75
ELSEIF RGRN_F > 499
	;忠義シリーズの基礎値
	RETURN 56
ELSE
	RETURN 40
ENDIF

;　최저공격횟수
@SKILL_MINATTACKNUMBER_グロリアスストライザー, ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_グロリアスストライザー, ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_グロリアスストライザー, ARG
RETURN 3
;消費なし＝1

;　코스트량
@SKILL_COST_グロリアスストライザー, ARG
RETURN 58

;　상성
@SKILL_TYPE_グロリアスストライザー, ARG
#LOCALSSIZE 4
#LOCALSIZE 4
SIF ARG < 0 || 스킬속성표시중
	RETURN GET_TYPE_NUM("만능")
SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
	RETURN GET_TYPE_NUM("만능")
LOCAL = POS(CFLAG:ARG:타겟)
SIF LOCAL < 0 || CFLAG:LOCAL:마법반사플래그 || CFLAG:LOCAL:무효플래그
	RETURN GET_TYPE_NUM("만능")
LOCAL:2 = -1000
LOCALS = 만능
LOCALS:1 = 검격
LOCALS:2 = 전격
LOCALS:3 = 만능
FOR LOCAL:1, 1, 4
	;判定用に数値を仕舞う。反射は-200扱い
	LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
	LOCAL:3 = !(HAVE_SKILL(ARG, [[스킬:영세라이도우]]) || HAVE_SKILL(ARG,2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
	LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
	SELECTCASE LOCAL:1
		CASE 1
			LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
		CASE 2
			LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
		CASEELSE
	ENDSELECT
	IF LOCAL:3 > LOCAL:2
		LOCAL:2 = LOCAL:3
		LOCALS = %LOCALS:(LOCAL:1)%
	ENDIF
NEXT
RETURN GET_TYPE_NUM(LOCALS)

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_グロリアスストライザー, ARG
RETURN 1

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_グロリアスストライザー, ARG
RETURN 1

;　対象が1敵か2味方か3場全体か4特殊か
@SKILL_TARGET_グロリアスストライザー, ARG
RETURN 1

;　사거리
@SKILL_RANGE_グロリアスストライザー, ARG
RETURN 1

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_グロリアスストライザー, ARG
RETURN 1

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_グロリアスストライザー, ARG
RETURN 1

;속도보정
@SKILL_SPEED_グロリアスストライザー, ARG = -1
RETURN 0

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_グロリアスストライザー, ARG = -1
RETURN 0

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_グロリアスストライザー, ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_グロリアスストライザー, ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_グロリアスストライザー, ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_グロリアスストライザー, ARG
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_グロリアスストライザー, ARG, ARG:1, SKILL_NUM
#LOCALSIZE 4
#LOCALSSIZE 1
#DIM SKILL_NUM
#DIM RGRN_F
#DIM ボス殺したフラグ

RGRN_F = SKILLGAGE_F_GET(ARG, [[스킬:뇌격린]])

;報恩謝徳。スパークカリバー並びにグロリアスストライザーはシアノのスキルを参考にさせていただいた
LOCAL:0 = MAXBASE:ARG:공격
LOCAL:1 = CFLAG:ARG:공격강화

MAXBASE:ARG:공격 = MAXBASE:ARG:마법위력
IF RGRN_F > 7999
	CFLAG:ARG:공격강화 += CFLAG:ARG:마법위력강화
ELSE
	CFLAG:ARG:공격강화 = CFLAG:ARG:마법위력강화
ENDIF

LOCAL:2 = POS(ARG:1)
;LOCAL:3 = 0
;SIF CFLAG:(LOCAL:2):ボスフラグ == 1 && CFLAG:ARG:PTフラグ > 0
;	LOCAL:3 = 1

CALL SKILL_TYPE_グロリアスストライザー, ARG
PRINT 상성판정：
SELECTCASE RESULT
	CASE 0
		PRINT 검격
	CASE 6
		PRINT 전격
	CASE 17
		PRINT 만능
ENDSELECT
PRINTL ！
CALL ATTACK_SINGLE, ARG, LOCAL:2, SKILL_NUM

;IF CFLAG:ARG:PTフラグ > 0 && LOCAL:3 == 1
;	ボス殺したフラグ = 0
;	LOCALS = 포지션{ARG:1}
;	;この攻撃でボスが死んだら
;	IF FLAG:LOCALS == -1
;		ボス殺したフラグ += 1
;	ELSEIF GET_STATE(CFLAG:(FLAG:LOCALS):ステート) == "DYING"
;		ボス殺したフラグ += 1
;	ENDIF
;ENDIF

;善始善終。変動した値は元に戻す
MAXBASE:ARG:공격 = LOCAL:0
CFLAG:ARG:공격강화 = LOCAL:1

;IF ボス殺したフラグ > 0
;	PRINTFORMW SKILL FINISH　Kudos + 1000
;	SIF SKILLGAGE_F_GET(ARG, [[스킬:뇌격린]]) == 0
;		CALL SKILLGAGE_F_SET, ARG, [[스킬:뇌격린]], 1
;ENDIF
;鴉雀無声。謡精の歌唱はここで終わり
CALL SKILLGAGE_F_SET, ARG, [[스킬:SONG OF DIVA]], 0
;金科玉条。クードスは没収させてもらう
SIF SKILLGAGE_F_GET(ARG, [[스킬:뇌격린]]) > 0
	PRINTFORML ＞%CALLNAME:ARG%의 쿠도스가 리셋되었다！
CALL SKILLGAGE_F_SET, ARG, [[스킬:뇌격린]], 0

LOCAL = RAND:4
IF SKILL_CHANGE(ARG, [[キャラ:GV]]) && EQUIP:ARG:악세서리 == 5349 && LOCAL == 0
ELSE
	;規則厳守。SP3消費=ゲージDを12加算
	CALL SKILLGAGE_D_ADD, ARG, [[스킬:뇌격린]], 12
ENDIF

CALL SKILLGAGE_D_SET, ARG, SKILL_NUM, 4
;====================================================
;실행내용 여기까지
;====================================================

;　説明文の表示
@SKILL_EXPLAIN_グロリアスストライザー, ARG
PRINTFORML 뇌검의 진정한 힘을 해방해、궁극의 일격을 날리는 %CALLNAME:ARG%의 ＳＰ스킬。
PRINTL 　
PRINTL 적 1체에 검격과 전격과 만능의 복합속성으로 데미지를 준다。후열 사용 불가。
PRINTL 위력은 현재의 쿠도스에 따라 상승한다。
PRINTL 또한、물리공격이지만 데미지계산은 마법위력을 사용한다。
PRINTL 다만 사용 후에 쿠도스는 ０이 되며、재사용까지 ４턴의 쿨타임을 필요로 한다。
PRINTL ＭＰ와는 별도로 ＳＰ를３ 소비한다。
PRINTL ※ＳＰ는 ４턴에 １ 회복되고、전투종료 후에 전부 회복한다。
PRINTL ※스킬「뇌격린」을 소지하고 있지 않을 경우 사용 불가능。
;슈퍼 건볼트는 스트라이저를 쓰지 않기 때문에 SONG OF DIVA 중의 강화도 없음
;역시 이놈에게 쿠도스 소비회피는 주어지지 않음
IF EQUIP:ARG:악세서리 == 5349
	SETCOLOR 0x144ff4
	PRINTL 번개의렌즈 장비중　２５％의 확률로 ＳＰ소비를 회피
	RESETCOLOR
ENDIF

;사용조건
@SKILL_SPECIAL_ACTIONABLE_グロリアスストライザー, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG, "グロリアスストライザー")
SIF SKILLGAGE_D_GET(ARG, SKILL_NUM)
	RETURN 0
SIF !HAVE_SKILL(ARG, [[스킬:뇌격린]])
	RETURN 0
SIF SKILLGAGE_D_GET(ARG, [[스킬:뇌격린]]) != 0
	RETURN 0
RETURN 1

