;스킬명
@SKILL_NAME_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
;専用技が封印されている
IF CFLAG:ARG:998 == 0
	RESULTS = ？？？？？
ELSEIF TFLAG:전투중 == 0 && SKILLGAGE_F_GET(ARG,SKILL_NUM) == 0
	RESULTS = 設置スキル
ELSE
	SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
		CASE 1
			RESULTS = バリケード
		CASE 2
			RESULTS = バリケードLV2
		CASE 3
			RESULTS = バリケードLV3
		CASE 4
			RESULTS = 地雷
		CASE 5
			RESULTS = 空爆
		CASE 6
			RESULTS = 対地タレット
		CASE 7
			RESULTS = 対空タレット
		CASE 8
			RESULTS = スパイク
		CASEELSE
			IF SKILLGAGE_D_GET(ARG,SKILL_NUM) > 0 || SKILLGAGE_H_GET(ARG,SKILL_NUM) != 0
				RESULTS = 防衛設備作動中
			ELSE
				RESULTS = ＥＭＰＴＹ
			ENDIF
	ENDSELECT
ENDIF


;　명중
@SKILL_HITRATE_カードスキルB, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1
		;タレット掃射
		RETURN 100
	CASE -2
		;地雷爆発
		RETURN 160
	CASE -3
		;地面突き刺し
		RETURN 145
	CASE 1
		;バリケード
		RETURN 100
	CASE 2
		;バリケードLV2
		RETURN 100
	CASE 3
		;バリケードLV3
		RETURN 100
	CASE 4
		;地雷
		RETURN 160
	CASE 5
		;空爆
		RETURN 130
	CASE 6
		;対地タレット
		RETURN 100
	CASE 7
		;対空タレット
		RETURN 100
	CASE 8
		;スパイク
		RETURN 145
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 100
ENDSELECT

;　위력
@SKILL_POWER_カードスキルB, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1
		;タレット掃射
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 125
		RETURN 60
	CASE -2
		;地雷爆発
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 500
		RETURN 350
	CASE -3
		;地面突き刺し
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 300
		RETURN 200
	CASE 1
		;バリケード
		RETURN 0
	CASE 2
		;バリケードLV2
		RETURN 0
	CASE 3
		;バリケードLV3
		RETURN 0
	CASE 4
		;地雷(1체　800*125%=1000＋SHOCK　30～95)
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 500
		RETURN 350
	CASE 5
		;空爆(전체　3000*125%=3750＋FLAME　15～50)
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 400
		RETURN 300
	CASE 6
		;対地タレット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 125
		RETURN 60
	CASE 7
		;対空タレット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 125
		RETURN 60
	CASE 8
		;スパイク
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 300
		RETURN 200
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 100
ENDSELECT

;　クリティカル率
@SKILL_CRITICAL_RATE_カードスキルB, ARG
;空爆
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 5
	RETURN 3
RETURN 0

;　최저공격횟수
@SKILL_MINATTACKNUMBER_カードスキルB,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_カードスキルB,ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_カードスキルB, ARG
;ＥＭＰＴＹ
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 0
	RETURN 1
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SIF SKILLGAGE_D_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) > 0
	RETURN 0
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1, -2, -3
		;タレット掃射、地雷爆発、地面突き刺し
		RETURN 0
	CASE 1
		;バリケード
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 10
		RETURN 30
	CASE 2
		;バリケードLV2
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 20
		RETURN 50
	CASE 3
		;バリケードLV3
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 30
		RETURN 70
	CASE 4
		;地雷
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 15
		RETURN 30
	CASE 5
		;空爆
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 20
		RETURN 40
	CASE 6
		;対地タレット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 15
		RETURN 30
	CASE 7
		;対空タレット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 15
		RETURN 30
	CASE 8
		;スパイク
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 20
		RETURN 40
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 0
ENDSELECT

;　상성
@SKILL_TYPE_カードスキルB,ARG
#DIM SKILL_NUM
SIF ARG < 0
	RETURN GET_TYPE_NUM("비구")

SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1
		;タレット掃射
		RETURN GET_TYPE_NUM("만능")
	CASE -2
		;地雷爆発
		RETURN GET_TYPE_NUM("핵열")
	CASE -3
		;地面突き刺し
		RETURN GET_TYPE_NUM("지변")
	CASE 1
		;バリケード
		RETURN 0
	CASE 2
		;バリケードLV2
		RETURN 0
	CASE 3
		;バリケードLV3
		RETURN 0
	CASE 4
		;地雷
		RETURN GET_TYPE_NUM("핵열")
	CASE 5
		;空爆
		RETURN GET_TYPE_NUM("핵열")
	CASE 6
		;対地タレット
		RETURN GET_TYPE_NUM("만능")
	CASE 7
		;対空タレット
		RETURN GET_TYPE_NUM("만능")
	CASE 8
		;スパイク
		RETURN GET_TYPE_NUM("지변")
	CASEELSE
		;ＥＭＰＴＹ
		RETURN GET_TYPE_NUM("비구")
ENDSELECT

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_カードスキルB, ARG
RETURN 1

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_カードスキルB,ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1, -2, -3
		;タレット掃射、地雷爆発、地面突き刺し
		RETURN 1
	CASE 1
		;バリケード
		RETURN 2
	CASE 2
		;バリケードLV2
		RETURN 2
	CASE 3
		;バリケードLV3
		RETURN 2
	CASE 4
		;地雷
		RETURN 4
	CASE 5
		;空爆
		RETURN 1
	CASE 6
		;対地タレット
		RETURN 4
	CASE 7
		;対空タレット
		RETURN 4
	CASE 8
		;スパイク
		RETURN 4
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 4
ENDSELECT

;　사거리
@SKILL_RANGE_カードスキルB,ARG
RETURN 3

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1
		;タレット掃射
		RETURN 2
	CASE -2, -3
		;地雷爆発、地面突き刺し
		RETURN 1
	CASE 1
		;バリケード
		RETURN 2
	CASE 2
		;バリケードLV2
		RETURN 2
	CASE 3
		;バリケードLV3
		RETURN 2
	CASE 4
		;地雷
		RETURN 4
	CASE 5
		;空爆
		RETURN 3
	CASE 6
		;対地タレット
		RETURN 2
	CASE 7
		;対空タレット
		RETURN 2
	CASE 8
		;スパイク
		RETURN 4
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 4
ENDSELECT

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1
		;タレット掃射
		RETURN 1
	CASE -2, -3
		;地雷爆発、地面突き刺し
		RETURN 1
	CASE 1
		;バリケード
		RETURN 4
	CASE 2
		;バリケードLV2
		RETURN 4
	CASE 3
		;バリケードLV3
		RETURN 4
	CASE 4
		;地雷
		RETURN 4
	CASE 5
		;空爆
		RETURN 1
	CASE 6
		;対地タレット
		RETURN 4
	CASE 7
		;対空タレット
		RETURN 4
	CASE 8
		;スパイク
		RETURN 4
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 4
ENDSELECT

;　 특수처리 여부 및 처리내용
;@SKILL_SPECIALEFECT_カードスキルB,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
;SIF ARG == 1
;	RETURN 0
;아래쪽에 특수처리를 기술합니다

;속도보정
@SKILL_SPEED_カードスキルB, ARG = -1
;ＥＭＰＴＹ
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 0
	RETURN 0
;タレット掃射
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -1
	RETURN 1
;地雷爆発
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -2
	RETURN 1
;地面突き刺し
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -3
	RETURN 1
RETURN -1

;;　구상의 표시와 그 타입
;@SKILL_MESSAGE_カードスキルB,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_カードスキルB, ARG = -1
RETURN 0

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_カードスキルB,ARG
;地雷爆発
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -2
	RETURN GET_STATE_NUM("SHOCK")
;地面突き刺し
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -3
	RETURN GET_STATE_NUM("PANIC")
;空爆
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 5
	RETURN GET_STATE_NUM("FLAME")
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_カードスキルB,ARG
;地雷爆発
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -2
	RETURN 6
;地面突き刺し
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -3
	RETURN 9
;空爆
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 5
	RETURN 4
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_カードスキルB,ARG
;地雷爆発
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -2
	RETURN 30
;地面突き刺し
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -3
	RETURN 20
;空爆
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 5
	RETURN 15
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_カードスキルB,ARG
;地雷爆発
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -2
	RETURN 95
;地面突き刺し
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -3
	RETURN 75
;空爆
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 5
	RETURN 50
RETURN 0

;;拡散攻撃用追加処理
;;ブレスなどの拡散攻撃に使う
;@SKILL_SPREAD_カードスキルB,ARG
;RETURN 0

;====================================================
;トリガー発動条件
;====================================================
@SKILL_TRIGGER_ACTIONABLE_カードスキルB,ARG,ARG:1
SIF SKILLGAGE_D_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) <= 0
	RETURN 0
IF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 1
	RESULT:1 = 20
	SIF CFLAG:(ARG):PT플래그 == 0
		RESULT:1 = 17
ELSEIF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 2
	RESULT:1 = 21
	SIF CFLAG:(ARG):PT플래그 == 0
		RESULT:1 = 18
ENDIF
RETURN 1

;====================================================
;カウンター発動条件
;====================================================
@SKILL_COUNTER_ACTIONABLE_カードスキルB,ARG,ARG:1
SIF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 0
	RETURN 0
SIF ARG:1 < 0 || GET_STATE(CFLAG:(ARG:1):상태이상) == "DYING"
	RETURN 0
;イベント用　国境門
IF CFLAG:ARG:998 == 2 && GETCHARA([[キャラ:국경문]]) > -1
	SIF !GETBIT(CFLAG:GETCHARA([[キャラ:국경문]]):받음플래그,18) && !GETBIT(CFLAG:GETCHARA([[キャラ:국경문]]):받음플래그,19)
		RETURN 0
	SIF !CFLAG:GETCHARA([[キャラ:국경문]]):현데미지
		RETURN 0
	SIF INRANGE(CPOS(ARG:1), 1, 3) || INRANGE(CPOS(ARG:1), 7, 11)
		RETURN 1
ELSE
	SIF !GETBIT(CFLAG:ARG:받음플래그,18) && !GETBIT(CFLAG:ARG:받음플래그,19)
		RETURN 0
	SIF !CFLAG:ARG:현데미지
		RETURN 0
	SIF INRANGE(CPOS(ARG:1), 1, 3) || INRANGE(CPOS(ARG:1), 7, 11)
		RETURN 1
ENDIF
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_カードスキルB,ARG,ARG:1,SKILL_NUM
#DIM SKILL_NUM
#DIM TEMP
IF INRANGE(ARG:1, 1, 16)
	LOCAL = POS(ARG:1)
ELSE
	LOCAL = 0
ENDIF
LOCAL:1 = 0
LOCAL:2 = 0
LOCAL:3 = 0
LOCAL:10 = MAXBASE:ARG:공격
LOCAL:11 = MAXBASE:ARG:명중
LOCAL:12 = MAXBASE:ARG:마법위력
LOCAL:13 = MAXBASE:ARG:마법효과
IF EQUIP:ARG:총 != 0
	MAXBASE:ARG:공격 = MAXBASE:ARG:총공격
	MAXBASE:ARG:명중 = MAXBASE:ARG:총명중
	MAXBASE:ARG:마법위력 = MAXBASE:ARG:총공격
	MAXBASE:ARG:마법효과 = MAXBASE:ARG:총명중
ELSE
	MAXBASE:ARG:공격 /= 2
	MAXBASE:ARG:마법위력 /= 2
ENDIF

;タレット掃射
IF SKILLGAGE_D_GET(ARG,SKILL_NUM) > 0 && ( (CFLAG:ARG:PT플래그 > 0 && GROUPMATCH(ARG:1, 20, 21)) || (CFLAG:ARG:PT플래그 == 0 && GROUPMATCH(ARG:1, 17, 18)) )
	TEMP = SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,-1
	
	PRINT ＞
	IF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 1
		PRINT 対地
	ELSEIF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 2
		PRINT 対空
	ENDIF
	PRINTW タレットの掃射が始まった！
	
	SIF ARG == FLAG:행동순1
		FLAG:카운터중 = 0
	CALL ATTACK_FIELD,ARG,ARG:1,SKILL_NUM
	SIF ARG == FLAG:행동순1
		FLAG:카운터중 = 1
		
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,TEMP
;地雷爆発
ELSEIF SKILLGAGE_H_GET(ARG,SKILL_NUM) > 0 && INRANGE(ARG:1, 1, 16) && CFLAG:ARG:PT플래그 != CFLAG:(ARG:1):PT플래그
	TEMP = SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,-2
	
	PRINTW ＞地雷が爆発した！
	
	CALL SKILL_POWER_カードスキルB, ARG
	LOCAL:1 = RESULT
	CALLFORM SKILL_TYPE_カードスキルB, ARG
	LOCAL:2 = RESULT
	TRYCCALLFORM SKILL_CRITICAL_RATE_カードスキルB, ARG
		LOCAL:3 = RESULT
	CATCH
		LOCAL:3 = 5
	ENDCATCH
	PRINTFORM 　　TARGET:[{CPOS(LOCAL),2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
	CALL DAMAGE_1,ARG,LOCAL,LOCAL:1,LOCAL:2,LOCAL:3
	PRINTL 
	
	CALL SKILLGAGE_H_ADD,ARG,SKILL_NUM,-1
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,TEMP
;地面突き刺し
ELSEIF SKILLGAGE_H_GET(ARG,SKILL_NUM) < 0 && INRANGE(ARG:1, 1, 16) && CFLAG:ARG:PT플래그 != CFLAG:(ARG:1):PT플래그
	TEMP = SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,-3
	
	PRINTW ＞スパイクのトゲが敵を突き刺した！
	
	CALL SKILL_POWER_カードスキルB, ARG
	LOCAL:1 = RESULT
	CALLFORM SKILL_TYPE_カードスキルB, ARG
	LOCAL:2 = RESULT
	TRYCCALLFORM SKILL_CRITICAL_RATE_カードスキルB, ARG
		LOCAL:3 = RESULT
	CATCH
		LOCAL:3 = 5
	ENDCATCH
	PRINTFORM 　　TARGET:[{CPOS(LOCAL),2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
	CALL DAMAGE_1,ARG,LOCAL,LOCAL:1,LOCAL:2,LOCAL:3
	PRINTL 
	
	;CALL SKILLGAGE_H_ADD,ARG,SKILL_NUM,1
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,TEMP
ELSE
	SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
		CASE 1
			;バリケード
			CALL SKILL_WALL , ARG , "검격" , CFLAG:ARG:타겟 , 1, "非表示"
			CALL SKILL_WALL , ARG , "비구" , CFLAG:ARG:타겟 , 1, "非表示"
			CALL SKILL_WALL , ARG , "타격" , CFLAG:ARG:타겟 , 1, "非表示"
			CALL SKILL_WALL , ARG , "전술" , CFLAG:ARG:타겟 , 1, "非表示"
			
			LOCAL:1 = AUTO_SPLIT_INT("1_4_1_7_12_7" , "_" , CFLAG:ARG:타겟 - 17)
			LOCAL:2 = AUTO_SPLIT_INT("4_7_7_12_17_17" , "_" , CFLAG:ARG:타겟 - 17)
			FOR LOCAL:3, LOCAL:1 , LOCAL:2
				SIF POS(LOCAL:3) > -1
					PRINTFORML 　　TARGET:[{LOCAL:3,2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　剣撃、飛具、打撃、戦技を無効化！
			NEXT
			WAIT
		CASE 2
			;バリケードLV2
			CALL SKILL_WALL , ARG , "검격" , CFLAG:ARG:타겟 , 2, "非表示"
			CALL SKILL_WALL , ARG , "비구" , CFLAG:ARG:타겟 , 2, "非表示"
			CALL SKILL_WALL , ARG , "타격" , CFLAG:ARG:타겟 , 2, "非表示"
			CALL SKILL_WALL , ARG , "전술" , CFLAG:ARG:타겟 , 2, "非表示"
			
			LOCAL:1 = AUTO_SPLIT_INT("1_4_1_7_12_7" , "_" , CFLAG:ARG:타겟 - 17)
			LOCAL:2 = AUTO_SPLIT_INT("4_7_7_12_17_17" , "_" , CFLAG:ARG:타겟 - 17)
			FOR LOCAL:3, LOCAL:1 , LOCAL:2
				SIF POS(LOCAL:3) > -1
					PRINTFORML 　　TARGET:[{LOCAL:3,2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　剣撃、飛具、打撃、戦技を無効化！
			NEXT
			WAIT
		CASE 3
			;バリケードLV3
			CALL SKILL_WALL , ARG , "검격" , CFLAG:ARG:타겟 , 3, "非表示"
			CALL SKILL_WALL , ARG , "비구" , CFLAG:ARG:타겟 , 3, "非表示"
			CALL SKILL_WALL , ARG , "타격" , CFLAG:ARG:타겟 , 3, "非表示"
			CALL SKILL_WALL , ARG , "전술" , CFLAG:ARG:타겟 , 3, "非表示"
			
			LOCAL:1 = AUTO_SPLIT_INT("1_4_1_7_12_7" , "_" , CFLAG:ARG:타겟 - 17)
			LOCAL:2 = AUTO_SPLIT_INT("4_7_7_12_17_17" , "_" , CFLAG:ARG:타겟 - 17)
			FOR LOCAL:3, LOCAL:1 , LOCAL:2
				SIF POS(LOCAL:3) > -1
					PRINTFORML 　　TARGET:[{LOCAL:3,2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　剣撃、飛具、打撃、戦技を無効化！
			NEXT
			WAIT
		CASE 4
			;地雷
			CALL SKILLGAGE_H_ADD,ARG,SKILL_NUM,2
			PRINTFORMW 2つの地雷を設置した！
		CASE 5
			;空爆
			CALL ATTACK_FIELD,ARG,ARG:1,SKILL_NUM
		CASE 6
			;対地タレット
			CALL SKILLGAGE_H_SET,ARG,PU_SKILLNUM_GET(ARG,"カードスキルA"),1
			CALL SKILLGAGE_D_SET,ARG,SKILL_NUM,4
			PRINTFORMW 対地タレットを設置した！
		CASE 7
			;対空タレット
			CALL SKILLGAGE_H_SET,ARG,PU_SKILLNUM_GET(ARG,"カードスキルA"),2
			CALL SKILLGAGE_D_SET,ARG,SKILL_NUM,4
			PRINTFORMW 対空タレットを設置した！
		CASE 8
			;スパイク
			CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,-1
			PRINTFORMW スパイクを設置した！
		CASEELSE
			;ＥＭＰＴＹ
	ENDSELECT
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,0
ENDIF
MAXBASE:ARG:공격 = LOCAL:10
MAXBASE:ARG:명중 = LOCAL:11
MAXBASE:ARG:마법위력 = LOCAL:12
MAXBASE:ARG:마법효과 = LOCAL:13

;CALL ATTACK_SINGLE,ARG,POS(ARG:1),SKILL_NUM;	1체攻撃
;CALL ATTACK_FIELD,ARG,ARG:1,SKILL_NUM;			範囲攻撃(列、전체)
;CALL ATTACK_SPREAD,ARG,ARG:1,SKILL_NUM;		拡散攻撃
;CALL ATTACK_RAND,ARG,ARG:1,SKILL_NUM;			ランダム攻撃


;====================================================
;실행내용 여기까지
;====================================================

;　説明文の表示
@SKILL_EXPLAIN_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
;専用技が封印されている
IF CFLAG:ARG:998 == 0
	PRINTL （個別イベント専用スキル ＆ 個別イベント１～４をすべてクリアしたら…？）
ELSE
	SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
		CASE 1
			;バリケード
			PRINTL 仲間を守るバリケードを設置します
			PRINTL 味方1열への物理相性の攻撃をそれぞれ１回ずつ防ぐ壁を張る
		CASE 2
			;バリケードLV2
			PRINTL 仲間を守るバリケードを設置します
			PRINTL 味方1열への物理相性の攻撃をそれぞれ２回ずつ防ぐ壁を張る
		CASE 3
			;バリケードLV3
			PRINTL 仲間を守るバリケードを設置します
			PRINTL 味方1열への物理相性の攻撃をそれぞれ３回ずつ防ぐ壁を張る
		CASE 4
			;地雷
			PRINTL 攻撃してきた前列の敵にダメージを与える地雷を2つ設置します
			;イベント用
			IF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
				PRINT 적 1체에 核熱相性の특대 데미지＋SHOCK
				RESETCOLOR
				PRINTL 　クリティカルしない　スパイクと共存できない
			ELSE
				PRINTL 적 1체에 核熱相性の大ダメージ＋SHOCK　クリティカルしない　スパイクと共存できない
			ENDIF
		CASE 5
			;空爆
			PRINTL 広範囲の敵を攻撃する空爆を要請します
			;イベント用
			SIF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
			PRINT 적 전체에 核熱相性の大ダメージ＋FLAME
			RESETCOLOR
			PRINTL 　クリティカル率が低い
		CASE 6
			;対地タレット
			PRINTL 4턴 간、前列の敵に攻撃を行うタレットを設置します
			;イベント用
			IF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
				PRINT 自身と味方の行動後に発動し、적 전열에 万能相性のダメージ
				RESETCOLOR
				PRINTL 　クリティカルしない
			ELSE
				PRINTL 自身と味方の行動後に発動し、적 전열에 万能相性の極小ダメージ　クリティカルしない
			ENDIF
		CASE 7
			;対空タレット
			PRINTL 4턴 간、後列の敵に攻撃を行うタレットを設置します
			;イベント用
			IF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
				PRINT 自身と味方の行動後に発動し、敵後列に万能相性のダメージ
				RESETCOLOR
				PRINTL 　クリティカルしない
			ELSE
				PRINTL 自身と味方の行動後に発動し、敵後列に万能相性の極小ダメージ　クリティカルしない
			ENDIF
		CASE 8
			;スパイク
			PRINTL 攻撃してきた前列の敵に回数制限なくダメージを与えるスパイクを設置します
			;イベント用
			IF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
				PRINT 적 1체에 地変相性の大ダメージ＋PANIC
				RESETCOLOR
				PRINTL 　クリティカルしない　地雷と共存できない
			ELSE
				PRINTL 적 1체에 地変相性の中ダメージ＋PANIC　クリティカルしない　地雷と共存できない
			ENDIF
		CASEELSE
			;ＥＭＰＴＹ
			PRINTL 防衛設備を設置するスキル
			PRINTL 特定のカードを引くと、対応する設置スキルに切り替わります
	ENDSELECT
	SIF SKILLGAGE_F_GET(ARG,SKILL_NUM) > 0
		PRINTL このスキルは銃攻撃と銃命中でダメージを計算し、使用後は「ＥＭＰＴＹ」に戻ります
ENDIF

;사용조건
@SKILL_SPECIAL_ACTIONABLE_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
;専用技が封印されている
SIF CFLAG:ARG:998 == 0
	RETURN 0
SIF SKILLGAGE_F_GET(ARG, SKILL_NUM) > 0
	RETURN 1
RETURN 0
