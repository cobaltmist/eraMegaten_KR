;스킬명
@SKILL_NAME_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
;専用技が封印されている
IF CFLAG:ARG:998 == 0
	RESULTS = ？？？？？
ELSEIF TFLAG:전투중 == 0 && SKILLGAGE_F_GET(ARG,SKILL_NUM) == 0
	RESULTS = 설치스킬
ELSE
	SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
		CASE 1
			RESULTS = 바리케이트
		CASE 2
			RESULTS = 바리케이트LV2
		CASE 3
			RESULTS = 바리케이트LV3
		CASE 4
			RESULTS = 지뢰
		CASE 5
			RESULTS = 공습
		CASE 6
			RESULTS = 대지터렛
		CASE 7
			RESULTS = 대공터렛
		CASE 8
			RESULTS = 스파이크
		CASEELSE
			IF SKILLGAGE_D_GET(ARG,SKILL_NUM) > 0 || SKILLGAGE_H_GET(ARG,SKILL_NUM) != 0
				RESULTS = 방위설비 작동중
			ELSE
				RESULTS = ＥＭＰＴＹ
			ENDIF
	ENDSELECT
ENDIF


;　명중
@SKILL_HITRATE_カードスキルB, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1
		;タレット掃射
		RETURN 100
	CASE -2
		;地雷爆発
		RETURN 160
	CASE -3
		;地面突き刺し
		RETURN 145
	CASE 1
		;バリケード
		RETURN 100
	CASE 2
		;バリケードLV2
		RETURN 100
	CASE 3
		;バリケードLV3
		RETURN 100
	CASE 4
		;地雷
		RETURN 160
	CASE 5
		;空爆
		RETURN 130
	CASE 6
		;対地タレット
		RETURN 100
	CASE 7
		;対空タレット
		RETURN 100
	CASE 8
		;スパイク
		RETURN 145
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 100
ENDSELECT

;　위력
@SKILL_POWER_カードスキルB, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1
		;タレット掃射
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 125
		RETURN 60
	CASE -2
		;地雷爆発
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 500
		RETURN 350
	CASE -3
		;地面突き刺し
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 300
		RETURN 200
	CASE 1
		;バリケード
		RETURN 0
	CASE 2
		;バリケードLV2
		RETURN 0
	CASE 3
		;バリケードLV3
		RETURN 0
	CASE 4
		;地雷(単体　800*125%=1000＋SHOCK　30～95)
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 500
		RETURN 350
	CASE 5
		;空爆(全体　3000*125%=3750＋FLAME　15～50)
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 400
		RETURN 300
	CASE 6
		;対地タレット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 125
		RETURN 60
	CASE 7
		;対空タレット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 125
		RETURN 60
	CASE 8
		;スパイク
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 300
		RETURN 200
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 100
ENDSELECT

;　クリティカル率
@SKILL_CRITICAL_RATE_カードスキルB, ARG
;空爆
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 5
	RETURN 3
RETURN 0

;　최저공격횟수
@SKILL_MINATTACKNUMBER_カードスキルB,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_カードスキルB,ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_カードスキルB, ARG
;ＥＭＰＴＹ
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 0
	RETURN 1
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SIF SKILLGAGE_D_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) > 0
	RETURN 0
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1, -2, -3
		;タレット掃射、地雷爆発、地面突き刺し
		RETURN 0
	CASE 1
		;バリケード
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 10
		RETURN 30
	CASE 2
		;バリケードLV2
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 20
		RETURN 50
	CASE 3
		;バリケードLV3
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 30
		RETURN 70
	CASE 4
		;地雷
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 15
		RETURN 30
	CASE 5
		;空爆
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 20
		RETURN 40
	CASE 6
		;対地タレット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 15
		RETURN 30
	CASE 7
		;対空タレット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 15
		RETURN 30
	CASE 8
		;スパイク
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 20
		RETURN 40
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 0
ENDSELECT

;　상성
@SKILL_TYPE_カードスキルB,ARG
#DIM SKILL_NUM
SIF ARG < 0
	RETURN GET_TYPE_NUM("비구")

SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1
		;タレット掃射
		RETURN GET_TYPE_NUM("만능")
	CASE -2
		;地雷爆発
		RETURN GET_TYPE_NUM("핵열")
	CASE -3
		;地面突き刺し
		RETURN GET_TYPE_NUM("지변")
	CASE 1
		;バリケード
		RETURN 0
	CASE 2
		;バリケードLV2
		RETURN 0
	CASE 3
		;バリケードLV3
		RETURN 0
	CASE 4
		;地雷
		RETURN GET_TYPE_NUM("핵열")
	CASE 5
		;空爆
		RETURN GET_TYPE_NUM("핵열")
	CASE 6
		;対地タレット
		RETURN GET_TYPE_NUM("만능")
	CASE 7
		;対空タレット
		RETURN GET_TYPE_NUM("만능")
	CASE 8
		;スパイク
		RETURN GET_TYPE_NUM("지변")
	CASEELSE
		;ＥＭＰＴＹ
		RETURN GET_TYPE_NUM("비구")
ENDSELECT

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_カードスキルB, ARG
RETURN 1

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_カードスキルB,ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1, -2, -3
		;タレット掃射、地雷爆発、地面突き刺し
		RETURN 1
	CASE 1
		;バリケード
		RETURN 2
	CASE 2
		;バリケードLV2
		RETURN 2
	CASE 3
		;バリケードLV3
		RETURN 2
	CASE 4
		;地雷
		RETURN 4
	CASE 5
		;空爆
		RETURN 1
	CASE 6
		;対地タレット
		RETURN 4
	CASE 7
		;対空タレット
		RETURN 4
	CASE 8
		;スパイク
		RETURN 4
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 4
ENDSELECT

;　사거리
@SKILL_RANGE_カードスキルB,ARG
RETURN 3

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1
		;タレット掃射
		RETURN 2
	CASE -2, -3
		;地雷爆発、地面突き刺し
		RETURN 1
	CASE 1
		;バリケード
		RETURN 2
	CASE 2
		;バリケードLV2
		RETURN 2
	CASE 3
		;バリケードLV3
		RETURN 2
	CASE 4
		;地雷
		RETURN 4
	CASE 5
		;空爆
		RETURN 3
	CASE 6
		;対地タレット
		RETURN 2
	CASE 7
		;対空タレット
		RETURN 2
	CASE 8
		;スパイク
		RETURN 4
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 4
ENDSELECT

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE -1
		;タレット掃射
		RETURN 1
	CASE -2, -3
		;地雷爆発、地面突き刺し
		RETURN 1
	CASE 1
		;バリケード
		RETURN 4
	CASE 2
		;バリケードLV2
		RETURN 4
	CASE 3
		;バリケードLV3
		RETURN 4
	CASE 4
		;地雷
		RETURN 4
	CASE 5
		;空爆
		RETURN 1
	CASE 6
		;対地タレット
		RETURN 4
	CASE 7
		;対空タレット
		RETURN 4
	CASE 8
		;スパイク
		RETURN 4
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 4
ENDSELECT

;　 특수처리 여부 및 처리내용
;@SKILL_SPECIALEFECT_カードスキルB,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
;SIF ARG == 1
;	RETURN 0
;아래쪽에 특수처리를 기술합니다

;속도보정
@SKILL_SPEED_カードスキルB, ARG = -1
;ＥＭＰＴＹ
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 0
	RETURN 0
;タレット掃射
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -1
	RETURN 1
;地雷爆発
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -2
	RETURN 1
;地面突き刺し
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -3
	RETURN 1
RETURN -1

;;　구상의 표시와 그 타입
;@SKILL_MESSAGE_カードスキルB,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_カードスキルB, ARG = -1
RETURN 0

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_カードスキルB,ARG
;地雷爆発
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -2
	RETURN GET_STATE_NUM("SHOCK")
;地面突き刺し
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -3
	RETURN GET_STATE_NUM("PANIC")
;空爆
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 5
	RETURN GET_STATE_NUM("FLAME")
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_カードスキルB,ARG
;地雷爆発
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -2
	RETURN 6
;地面突き刺し
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -3
	RETURN 9
;空爆
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 5
	RETURN 4
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_カードスキルB,ARG
;地雷爆発
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -2
	RETURN 30
;地面突き刺し
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -3
	RETURN 20
;空爆
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 5
	RETURN 15
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_カードスキルB,ARG
;地雷爆発
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -2
	RETURN 95
;地面突き刺し
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == -3
	RETURN 75
;空爆
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 5
	RETURN 50
RETURN 0

;;拡散攻撃用追加処理
;;ブレスなどの拡散攻撃に使う
;@SKILL_SPREAD_カードスキルB,ARG
;RETURN 0

;====================================================
;トリガー発動条件
;====================================================
@SKILL_TRIGGER_ACTIONABLE_カードスキルB,ARG,ARG:1
SIF SKILLGAGE_D_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) <= 0
	RETURN 0
IF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 1
	RESULT:1 = 20
	SIF CFLAG:(ARG):PT플래그 == 0
		RESULT:1 = 17
ELSEIF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 2
	RESULT:1 = 21
	SIF CFLAG:(ARG):PT플래그 == 0
		RESULT:1 = 18
ENDIF
RETURN 1

;====================================================
;カウンター発動条件
;====================================================
@SKILL_COUNTER_ACTIONABLE_カードスキルB,ARG,ARG:1
SIF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルB")) == 0
	RETURN 0
SIF ARG:1 < 0 || GET_STATE(CFLAG:(ARG:1):상태이상) == "DYING"
	RETURN 0
;イベント用　国境門
IF CFLAG:ARG:998 == 2 && GETCHARA([[キャラ:국경문]]) > -1
	SIF !GETBIT(CFLAG:GETCHARA([[キャラ:국경문]]):받음플래그,18) && !GETBIT(CFLAG:GETCHARA([[キャラ:국경문]]):받음플래그,19)
		RETURN 0
	SIF !CFLAG:GETCHARA([[キャラ:국경문]]):현데미지
		RETURN 0
	SIF INRANGE(CPOS(ARG:1), 1, 3) || INRANGE(CPOS(ARG:1), 7, 11)
		RETURN 1
ELSE
	SIF !GETBIT(CFLAG:ARG:받음플래그,18) && !GETBIT(CFLAG:ARG:받음플래그,19)
		RETURN 0
	SIF !CFLAG:ARG:현데미지
		RETURN 0
	SIF INRANGE(CPOS(ARG:1), 1, 3) || INRANGE(CPOS(ARG:1), 7, 11)
		RETURN 1
ENDIF
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_カードスキルB,ARG,ARG:1,SKILL_NUM
#DIM SKILL_NUM
#DIM TEMP
IF INRANGE(ARG:1, 1, 16)
	LOCAL = POS(ARG:1)
ELSE
	LOCAL = 0
ENDIF
LOCAL:1 = 0
LOCAL:2 = 0
LOCAL:3 = 0
LOCAL:10 = MAXBASE:ARG:공격
LOCAL:11 = MAXBASE:ARG:명중
LOCAL:12 = MAXBASE:ARG:마법위력
LOCAL:13 = MAXBASE:ARG:마법효과
IF EQUIP:ARG:총 != 0
	MAXBASE:ARG:공격 = MAXBASE:ARG:총공격
	MAXBASE:ARG:명중 = MAXBASE:ARG:총명중
	MAXBASE:ARG:마법위력 = MAXBASE:ARG:총공격
	MAXBASE:ARG:마법효과 = MAXBASE:ARG:총명중
ELSE
	MAXBASE:ARG:공격 /= 2
	MAXBASE:ARG:마법위력 /= 2
ENDIF

;タレット掃射
IF SKILLGAGE_D_GET(ARG,SKILL_NUM) > 0 && ( (CFLAG:ARG:PT플래그 > 0 && GROUPMATCH(ARG:1, 20, 21)) || (CFLAG:ARG:PT플래그 == 0 && GROUPMATCH(ARG:1, 17, 18)) )
	TEMP = SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,-1
	
	PRINT ＞
	IF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 1
		PRINT 대지
	ELSEIF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 2
		PRINT 대공
	ENDIF
	PRINTW 터렛의 사격이 시작되었다！
	
	SIF ARG == FLAG:행동순1
		FLAG:카운터중 = 0
	CALL ATTACK_FIELD,ARG,ARG:1,SKILL_NUM
	SIF ARG == FLAG:행동순1
		FLAG:카운터중 = 1
		
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,TEMP
;地雷爆発
ELSEIF SKILLGAGE_H_GET(ARG,SKILL_NUM) > 0 && INRANGE(ARG:1, 1, 16) && CFLAG:ARG:PT플래그 != CFLAG:(ARG:1):PT플래그
	TEMP = SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,-2
	
	PRINTW ＞지뢰가 폭발했다！
	
	CALL SKILL_POWER_カードスキルB, ARG
	LOCAL:1 = RESULT
	CALLFORM SKILL_TYPE_カードスキルB, ARG
	LOCAL:2 = RESULT
	TRYCCALLFORM SKILL_CRITICAL_RATE_カードスキルB, ARG
		LOCAL:3 = RESULT
	CATCH
		LOCAL:3 = 5
	ENDCATCH
	PRINTFORM 　　TARGET:[{CPOS(LOCAL),2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
	CALL DAMAGE_1,ARG,LOCAL,LOCAL:1,LOCAL:2,LOCAL:3
	PRINTL 
	
	CALL SKILLGAGE_H_ADD,ARG,SKILL_NUM,-1
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,TEMP
;地面突き刺し
ELSEIF SKILLGAGE_H_GET(ARG,SKILL_NUM) < 0 && INRANGE(ARG:1, 1, 16) && CFLAG:ARG:PT플래그 != CFLAG:(ARG:1):PT플래그
	TEMP = SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,-3
	
	PRINTW ＞스파이크의 가시가 적을 찔렀다！
	
	CALL SKILL_POWER_カードスキルB, ARG
	LOCAL:1 = RESULT
	CALLFORM SKILL_TYPE_カードスキルB, ARG
	LOCAL:2 = RESULT
	TRYCCALLFORM SKILL_CRITICAL_RATE_カードスキルB, ARG
		LOCAL:3 = RESULT
	CATCH
		LOCAL:3 = 5
	ENDCATCH
	PRINTFORM 　　TARGET:[{CPOS(LOCAL),2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
	CALL DAMAGE_1,ARG,LOCAL,LOCAL:1,LOCAL:2,LOCAL:3
	PRINTL 
	
	;CALL SKILLGAGE_H_ADD,ARG,SKILL_NUM,1
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,TEMP
ELSE
	SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
		CASE 1
			;バリケード
			CALL SKILL_WALL , ARG , "검격" , CFLAG:ARG:타겟 , 1, "非表示"
			CALL SKILL_WALL , ARG , "비구" , CFLAG:ARG:타겟 , 1, "非表示"
			CALL SKILL_WALL , ARG , "타격" , CFLAG:ARG:타겟 , 1, "非表示"
			CALL SKILL_WALL , ARG , "전술" , CFLAG:ARG:타겟 , 1, "非表示"
			
			LOCAL:1 = AUTO_SPLIT_INT("1_4_1_7_12_7" , "_" , CFLAG:ARG:타겟 - 17)
			LOCAL:2 = AUTO_SPLIT_INT("4_7_7_12_17_17" , "_" , CFLAG:ARG:타겟 - 17)
			FOR LOCAL:3, LOCAL:1 , LOCAL:2
				SIF POS(LOCAL:3) > -1
					PRINTFORML 　　TARGET:[{LOCAL:3,2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　검격、비구、타격、전술을 무효화！
			NEXT
			WAIT
		CASE 2
			;バリケードLV2
			CALL SKILL_WALL , ARG , "검격" , CFLAG:ARG:타겟 , 2, "非表示"
			CALL SKILL_WALL , ARG , "비구" , CFLAG:ARG:타겟 , 2, "非表示"
			CALL SKILL_WALL , ARG , "타격" , CFLAG:ARG:타겟 , 2, "非表示"
			CALL SKILL_WALL , ARG , "전술" , CFLAG:ARG:타겟 , 2, "非表示"
			
			LOCAL:1 = AUTO_SPLIT_INT("1_4_1_7_12_7" , "_" , CFLAG:ARG:타겟 - 17)
			LOCAL:2 = AUTO_SPLIT_INT("4_7_7_12_17_17" , "_" , CFLAG:ARG:타겟 - 17)
			FOR LOCAL:3, LOCAL:1 , LOCAL:2
				SIF POS(LOCAL:3) > -1
					PRINTFORML 　　TARGET:[{LOCAL:3,2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　검격、비구、타격、전술을 무효화！
			NEXT
			WAIT
		CASE 3
			;バリケードLV3
			CALL SKILL_WALL , ARG , "검격" , CFLAG:ARG:타겟 , 3, "非表示"
			CALL SKILL_WALL , ARG , "비구" , CFLAG:ARG:타겟 , 3, "非表示"
			CALL SKILL_WALL , ARG , "타격" , CFLAG:ARG:타겟 , 3, "非表示"
			CALL SKILL_WALL , ARG , "전술" , CFLAG:ARG:타겟 , 3, "非表示"
			
			LOCAL:1 = AUTO_SPLIT_INT("1_4_1_7_12_7" , "_" , CFLAG:ARG:타겟 - 17)
			LOCAL:2 = AUTO_SPLIT_INT("4_7_7_12_17_17" , "_" , CFLAG:ARG:타겟 - 17)
			FOR LOCAL:3, LOCAL:1 , LOCAL:2
				SIF POS(LOCAL:3) > -1
					PRINTFORML 　　TARGET:[{LOCAL:3,2}] %CALLNAME:POS(LOCAL:3),20,LEFT%　>>>>>>　검격、비구、타격、전술을 무효화！
			NEXT
			WAIT
		CASE 4
			;地雷
			CALL SKILLGAGE_H_ADD,ARG,SKILL_NUM,2
			PRINTFORMW 2개의 지뢰를 설치했다！
		CASE 5
			;空爆
			CALL ATTACK_FIELD,ARG,ARG:1,SKILL_NUM
		CASE 6
			;対地タレット
			CALL SKILLGAGE_H_SET,ARG,PU_SKILLNUM_GET(ARG,"カードスキルA"),1
			CALL SKILLGAGE_D_SET,ARG,SKILL_NUM,4
			PRINTFORMW 대지터렛을 설치했다！
		CASE 7
			;対空タレット
			CALL SKILLGAGE_H_SET,ARG,PU_SKILLNUM_GET(ARG,"カードスキルA"),2
			CALL SKILLGAGE_D_SET,ARG,SKILL_NUM,4
			PRINTFORMW 대공터렛을 설치했다！
		CASE 8
			;スパイク
			CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,-1
			PRINTFORMW 스파이크를 설치했다！
		CASEELSE
			;ＥＭＰＴＹ
	ENDSELECT
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,0
ENDIF
MAXBASE:ARG:공격 = LOCAL:10
MAXBASE:ARG:명중 = LOCAL:11
MAXBASE:ARG:마법위력 = LOCAL:12
MAXBASE:ARG:마법효과 = LOCAL:13

;CALL ATTACK_SINGLE,ARG,POS(ARG:1),SKILL_NUM;	単体攻撃
;CALL ATTACK_FIELD,ARG,ARG:1,SKILL_NUM;			範囲攻撃(列、全体)
;CALL ATTACK_SPREAD,ARG,ARG:1,SKILL_NUM;		拡散攻撃
;CALL ATTACK_RAND,ARG,ARG:1,SKILL_NUM;			ランダム攻撃


;====================================================
;실행내용 여기까지
;====================================================

;　説明文の表示
@SKILL_EXPLAIN_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
;専用技が封印されている
IF CFLAG:ARG:998 == 0
	PRINTL （개별이벤트 전용스킬 ＆ 개별이벤트 １～４를 모두 클리어하면…？）
ELSE
	SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
		CASE 1
			;バリケード
			PRINTL 동료를 지키는 바리케이트를 설치합니다
			PRINTL 아군 1열에의 물리상성의 공격을 각각 １회씩 막는 벽을 친다
		CASE 2
			;バリケードLV2
			PRINTL 동료를 지키는 바리케이트를 설치합니다
			PRINTL 아군 1열에의 물리상성의 공격을 각각 ２회씩 막는 벽을 친다
		CASE 3
			;バリケードLV3
			PRINTL 동료를 지키는 바리케이트를 설치합니다
			PRINTL 아군 1열에의 물리상성의 공격을 각각 ３회씩 막는 벽을 친다
		CASE 4
			;地雷
			PRINTL 공격해 온 전열의 적에게 데미지를 주는 지뢰를 2개 설치합니다
			;イベント用
			IF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
				PRINT 적 1체에 핵열 상성의 특대 데미지＋SHOCK
				RESETCOLOR
				PRINTL 　크리티컬하지 않는다　스파이크와 공존할 수 없다
			ELSE
				PRINTL 적 1체에 핵열 상성의 대 데미지＋SHOCK　크리티컬하지 않음　스파이크와 공존할 수 없다
			ENDIF
		CASE 5
			;空爆
			PRINTL 광범위의 적을 공격하는 공습을 요청합니다
			;イベント用
			SIF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
			PRINT 적 전체에 핵열 상성의 대 데미지＋FLAME
			RESETCOLOR
			PRINTL 　크리티컬률이 낮다
		CASE 6
			;対地タレット
			PRINTL 4턴간、전열의 적에게 공격을 하는 터렛을 설치합니다
			;イベント用
			IF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
				PRINT 자신과 아군의 행동 후에 발동하고、적 전열에 만능 상성의 데미지
				RESETCOLOR
				PRINTL 　크리티컬하지 않는다
			ELSE
				PRINTL 자신과 아군의 행동 후에 발동하고、적 전열에 만능 상성의 극소 데미지　크리티컬하지 않는다
			ENDIF
		CASE 7
			;対空タレット
			PRINTL 4턴간、후열의 적에게 공격을 하는 터렛을 설치합니다
			;イベント用
			IF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
				PRINT 자신과 아군의 행동 후에 발동하고、적 후열에 만능 상성의 데미지
				RESETCOLOR
				PRINTL 　크리티컬하지 않는다
			ELSE
				PRINTL 자신과 아군의 행동 후에 발동하고、적 후열에 만능 상성의 극소 데미지　크리티컬하지 않는다
			ENDIF
		CASE 8
			;スパイク
			PRINTL 공격해 온 전열의 적에게 횟수 제한 없이 데미지를 스파이크를 설치합니다
			;イベント用
			IF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
				PRINT 적 1체에 지변 상성의 대 데미지＋PANIC
				RESETCOLOR
				PRINTL 　크리티컬하지 않는다　지뢰와 공존할 수 없다
			ELSE
				PRINTL 적 1체에 지변 상성의 중 데미지＋PANIC　크리티컬하지 않는다　지뢰와 공존할 수 없다
			ENDIF
		CASEELSE
			;ＥＭＰＴＹ
			PRINTL 방위설비를 설치하는 스킬
			PRINTL 특정 카드를 뽑으면、대응하는 설치스킬로 전환됩니다
	ENDSELECT
	SIF SKILLGAGE_F_GET(ARG,SKILL_NUM) > 0
		PRINTL 이 스킬은 총공격과 총명중으로 데미지를 계산하고、사용 후에는「ＥＭＰＴＹ」로 되돌아갑니다
ENDIF

;사용조건
@SKILL_SPECIAL_ACTIONABLE_カードスキルB,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルB")
;専用技が封印されている
SIF CFLAG:ARG:998 == 0
	RETURN 0
SIF SKILLGAGE_F_GET(ARG, SKILL_NUM) > 0
	RETURN 1
RETURN 0
