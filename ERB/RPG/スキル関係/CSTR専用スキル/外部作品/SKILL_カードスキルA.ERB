;스킬명
@SKILL_NAME_カードスキルA,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルA")
;専用技が封印されている
IF CFLAG:ARG:998 == 0
	RESULTS = ？？？？？
ELSEIF TFLAG:전투중 == 0 && SKILLGAGE_F_GET(ARG,SKILL_NUM) == 0
	RESULTS = 무기스킬
ELSE
	SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
		CASE 1
			RESULTS = 와이드 샷
		CASE 2
			RESULTS = 버즈소 샷
		CASE 3
			RESULTS = 시저스 샷
		CASE 4
			RESULTS = 크레딧 샷
		CASE 5
			RESULTS = 리페어 샷
		CASE 6
			RESULTS = 전열포
		CASE 7
			RESULTS = 파일 샷
		CASEELSE
			RESULTS = ＥＭＰＴＹ
	ENDSELECT
ENDIF


;　명중
@SKILL_HITRATE_カードスキルA, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルA")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE 1
		;ワイドショット
		RETURN 140
	CASE 2
		;バズソーショット
		SIF FLAG:확산플래그
			RETURN 100
		RETURN 120
	CASE 3
		;シザーズショット
		RETURN 160
	CASE 4
		;クレジットショット
		RETURN 100
	CASE 5
		;リペアショット
		RETURN 100
	CASE 6
		;電熱砲
		IF FLAG:확산플래그 == 3
			RETURN 220
		ELSEIF FLAG:확산플래그 == 2
			RETURN 200
		ELSEIF FLAG:확산플래그 == 1
			RETURN 190
		ELSE
			RETURN 180
		ENDIF
	CASE 7
		;パイルショット
		RETURN 150
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 100
ENDSELECT

;　위력
@SKILL_POWER_カードスキルA, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルA")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE 1
		;ワイドショット(一列　1500)
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 350
		RETURN 300
	CASE 2
		;バズソーショット(拡散　1450)
		;イベント用
		IF CFLAG:ARG:998 == 2
			SIF FLAG:확산플래그
				RETURN 400
			RETURN 450
		ELSE
			SIF FLAG:확산플래그
				RETURN 350
			RETURN 400
		ENDIF
	CASE 3
		;シザーズショット(敵の前列と後列を貫く　900*125%=1125)
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 500
		RETURN 450
	CASE 4
		;クレジットショット(一列　１～５回　特殊通貨の量による　3750)
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 200
		RETURN 150
	CASE 5
		;リペアショット(回復　150)
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 200
		RETURN 150
	CASE 6
		;電熱砲(全体～単体　切り札　マルフーシャ専用　800～5000*125%=1000～6250)
		;相性貫通ダメージアップ＋防御半減
		;『溶鉄のマルフーシャ』はラスボスも電熱砲で一発で倒せる
		;イベント用
		IF CFLAG:ARG:998 == 2
			;１体＋相性貫通
			IF FLAG:확산플래그 == 3
				RETURN 2000
			;１体 or ２～３体＋相性貫通
			ELSEIF FLAG:확산플래그 == 2
				RETURN 1500
			;２～３体 or 相性貫通
			ELSEIF FLAG:확산플래그 == 1
				RETURN 999
			ELSE
				RETURN 800
			ENDIF
		ELSE
			;１体＋相性貫通
			IF FLAG:확산플래그 == 3
				RETURN 999
			;１体 or ２～３体＋相性貫通
			ELSEIF FLAG:확산플래그 == 2
				RETURN 800
			;２～３体 or 相性貫通
			ELSEIF FLAG:확산플래그 == 1
				RETURN 650
			ELSE
				RETURN 500
			ENDIF
		ENDIF
	CASE 7
		;パイルショット(単体　250*3*125%=937.5)
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 350
		RETURN 250
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 100
ENDSELECT

;　クリティカル率
@SKILL_CRITICAL_RATE_カードスキルA, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルA")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE 1
		;ワイドショット
		RETURN 6
	CASE 2
		;バズソーショット
		RETURN 10
	CASE 3
		;シザーズショット
		RETURN 4
	CASE 4
		;クレジットショット
		RETURN 6
	CASE 5
		;リペアショット
		RETURN 0
	CASE 6
		;電熱砲
		RETURN 1
	CASE 7
		;パイルショット
		RETURN 8
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 5
ENDSELECT

;　최저공격횟수
@SKILL_MINATTACKNUMBER_カードスキルA,ARG
;クレジットショット
IF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 4
	SELECTCASE SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カード購入"))
		CASE IS >= 100
			RETURN 5
		CASE IS >= 60
			RETURN 4
		CASE IS >= 30
			RETURN 3
		CASE IS >= 10
			RETURN 2
	ENDSELECT
ENDIF
;パイルショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 7
	RETURN 3
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_カードスキルA,ARG
;クレジットショット
IF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 4
	SELECTCASE SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カード購入"))
		CASE IS >= 100
			RETURN 5
		CASE IS >= 60
			RETURN 4
		CASE IS >= 30
			RETURN 3
		CASE IS >= 10
			RETURN 2
	ENDSELECT
ENDIF
;パイルショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 7
	RETURN 3
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_カードスキルA, ARG
;ＥＭＰＴＹ
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 0
	RETURN 1
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_カードスキルA,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルA")
SIF SKILLGAGE_D_GET(ARG,SKILL_NUM) > 0
	RETURN 0
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE 1
		;ワイドショット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 15
		RETURN 30
	CASE 2
		;バズソーショット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 15
		RETURN 30
	CASE 3
		;シザーズショット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 15
		RETURN 30
	CASE 4
		;クレジットショット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 20
		RETURN 40
	CASE 5
		;リペアショット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 10
		RETURN 20
	CASE 6
		;電熱砲
		;イベント用
		IF CFLAG:ARG:998 == 2 && GETCHARA([[キャラ:국경문]]) > -1
			IF MAXBASE:ARG:ＭＰ < 100
				IF BASE:ARG:ＭＰ < MAXBASE:ARG:ＭＰ && BASE:GETCHARA([[キャラ:국경문]]):ＨＰ < MAXBASE:GETCHARA([[キャラ:국경문]]):ＨＰ / 3
					RETURN BASE:ARG:ＭＰ
				ELSE
					RETURN MAXBASE:ARG:ＭＰ
				ENDIF
			ELSEIF BASE:ARG:ＭＰ < 100 && BASE:GETCHARA([[キャラ:국경문]]):ＨＰ < MAXBASE:GETCHARA([[キャラ:국경문]]):ＨＰ / 3
				RETURN BASE:ARG:ＭＰ
			ELSE
				RETURN 100
			ENDIF
		ELSE
			IF MAXBASE:ARG:ＭＰ > 50 && MAXBASE:ARG:ＭＰ < 150
				IF BASE:ARG:ＭＰ < MAXBASE:ARG:ＭＰ && BASE:ARG:ＨＰ < MAXBASE:ARG:ＨＰ / 3
					RETURN BASE:ARG:ＭＰ
				ELSE
					RETURN MAXBASE:ARG:ＭＰ
				ENDIF
			ELSEIF BASE:ARG:ＭＰ < 150 && BASE:ARG:ＨＰ < MAXBASE:ARG:ＨＰ / 3
				RETURN BASE:ARG:ＭＰ
			ELSE
				RETURN 150
			ENDIF
		ENDIF
	CASE 7
		;パイルショット
		;イベント用
		SIF CFLAG:ARG:998 == 2
			RETURN 20
		RETURN 40
	CASEELSE
		;ＥＭＰＴＹ
		RETURN 0
ENDSELECT

;　상성
@SKILL_TYPE_カードスキルA,ARG
#DIM SKILL_NUM
#LOCALSSIZE 3
#LOCALSIZE 4
SIF ARG < 0
	RETURN GET_TYPE_NUM("비구")

SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルA")
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE 1
		;ワイドショット
		RETURN GET_TYPE_NUM("비구")
	CASE 2
		;バズソーショット
		RETURN GET_TYPE_NUM("검격")
	CASE 3
		;シザーズショット
		RETURN GET_TYPE_NUM("핵열")
	CASE 4
		;クレジットショット
		RETURN GET_TYPE_NUM("비구")
	CASE 5
		;リペアショット
		RETURN 0
	CASE 6
		;電熱砲
		SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
			RETURN GET_TYPE_NUM("핵열")
		LOCAL = POS(CFLAG:ARG:타겟)
		SIF LOCAL < 0 || CFLAG:LOCAL:마법반사플래그 || CFLAG:LOCAL:무효플래그
			RETURN GET_TYPE_NUM("핵열")
		;溶鉄のマルフーシャ
		IF HAVE_SKILL(ARG, [[스킬:핵열부스터]])
			LOCAL:2 = 125
		ELSE
			LOCAL:2 = 100
		ENDIF
		LOCALS = 核熱
		LOCALS:1 = 火炎
		LOCALS:2 = 核熱
		FOR LOCAL:1, 0, 3
			;判定用に数値を仕舞う。反射は-200扱い
			LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
			LOCAL:3 = !(HAVE_SKILL(ARG, [[스킬:영세라이도우]]) || HAVE_SKILL(ARG,2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
			LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
			SELECTCASE LOCAL:1
				CASE 1
					LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
				CASE 2
					LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
				CASEELSE
			ENDSELECT
			IF LOCAL:3 > LOCAL:2
				LOCAL:2 = LOCAL:3
				LOCALS = %LOCALS:(LOCAL:1)%
			ENDIF
		NEXT
		RETURN GET_TYPE_NUM(LOCALS)
	CASE 7
		;パイルショット
		RETURN GET_TYPE_NUM("핵열")
	CASEELSE
		;ＥＭＰＴＹ
		RETURN GET_TYPE_NUM("비구")
ENDSELECT

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_カードスキルA, ARG
RETURN 1

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_カードスキルA,ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_カードスキルA,ARG
;ＥＭＰＴＹ
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 0
	RETURN 4
;リペアショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 5
	RETURN 2
RETURN 1

;　사거리
@SKILL_RANGE_カードスキルA,ARG
;ワイドショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 1
	RETURN 2
;シザーズショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 3
	RETURN 2
;クレジットショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 4
	RETURN 2
;パイルショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 7
	RETURN 2
RETURN 3

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_カードスキルA,ARG
;ＥＭＰＴＹ
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 0
	RETURN 4
;ワイドショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 1
	RETURN 2
;クレジットショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 4
	RETURN 2
;電熱砲
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 6
	RETURN 3
RETURN 1

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_カードスキルA,ARG
;ＥＭＰＴＹ
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 0
	RETURN 4
;リペアショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 5
	RETURN 2
RETURN 1

;　 특수처리 여부 및 처리내용
;@SKILL_SPECIALEFECT_カードスキルA,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
;SIF ARG == 1
;	RETURN 0
;아래쪽에 특수처리를 기술합니다

;속도보정
@SKILL_SPEED_カードスキルA, ARG = -1
;電熱砲
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 6
	RETURN -2
;パイルショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 7
	RETURN 2
RETURN 0

;;　구상의 표시와 그 타입
;@SKILL_MESSAGE_カードスキルA,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_カードスキルA, ARG = -1
RETURN 0

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_カードスキルA,ARG
;パイルショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 7
	RETURN GET_STATE_NUM("BRAND")
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_カードスキルA,ARG
;パイルショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 7
	RETURN 10
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_カードスキルA,ARG
;パイルショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 7
	RETURN 25
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_カードスキルA,ARG
;パイルショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 7
	RETURN 50
RETURN 0

;拡散攻撃用追加処理
;ブレスなどの拡散攻撃に使う
@SKILL_SPREAD_カードスキルA,ARG
;バズソーショット
SIF SKILLGAGE_F_GET(ARG,PU_SKILLNUM_GET(ARG,"カードスキルA")) == 2
	RETURN 1
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_カードスキルA,ARG,ARG:1,SKILL_NUM
#DIM SKILL_NUM
IF INRANGE(ARG:1, 1, 16)
	LOCAL = POS(ARG:1)
ELSE
	LOCAL = 0
ENDIF
LOCAL:1 = 0
LOCAL:2 = 0
LOCAL:3 = 0
LOCAL:10 = MAXBASE:ARG:공격
LOCAL:11 = MAXBASE:ARG:명중
LOCAL:12 = MAXBASE:ARG:마법위력
LOCAL:13 = MAXBASE:ARG:마법효과
IF EQUIP:ARG:총 != 0
	MAXBASE:ARG:공격 = MAXBASE:ARG:총공격
	MAXBASE:ARG:명중 = MAXBASE:ARG:총명중
	MAXBASE:ARG:마법위력 = MAXBASE:ARG:총공격
	MAXBASE:ARG:마법효과 = MAXBASE:ARG:총명중
ELSE
	MAXBASE:ARG:공격 /= 2
	MAXBASE:ARG:마법위력 /= 2
ENDIF
SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
	CASE 1
		;ワイドショット
		CALL ATTACK_FIELD,ARG,ARG:1,SKILL_NUM
	CASE 2
		;バズソーショット
		CALL ATTACK_SPREAD,ARG,ARG:1,SKILL_NUM
	CASE 3
		;シザーズショット
		CALL ATTACK_SINGLE,ARG,LOCAL,SKILL_NUM
		IF CFLAG:ARG:PT플래그 == 0
			IF ARG:1 < 4
				LOCAL:1 = ARG:1 + 3
			ENDIF
		ELSE
			IF ARG:1 < 12
				LOCAL:1 = ARG:1 + 5
			ENDIF
		ENDIF
		LOCALS = 포지션{LOCAL:1}
		IF FLAG:LOCALS > -1 && GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) != "DYING"
			PRINTW ＞열선이 후열의 적까지 관통했다！
			CALL ATTACK_SINGLE, ARG, POS(LOCAL:1), SKILL_NUM
		ENDIF
	CASE 4
		;クレジットショット
		CALL ATTACK_FIELD,ARG,ARG:1,SKILL_NUM
	CASE 5
		;リペアショット
		CALL HEAL_SINGLE,ARG,LOCAL,SKILL_NUM
	CASE 6
		;電熱砲
		IF GETCONFIGS("描画インターフェース") != "WINAPI"
			SIF NO:ARG == [[キャラ:마르푸샤]]
				CALL 電熱砲
		ENDIF
		
		A = 0
		B = 0
		C = 0
		D = 0
		IF CFLAG:ARG:포지션 < 7
			LOCAL:1 = 7
			LOCAL:2 = 17 
		ELSE
			LOCAL:1 = 1
			LOCAL:2 = 7
		ENDIF
		FOR LOCAL, LOCAL:1, LOCAL:2
			LOCALS = 포지션{LOCAL}
			SIF FLAG:LOCALS < 0
				CONTINUE
			SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
				CONTINUE
			LOCAL:3 ++
		NEXT
		IF LOCAL:3 == 1
			FLAG:확산플래그 = 2
		ELSEIF LOCAL:3 == 2 || LOCAL:3 == 3
			FLAG:확산플래그 = 1
		ENDIF
		FOR LOCAL, LOCAL:1, LOCAL:2
			LOCALS = 포지션{LOCAL}
			SIF FLAG:LOCALS < 0
				CONTINUE
			SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
				CONTINUE
			CFLAG:ARG:타겟 = LOCAL
			CALL SKILL_TYPE_カードスキルA, ARG
			IF RESULT == GET_TYPE_NUM("핵열")
				IF (MAXBASE:(FLAG:LOCALS):핵열 < 100 || MAXBASE:(FLAG:LOCALS):핵열 == 999) && CFLAG:(FLAG:LOCALS):핵열가드킬 < 1
					CFLAG:(FLAG:LOCALS):핵열가드킬 = 1
					A = 1
				ENDIF
				IF CFLAG:(FLAG:LOCALS):마법반사플래그 == 1
					B = CFLAG:(FLAG:LOCALS):마법반사플래그
					CFLAG:(FLAG:LOCALS):마법반사플래그 = 0
				ENDIF
				IF CFLAG:(FLAG:LOCALS):무효플래그 > 0
					C = CFLAG:(FLAG:LOCALS):무효플래그
					CFLAG:(FLAG:LOCALS):무효플래그 = 0
				ENDIF
				IF CFLAG:(FLAG:LOCALS):핵열무효화횟수 > 0
					D = CFLAG:(FLAG:LOCALS):핵열무효화횟수
					CFLAG:(FLAG:LOCALS):핵열무효화횟수 = 0
				ENDIF
				IF A + B + C + D > 0
					PRINTFORML 　　TARGET:[{LOCAL,2}] %CALLNAME:(FLAG:LOCALS),20,LEFT%　>>>>>>　핵열 상성을 관통했다！
					FLAG:확산플래그 ++
				ENDIF
				CFLAG:ARG:방어반감플래그 = 1
				CALL ATTACK_SINGLE,ARG,FLAG:LOCALS,SKILL_NUM
				CFLAG:ARG:방어반감플래그 = 0
				SIF A + B + C + D > 0
					FLAG:확산플래그 --
				SIF FLAG:LOCALS < 0
					CONTINUE
				SIF A == 1
					CFLAG:(FLAG:LOCALS):핵열가드킬 = 0
				SIF B > 0
					CFLAG:(FLAG:LOCALS):마법반사플래그 = B
				SIF C > 0
					CFLAG:(FLAG:LOCALS):무효플래그 = C
				SIF D > 0
					CFLAG:(FLAG:LOCALS):핵열무효화횟수 = D
			ELSEIF RESULT == GET_TYPE_NUM("화염")
				IF (MAXBASE:(FLAG:LOCALS):화염 < 100 || MAXBASE:(FLAG:LOCALS):화염 == 999) && CFLAG:(FLAG:LOCALS):화염가드킬 < 1
					CFLAG:(FLAG:LOCALS):화염가드킬 = 1
					A = 1
				ENDIF
				IF CFLAG:(FLAG:LOCALS):마법반사플래그 == 1
					B = CFLAG:(FLAG:LOCALS):마법반사플래그
					CFLAG:(FLAG:LOCALS):마법반사플래그 = 0
				ENDIF
				IF CFLAG:(FLAG:LOCALS):무효플래그 > 0
					C = CFLAG:(FLAG:LOCALS):무효플래그
					CFLAG:(FLAG:LOCALS):무효플래그 = 0
				ENDIF
				IF CFLAG:(FLAG:LOCALS):화염무효화횟수 > 0
					D = CFLAG:(FLAG:LOCALS):화염무효화횟수
					CFLAG:(FLAG:LOCALS):화염무효화횟수 = 0
				ENDIF
				IF A + B + C + D > 0
					PRINTFORML 　　TARGET:[{LOCAL,2}] %CALLNAME:(FLAG:LOCALS),20,LEFT%　>>>>>>　화염 상성을 관통했다！
					FLAG:확산플래그 ++
				ENDIF
				CFLAG:ARG:방어반감플래그 = 1
				CALL ATTACK_SINGLE,ARG,FLAG:LOCALS,SKILL_NUM
				CFLAG:ARG:방어반감플래그 = 0
				SIF A + B + C + D > 0
					FLAG:확산플래그 --
				SIF FLAG:LOCALS < 0
					CONTINUE
				SIF A == 1
					CFLAG:(FLAG:LOCALS):화염가드킬 = 0
				SIF B > 0
					CFLAG:(FLAG:LOCALS):마법반사플래그 = B
				SIF C > 0
					CFLAG:(FLAG:LOCALS):무효플래그 = C
				SIF D > 0
					CFLAG:(FLAG:LOCALS):화염무효화횟수 = D
			ENDIF
		NEXT
		CFLAG:ARG:타겟 = CFLAG:ARG:포지션 < 7 ? 22 # 19
		FLAG:확산플래그 = 0
	CASE 7
		;パイルショット
		CALL ATTACK_SINGLE,ARG,LOCAL,SKILL_NUM
	CASEELSE
		;ＥＭＰＴＹ
ENDSELECT
MAXBASE:ARG:공격 = LOCAL:10
MAXBASE:ARG:명중 = LOCAL:11
MAXBASE:ARG:마법위력 = LOCAL:12
MAXBASE:ARG:마법효과 = LOCAL:13
CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,0

;CALL ATTACK_SINGLE,ARG,POS(ARG:1),SKILL_NUM;	単体攻撃
;CALL ATTACK_FIELD,ARG,ARG:1,SKILL_NUM;			範囲攻撃(列、全体)
;CALL ATTACK_SPREAD,ARG,ARG:1,SKILL_NUM;		拡散攻撃
;CALL ATTACK_RAND,ARG,ARG:1,SKILL_NUM;			ランダム攻撃


;====================================================
;실행내용 여기까지
;====================================================

;　説明文の表示
@SKILL_EXPLAIN_カードスキルA,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルA")
;専用技が封印されている
IF CFLAG:ARG:998 == 0
	PRINTL （개별이벤트 전용스킬 ＆ 개별이벤트 １～４를 모두 클리어하면…？）
ELSE
	SELECTCASE SKILLGAGE_F_GET(ARG,SKILL_NUM)
		CASE 1
			;ワイドショット
			PRINTL 7방향 동시에 사격하는 와이드 샷을 쏩니다
			;イベント用
			SIF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
			PRINTL 적 전열에 비구 상성의 대 데미지
			RESETCOLOR
		CASE 2
			;バズソーショット
			PRINTL 적을 저며내는 톱을 쏩니다
			;イベント用
			SIF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
			PRINT 대상과 그 주위에 검격 상성의 대 데미지
			RESETCOLOR
			PRINTL 　크리티컬률이 다소 높다
		CASE 3
			;シザーズショット
			PRINTL 적을 끼워넣는 열선을 쏩니다
			;イベント用
			SIF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
			PRINT 적 1체와 그 뒤의 적에게 핵열 상성의 대 데미지
			RESETCOLOR
			PRINTL 　크리티컬률이 다소 낮다
		CASE 4
			;クレジットショット
			PRINTL 강력한 산탄을 쏩니다、특수통화가 많을수록 공격횟수가 늘어납니다
			;イベント用
			SIF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
			PRINTL 적 전열에 １～５회 비구 상성의 중 데미지
			RESETCOLOR
		CASE 5
			;リペアショット
			PRINTL 동료를 향해 쏘면 회복시키는 탄환을 쏩니다
			;イベント用
			SIF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
			PRINTL 아군 1인을 크게 회복한다
			RESETCOLOR
		CASE 6
			;電熱砲
			PRINTL 적을 일소하는 전열포를 사용합니다
			;イベント用
			IF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
				PRINTL 온갖 방호를 꿰뚫는 포격으로 적 전체～적 1체에 화염＋핵열의 복합 상성의 초특대～？？？ 데미지
				RESETCOLOR
			ELSE
				PRINTL 온갖 방호를 꿰뚫는 포격으로 적 전체～적 1체에 화염＋핵열의 복합 상성의 특대～초특대 데미지
			ENDIF
			PRINTL 비정상적으로 강력하지만、코스트가 무겁고、또한 크리티컬률이 매우 낮지만、위기 상황에서는 낮은 코스트로 사용할 수 있다
			;相性貫通ダメージアップ＋防御力を二分の一として計算します。
		CASE 7
			;パイルショット
			PRINTL 사거리가 짧은 뜨거운 기둥을 3개 박아넣습니다
			;イベント用
			IF CFLAG:ARG:998 == 2
				SETCOLOR 0xFFA500
				PRINTL 전열의 적 1체에 ３회 핵열 상성의 대 데미지를 주고、낙인 상태로 만든다
				RESETCOLOR
			ELSE
				PRINTL 전열의 적 1체에 ３회 핵열 상성의 중 데미지를 주고、낙인 상태로 만든다
			ENDIF
		CASEELSE
			;ＥＭＰＴＹ
			PRINTL 일회용 무기스킬
			PRINTL 특정 카드를 뽑으면、대응하는 무기스킬로 전환됩니다
	ENDSELECT
	SIF SKILLGAGE_F_GET(ARG,SKILL_NUM) > 0
		PRINTL 이 스킬은 총공격과 총명중으로 데미지를 계산하고、사용 후에는「ＥＭＰＴＹ」로 되돌아갑니다
ENDIF

;사용조건
@SKILL_SPECIAL_ACTIONABLE_カードスキルA,ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"カードスキルA")
;専用技が封印されている
SIF CFLAG:ARG:998 == 0
	RETURN 0
SIF SKILLGAGE_F_GET(ARG, SKILL_NUM) > 0
	RETURN 1
RETURN 0

@電熱砲
LOCAL = 1250
FOR LOCAL:1, 1, 28
	CALL PRINT_IMAGE_BLOWBACK(@"電熱砲_{LOCAL:1}",LOCAL,3)
	TWAIT 100, 0
	CLEARLINE LOCAL/100
NEXT
FOR LOCAL:1, 28, 32
	CALL PRINT_IMAGE_BLOWBACK(@"電熱砲_{LOCAL:1}",LOCAL,3 - 1 * (LOCAL:1 - 28))
	TWAIT 100, 0
	CLEARLINE LOCAL/100
NEXT
CALL PRINT_IMAGE_BLOWBACK(@"電熱砲_31",LOCAL,0)
TWAIT 100, 0
CLEARLINE LOCAL/100