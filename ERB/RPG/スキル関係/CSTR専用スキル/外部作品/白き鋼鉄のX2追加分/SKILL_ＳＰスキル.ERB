;스킬명
@SKILL_NAME_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	;フレイミングシャワー　→　メギドゲイザーとプリュイのいいとこどり。中央だと火炎＋疾風で威力が上がり、それ以外だと火炎で命中が上がる
	;遭遇時期はLV40
	RESULTS = FLAMING SHOWER
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	;メギドゲイザー　→　強化版デッドリーバーン。威力は据え置きだが炎上付与率が高い。
	;遭遇時期はLV35
	RESULTS = MEGIDO GAZER
ELSEIF SKILL_CHANGE(ARG, [[キャラ:베스파(X2)]])
	;黄昏時のプリュイ　→　ツインクルネビュラ亜種。中央だと疾風で威力が高く（といってもオリジナル相当だが）、それ以外は飛具で命中が高い。
	;遭遇時期はLV35
	IF FLAG:행동순1 == ARG
		RESULTS = PLUIE AU CR%UNICODE(0x00C9)%PUSCULE
	ELSE
		RESULTS = P.AU CR%UNICODE(0x00C9)%PUSCULE
	ENDIF
ELSEIF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	;ドゥームズデイブラスト　→　旋回速弾亜種。威力は下げて核熱複合相性、発動激遅化
	;遭遇時期はLV45か50
	RESULTS = DOOMSDAY BLAST
ELSEIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	;ターミナルデストラクション　→　超強化版玉砕破。なお敵が使う場合はイベント仕様の模様
	;ファイナルオペレーションはただの降車なのでスキルにしない
	;上記理由のためこの形で遭遇はしない
	IF FLAG:행동순1 == ARG
		RESULTS = TERMINAL DESTRUCTION
	ELSE
		RESULTS = T.DESTRUCTION
	ENDIF
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	;スタグネーションワールド　→　強化版ニブルヘイム。威力は据え置きだが状態異常率上昇、攻撃の間「だけ」魔法効果一段階低下（上限突破）
	;遭遇時期はLV45か50
	RESULTS = STAGNATION WORLD
ELSEIF SKILL_CHANGE(ARG, [[キャラ:슬러그(X2)]])
	;ツインメテオバースト　→　八艘跳び亜種。あちらは8*100=800、こちらは9*90=810
	;遭遇時期はLV45か50
	RESULTS = TWIN METEOR BURST
ELSE
	RESULTS = ＳＰ 스킬
ENDIF

;　명중
@SKILL_HITRATE_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	IF FLAG:확산플래그 == 0
		RETURN 120
	ELSE
		RETURN 100
	ENDIF
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	RETURN 100
ELSEIF SKILL_CHANGE(ARG, [[キャラ:베스파(X2)]])
	IF FLAG:확산플래그 == 0
		;せめて足場を狭めるところから命中率ぐらいは恵んであげる
		RETURN 150
	ELSE
		RETURN 100
	ENDIF
ELSEIF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	RETURN 95
ELSEIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	RETURN 200
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	RETURN 100
ELSEIF SKILL_CHANGE(ARG, [[キャラ:슬러그(X2)]])
	RETURN 90
ELSE
	RETURN 100
ENDIF

;　위력
@SKILL_POWER_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	IF FLAG:확산플래그 == 0
		RETURN 320
	ELSE
		RETURN 420
	ENDIF
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	RETURN 420
ELSEIF SKILL_CHANGE(ARG, [[キャラ:베스파(X2)]])
	IF FLAG:확산플래그 == 0
		RETURN 320
	ELSE
		RETURN 420
	ENDIF
ELSEIF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	RETURN 460
ELSEIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	RETURN 600
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	RETURN 395
ELSEIF SKILL_CHANGE(ARG, [[キャラ:슬러그(X2)]])
	RETURN 90
ELSE
	RETURN 100
ENDIF

;　최저공격횟수
@SKILL_MINATTACKNUMBER_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:베스파(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:슬러그(X2)]])
	RETURN 9
ELSE
	RETURN 1
ENDIF

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:베스파(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:슬러그(X2)]])
	RETURN 9
ELSE
	RETURN 1
ENDIF

;一体辺りの最大ＨＩＴ数
;※今のところスラグ限定　半分以上ヒットするぞ
@SKILL_MAXATK_PER_ＳＰスキル, ARG
RETURN 5

;一体辺りの最低ＨＩＴ数
;※今のところスラグ限定　めっちゃ片寄るぞ
@SKILL_MINATK_PER_ＳＰスキル, ARG
RETURN 0

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_ＳＰスキル, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	;せめてデイサイトとヴェスパよりは高くしようぐらいの気持ち
	RETURN 50
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	;比較：デッドリーバーンが消費40　状態異常率だけ跳ね上げたので1.1倍
	RETURN 44
ELSEIF SKILL_CHANGE(ARG, [[キャラ:베스파(X2)]])
	;比較：ツインクルネビュラが消費50　単一属性になったのでこんなもんでは
	RETURN 45
ELSEIF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	;比較：旋回速弾が消費48　威力低下＋発動遅延と複合相性でトントンに
	RETURN 48
ELSEIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	;比較：玉砕破が消費10　超時間差自爆なのでコストはそこまで重要じゃないと思う……？
	RETURN 10
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	;比較：ニブルヘイムが消費42　限界突破型魔法効果低下という裏技を使ったので1.3倍に
	RETURN 54
ELSEIF SKILL_CHANGE(ARG, [[キャラ:슬러그(X2)]])
	;比較：八艘跳びが消費50　向こうと違って最大5ヒットなのでトントンに
	RETURN 50
ELSE
	RETURN 1
ENDIF

;　상성
@SKILL_TYPE_ＳＰスキル, ARG
#LOCALSSIZE 3
#LOCALSIZE 4
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	IF FLAG:확산플래그 == 1
		SIF ARG < 0 || 스킬속성표시중
			RETURN GET_TYPE_NUM("화염")
		SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
			RETURN GET_TYPE_NUM("화염")
		LOCAL = POS(CFLAG:ARG:타겟)
		SIF LOCAL < 0
			RETURN GET_TYPE_NUM("화염")
		LOCAL:2 = -1000
		LOCALS = 화염
		LOCALS:1 = 화염
		LOCALS:2 = 질풍
		FOR LOCAL:1, 1, 3
			;判定用に数値を仕舞う。反射は-200扱い
			LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
			LOCAL:3 = !(HAVE_SKILL(ARG, [[스킬:영세라이도우]]) || HAVE_SKILL(ARG, 2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
			LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
			IF LOCAL:3 > LOCAL:2
				LOCAL:2 = LOCAL:3
				LOCALS = %LOCALS:(LOCAL:1)%
			ENDIF
		NEXT
		RETURN GET_TYPE_NUM(LOCALS)
	ELSE
		RETURN GET_TYPE_NUM("화염")
	ENDIF
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	RETURN GET_TYPE_NUM("화염")
ELSEIF SKILL_CHANGE(ARG, [[キャラ:베스파(X2)]])
	IF FLAG:확산플래그 == 1
		RETURN GET_TYPE_NUM("질풍")
	ELSE
		RETURN GET_TYPE_NUM("비구")
	ENDIF
ELSEIF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	SIF ARG < 0 || 스킬속성표시중
		RETURN GET_TYPE_NUM("핵열")
	SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
		RETURN GET_TYPE_NUM("핵열")
	LOCAL = POS(CFLAG:ARG:타겟)
	SIF LOCAL < 0
		RETURN GET_TYPE_NUM("핵열")
	LOCAL:2 = -1000
	LOCALS = 핵열
	LOCALS:1 = 핵열
	LOCALS:2 = 비구
	FOR LOCAL:1, 1, 3
		;判定用に数値を仕舞う。反射は-200扱い
		LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
		LOCAL:3 = !(HAVE_SKILL(ARG, [[스킬:영세라이도우]]) || HAVE_SKILL(ARG, 2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
		LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
		IF LOCAL:3 > LOCAL:2
			LOCAL:2 = LOCAL:3
			LOCALS = %LOCALS:(LOCAL:1)%
		ENDIF
	NEXT
	RETURN GET_TYPE_NUM(LOCALS)
ELSEIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	RETURN GET_TYPE_NUM("만능")
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	RETURN GET_TYPE_NUM("빙결")
ELSEIF SKILL_CHANGE(ARG, [[キャラ:슬러그(X2)]])
	RETURN GET_TYPE_NUM("검격")
ELSE
	RETURN 0
ENDIF

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_ＳＰスキル, ARG
RETURN 1

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:베스파(X2)]])
	RETURN 2
ELSEIF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	RETURN 1
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	RETURN 2
ELSEIF SKILL_CHANGE(ARG, [[キャラ:슬러그(X2)]])
	RETURN 1
ELSE
	RETURN 1
ENDIF

;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_ＳＰスキル, ARG
RETURN 1

;　사거리
@SKILL_RANGE_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	RETURN 3
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	RETURN 3
ELSEIF SKILL_CHANGE(ARG, [[キャラ:베스파(X2)]])
	RETURN 3
ELSEIF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	RETURN 3
ELSEIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	RETURN 3
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	RETURN 3
ELSEIF SKILL_CHANGE(ARG, [[キャラ:슬러그(X2)]])
	RETURN 3
ELSE
	RETURN 1
ENDIF

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	RETURN 3
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	RETURN 3
ELSEIF SKILL_CHANGE(ARG, [[キャラ:베스파(X2)]])
	RETURN 3
ELSEIF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	RETURN 3
ELSEIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	RETURN 3
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	RETURN 3
ELSEIF SKILL_CHANGE(ARG, [[キャラ:슬러그(X2)]])
	RETURN 3
ELSE
	RETURN 1
ENDIF

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_ＳＰスキル, ARG
RETURN 1

;속도보정
@SKILL_SPEED_ＳＰスキル, ARG = -1
IF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	RETURN -10
ELSE
	RETURN 0
ENDIF

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_ＳＰスキル, ARG = -1
RETURN 0

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	RETURN GET_STATE_NUM("FLAME")
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	RETURN GET_STATE_NUM("FLAME")
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	RETURN GET_STATE_NUM("FREEZE")
ELSE
	RETURN 0
ENDIF

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	RETURN GET_TYPE_NUM("화염")
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	RETURN GET_TYPE_NUM("화염")
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	RETURN GET_TYPE_NUM("빙결")
ELSE
	RETURN 0
ENDIF

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	RETURN 40
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	RETURN 40
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	;どうせ連発できないし1ターンで解除されるし……
	RETURN 80
ELSE
	RETURN 0
ENDIF

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	RETURN 95
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	RETURN 95
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	RETURN 95
ELSE
	RETURN 0
ENDIF

;====================================================
;トリガー発動条件
;====================================================
@SKILL_TRIGGER_ACTIONABLE_ＳＰスキル, ARG, ARG:1
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG, "ＳＰスキル")
SIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]]) && SKILLGAGE_H_GET(ARG, SKILL_NUM) && SKILLGAGE_D_GET(ARG, SKILL_NUM) == 0
	RETURN 1
RETURN 0

;====================================================
;行動によるスキルゲージ操作
;====================================================
@SKILLGAGE_CHARGE_ＳＰスキル, ARG, ARG:1
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG, "ＳＰスキル")
IF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]]) && SKILLGAGE_H_GET(ARG, SKILL_NUM) && BASE:ARG:ＨＰ < 1
	;死んだら
	IF BASE:ARG:ＨＰ < 1
		CALL SKILLGAGE_D_SET, ARG, SKILL_NUM ,0
		CALL SKILLGAGE_H_SET, ARG, SKILL_NUM ,0
	ENDIF
ENDIF

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_ＳＰスキル, ARG, ARG:1, SKILL_NUM
#LOCALSIZE 3
#DIM SKILL_NUM
#DIM ターゲット配列,17

IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	IF CFLAG:ARG:포지션 < 7
		LOCAL:1 = 7
		LOCAL:2 = 17
	ELSE
		LOCAL:1 = 1
		LOCAL:2 = 7
	ENDIF
	FOR LOCAL, LOCAL:1, LOCAL:2
		FLAG:확산플래그 = 0
		LOCALS = 포지션{LOCAL}
		SIF FLAG:LOCALS < 0
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
			CONTINUE
		;中央にいたら
		SELECTCASE LOCAL
		CASE 2, 5, 8, 9, 10, 13, 14, 15
			FLAG:확산플래그 = 1
			CFLAG:ARG:타겟 = LOCAL
			CALL SKILL_TYPE_ＳＰスキル, ARG
			PRINT 상성판정：
			SELECTCASE RESULT
				CASE 4
					PRINT 화염
				CASE 14
					PRINT 질풍
			ENDSELECT
			PRINTL ！
		CASEELSE
		ENDSELECT
		CALL ATTACK_SINGLE, ARG, FLAG:LOCALS, SKILL_NUM
	NEXT
	FLAG:확산플래그 = 0
	CFLAG:ARG:타겟 = CFLAG:ARG:포지션 < 7 ? 22 # 19
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	CALL ATTACK_FIELD, ARG, ARG:1, SKILL_NUM
ELSEIF SKILL_CHANGE(ARG, [[キャラ:베스파(X2)]])
	IF CFLAG:ARG:포지션 < 7
		LOCAL:1 = 7
		LOCAL:2 = 17 
	ELSE
		LOCAL:1 = 1
		LOCAL:2 = 7
	ENDIF
	FOR LOCAL, LOCAL:1, LOCAL:2
		FLAG:확산플래그 = 0
		LOCALS = 포지션{LOCAL}
		SIF FLAG:LOCALS < 0
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
			CONTINUE
		;中央にいたら
		SELECTCASE LOCAL
		CASE 2, 5, 8, 9, 10, 13, 14, 15
			FLAG:확산플래그 = 1
		CASEELSE
		ENDSELECT
		CALL ATTACK_SINGLE, ARG, FLAG:LOCALS, SKILL_NUM
	NEXT
	FLAG:확산플래그 = 0
	CFLAG:ARG:타겟 = CFLAG:ARG:포지션 < 7 ? 22 # 19
ELSEIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	IF FLAG:카운터중
		PRINTFORMW 카운트가 ０가 되었다！　폭탄이 기폭한다！
		IF CFLAG:ARG:포지션 < 7
			;22は敵全体を指す
			CALL ATTACK_FIELD, ARG, 22, SKILL_NUM
		ELSE
			;19は味方全体を指す
			CALL ATTACK_FIELD, ARG, 19, SKILL_NUM
		ENDIF
		SIF CFLAG:ARG:상태이상 < GET_STATE_NUM("DYING")
			CALL VAR_HP, ARG, -BASE:ARG:ＨＰ, 0
	ELSE
		PRINTFORMW %ARG는% 주위에 폭탄을 설치했다！　５턴 후에 기폭한다！
		CALL SKILLGAGE_H_SET, ARG, SKILL_NUM ,1
	ENDIF
ELSEIF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	IF CFLAG:ARG:포지션 < 7
		LOCAL:1 = 7
		LOCAL:2 = 17 
	ELSE
		LOCAL:1 = 1
		LOCAL:2 = 7
	ENDIF
	FOR LOCAL, LOCAL:1, LOCAL:2
		LOCALS = 포지션{LOCAL}
		SIF FLAG:LOCALS < 0
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
			CONTINUE
		CFLAG:ARG:타겟 = LOCAL
		CALL SKILL_TYPE_ＳＰスキル, ARG
		PRINT 상성판정：
		SELECTCASE RESULT
			CASE 1
				PRINT 비구
			CASE 16
				PRINT 핵열
		ENDSELECT
		PRINTL ！
		CALL ATTACK_SINGLE, ARG, FLAG:LOCALS, SKILL_NUM
	NEXT
	CFLAG:ARG:타겟 = CFLAG:ARG:포지션 < 7 ? 22 # 19
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	VARSET ターゲット配列, -1
	;流石に味方側で使う時は条件をつける。いずれな
	IF CFLAG:ARG:포지션 < 7
		LOCAL:1 = 7
		LOCAL:2 = 17 
	ELSE
		LOCAL:1 = 1
		LOCAL:2 = 7
	ENDIF
	FOR LOCAL, LOCAL:1, LOCAL:2
		LOCALS = 포지션{LOCAL}
		SIF FLAG:LOCALS < 0
			CONTINUE
		ターゲット配列:LOCAL = FLAG:LOCALS
		CFLAG:(ターゲット配列:LOCAL):BS부착률강화 =MIN(4, CFLAG:(ターゲット配列:LOCAL):BS부착률강화 + 4)
		;一時的に下限だろうと超える
		CFLAG:(ターゲット配列:LOCAL):마법효과강화 = CFLAG:(ターゲット配列:LOCAL):마법효과강화 - 4
	NEXT
	CALL ATTACK_FIELD, ARG, ARG:1, SKILL_NUM
	FOR LOCAL, LOCAL:1, LOCAL:2
		;攻撃前にいたやつはそのポジションの添え字にキャラ番号が入っている、それ以外は-1
		SIF ターゲット配列:LOCAL < 0
			CONTINUE
		;下げた分を元に戻す。こうすれば死んでCOMPに戻った奴も戻せるはず
		CFLAG:(ターゲット配列:LOCAL):마법효과강화 = CFLAG:(ターゲット配列:LOCAL):마법효과강화 + 4
	NEXT
ELSEIF SKILL_CHANGE(ARG, [[キャラ:슬러그(X2)]])
	CALL ATTACK_RAND, ARG, ARG:1, SKILL_NUM
ENDIF

CALL SKILLGAGE_D_SET, ARG, SKILL_NUM ,5
;====================================================
;실행내용 여기까지
;====================================================

;====================================================
;戦闘中スキルゲージの可視化
;====================================================
@SKILLGAGE_DISPLAY_ＳＰスキル, ARG
#DIM SKILL_NUM
#DIM 猶予
SKILL_NUM = PU_SKILLNUM_GET(ARG, "ＳＰスキル")
IF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]]) && SKILLGAGE_H_GET(ARG, SKILL_NUM)
	猶予 = SKILLGAGE_D_GET(ARG, SKILL_NUM)
	PRINTFORML 폭파까지 앞으로 {猶予}턴 남았습니다
ENDIF

;　説明文の表示
@SKILL_EXPLAIN_ＳＰスキル, ARG
IF SKILL_CHANGE(ARG, [[キャラ:입실론(X2)]])
	PRINTFORML 중앙에 거대한 화염의 선풍을、양 옆으로 불티를 튀기는 %CALLNAME:ARG%의 ＳＰ스킬。
	;オリジナルです
	PRINTL 악랄한 배덕자에게 내리는 심판。죄에 상응하는 벌로서 열화의 비가 쏟아진다。
	PRINTL 　
	PRINTL 적 전체에 중앙의 포지션에게는 화염과 질풍의 복합、그 이외에는 화염 상성으로 대 데미지를 주고、고확률로 염상을 부여한다。
	PRINTL 한 번 사용하면、재사용까지 ５턴의 쿨타임을 필요로 한다。
ELSEIF SKILL_CHANGE(ARG, [[キャラ:데이사이트(X2)]])
	PRINTFORML 양팔에 화포를 장착하여、압도적인 화력으로 후려치는 %CALLNAME:ARG%의 ＳＰ스킬。
	;発表してすぐの頃に公開していた没詠唱をパクった
	PRINTL 속에서 타오르는 불길은、그을리는 지옥의 업화처럼 조용하게 휘몰아쳐、모든 것을 태운다。
	PRINTL 　
	PRINTL 적 전체에 화염 상성으로 대 데미지를 주고、고확률로 염상을 부여한다。
	PRINTL 한 번 사용하면、재사용까지 ５턴의 쿨타임을 필요로 한다。
ELSEIF SKILL_CHANGE(ARG, [[キャラ:베스파(X2)]])
	PRINTFORML 중앙에 거대한 회오리를、양 옆에 폭탄을 투하하는 %CALLNAME:ARG%의 ＳＰ스킬。
	;発表してすぐの頃に公開していた没詠唱をパクった
	PRINTL 고귀한 폭풍에 안겨、소녀는 하늘을 우러러본다。지상에 쏟아지는 것은 미사일의 호우。
	PRINTL 　
	PRINTL 적 전체에 중앙의 포지션에는 질풍、그 이외에는 비구 상성으로 대 데미지를 준다。명중률이 높다。
	PRINTL 한 번 사용하면、재사용까지 ５턴의 쿨타임을 필요로 한다。
ELSEIF SKILL_CHANGE(ARG, [[キャラ:오토크롬(X2)]])
	PRINTFORML 한계까지 충전한 에너지를 발사하는 %CALLNAME:ARG%의 ＳＰ스킬。
	;オリジナルです
	PRINTL 찬란한 빛의 기둥。그들은 하늘에서 쏟아져、삿된 침입자를 파괴한다.。
	PRINTL 　
	PRINTL 적 전체에 비구와 핵열의 복합상성으로 대 데미지를 준다。발동이 느리다。
	PRINTL 한 번 사용하면、재사용까지 ５턴의 쿨타임을 필요로 한다。
ELSEIF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	PRINTFORML 주위에 폭탄을 설치해、성대한 자폭을 감행하는 %CALLNAME:ARG%의 ＳＰ스킬。
	;オリジナルです
	PRINTL 기폭 시퀀스를 기동하고、카운트다운을 시작한다。죽는다면 다 같이！
	PRINTL 　
	PRINTL 적 전체에 만능 상성으로 특대 데미지를 준다。
	PRINTL 다만 실행까지 ５턴이 필요하며、사용 후 자신은 사망한다。
	;KR판 임의수정, PRINTL > PRINTFORML
	PRINTFORML %ARG가% 사망하면 카운트다운은 정지되고、기폭 시퀀스는 리셋된다。
ELSEIF SKILL_CHANGE(ARG, [[キャラ:헤일(X2)]])
	PRINTFORML 자신 이외의 시간을 정체시키면서、상대를 동결시키는 %CALLNAME:ARG%의 ＳＰ스킬。
	;オリジナルです
	PRINTL 절대영도를 넘어 시간마저 얼리는 냉기로 감싸、세계를 잠으로 이끈다。
	PRINTL 　
	PRINTL 적 전체에 빙결 상성으로 대 데미지를 준다。고확률로 동결을 부여한다。
	PRINTL 한 번 사용하면、재사용까지 ５턴의 쿨타임을 필요로 한다。
ELSEIF SKILL_CHANGE(ARG, [[キャラ:슬러그(X2)]])
	PRINTFORML 탑재된 부메랑을 자유자재로 조종하여、적을 조각내는 %CALLNAME:ARG%의 ＳＰ스킬。
	;オリジナルです
	PRINTL 아홉개의 칼날이 전장을 누빈다。그들은 주저없이、자비도 없이、적을 즉각 양단한다。
	PRINTL 　
	PRINTL 복수의 적에게 검격 상성으로 ９회 소 데미지를 준다。
	PRINTL 한 번 사용하면、재사용까지 ５턴의 쿨타임을 필요로 한다。
ELSE
	PRINTFORML ！ＥＲＲＯＲ！대상외의 캐릭터에게 습득되어 있습니다。
	PRINTFORML 선택해도 아무것도 실행되지 않습니다。
ENDIF

;사용조건
@SKILL_SPECIAL_ACTIONABLE_ＳＰスキル, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG, "ＳＰスキル")
IF SKILL_CHANGE(ARG, [[キャラ:브리게이드(X2)]])
	SIF SKILLGAGE_D_GET(ARG, SKILL_NUM)
		RETURN 0
	SIF SKILLGAGE_H_GET(ARG, SKILL_NUM)
		RETURN 0
	RETURN 1
ELSE
	SIF SKILLGAGE_D_GET(ARG, SKILL_NUM)
		RETURN 0
	RETURN 1
ENDIF

