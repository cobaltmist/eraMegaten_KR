;스킬명
@SKILL_NAME_ラピッドリッパー, ARG
RESULTS = 래피드 리퍼

;　명중
@SKILL_HITRATE_ラピッドリッパー, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG, "ラピッドリッパー")
IF SKILLGAGE_D_GET(ARG, SKILL_NUM) > 0
	RETURN 120
ELSE
	RETURN 100
ENDIF

;　위력
@SKILL_POWER_ラピッドリッパー, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG, "ラピッドリッパー")
IF SKILLGAGE_D_GET(ARG, SKILL_NUM) > 0
	IF SKILL_CHANGE(ARG, [[キャラ:아큐라(X)]]) && EQUIP:ARG:악세서리 == 5349 && FLAG:카운터중
		;約1.25倍
		RETURN 125
	ELSE
		RETURN 100
	ENDIF
ELSE
	RETURN 200
ENDIF

;　최저공격횟수
@SKILL_MINATTACKNUMBER_ラピッドリッパー, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG, "ラピッドリッパー")
IF SKILLGAGE_D_GET(ARG, SKILL_NUM) > 0
	RETURN 2
ELSE
	RETURN 1
ENDIF

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_ラピッドリッパー, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG, "ラピッドリッパー")
IF SKILLGAGE_D_GET(ARG, SKILL_NUM) > 0
	RETURN 4
ELSE
	RETURN 1
ENDIF

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_ラピッドリッパー, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_ラピッドリッパー, ARG
;다크니스トリガー発動中はコスト1/3
SIF SKILLGAGE_H_GET(ARG, [[스킬:DARKNESS TRIGGER]]) == 1
	RETURN 6
RETURN 18

;　상성
@SKILL_TYPE_ラピッドリッパー, ARG
RETURN GET_TYPE_NUM("검격")

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_ラピッドリッパー, ARG
RETURN 1

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_ラピッドリッパー, ARG
RETURN 1

;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_ラピッドリッパー, ARG
#DIM COPEN
COPEN = 0
IF U > 0 && U < CHARANUM && NO:U == [[キャラ:아큐라(X)]]
	COPEN = U
ELSEIF FLAG:행동순1 > 0 && NO:(FLAG:행동순1) == [[キャラ:아큐라(X)]]
	COPEN = FLAG:행동순1
ENDIF
;ロック中の攻撃・ロック対象に強制
IF COPEN > 0 && BLITZ_TARGET_JUDG(COPEN) == 1
	RETURN 4
;非ロック中の攻撃・敵単体
ELSE
	RETURN 1
ENDIF

;　사거리
@SKILL_RANGE_ラピッドリッパー, ARG
RETURN 3

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_ラピッドリッパー, ARG
;ロック中の攻撃・ロック対象に強制
IF BLITZ_TARGET_JUDG(ARG) == 1
	RETURN 4
;非ロック中の攻撃・敵単体
ELSE
	RETURN 1
ENDIF

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_ラピッドリッパー, ARG
RETURN 1

;속도보정
@SKILL_SPEED_ラピッドリッパー, ARG = -1
RETURN 0

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_ラピッドリッパー, ARG = -1
RETURN 0

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_ラピッドリッパー, ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_ラピッドリッパー, ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_ラピッドリッパー, ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_ラピッドリッパー, ARG
RETURN 0

;====================================================
;トリガー発動条件
;====================================================
@SKILL_TRIGGER_ACTIONABLE_ラピッドリッパー, ARG, ARG:1
#DIM SKILL_NUM
#DIM ROCK_H
#DIM ループ用
#DIM 攻撃前敵カウント
#DIM 攻撃後敵カウント
#DIM 差分
#DIM 加算クードス

;自分じゃなければNG
SIF ARG != ARG:1
	RETURN 0
;X-ROSS DISASTER,SONG OF DIVA,DARKNESS TRIGGERならNG
SIF CFLAG:(ARG:1):입력행동 == [[스킬:X-ROSS DISASTER]] || CFLAG:(ARG:1):입력행동 == [[스킬:SONG OF DIVA]] || CFLAG:(ARG:1):입력행동 == [[스킬:DARKNESS TRIGGER]]
	RETURN 0
;EXウェポン11番=ラピッドリッパーじゃないとNG
SIF SKILLGAGE_F_GET(ARG, [[스킬:DARKNESS TRIGGER]]) != 11
	RETURN 0
;ロック状態の敵が健在かつダークネストリガー発動中
IF BLITZ_TARGET_JUDG(ARG) == 1 && SKILLGAGE_H_GET(ARG, [[스킬:DARKNESS TRIGGER]]) == 1
	;1桁は被弾回数,2桁以上はターゲットを割り当てるようにした
	ROCK_H = SKILLGAGE_H_GET(ARG, [[스킬:블리츠대쉬]]) / 10
	攻撃前敵カウント = 0
	攻撃後敵カウント = 0
	IF CFLAG:ARG:PT플래그 > 0
		FOR ループ用, 7, 17
			LOCALS = 포지션{ループ用}
			SIF FLAG:LOCALS == -1
				CONTINUE
			SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
				CONTINUE
			攻撃前敵カウント += 1
		NEXT
	ENDIF
	
	SETCOLOR 0x9933cd
	PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓
	PRINTFORM ┃[{CPOS(ARG), 2}] %"ＲｏＲｏ",21,LEFT%┃　┃
	PRINTFORML %"래피드 리퍼",40,LEFT%┃
	PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
	RESETCOLOR
	SKILL_NUM = PU_SKILLNUM_GET(ARG, "ラピッドリッパー")
	CALL SKILLGAGE_D_SET, ARG, SKILL_NUM, 1
	CALL ATTACK_SINGLE, ARG, ROCK_H, SKILL_NUM
	
	IF CFLAG:ARG:PT플래그 > 0
		FOR ループ用, 7, 17
			LOCALS = 포지션{ループ用}
			SIF FLAG:LOCALS == -1
				CONTINUE
			SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
				CONTINUE
			攻撃後敵カウント += 1
		NEXT
	ENDIF
	
	差分 = 攻撃前敵カウント - 攻撃後敵カウント
	
	IF 差分 > 0
		加算クードス = 20
		IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
			TIMES 加算クードス, 1.25
		ELSEIF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
			TIMES 加算クードス, 0.5
		ENDIF
		PRINTFORMW AERIAL EX　Kudos + {加算クードス}
		CALL SKILLGAGE_F_ADD, ARG, [[스킬:블리츠대쉬]], 加算クードス
	ENDIF
	IF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
		;アパシーマンの上限は1200
		SIF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 1200
			CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 1200
	ELSE
		;上限は99999
		SIF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 99999
			CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 99999
	ENDIF
ENDIF
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_ラピッドリッパー, ARG, ARG:1, SKILL_NUM
#DIM SKILL_NUM
#DIM ROCK_H
#DIM ループ用
#DIM 攻撃前敵カウント
#DIM 攻撃後敵カウント
#DIM 差分
#DIM 加算クードス

攻撃前敵カウント = 0
攻撃後敵カウント = 0
IF CFLAG:ARG:PT플래그 > 0
	FOR ループ用, 7, 17
		LOCALS = 포지션{ループ用}
		SIF FLAG:LOCALS == -1
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
			CONTINUE
		攻撃前敵カウント += 1
	NEXT
ENDIF

;ロック中かつロック対象が生存中なら強制的にロック対象へ攻撃
IF ARG:1 == 23
	IF BLITZ_TARGET_JUDG(ARG) == 1
		ROCK_H = SKILLGAGE_H_GET(ARG, [[스킬:블리츠대쉬]]) / 10
		CALL SKILLGAGE_D_SET, ARG, SKILL_NUM, 1
		CALL ATTACK_SINGLE, ARG, ROCK_H, SKILL_NUM
	ENDIF
ELSE
	CALL ATTACK_SINGLE, ARG, POS(ARG:1), SKILL_NUM
ENDIF

IF CFLAG:ARG:PT플래그 > 0
	FOR ループ用, 7, 17
		LOCALS = 포지션{ループ用}
		SIF FLAG:LOCALS == -1
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
			CONTINUE
		攻撃後敵カウント += 1
	NEXT
ENDIF

差分 = 攻撃前敵カウント - 攻撃後敵カウント

IF 差分 > 0
	加算クードス = 20
	IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
		TIMES 加算クードス, 1.25
	ELSEIF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
		TIMES 加算クードス, 0.5
	ENDIF
	PRINTFORMW AERIAL EX　Kudos + {加算クードス}
	CALL SKILLGAGE_F_ADD, ARG, [[스킬:블리츠대쉬]], 加算クードス
ENDIF
IF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
	;アパシーマンの上限は1200
	SIF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 1200
		CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 1200
ELSE
	;上限は99999
	SIF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 99999
		CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 99999
ENDIF

;EXウェポン11番=ラピッドリッパー
CALL SKILLGAGE_F_SET, ARG, [[스킬:DARKNESS TRIGGER]], 11
;====================================================
;실행내용 여기까지
;====================================================

;　説明文の表示
@SKILL_EXPLAIN_ラピッドリッパー, ARG
PRINTL 부메랑을 다루는 워커・슬러그의 능력을 재현한 ＥＸ 웨폰。
PRINTL ＨＰ를 소비하는 커맨드 「블리츠 대쉬」에 의해 LOCK된 적이 있는 경우、강제적으로 그 대상에게 공격한다。
PRINTL 
PRINTL 공격 대상이 LOCK ON 상태인 경우　　　　…적 1체에 검격 상성으로 ２～４회 소 데미지를 준다。
PRINTL 공격 대상이 LOCK되지 않은 상태인 경우　…적 1체에 검격 상성으로 중 데미지를 준다。
IF U > 0 && U < CHARANUM && NO:U == [[キャラ:아큐라(X)]] && EQUIP:U:악세서리 == 5349 && SKILLGAGE_H_GET(U, [[스킬:DARKNESS TRIGGER]]) == 1
	SETCOLOR 0x9933cd
	PRINTL 다크니스 트리거Ｘ 장비중　다크니스 트리거에 의한 추격시 데미지 상승
	RESETCOLOR
ENDIF
PRINTL ※이 스킬로 적을 쓰러뜨렸을 경우、쿠도스가 ２０ 가산된다

;사용조건
@SKILL_SPECIAL_ACTIONABLE_ラピッドリッパー, ARG
;ターゲット=23が入るのはロック中のみ
;かつ実行時にターゲットが死んだりするとダメ
SIF ARG > 0 && CFLAG:ARG:타겟 == 23 && BLITZ_TARGET_JUDG(ARG) != 1
	RETURN 0
RETURN 1

