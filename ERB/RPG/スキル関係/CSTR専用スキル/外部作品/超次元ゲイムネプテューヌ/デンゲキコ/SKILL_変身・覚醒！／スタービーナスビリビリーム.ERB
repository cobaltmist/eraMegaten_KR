;스킬명
@SKILL_NAME_変身・覚醒！／スタービーナスビリビリーム,ARG
IF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム")) < 1
	RESULTS = 변신・각성！
ELSE
	RESULTS = 스타 비너스 찌릿찌릿 빔
ENDIF

;　명중
@SKILL_HITRATE_変身・覚醒！／スタービーナスビリビリーム, ARG
#DIM ACC
ACC = 100
SIF ARG < 0
	RETURN ACC
ACC -= MAX(MEASURE_FRONT(CPOS(ARG),CFLAG:ARG:타겟)*20,0)
RETURN MAX(ACC,5)

;　위력
@SKILL_POWER_変身・覚醒！／スタービーナスビリビリーム, ARG
#DIM L_POW
L_POW = 20
;620
L_POW -= MAX(MEASURE_FRONT(CPOS(ARG),CFLAG:ARG:타겟)*2,0)
RETURN MAX(L_POW,6)

;クリティカル率
@SKILL_CRITICAL_RATE_変身・覚醒！／スタービーナスビリビリーム, ARG
RETURN 3

;;　吸収率
;@SKILL_HP_ABSORBER_変身・覚醒！／スタービーナスビリビリーム, ARG
;RETURN 0

;　최저공격횟수
@SKILL_MINATTACKNUMBER_変身・覚醒！／スタービーナスビリビリーム,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_変身・覚醒！／スタービーナスビリビリーム,ARG
RETURN 1

;;一体辺りの最大ＨＩＴ数
;;※ランダム攻撃系に使う
;@SKILL_MAXATK_PER_変身・覚醒！／スタービーナスビリビリーム,ARG
;RETURN 1

;;一体辺りの最低ＨＩＴ数
;;※ランダム攻撃系に使う
;@SKILL_MINATK_PER_変身・覚醒！／スタービーナスビリビリーム,ARG
;RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_変身・覚醒！／スタービーナスビリビリーム, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_変身・覚醒！／スタービーナスビリビリーム,ARG
RETURN 60

;　상성
@SKILL_TYPE_変身・覚醒！／スタービーナスビリビリーム,ARG
#LOCALSSIZE 4
#LOCALSIZE 4

SIF ARG < 0 || 스킬속성표시중
	RETURN GET_TYPE_NUM("검격")
SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
	RETURN GET_TYPE_NUM("검격")
LOCAL = POS(CFLAG:ARG:타겟)
SIF LOCAL < 0
	RETURN GET_TYPE_NUM("검격")
LOCAL:2 = -300
LOCALS = 검격
LOCALS:1 = 검격
LOCALS:2 = 만능
LOCALS:3 = 파마
FOR LOCAL:1, 1, 4
	;判定用に数値を仕舞う。反射は-200扱い
	LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
	LOCAL:3 = !(HAVE_SKILL(ARG,[[스킬:영세라이도우]]) || HAVE_SKILL(ARG,2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
	LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
	SELECTCASE LOCAL:1
		CASE 1
			LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
		CASE 2
			LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
		CASEELSE
	ENDSELECT
	IF LOCAL:3 > LOCAL:2
		LOCAL:2 = LOCAL:3
		LOCALS = %LOCALS:(LOCAL:1)%
	ENDIF
NEXT
RETURN GET_TYPE_NUM(LOCALS)
;검격

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_変身・覚醒！／スタービーナスビリビリーム,ARG
IF ARG < 0 || SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム")) < 1
	RETURN 0
ELSE
	RETURN 1
ENDIF

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_変身・覚醒！／スタービーナスビリビリーム,ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_変身・覚醒！／スタービーナスビリビリーム,ARG
RETURN 1

;　사거리
@SKILL_RANGE_変身・覚醒！／スタービーナスビリビリーム,ARG
RETURN 1

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_変身・覚醒！／スタービーナスビリビリーム,ARG
RETURN 1

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_変身・覚醒！／スタービーナスビリビリーム,ARG
RETURN 1

;　 특수처리 여부 및 처리내용
;@SKILL_SPECIALEFECT_変身・覚醒！／スタービーナスビリビリーム,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
;SIF ARG == 1
;	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
;@SKILL_SUCCESSION_TYPE_変身・覚醒！／スタービーナスビリビリーム,ARG
;RETURN 23
;검격

;속도보정
@SKILL_SPEED_変身・覚醒！／スタービーナスビリビリーム, ARG = -1
RETURN -10

;;　구상의 표시와 그 타입
;@SKILL_MESSAGE_変身・覚醒！／スタービーナスビリビリーム,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_変身・覚醒！／スタービーナスビリビリーム, ARG = -1
RETURN 0

;没
;@SKILL_RANK_変身・覚醒！／スタービーナスビリビリーム
;@SKILL_RANKUP_変身・覚醒！／スタービーナスビリビリーム
;@계승불능_変身・覚醒！／スタービーナスビリビリーム

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_変身・覚醒！／スタービーナスビリビリーム,ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_変身・覚醒！／スタービーナスビリビリーム,ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_変身・覚醒！／スタービーナスビリビリーム,ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_変身・覚醒！／スタービーナスビリビリーム,ARG
RETURN 0

;;拡散攻撃用追加処理
;;ブレスなどの拡散攻撃に使う
;@SKILL_SPREAD_変身・覚醒！／スタービーナスビリビリーム,ARG
;RETURN 0

;====================================================
;トリガー発動条件
;====================================================
;@SKILL_TRIGGER_ACTIONABLE_変身・覚醒！／スタービーナスビリビリーム,ARG,ARG:1
;RETURN 0

;====================================================
;カウンター発動条件
;====================================================
;@SKILL_COUNTER_ACTIONABLE_変身・覚醒！／スタービーナスビリビリーム,ARG,ARG:1
;RETURN RESULT

;====================================================
;ヒット時ダメージ前処理
;====================================================
;@SKILL_HIT_EFFECT_変身・覚醒！／スタービーナスビリビリーム,ARG,ARG:1

;====================================================
;ダメージ発動時
;====================================================
;@SKILL_DAMAGE_EFFECT_変身・覚醒！／スタービーナスビリビリーム,ARG,ARG:1

;====================================================
;行動によるスキルゲージ操作
;====================================================
@SKILLGAGE_CHARGE_変身・覚醒！／スタービーナスビリビリーム,ARG,ARG:1
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム")

IF SKILLGAGE_H_GET(ARG,SKILL_NUM) < 1 && SKILLGAGE_F_GET(ARG,SKILL_NUM) < 100
	;自分の行動の場合
	IF ARG == ARG:1
		;ダメージを受けた敵の数に応じてスキルゲージアップ
		FOR LOCAL,0,CHARANUM
			SIF CFLAG:LOCAL:PT플래그 == CFLAG:ARG:PT플래그
				CONTINUE
			IF CFLAG:LOCAL:현데미지 > 0
				CALL SKILLGAGE_F_ADD,ARG,SKILL_NUM,3
				IF CFLAG:LOCAL:피탄횟수 > 1
					;PRINTFORMW  被弾回数：{CFLAG:LOCAL:피탄횟수}
					SELECTCASE CFLAG:ARG:입력행동
						CASE PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム")
							;スタービーナスビリビリームの場合はゲージが増加しない
						CASEELSE
							CALL SKILLGAGE_F_ADD,ARG,SKILL_NUM,(3*CFLAG:LOCAL:피탄횟수)
					ENDSELECT
				ENDIF
			ELSE
				CONTINUE
			ENDIF
			;会心、弱点を出すとさらにアップ
			SIF GETBIT(CFLAG:LOCAL:받음플래그,21)
				CALL SKILLGAGE_F_ADD,ARG,SKILL_NUM,10
			SIF GETBIT(CFLAG:LOCAL:받음플래그,22)
				CALL SKILLGAGE_F_ADD,ARG,SKILL_NUM,10
		NEXT
	ENDIF
	;ダメージを受けた又はカウンター、トリガー以外の攻撃を受けたら
	;怒りゲージが増える
	IF CFLAG:ARG:현데미지 > 0 || GETBIT(CFLAG:ARG:받음플래그,18) || GETBIT(CFLAG:ARG:받음플래그,19)
		LOCAL = 60 * CFLAG:ARG:현데미지 / MAXBASE:ARG:ＨＰ
		LOCAL = LOCAL < 2 ? 2 # LOCAL
		CALL SKILLGAGE_F_ADD,ARG,SKILL_NUM,LOCAL
	ENDIF
	;スキルゲージが100以上だと100にする
	SIF SKILLGAGE_F_GET(ARG,SKILL_NUM) > 100
		CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,100
ELSE
	CALL AUTO_PU_SKILL_変身・覚醒！／スタービーナスビリビリーム,ARG,"溜め攻撃処理",,2
	IF RESULT > 0 && (CFLAG:ARG:입력행동 != SKILL_NUM || (CFLAG:ARG:현데미지 > 0 || GETBIT(CFLAG:ARG:받음플래그,18) || GETBIT(CFLAG:ARG:받음플래그,19)) && CFLAG:ARG:회피실패)
		;溜め攻撃を開始している最中に別の行動を取ったor被弾した場合はリセット
		CALL AUTO_PU_SKILL_変身・覚醒！／スタービーナスビリビリーム,ARG,"溜め攻撃処理",,0
		PRINTL EXE DRIVE 차지에 실패했다！
		PRINTFORMW %CALLNAME:ARG%의 행동이 중단되었다！
		CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,104
		SETCOLOR CFLAG:ARG:PT플래그 == 0 ? 0xff0033 # 0x33ffcc
		CALL 구상호출 , "BATTLE_MESSAGE" , ARG , ARG
		;一時的に変更していたHゲージを1に戻す
		CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,1
		RESETCOLOR
		CFLAG:ARG:입력행동 = -1
	ENDIF
ENDIF
RETURN 0

;====================================================
;戦闘終了時の処理
;====================================================
@SKILL_BATTLE_END_変身・覚醒！／スタービーナスビリビリーム,ARG

CALL AUTO_PU_SKILL_変身・覚醒！／スタービーナスビリビリーム,ARG,"溜め攻撃処理",,0

;====================================================
;ターン終了時の処理
;====================================================
@SKILL_TURNEND_変身・覚醒！／スタービーナスビリビリーム,ARG
#DIM SKILL_NUM

SIF ARG < 0 || GET_STATE(CFLAG:ARG:상태이상) == "DYING"
	RETURN
SKILL_NUM = PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム")
;PRINTW デンビリキャッチしました
;変身中の場合、Fゲージを減らす
IF SKILLGAGE_H_GET(ARG,SKILL_NUM) > 0
	CALL SKILLGAGE_F_ADD,ARG,SKILL_NUM,-10
	IF SKILLGAGE_F_GET(ARG,SKILL_NUM) < 1
		CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,0
		PRINTFORMW %CALLNAME:ARG%의 변신 상태가 종료되어 버렸다……
		;変身状態終了時の口上
		SETCOLOR CFLAG:ARG:PT플래그 == 0 ? 0xff0033 # 0x33ffcc
		CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,103
		CALL 구상호출 , "BATTLE_MESSAGE" , ARG , ARG
		RESETCOLOR
		CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,0
	ENDIF
ENDIF


;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_変身・覚醒！／スタービーナスビリビリーム,ARG,ARG:1,SKILL_NUM
#DIM SKILL_NUM
#DIM トドメまでお前を殴るのを止めないフラグ
#DIM HIT_COUNT
#DIM L_攻撃強化
#DIM LCOUNT,2

;状況に応じて口上側で喋るセリフを選択するので、口上関数を呼ぶ直前にHゲージを状況に応じて特定値に変更させる。 ex:1 → 101 or 102 or 103,etc. → 1
;口上関数が終わって戻ったらHゲージを0にするか、元々の値に戻しておく。

SKILL_NUM = PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム")
IF SKILLGAGE_H_GET(ARG,SKILL_NUM) < 1
	CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,1
	PRINTFORML %ARG는% 프로세서 유닛을 장착했다！
ELSEIF SKILLGAGE_H_GET(ARG,SKILL_NUM) == 100
	PRINTFORML %ARG는% 변신을 패제했다！
ELSEIF SKILLGAGE_H_GET(ARG,SKILL_NUM) > 0 && SKILLGAGE_H_GET(ARG,SKILL_NUM) < 100
	CALL AUTO_PU_SKILL_変身・覚醒！／スタービーナスビリビリーム,ARG,"溜め攻撃処理",,2
	LCOUNT:0 = RESULT
	IF LCOUNT:0 < 7
		PRINTL 차지함으로써 공격을 중단하는 대신、공격 발동시에 차지수에 따라 공격강화 이외의 버프가 적용되게 됩니다
		PRINTL 차지했을 경우、사용한 ＭＰ는 반환됩니다
		PRINT 차지합니까？　현재 차지 단계：공격
		CALL AUTO_PU_SKILL_変身・覚醒！／スタービーナスビリビリーム,ARG,"溜め攻撃処理",,2
		FOR LCOUNT:1,1,8
			PRINT  + 
			SELECTCASE LCOUNT:1
				CASE 1
					SETCOLOR LCOUNT:0 > 0 ? 0x7fffd4 # 0x696969
					PRINTFORM 마위
				CASE 2
					SETCOLOR LCOUNT:0 > 1 ? 0x7fffd4 # 0x696969
					PRINTFORM 마효
				CASE 3
					SETCOLOR LCOUNT:0 > 2 ? 0x7fffd4 # 0x696969
					PRINTFORM ＣＴ
				CASE 4
					SETCOLOR LCOUNT:0 > 3 ? 0x7fffd4 # 0x696969
					PRINTFORM ＢＳ
				CASE 5
					SETCOLOR LCOUNT:0 > 4 ? 0x7fffd4 # 0x696969
					PRINTFORM 방어
				CASE 6
					SETCOLOR LCOUNT:0 > 5 ? 0x7fffd4 # 0x696969
					PRINTFORM 명중
				CASE 7
					SETCOLOR 0x696969
					PRINTFORM 회피
			ENDSELECT
			RESETCOLOR
		NEXT
		PRINTL
		CALL INPUT_YN
		IF RESULT == 0
			CALL AUTO_PU_SKILL_変身・覚醒！／スタービーナスビリビリーム,ARG,"溜め攻撃処理",,1
			PRINTL
			PRINTFORML %ARG는% 공격을 축적했다！
			CALL TRUE_SKILL_COST,ARG,SKILL_NUM
			CALL VAR_MP,ARG,RESULT,3
			;チャージ時のセリフを出すようにHゲージの値を一時変更
			CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,101
			SETCOLOR CFLAG:ARG:PT플래그 == 0 ? 0xff0033 # 0x33ffcc
			CALL 구상호출 , "BATTLE_MESSAGE" , ARG , ARG
			;一時的に変更していたHゲージを1に戻す
			CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,1
			RESETCOLOR
			WAIT
			RETURN 0
		ENDIF
	ENDIF
	トドメまでお前を殴るのを止めないフラグ = 0
	;相手が元から不死身じゃないなら不死身にしてボコボコにしても問題ないな！
	IF CFLAG:POS(ARG:1):불사신플래그 < 1
		CFLAG:POS(ARG:1):불사신플래그 = 1
		トドメまでお前を殴るのを止めないフラグ = 1
	ENDIF
	IF LCOUNT:0 > 0
		L_攻撃強化 = CFLAG:ARG:공격강화
		FOR LCOUNT:1,1,8
			SELECTCASE LCOUNT:1
				CASE 1
					;命中強化加算の条件を満たせなければ飛ばす
					SIF LCOUNT:0 < 6
						CONTINUE
				CASE 2
					;防御強化加算の条件を満たせなければ飛ばす
					SIF LCOUNT:0 < 5
						CONTINUE
				CASE 3
					;回避強化加算の条件を満たせなければ飛ばす
					SIF LCOUNT:0 < 7
						CONTINUE
				CASE 5
					;魔法効果強化加算の条件を満たせなければ飛ばす
					SIF LCOUNT:0 < 2
						CONTINUE
				CASE 6
					;クリティカル強化加算の条件を満たせなければ飛ばす
					SIF LCOUNT:0 < 3
						CONTINUE
				CASE 7
					;BS付着率強化加算の条件を満たせなければ飛ばす
					SIF LCOUNT:0 < 4
						CONTINUE
			ENDSELECT
			CFLAG:ARG:공격강화 += MAX(CFLAG:ARG:(CFLAGNAME:(GETNUM(CFLAG,"공격강화") + LCOUNT:1)),0)
		NEXT
	ENDIF
	FOR HIT_COUNT,1,32
		;スキル発動で口上を出す処理
		IF HIT_COUNT == 1
			;攻撃開始時のセリフを出すようにHゲージの値を一時変更
			CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,102
			SETCOLOR CFLAG:ARG:PT플래그 == 0 ? 0xff0033 # 0x33ffcc 
			CALL 구상호출 , "BATTLE_MESSAGE" , ARG , ARG
			RESETCOLOR
			CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,1
		ENDIF
		CALL SKILLGAGE_D_ADD,ARG,SKILL_NUM,1
		SELECTCASE HIT_COUNT
			CASE 21
				;21ヒット目に구상호출
				SETCOLOR CFLAG:ARG:PT플래그 == 0 ? 0xff0033 # 0x33ffcc
				CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,102
				CALL 구상호출 , "BATTLE_MESSAGE" , ARG , ARG
				RESETCOLOR
				CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,1
			CASE 31
				;トドメまでお前を殴るのを止めないフラグで付与された不死身フラグを解除してトドメを刺す
				IF トドメまでお前を殴るのを止めないフラグ == 1
					SIF BASE:(POS(ARG:1)):ＨＰ < 1
						BASE:(POS(ARG:1)):ＨＰ = 1
					CFLAG:(POS(ARG:1)):불사신플래그 = 0
				ENDIF
		ENDSELECT
		;--------------------
		CALL ATTACK_SINGLE,ARG,POS(ARG:1),SKILL_NUM
		SIF HIT_COUNT == 31
			CALL SKILLGAGE_D_SET,ARG,SKILL_NUM,0
	NEXT
	CALL AUTO_PU_SKILL_変身・覚醒！／スタービーナスビリビリーム,ARG,"溜め攻撃処理",,0
	CFLAG:ARG:공격강화 = L_攻撃強化
	L_攻撃強化 = 0
	CALL SKILLGAGE_F_SET,ARG,SKILL_NUM,0
	CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,0
	PRINTFORMW %CALLNAME:ARG%의 변신 상태가 종료되어 버렸다……
	;変身状態終了時の口上
	SETCOLOR CFLAG:ARG:PT플래그 == 0 ? 0xff0033 # 0x33ffcc
	CALL 구상호출 , "BATTLE_MESSAGE" , ARG , ARG
	RESETCOLOR
ENDIF
;CALL ATTACK_SINGLE,ARG,POS(ARG:1),SKILL_NUM;	単体攻撃
;CALL ATTACK_FIELD,ARG,ARG:1,SKILL_NUM;			範囲攻撃(列、全体)
;CALL ATTACK_SPREAD,ARG,ARG:1,SKILL_NUM;		拡散攻撃
;CALL ATTACK_RAND,ARG,ARG:1,SKILL_NUM;			ランダム攻撃


;====================================================
;실행내용 여기까지
;====================================================

;====================================================
;전투중 이외에서의 실행
;====================================================
;@ACTION_FIELD_変身・覚醒！／スタービーナスビリビリーム,ARG,ARG:1,SKILL_NUM
;#DIM SKILL_NUM

;CALL HEAL_SINGLE,ARG,POS(ARG:1),SKILL_NUM	;単体回復
;CALL HEAL_FIELD,ARG,ARG:1,SKILL_NUM		;範囲回復(列、全体)
;CALL ALL_HEAL, POS(ARG:1), 0				;単体前回復

;====================================================
;실행내용 여기까지
;====================================================

;====================================================
;追加コマンド(EXTRAの下)
;====================================================
;@SKILL_SPECIAL_COMMAND_EXTRA_変身・覚醒！／スタービーナスビリビリーム,ARG,LOCAL,SKILL_NUM
;#DIM SKILL_NUM
;#LOCALSIZE 1
;PRINTFORML [{LOCAL}] 剣質変化
;C:(LOCAL++) = 10000 + SKILL_NUM;←必須
;RETURN LOCAL;←必須

;====================================================
;追加コマンド(MAGICの下、魔封だと消える)
;====================================================
;@SKILL_SPECIAL_COMMAND_MAGIC_変身・覚醒！／スタービーナスビリビリーム,ARG,LOCAL,SKILL_NUM
;#DIM SKILL_NUM
;#LOCALSIZE 1
;PRINTFORML [{LOCAL}] 剣質変化
;C:(LOCAL++) = 10000 + SKILL_NUM;←必須
;RETURN LOCAL;←必須

;====================================================
;追加コマンド(一番下)
;====================================================
@SKILL_SPECIAL_COMMAND_変身・覚醒！／スタービーナスビリビリーム,ARG,LOCAL,SKILL_NUM
#DIM SKILL_NUM
#LOCALSIZE 1
SKILL_NUM = PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム")
IF SKILLGAGE_F_GET(ARG,SKILL_NUM) > 49 && SKILLGAGE_H_GET(ARG,SKILL_NUM) < 1
	SETCOLOR 0x7fffd4
	PRINTFORML [{LOCAL}] 변신・각성！
	RESETCOLOR
	C:(LOCAL++) = 10000 + SKILL_NUM;←必須
ELSEIF SKILLGAGE_H_GET(ARG,SKILL_NUM) > 0
	SETCOLOR 0x7fffd4
	PRINTFORML [{LOCAL}] 변신해제
	RESETCOLOR
	C:(LOCAL++) = 10000 + SKILL_NUM;←必須
ENDIF
RETURN LOCAL;←必須

;====================================================
;コマンド実行
;====================================================
@INPUT_COM_変身・覚醒！／スタービーナスビリビリーム,ARG
#LOCALSIZE 1
#DIM SKILL_NUM

SKILL_NUM = PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム")

LOCAL = CFLAG:ARG:입력행동
CFLAG:ARG:입력행동 = PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム")
SETCOLOR 0x33ffcc
PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓
PRINTFORM ┃[{CPOS(ARG),2}] %CALLNAME:ARG,21,LEFT%┃　┃
IF SKILLGAGE_H_GET(ARG,SKILL_NUM) < 1
	CALLFORM SKILL_NAME_変身・覚醒！／スタービーナスビリビリーム,ARG
	PRINTFORML %RESULTS,40,LEFT%┃
ELSEIF SKILLGAGE_H_GET(ARG,SKILL_NUM) > 0
	;変身解除用の口上に備えて100にしとく
	CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,100
	PRINTFORML %"変身解除",40,LEFT%┃
ENDIF
PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
RESETCOLOR
SWAP TARGET,ARG
CALL ACTION_変身・覚醒！／スタービーナスビリビリーム,TARGET
SETCOLOR CFLAG:ARG:PT플래그 == 0 ? 0xff0033 # 0x33ffcc
;変身用の口上に備えて99にしとく
SIF SKILLGAGE_H_GET(TARGET,SKILL_NUM) != 100
	CALL SKILLGAGE_H_SET,TARGET,SKILL_NUM,99
CALL 구상호출 , "BATTLE_MESSAGE" , TARGET , TARGET
RESETCOLOR
IF SKILLGAGE_H_GET(TARGET,SKILL_NUM) == 100
	;口上用の値にする必要がなくなったので0にする
	CALL SKILLGAGE_H_SET,TARGET,SKILL_NUM,0
ELSEIF SKILLGAGE_H_GET(TARGET,SKILL_NUM) == 99
	CALL SKILLGAGE_H_SET,TARGET,SKILL_NUM,1
ENDIF
WAIT
SWAP TARGET,ARG
CFLAG:ARG:입력행동 = LOCAL
RETURN -1
;-1 再びコマンド選択へ
; 1 キャラ選択へ

;====================================================
;戦闘中スキルゲージの可視化
;====================================================
@SKILLGAGE_DISPLAY_変身・覚醒！／スタービーナスビリビリーム,ARG
#DIM SKILL_NUM
#DIM F_GAGE
SKILL_NUM = PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム")
F_GAGE = SKILLGAGE_F_GET(ARG,SKILL_NUM)
PRINTFORM \@SKILLGAGE_H_GET(ARG,SKILL_NUM) < 1 ? ＥＸＥ ＤＲＩＶＥ # ＨＤＤ ＧＵＡＧＥ \@|
CALL PRINT_COLORBAR, F_GAGE, 100, 25, UNICODE(0x2585), UNICODE(0x2585), 0xe5001e, 0x696969
RESETCOLOR
PRINTFORML 　{F_GAGE}％
;SKILLGAGE_D_GET(ARG,SKILL_NUM)


;　説明文の表示
@SKILL_EXPLAIN_変身・覚醒！／スタービーナスビリビリーム,ARG
IF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム")) < 1
	PRINTL ＥＸＥ ＤＲＩＶＥ 게이지 추가。게이지 값이 50% 이상의 타이밍에 사용 가능。
	PRINTL 덴게키코용으로 조정된 프로세서 유닛을 장착해、4여신들의 여신화에 필적할 정도의 상태가 된다。
	PRINTL 여데미지율과 피데미지 경감률이 상승하고、『스타 비너스 찌릿찌릿 빔』이 해금된다。
	PRINTL 변신 유지시간은 변신 개시시의 게이지값에 따라 변동한다。
ELSE
	PRINTL 적 1체에 높은 극소 데미지의 검격 파마 만능 복합상성의 물리공격을 ３１회 행한다。발동이 느리다。
	PRINTL 공격대상과 인접해 있을 때는 높은 성능을 발휘하지만、반대로 멀어질수록 성능이 저하한다。
	PRINTL 사용 시 차지 여부를 선택 가능。차지할 때마다 공격강화 이외의 버프의 영향을 받게 된다。
	PRINTL 단、차지 중에 피격되거나、혹은 차지 중에 다른 행동을 취해버릴 경우는 차지가 중단된다。
	SETCOLOR COLOR("RED")
	PRINTL 사용 후、변신 상태가 해제된다。
	RESETCOLOR
	PRINTL
	PRINTL 이도류에 의한 경이로운 ３１연격。절대적인 파괴력을 지녔지만、가성비가 나쁜 것이 티。
ENDIF
PRINTL

;没
;@계승부위_変身・覚醒！／スタービーナスビリビリーム,ARG

;사용조건
@SKILL_SPECIAL_ACTIONABLE_変身・覚醒！／スタービーナスビリビリーム,ARG
SIF SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム")) < 1
	RETURN 0
RETURN 1

;타겟조건
;↓のSKILL_T_RESERVEがRETURN 1 の時に使用する
;@SKILL_SPECIAL_TARGET_変身・覚醒！／スタービーナスビリビリーム,USE_CHARA,ARG
;#DIM USE_CHARA
;RETURN 0

;控えに使えるか
;@SKILL_T_RESERVE_変身・覚醒！／スタービーナスビリビリーム,ARG
;RETURN 0

;어떤 배드스테이터스를 고칠수 있는가
;@SKILL_CURE_STATE_変身・覚醒！／スタービーナスビリビリーム,USE_CHARA,ARG
;#DIM USE_CHARA
;SELECTCASE GET_STATE(ARG)
;	CASE "HAPPY","CLOSE"
;		RETURN 1
;ENDSELECT
;RETURN 0

;この技を複数持てるか
;@중복습득수_変身・覚醒！／スタービーナスビリビリーム,ARG
;RETURN 1

;スキル重複で役に立つ関数
;HAVE_SKILL_OVERLAP(ARG,PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム"))

;スキルゲージで役に立つ関数
;SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム"))
;CALL SKILLGAGE_H_SET,ARG,PU_SKILLNUM_GET(ARG,"変身・覚醒！／スタービーナスビリビリーム"),0

;====================================================
;独自処理
;====================================================
;（予定）と付けられている方は仮置きの物。実際に採用されるかどうかはわからない。
@AUTO_PU_SKILL_変身・覚醒！／スタービーナスビリビリーム,ARG,ARGS,ARG:1,ARG:2
#DIM SKILL_NUM
#DIM L_USER
#DIM S_CHARGE
;ARG = 攻撃者
;ARGS （ダメージ↑、被ダメージ↑、命中↑、回避↑、魔威↑、魔効↑、etc…）といった感じの奴
;ARG:1 = 防御者
;ARG:2 = etc枠。
;文字列を参考にする都合上、どの文字列を使うか事前に定めておいた方がいいかも
;CSTR:ARG:100 = EXTRA勢出典作品、『超次元ゲイムネプテューヌ』とでも書き込む（予定）
;CSTR:ARG:101 = EXTRA勢出典作品でどのキャラなのか、とりあえず『デンゲキコ』とでも書き込む（予定）
L_USER = -1
;ARGSに応じて、攻撃者側か防御者側にいるスキル所有者の番号をL_USERに代入
SELECTCASE ARGS
	CASE "溜め攻撃処理","ターン終了時処理","ダメージ↑","戦闘終了時処理"
		SIF NO:ARG == [[キャラ:덴게키코]]
			L_USER = ARG
	CASE "被ダメージ↓"
		SIF NO:(ARG:1) == [[キャラ:덴게키코]]
			L_USER = ARG:1
ENDSELECT
;代入できず さらに ARGS が戦闘終了時処理でなければ終了
SIF L_USER == -1
	RETURN 0
SKILL_NUM = PU_SKILLNUM_GET(L_USER,"変身・覚醒！／スタービーナスビリビリーム")
SELECTCASE ARGS
	CASE "ダメージ↑","被ダメージ↓"
		;変身中でなければ適用はされない
		SIF SKILLGAGE_H_GET(L_USER,SKILL_NUM) < 1
			RETURN 0
		;L_ダメージ補正は加算式の為、値を加算する
		SIF ARGS == "ダメージ↑"
			RETURN 20
		;被ダメージは乗算であり、尚且つRETURNでは小数では返せないので100で割る値を返す
		;0.8倍なら80、0.65倍ならば65、etc。
		SIF ARGS == "被ダメージ↓"
			RETURN 80
	CASE "溜め攻撃処理"
		SELECTCASE ARG:2
			CASE 0
				;初期化
				S_CHARGE = 0
			CASE 1
				;加算
				S_CHARGE += 1
			CASE 2
				;参照
				RETURN S_CHARGE
		ENDSELECT
	;CASE "ターン終了時処理"
	;	;変身中の場合、Fゲージを減らす
	;	IF SKILLGAGE_H_GET(L_USER,SKILL_NUM) > 0
	;		CALL SKILLGAGE_F_ADD,L_USER,SKILL_NUM,-10
	;		IF SKILLGAGE_F_GET(L_USER,SKILL_NUM) < 1
	;			CALL SKILLGAGE_F_SET,L_USER,SKILL_NUM,0
	;			PRINTFORMW %CALLNAME:L_USER%의 변신 상태가 종료되어 버렸다……
	;			;変身状態終了時の口上
	;			SETCOLOR CFLAG:ARG:PT플래그 == 0 ? 0xff0033 # 0x33ffcc
	;			CALL SKILLGAGE_H_SET,L_USER,SKILL_NUM,103
	;			CALL 구상호출 , "BATTLE_MESSAGE" , L_USER , L_USER
	;			RESETCOLOR
	;			CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,0
	;		ENDIF
	;	ENDIF
	;CASE "戦闘終了時処理"
	;	S_CHARGE = 0
ENDSELECT
RETURN 0
