;스킬명　リミットブレイク（ＦＦ７）　今後実装されるかどうかはさておいて

@SKILL_NAME_リミットブレイク（ＦＦ７）,ARG
IF SKILL_CHANGE(ARG, [[キャラ:티파]])
	RESULTS = 『슬롯』
ELSE
	RESULTS = 리미트 브레이크（ＦＦ７）
ENDIF

;　명중
@SKILL_HITRATE_リミットブレイク（ＦＦ７）, ARG
IF SKILL_CHANGE(ARG, [[キャラ:티파]])
	RETURN 120
ELSE
	RETURN 100
ENDIF

;　위력
@SKILL_POWER_リミットブレイク（ＦＦ７）, ARG
IF SKILL_CHANGE(ARG, [[キャラ:티파]])
	SIF ARG == -1
		RETURN 100
	;実際の攻撃威力計算時、ランダムで０．８倍、１倍、１．２５倍にチェンジ。配分基準はレベルに応じて強化
	;レベル＋１００までのランダム。イェア率は基本的に低く、Lv25から解禁。レベルが上がるほど出やすくなる。
	IF FLAG:확산플래그 == 1
		SIF FLAG:공격횟수 == 0
			;KR판 번역 주, 원명 掌打ラッシュ지만, 번역판 기술명 비트 러시(Beat Rush)로 퍼져있으므로 해당 기술명으로 차용
			PRINTFORM 　비트 러시　　　　　＞
		SIF FLAG:공격횟수 == 1
			PRINTFORM 　섬머솔트　　　　　 ＞
		SIF FLAG:공격횟수 == 2
			PRINTFORM 　수면차기　　　　　 ＞
		SIF FLAG:공격횟수 == 3
			PRINTFORM 　메테오 드라이브　　＞
		SIF FLAG:공격횟수 == 4
			PRINTFORM 　돌핀 블로　　　　　＞
		SIF FLAG:공격횟수 == 5
			PRINTFORM 　메테오 스트라이크　＞
		SIF FLAG:공격횟수 == 6
			PRINTFORM 　파이널 헤븐　　　　＞
		LOCAL = RAND:(100 + BASE:ARG:LV)
		IF LOCAL < 20
			SETCOLOR 0xff0033
				PRINTFORM Miss 　
			RESETCOLOR
			RETURN 80
		ELSEIF LOCAL < 124
			SETCOLOR 0x009e25
				PRINTFORM Hit　　
			RESETCOLOR
			RETURN 100
		ELSE
			SETCOLOR 0xff6c00
				PRINTFORM Yeah!　
			RESETCOLOR
			RETURN 125
		ENDIF
	;実際の攻撃ではない時の一旦の表示用
	ELSE
		RETURN 100
	ENDIF
ELSE
	RETURN 100
ENDIF

;　최저공격횟수
@SKILL_MINATTACKNUMBER_リミットブレイク（ＦＦ７）,ARG
IF SKILL_CHANGE(ARG, [[キャラ:티파]])
	SIF ARG == -1
		RETURN 1
	IF BASE:ARG:LV > 59
		RETURN 7
	ELSEIF BASE:ARG:LV > 49
		RETURN 6
	ELSEIF BASE:ARG:LV > 39
		RETURN 5
	ELSEIF BASE:ARG:LV > 34
		RETURN 4
	ELSEIF BASE:ARG:LV > 29
		RETURN 3
	ELSEIF BASE:ARG:LV > 24
		RETURN 2
	ELSE
		RETURN 1
	ENDIF
ELSE
	RETURN 1
ENDIF

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_リミットブレイク（ＦＦ７）,ARG
IF SKILL_CHANGE(ARG, [[キャラ:티파]])
	SIF ARG == -1
		RETURN 1
	IF BASE:ARG:LV > 59
		RETURN 7
	ELSEIF BASE:ARG:LV > 49
		RETURN 6
	ELSEIF BASE:ARG:LV > 39
		RETURN 5
	ELSEIF BASE:ARG:LV > 34
		RETURN 4
	ELSEIF BASE:ARG:LV > 29
		RETURN 3
	ELSEIF BASE:ARG:LV > 24
		RETURN 2
	ELSE
		RETURN 1
	ENDIF
ELSE
	RETURN 1
ENDIF

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_リミットブレイク（ＦＦ７）, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_リミットブレイク（ＦＦ７）,ARG
IF SKILL_CHANGE(ARG, [[キャラ:티파]])
	SIF ARG == -1
		RETURN 1
	IF BASE:ARG:LV >= 59
		RETURN 70
	ELSEIF BASE:ARG:LV > 49
		RETURN 60
	ELSEIF BASE:ARG:LV > 39
		RETURN 50
	ELSEIF BASE:ARG:LV > 34
		RETURN 40
	ELSEIF BASE:ARG:LV > 29
		RETURN 30
	ELSEIF BASE:ARG:LV > 24
		RETURN 20
	ELSE
		RETURN 10
	ENDIF
ELSE
	RETURN 10
ENDIF

;　상성
@SKILL_TYPE_リミットブレイク（ＦＦ７）,ARG
#LOCALSSIZE 5
#LOCALSIZE 4
IF SKILL_CHANGE(ARG, [[キャラ:티파]])
	SIF ARG < 0 || 스킬속성표시중
		RETURN GET_TYPE_NUM("타격")
	SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
		RETURN GET_TYPE_NUM("타격")
	LOCAL = POS(CFLAG:ARG:타겟)
	SIF LOCAL < 0
		RETURN GET_TYPE_NUM("타격")
		
	LOCAL:2 = -300
	LOCALS = 비구
	LOCALS:1 = 타격
	LOCALS:2 = 진술
	FOR LOCAL:1, 1, 3
		;判定用に数値を仕舞う。反射は-200扱い
		LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
		LOCAL:3 = !(HAVE_SKILL(ARG,[[스킬:영세라이도우]]) || HAVE_SKILL(ARG,2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
		LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
		SELECTCASE LOCAL:1
			CASE 1
				LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
			CASE 1 TO 3
				LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
			CASEELSE
		ENDSELECT
		IF LOCAL:3 > LOCAL:2
			LOCAL:2 = LOCAL:3
			LOCALS = %LOCALS:(LOCAL:1)%
		ENDIF
	NEXT
RETURN GET_TYPE_NUM(LOCALS)

ELSE
	RETURN GET_TYPE_NUM("검격")
ENDIF

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_リミットブレイク（ＦＦ７）, ARG
RETURN 1

;　1 물리데미지로 계산　2 마법데미지로 계산
;スキルチェンジ関数で行けるようになったらしいぞ、やったねたえちゃん
@SKILL_DAMAGETYPE_リミットブレイク（ＦＦ７）, ARG
IF SKILL_CHANGE(ARG, [[キャラ:티파]])
	RETURN 1
ELSE
	RETURN 1
ENDIF


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_リミットブレイク（ＦＦ７）,ARG
RETURN 1

;射程　1:S 2:M 3:L
@SKILL_RANGE_リミットブレイク（ＦＦ７）,ARG
IF SKILL_CHANGE(ARG, [[キャラ:티파]])
	RETURN 2
ELSE
	RETURN 1
ENDIF

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_リミットブレイク（ＦＦ７）,ARG
RETURN 1

@SKILL_CRITICAL_RATE_リミットブレイク（ＦＦ７）, ARG
RETURN 15

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_リミットブレイク（ＦＦ７）,ARG
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_リミットブレイク（ＦＦ７）,ARG
RETURN 0

;속도보정
@SKILL_SPEED_リミットブレイク（ＦＦ７）, ARG = -1
RETURN 0

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_リミットブレイク（ＦＦ７）, ARG = -1
RETURN 0

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_リミットブレイク（ＦＦ７）,ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_リミットブレイク（ＦＦ７）,ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_リミットブレイク（ＦＦ７）,ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_リミットブレイク（ＦＦ７）,ARG
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_リミットブレイク（ＦＦ７）,ARG,ARG:1,SKILL_NUM
#DIM SKILL_NUM
#DIM リミットゲージ
リミットゲージ = PU_SKILLNUM_GET(ARG,"リミットブレイク（ＦＦ７）")
IF SKILL_CHANGE(ARG, [[キャラ:티파]])
	FLAG:확산플래그 = 1
	CALL ATTACK_SINGLE,ARG,POS(ARG:1),SKILL_NUM;	単体攻撃
	PRINTFORML 　
	FLAG:확산플래그 = 0
ELSE
	CALL ATTACK_SINGLE,ARG,POS(ARG:1),SKILL_NUM;	単体攻撃
ENDIF

;使用したリミットゲージをクリア
CALL SKILLGAGE_F_SET,ARG,リミットゲージ,0

;====================================================
;실행내용 여기까지
;====================================================

;　 설명문 표시
@SKILL_EXPLAIN_リミットブレイク（ＦＦ７）,ARG
SETCOLOR 0x33ffcc
	PRINTL 리미트 게이지가 추가되어、평상시의 다양한 행동으로 상승하게 된다。
	PRINTL 주로 적으로부터 데미지를 입었을 때 크게 상승하며、최대 시 리미트기가 사용 가능해진다。
RESETCOLOR
IF U > 0 && U < CHARANUM && NO:U == [[キャラ:티파]]
	PRINTL 리미트 브레이크『슬롯』
	;あまりに煩雑すぎたが頑張ってスロット要素作ってみた。
	PRINTL 적 1체에、타격・비구・전술의 복합속성으로、소위력의 물리공격을 최대 ７회 행한다。고 크리티컬률
	PRINTL 이 공격은『레벨에 따라 공격횟수가 상승』＋『랜덤으로 공격력이 변동』하고、
	PRINT 『【0】～【Lv+100】까지의 난수』가
	SETCOLOR 0xff0033
		PRINTFORM 20 이하일 경우 0.8배
	RESETCOLOR
	PRINT 、
	SETCOLOR 0xff6c00
		PRINTFORM 125 이상일 경우 1.25배
	RESETCOLOR
	PRINTL 가 된다。
	PRINTL 
	PRINTL 분노의 에너지를 폭발시켜 날리는 필살기。티파는 위력의 안정성을 희생해、연속으로 공격한다。
	PRINTL 기술 내역은、처음부터 발동하는 순서대로、비트 러시、섬머솔트、수면차기、
	PRINTL 메테오 드라이브、돌핀 블로、메테오 스트라이크、파이널 헤븐。
	PRINTL 티파의 리미트 브레이크 스킬（CSTR전용）
ELSE
	PRINTL 
ENDIF

@SKILLGAGE_DISPLAY_リミットブレイク（ＦＦ７）,ARG
#DIM リミットゲージ
#LOCALSIZE 1
リミットゲージ = PU_SKILLNUM_GET(ARG,"リミットブレイク（ＦＦ７）")
SETCOLOR 0x999999
	;リミットゲージ量を表示。チャージ完了時白色で強調
	SIF SKILLGAGE_F_GET(ARG,リミットゲージ) == 100
		SETCOLOR 0xFFFFFF
	PRINTFORML 리미트 게이지：{SKILLGAGE_F_GET(ARG,リミットゲージ)}％
RESETCOLOR



@SKILLGAGE_CHARGE_リミットブレイク（ＦＦ７）,ARG,ARG:1
#DIM リミットゲージ
#LOCALSIZE 1
リミットゲージ = PU_SKILLNUM_GET(ARG,"リミットブレイク（ＦＦ７）")
;スキルゲージDがある＝発動中の場合でも貯まる。
;SIF SKILLGAGE_D_GET(ARG,リミットゲージ) > 0
;RETURN 0
	
;自分の行動の場合
IF ARG == ARG:1
	CALL SKILLGAGE_F_ADD,ARG,リミットゲージ,5
	;티파がプレミアムハートを所持時、更に＋５
	SIF SKILL_CHANGE(ARG, [[キャラ:티파]]) && EQUIP:ARG:검 == 2389
		CALL SKILLGAGE_F_ADD,ARG,リミットゲージ,5
	
	;ダメージを受けた敵の数に応じてスキルゲージアップ
	FOR LOCAL,0,CHARANUM
		SIF CFLAG:LOCAL:PT플래그 == CFLAG:ARG:PT플래그
			CONTINUE
		IF CFLAG:LOCAL:현데미지 > 0
			CALL SKILLGAGE_F_ADD,ARG,リミットゲージ,5
		ELSE
			CONTINUE
		ENDIF
		;会心、弱点を出すとさらにアップ
		SIF GETBIT(CFLAG:LOCAL:받음플래그,21)
			CALL SKILLGAGE_F_ADD,ARG,リミットゲージ,10
		SIF GETBIT(CFLAG:LOCAL:받음플래그,22)
			CALL SKILLGAGE_F_ADD,ARG,リミットゲージ,10
	NEXT
	;気合を入れても上がる
	SIF CFLAG:ARG:입력행동 == 511
		CALL SKILLGAGE_F_ADD,ARG,リミットゲージ,10
	;集中しても上がる
	SIF CFLAG:ARG:입력행동 == 512
		CALL SKILLGAGE_F_ADD,ARG,リミットゲージ,10
	;雄叫びしても上がる
	SIF CFLAG:ARG:입력행동 == 514
		CALL SKILLGAGE_F_ADD,ARG,リミットゲージ,10
	;挑発しても上がる
	SIF CFLAG:ARG:입력행동 == 515
		CALL SKILLGAGE_F_ADD,ARG,リミットゲージ,10

ENDIF
;ダメージを受けた又はカウンター、トリガー以外の攻撃を受けたら
;ゲージが増える。増加量は最大体力に対するダメージ割合の３倍
IF CFLAG:ARG:현데미지 > 0 || GETBIT(CFLAG:ARG:받음플래그,18) || GETBIT(CFLAG:ARG:받음플래그,19)
	LOCAL = 300 * CFLAG:ARG:현데미지 / MAXBASE:ARG:ＨＰ
	LOCAL = LOCAL < 2 ? 2 # LOCAL
	CALL SKILLGAGE_F_ADD,ARG,リミットゲージ,LOCAL
ENDIF
;ゲージ上限100
SIF SKILLGAGE_F_GET(ARG,リミットゲージ) > 100
	CALL SKILLGAGE_F_SET,ARG,リミットゲージ,100

;사용조건
@SKILL_SPECIAL_ACTIONABLE_リミットブレイク（ＦＦ７）,ARG
#DIM リミットゲージ
#LOCALSIZE 1
;リミットゲージ最大時のみ。
リミットゲージ = PU_SKILLNUM_GET(ARG,"リミットブレイク（ＦＦ７）")
SIF SKILLGAGE_F_GET(ARG,リミットゲージ) < 100
	RETURN 0
RETURN 1
