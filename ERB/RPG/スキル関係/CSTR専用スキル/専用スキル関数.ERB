;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:専用スキル関数.ERB
;	Facility	:専用スキル用の関数群
;
;	Licence		:。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		Jガン					新規作成
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================
;専用スキルのスキル番号を返す
;ARG   対象のキャラ
;ARGS  専用技名
;==============================
@PU_SKILLNUM_GET,ARG,ARGS = ""
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
SIF ARGS == ""
	RETURNF 0
FOR LOCAL,1,13
	LOCALS = %CSTR:ARG:("전용기"+TOSTR(LOCAL))%
	SIF !(LOCALS == "") && ARGS == LOCALS
		RETURNF 3903 + LOCAL
NEXT
RETURNF -1

;===============================
;専用スキルが設定されているかどうか
;ARG   対象のキャラ
;ARGS  専用技名
;ARG:1 専用技の位置を返すかどうか
;==============================
@PU_SKILL_CHECK,ARG,ARGS = "",ARG:1 = 0
#FUNCTION
#LOCALSIZE 1
SIF ARGS == ""
	RETURNF 0
FOR LOCAL,1,13
	IF CSTR:ARG:("전용기"+TOSTR(LOCAL)) == ARGS
		SIF ARG:1 == 1
			RETURNF LOCAL
		RETURNF 1
	ENDIF
NEXT
RETURNF 0

;===============================
;専用スキルを設定する
;ARG   対象のキャラ
;ARGS  専用技名
;RETURN 追加できたかどうか
;==============================
@PU_SKILL_ADD,ARG,ARGS = ""
#LOCALSIZE 1
#LOCALSSIZE 1
SIF ARGS == ""
	RETURN 0
FOR LOCAL,1,13
	LOCALS = %CSTR:ARG:("전용기"+TOSTR(LOCAL))%
	IF LOCALS == ARGS
		RETURN 0
	ELSEIF LOCALS == ""
		CSTR:ARG:("전용기"+TOSTR(LOCAL)) = %ARGS%
		RETURN 1
	ENDIF
NEXT
RETURN 0

;===============================
;特定の専用スキルを習得しているかどうか
;ARG   対象のキャラ
;ARGS  専用技名
;ARG:1 位置情報を返すかどうか
;==============================
@HAVE_PU_SKILL(ARG,ARGS = "",ARG:1 = 0)
#FUNCTION
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,ARGS)
SIF SKILL_NUM < 0
	RETURNF 0
RETURNF HAVE_SKILL(ARG,SKILL_NUM,ARG:1)

;===============================
;専用スキル習得
;ARG = 専用スキルを習得するキャラ
;ARGS = 習得する専用スキル名
;ARG:1 = 初期変身悪魔習得スキルの場合は真(1)
;===============================
@LEARN_PU_SKILL,ARG,ARGS = "",ARG:1
#DIM SKILL_NUM
SIF ARGS == ""
	RETURN 0
IF !PU_SKILL_CHECK(ARG,ARGS)
	CALL PU_SKILL_ADD,ARG,ARGS
	IF RESULT < 1
		SIF !(CSTR:ARG:전용기12 == "")
			PRINT 전용기술이 가득해서 
		PRINTFORMW %조사처리(ARGS,"를")% 기억할 수 없었습니다
		RETURN 0
	ENDIF
ENDIF
SKILL_NUM = PU_SKILLNUM_GET(ARG,ARGS)
CALL LEARN_SKILL,ARG,SKILL_NUM,ARG:1


