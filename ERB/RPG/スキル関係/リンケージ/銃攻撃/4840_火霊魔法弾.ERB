

;스킬명
@SKILL_NAME_4840,ARG
RESULTS = 화령마법탄

;　명중
@SKILL_HITRATE_4840, ARG
RETURN 150

;　위력
@SKILL_POWER_4840, ARG
SIF ARG == -1 || CFLAG:ARG:단독링케이지
	RETURN 85
RETURN 85 + MAXBASE:(CFLAG:ARG:링케이지참가자1):마력/2 + MAXBASE:(CFLAG:ARG:링케이지참가자1):지혜 / 4

;　최저공격횟수
;マシンガンというかランダムターゲット系は回数が1.5倍になる
@SKILL_MINATTACKNUMBER_4840,ARG
RESULT = 0
TRYCALLFORM GUN_RANDOMTARGET_{EQUIP:ARG:총}
IF RESULT == 1
	CALLFORM 최저공격횟수_{EQUIP:ARG:총},ARG
	RETURN RESULT * 3 / 2
ELSE
	CALLFORM 최저공격횟수_{EQUIP:ARG:총},ARG
	RETURN RESULT
ENDIF

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_4840,ARG
RESULT = 0
TRYCALLFORM GUN_RANDOMTARGET_{EQUIP:ARG:총}
IF RESULT == 1
	CALLFORM 최대공격횟수_{EQUIP:ARG:총},ARG
	RETURN RESULT * 3 / 2
ELSE
	CALLFORM 최대공격횟수_{EQUIP:ARG:총},ARG
	RETURN RESULT
ENDIF
;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_4840, ARG, ARG:1
RETURN 2
;소비없음＝1

;　코스트량
@SKILL_COST_4840, ARG
RETURN 5

;　상성
@SKILL_TYPE_4840,ARG
RETURN GET_TYPE_NUM("화염")
;　1 EXTRA　2 MAGIC

@SKILL_DECIDE_TYPE_4840, ARG
RETURN 1

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_4840, ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_4840
RETURN 1

;　사거리
@SKILL_RANGE_4840,ARG
CALLFORM 사거리_{EQUIP:ARG:총},ARG
RETURN RESULT

;　범위　1 1체　2 1열　3 전체　4 적아군전체
;　ランダムターゲットでない銃は範囲が一段階分強化される
@SKILL_SPHERE_4840,ARG
RESULT = 0
TRYCALLFORM GUN_RANDOMTARGET_{EQUIP:ARG:총}
IF RESULT == 1
	CALLFORM 공격범위_{EQUIP:ARG:총},ARG
	RETURN RESULT
ELSE
	CALLFORM 공격범위_{EQUIP:ARG:총},ARG
	RETURN LIMIT(RESULT + 1, 1, 3)
ENDIF
;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_4840
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_4840,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_4840
RETURN 23
;화염

;속도보정
@SKILL_SPEED_4840, ARG = -1
RETURN 20

;　구상의 표시와 그 타입
@SKILL_MESSAGE_4840,ARG

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_4840
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_4840
RETURN 0

@SKILL_RANKUP_4840
RETURN 0

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_4840,ARG
RETURN GET_STATE_NUM("FLAME")

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_4840,ARG
RETURN 4

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_4840,ARG
CALLFORM 총타입_{EQUIP:ARG:총}
SELECTCASE RESULT
	CASE 1
		RETURN 40
	CASE 2
		RETURN 30
	CASE 3
		RETURN 8
	CASE 4
		RETURN 50
	CASEELSE
		RETURN 25
ENDSELECT
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_4840,ARG
CALLFORM 총타입_{EQUIP:ARG:총}
SELECTCASE RESULT
	CASE 1
		RETURN 90
	CASE 2
		RETURN 70
	CASE 3
		RETURN 20
	CASE 4
		RETURN 95
	CASEELSE
		RETURN 65
ENDSELECT
RETURN 0
;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_4840,ARG,ARG:1
CALL SKILL_SPHERE_4840,ARG
IF RESULT == 1
	LOCAL = POS(ARG:1)
	CALL GUN_SINGLE,ARG,LOCAL,4840
ELSE
	RESULT = 0
	TRYCALLFORM GUN_RANDOMTARGET_{EQUIP:ARG:총}
	IF RESULT == 1
		CALL GUN_RAND,ARG,ARG:1,4840
	ELSE
		CALL GUN_FIELD,ARG,ARG:1,4840
	ENDIF
	
ENDIF
SIF CFLAG:ARG:PT플래그 > 0
	ITEM:5514 -= 1





;====================================================
;실행내용 여기까지
;====================================================

;　 설명문 표시
@SKILL_EXPLAIN_4840
PRINTL 화염을 두른 탄환이、맞은 상대를 태워버린다。

;계승에 필요한 부위
@계승부위_4840,ARG
RETURN 0

;사용조건
@SKILL_SPECIAL_ACTIONABLE_4840,ARG
CALL CHECK_COST,ARG,4840
SIF RESULT == 0
	RETURN 0
RETURN 1
;とくにない


;=====================================================
;링케이지설정
;=====================================================
@LINKAGE_4840
RETURN 1

@참가인수_4840
RETURN 1

@참가조건표시_4840,ARG
SELECTCASE ARG
	CASE 1
		PRINTFORM 아기、마하・라기、프레이、기가、또는 마하기가 중、하나
	CASE 2
		PRINTFORM 랜턴샷
ENDSELECT

@발동자조건_4840 , ARG
SIF EQUIP:ARG:총 == 0
	RETURN 0
SIF ITEM:5514 < 1
	RETURN 0
SIF !TALENT:ARG:건슬링거
	RETURN 0
TRYCALLFORM 특수탄창_{EQUIP:ARG:총},ARG
FOR LOCAL, 1, RESULT + 1
	LOCALS = 특수탄{LOCAL}
	SIF EQUIP:ARG:LOCALS == 5514
		RETURN 1
NEXT
RETURN 0


@참가자조건_4840, ARG , ARG:1
SIF CPOS(ARG) < 1 || CPOS(ARG) > 6
	RETURN 0
CALL CHECK_COST_LINCAGE, ARG, 4840, ARG:1
SIF RESULT == 0
	RETURN 0
SELECTCASE ARG:1
	CASE 1
		;アギを使えること
		IF HAVE_SKILL(ARG, [[스킬:아기]])
			CALL CHECK_ACTIONABLE , ARG , [[스킬:아기]]
			SIF RESULT
				RETURN 1
		ENDIF
		;マハ・ラギを使えること
		IF HAVE_SKILL(ARG, [[스킬:마하・라기]])
			CALL CHECK_ACTIONABLE , ARG , [[스킬:마하・라기]]
			SIF RESULT
				RETURN 1
		ENDIF
		;フレイを使えること
		IF HAVE_SKILL(ARG, [[스킬:프레이]])
			CALL CHECK_ACTIONABLE , ARG , [[스킬:프레이]]
			SIF RESULT
				RETURN 1
		ENDIF
		;ギガを使えること
		IF HAVE_SKILL(ARG, [[스킬:기가]])
			CALL CHECK_ACTIONABLE , ARG , [[스킬:기가]]
			SIF RESULT
				RETURN 1
		ENDIF
		;マハギーガを使えること
		IF HAVE_SKILL(ARG, [[스킬:마하기가]])
			CALL CHECK_ACTIONABLE , ARG , [[스킬:마하기가]]
			SIF RESULT
				RETURN 1
		ENDIF
		RETURN 0
ENDSELECT

@링케이지코스트타입_4840, ARG
SELECTCASE ARG
	CASE 1
		RETURN 3
ENDSELECT
@링케이지코스트_4840, ARG
SELECTCASE ARG
	CASE 1
		RETURN 4
ENDSELECT
