
;스킬명
@SKILL_NAME_3617,ARG
RESULTS = 하이볼티지
SIF SKILLGAGE_H_GET(ARG, 3617) == 1
	RESULTS = 롤링썬더
SIF SKILLGAGE_H_GET(ARG, 3617) == 2
	RESULTS = 썬더브로우

;　명중
@SKILL_HITRATE_3617, ARG
RETURN 100

;　위력
@SKILL_POWER_3617, ARG
IF SKILLGAGE_D_GET(ARG, 3600)
	IF SKILLGAGE_H_GET(ARG, 3617) == 2
		RETURN 600
	ELSEIF SKILLGAGE_H_GET(ARG, 3617) == 1
		RETURN 150
	ELSE
		RETURN 200
	ENDIF
ELSE
	IF SKILLGAGE_H_GET(ARG, 3617) == 2
		RETURN 500
	ELSEIF SKILLGAGE_H_GET(ARG, 3617) == 1
		RETURN 120
	ELSE
		RETURN 170
	ENDIF
ENDIF

;　최저공격횟수
@SKILL_MINATTACKNUMBER_3617,ARG
IF SKILLGAGE_H_GET(ARG, 3617) == 2
	RETURN 1
ELSEIF SKILLGAGE_H_GET(ARG, 3617) == 1 && SKILLGAGE_D_GET(ARG, 3600)
	RETURN 4
ELSEIF SKILLGAGE_H_GET(ARG, 3617) == 1
	RETURN 3
ELSE
	RETURN 2
ENDIF

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3617,ARG
IF SKILLGAGE_H_GET(ARG, 3617) == 2
	RETURN 1
ELSEIF SKILLGAGE_H_GET(ARG, 3617) == 1 && SKILLGAGE_D_GET(ARG, 3600)
	RETURN 4
ELSEIF SKILLGAGE_H_GET(ARG, 3617) == 1
	RETURN 3
ELSE
	RETURN 2
ENDIF

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3617, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_3617,ARG
RETURN 12

;　상성
@SKILL_TYPE_3617,ARG
#LOCALSSIZE 3
#LOCALSIZE 4
SIF ARG < 0 || 스킬속성표시중
	RETURN GET_TYPE_NUM("타격")
SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
	RETURN GET_TYPE_NUM("타격")
LOCAL = POS(CFLAG:ARG:타겟)
SIF LOCAL < 0
	RETURN GET_TYPE_NUM("타격")
	
LOCAL:2 = -300
LOCALS = 타격
LOCALS:1 = 전격
LOCALS:2 = 타격
FOR LOCAL:1, 1, 3
	;판정用に수치を仕마이(동방)う。반사は-200扱い
	LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
	LOCAL:3 = BTL_CHK_SKILL_관통(ARG) && INRANGE(MAXBASE:LOCAL:(LOCALS:(LOCAL:1)), 0, 100) ? 100 # LOCAL:3
	LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
	LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? 0 # LOCAL:3
	LOCAL:3 = GET_TYPE_NUM(LOCALS:(LOCAL:1)) < 17 && CFLAG:LOCAL:무효플래그 ? 0 # LOCAL:3
	IF GET_TYPE_NUM(LOCALS:(LOCAL:1)) < 4
		LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
	ELSEIF GET_TYPE_NUM(LOCALS:(LOCAL:1)) < 17
		LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
	ENDIF
	IF LOCAL:3 > LOCAL:2
		LOCAL:2 = LOCAL:3
		LOCALS = %LOCALS:(LOCAL:1)%
	ENDIF
NEXT
RETURN GET_TYPE_NUM(LOCALS)
;타격

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3617, ARG
RETURN 1
;どちらでもない

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3617, ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_3617
RETURN 1

;　사거리
@SKILL_RANGE_3617,ARG
RETURN 2

;　범위　1１체　2１열　3전체　4　적아군 전체
@SKILL_SPHERE_3617,ARG
RETURN 1

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3617
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_3617,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_3617
RETURN 6
;전격

;속도보정
@SKILL_SPEED_3617, ARG = -1
RETURN 0

;　구상의 표시와 그 타입
@SKILL_MESSAGE_3617,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3617
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3617
RETURN 0

;テンプレート用、추가효과시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3617,ARG
RETURN GET_STATE_NUM("SHOCK")

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3617,ARG
RETURN GET_TYPE_NUM("전격")

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_3617,ARG
RETURN 20

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_3617,ARG
RETURN 66

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_3617,ARG,ARG:1
CALL ATTACK_SINGLE,ARG,POS(ARG:1),3617
IF SKILLGAGE_H_GET(ARG, 3617) >= 2
	CALL SKILLGAGE_H_SET,ARG,3617,0
ELSEIF SKILLGAGE_H_GET(ARG, 3617) == 1
	CALL SKILLGAGE_H_SET,ARG,3617,2
ELSE
	CALL SKILLGAGE_H_SET,ARG,3617,1
ENDIF


;====================================================
;실행내용 여기까지
;====================================================

;　 설명문 표시
@SKILL_EXPLAIN_3617
IF SKILLGAGE_H_GET(U, 3617) == 2
	PRINTL 오른 주먹으로 혼신의 직구를 내지른다。
	PRINTL 적 1체에 전격 속성의 특대 데미지
	PRINTL 데빌트리거 발동중　위력 상승
ELSEIF SKILLGAGE_H_GET(U, 3617) == 1
	PRINTL 고속 회전하여 연격후、발차기를 반복한다。
	PRINTL 적 1체에 전격 속성으로 3회 공격
	PRINTL 데빌트리거 발동중　4회 공격하며 위력 상승
ELSE
	PRINTL 전기를 휘감은 회전을 계속하며 공중으로 날아오른다。
	PRINTL 적 1체에 전격 속성으로 2회 공격을 행한다
	PRINTL 데빌트리거 발동중　위력 상승
ENDIF

;계승에 필요한 부위
@계승부위_3617,ARG
RETURN 1

;사용조건
@SKILL_SPECIAL_ACTIONABLE_3617,ARG
SIF SKILLGAGE_D_GET(ARG, 2953)
	RETURN 1
RETURN 0

;■하이볼티지派生설명
;前提とした라운드트립を사용し、SKILLGAGE_D_GET(ARG, 2953)がある상태で사용可能
;当스킬사용毎にSKILLGAGE_H_GET(U, 3617)が증가し、단계ごとに性能も변화
;他스킬と組み合わせも可能

;・스킬組み合わせ例
;					라운드트립(起点)
;					↓					↓
;			하이볼티지			코레다(통상火힘)
;			↓			↓
;	이나즈마			ローリングサンダー
;		↓					↓
;코레다（火힘大)		サンダーブロウ

;サンダーブロウ/코레다を사용するとゲージ리셋
;코레다はSKILLGAGE_H_GET(U, 3617) == 2で火힘大
;ゲージ+데빌트리거発動で최대火힘を発揮

