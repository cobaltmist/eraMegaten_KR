;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:SKILL3628_スパークステラー.ERB
;	Facility	:SKILL:3628 (スパークステラー)。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2021/03/15		木綿豆腐				新規作成
;	002		2021/03/21		木綿豆腐				ゲージ周りの処理を変更。
;	003		2021/03/30		木綿豆腐				ロックオン周りの処理を変更。
;	004		2021/04/24		木綿豆腐				敵撃破時にクードス加算処理を追加
;	005		2021/08/24		木綿豆腐				専用アクセサリ装備時の処理追加
;													念のため継承できないようにした
;	009		2022/05/12		木綿豆腐				挙動をなるべく原作に近くした（ロックしたらそいつに強制的に攻撃）
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;スキル名
@SKILL_NAME_3628, ARG
RESULTS = 스파크스텔라

;　명중
@SKILL_HITRATE_3628, ARG
RETURN 100

;　위력
@SKILL_POWER_3628, ARG
SIF ARG < 0
	RETURN 225
IF SKILLGAGE_D_GET(ARG, [[스킬:스파크스텔라]]) > 0
	IF SKILL_CHANGE(ARG, [[キャラ:아큐라(X)]]) && EQUIP:ARG:악세서리 == 5349 && FLAG:카운터중
		;約1.25倍
		IF FLAG:확산플래그
			RETURN 85
		ELSE
			RETURN 115
		ENDIF
	ELSE
		IF FLAG:확산플래그
			RETURN 70
		ELSE
			RETURN 95
		ENDIF
	ENDIF
ELSE
	RETURN 225
ENDIF

;　최저공격횟수
@SKILL_MINATTACKNUMBER_3628, ARG
SIF SKILLGAGE_D_GET(ARG, [[스킬:스파크스텔라]]) > 0
	RETURN 3
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3628, ARG
SIF SKILLGAGE_D_GET(ARG, [[스킬:스파크스텔라]]) > 0
	RETURN 3
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3628, ARG
SIF FLAG:확산플래그
	RETURN 1
RETURN 3
;消費なし＝1

;　코스트량
@SKILL_COST_3628, ARG
SIF ARG < 0
	RETURN 12
;ダークネストリガー発動中はコスト1/3
SIF SKILLGAGE_H_GET(ARG, [[스킬:DARKNESS TRIGGER]]) == 1
	RETURN 4
RETURN 12

;　상성
@SKILL_TYPE_3628, ARG
RETURN GET_TYPE_NUM("전격")

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3628, ARG
RETURN 1
;EXTRA

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3628, ARG
RETURN 1

;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_3628
#DIM COPEN
COPEN = 0
SIF U > 0 && U < CHARANUM && NO:U == [[キャラ:아큐라(X)]]
	COPEN = U
SIF COPEN == 0 && FLAG:행동순1 > 0 && NO:(FLAG:행동순1) == [[キャラ:아큐라(X)]]
	COPEN = FLAG:행동순1
;ロック中の攻撃・ロック対象に強制
IF COPEN > 0 && BLITZ_TARGET_JUDG(COPEN) == 1
	RETURN 4
;非ロック中の攻撃・敵単体
ELSE
	RETURN 1
ENDIF

;　사거리
@SKILL_RANGE_3628, ARG
RETURN 3

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_3628, ARG
;ロック中の攻撃・ロック対象に強制
IF BLITZ_TARGET_JUDG(ARG) == 1
	RETURN 4
;非ロック中の攻撃・敵単体
ELSE
	RETURN 1
ENDIF

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3628
RETURN 1

;계승타입
@SKILL_SUCCESSION_TYPE_3628
RETURN GET_SUCCESSION_NUM("전격")

;속도보정
;スパークステラーは発動が音速い
@SKILL_SPEED_3628, ARG = -1
RETURN -3

;　口上の表示とそのタイプ
@SKILL_MESSAGE_3628, ARG

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3628
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3628
RETURN 23

@계승불능_3628
RETURN 1

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3628, ARG
RETURN GET_STATE_NUM("SHOCK")

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3628, ARG
RETURN GET_TYPE_NUM("전격")

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_3628, ARG
RETURN 10

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_3628, ARG
RETURN 50

;拡散攻撃用追加処理
;ブレスなどの拡散攻撃に使う
@SKILL_SPREAD_3628, ARG
IF SKILLGAGE_D_GET(ARG, [[스킬:스파크스텔라]]) > 0
	RETURN 1
ELSE
	RETURN 0
ENDIF

;====================================================
;トリガー発動条件
;====================================================
@SKILL_TRIGGER_ACTIONABLE_3628, ARG, ARG:1
#DIM ROCK_H
#DIM ループ用
#DIM 攻撃前敵カウント
#DIM 攻撃後敵カウント
#DIM 差分
#DIM 加算クードス

;自分じゃなければNG
SIF ARG != ARG:1
	RETURN 0
;X-ROSS DISASTER,SONG OF DIVA,DARKNESS TRIGGERならNG
SIF CFLAG:(ARG:1):입력행동 == [[스킬:X-ROSS DISASTER]] || CFLAG:(ARG:1):입력행동 == [[스킬:SONG OF DIVA]] || CFLAG:(ARG:1):입력행동 == [[스킬:DARKNESS TRIGGER]]
	RETURN 0
;EXウェポン0番=スパークステラーじゃないとNG
SIF SKILLGAGE_F_GET(ARG, [[스킬:DARKNESS TRIGGER]]) != 0
	RETURN 0
;ロック状態の敵が健在かつダークネストリガー発動中
IF BLITZ_TARGET_JUDG(ARG) == 1 && SKILLGAGE_H_GET(ARG, [[스킬:DARKNESS TRIGGER]]) == 1
	;1桁は被弾回数,2桁以上はターゲットを割り当てるようにした
	ROCK_H = SKILLGAGE_H_GET(ARG, [[스킬:블리츠대쉬]]) / 10
	攻撃前敵カウント = 0
	攻撃後敵カウント = 0
	IF CFLAG:ARG:PT플래그 > 0
		FOR ループ用, 7, 17
			LOCALS = 포지션{ループ用}
			SIF FLAG:LOCALS == -1
				CONTINUE
			SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
				CONTINUE
			攻撃前敵カウント += 1
		NEXT
	ENDIF
	
	SETCOLOR 0x9933cd
	PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓
	PRINTFORM ┃[{CPOS(ARG), 2}] %"ＲｏＲｏ",21,LEFT%┃　┃
	PRINTFORML %"스파크스텔라",40,LEFT%┃
	PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
	RESETCOLOR
	CALL SKILLGAGE_D_SET, ARG, [[스킬:스파크스텔라]], 1
	CALL ATTACK_SPREAD, ARG, CPOS(ROCK_H), 3628
	
	IF CFLAG:ARG:PT플래그 > 0
		FOR ループ用, 7, 17
		LOCALS = 포지션{ループ用}
			SIF FLAG:LOCALS == -1
				CONTINUE
			SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
				CONTINUE
			攻撃後敵カウント += 1
		NEXT
	ENDIF
	
	差分 = 攻撃前敵カウント - 攻撃後敵カウント
	
	IF 差分 > 0
		SELECTCASE 差分
		CASE 1
			加算クードス = 20
		CASE 2
			加算クードス = 40
		CASE 3
			加算クードス = 60
		CASE 4
			加算クードス = 80
		CASEELSE
			加算クードス = 100
		ENDSELECT
		IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
			TIMES 加算クードス, 1.25
		ELSEIF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
			TIMES 加算クードス, 0.5
		ENDIF
		PRINTFORMW AERIAL EX　Kudos + {加算クードス}
		CALL SKILLGAGE_F_ADD, ARG, [[스킬:블리츠대쉬]], 加算クードス
	ENDIF
	IF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
		;アパシーマンの上限は1200
		SIF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 1200
			CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 1200
	ELSE
		;上限は99999
		SIF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 99999
			CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 99999
	ENDIF
ENDIF
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_3628, ARG, ARG:1
#DIM ROCK_H
#DIM ループ用
#DIM 攻撃前敵カウント
#DIM 攻撃後敵カウント
#DIM 差分
#DIM 加算クードス

SIF ARG < 0
	CALL ATTACK_SINGLE, ARG, POS(ARG:1), 3628

攻撃前敵カウント = 0
攻撃後敵カウント = 0
IF CFLAG:ARG:PT플래그 > 0
	FOR ループ用, 7, 17
		LOCALS = 포지션{ループ用}
		SIF FLAG:LOCALS == -1
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
			CONTINUE
		攻撃前敵カウント += 1
	NEXT
ENDIF

;ロック中かつロック対象が生存中なら強制的にロック対象へ攻撃
IF ARG:1 == 23 
	IF BLITZ_TARGET_JUDG(ARG) == 1
		ROCK_H = SKILLGAGE_H_GET(ARG, [[스킬:블리츠대쉬]]) / 10
		CALL SKILLGAGE_D_SET, ARG, [[스킬:스파크스텔라]], 1
		CALL ATTACK_SPREAD, ARG, CPOS(ROCK_H), 3628
	ENDIF
ELSE
	CALL ATTACK_SINGLE, ARG, POS(ARG:1), 3628
ENDIF

IF CFLAG:ARG:PT플래그 > 0
	FOR ループ用, 7, 17
		LOCALS = 포지션{ループ用}
		SIF FLAG:LOCALS == -1
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
			CONTINUE
		攻撃後敵カウント += 1
	NEXT
ENDIF

差分 = 攻撃前敵カウント - 攻撃後敵カウント

IF 差分 > 0
	SELECTCASE 差分
	CASE 1
		加算クードス = 20
	CASE 2
		加算クードス = 40
	CASE 3
		加算クードス = 60
	CASE 4
		加算クードス = 80
	CASEELSE
		加算クードス = 100
	ENDSELECT
	IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
		TIMES 加算クードス, 1.25
	ELSEIF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
		TIMES 加算クードス, 0.5
	ENDIF
	PRINTFORMW AERIAL EX　Kudos + {加算クードス}
	CALL SKILLGAGE_F_ADD, ARG, [[스킬:블리츠대쉬]], 加算クードス
ENDIF
IF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
	;アパシーマンの上限は1200
	SIF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 1200
		CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 1200
ELSE
	;上限は99999
	SIF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 99999
		CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 99999
ENDIF

;EXウェポン0番=スパークステラー
CALL SKILLGAGE_F_SET, ARG, [[스킬:DARKNESS TRIGGER]], 0
;====================================================
;실행내용 여기까지
;====================================================

;　 説明文の表示
@SKILL_EXPLAIN_3628
PRINTL 雷撃を操るセプティマ・蒼き雷霆（アームドブルー）を再現したＥＸウェポン。
PRINTL ＨＰを消費するコマンド「ブリッツダッシュ」によってロック済みの敵がいる場合、強制的にその対象へ攻撃する。
PRINTL 
PRINTL 攻撃対象がロックオン状態の場合…敵単体とその周囲に電撃相性で３回小ダメージを与える。
PRINTL 攻撃対象が非ロック状態の場合　…敵単体に電撃相性で中ダメージを与える。
PRINTL どちらの場合も感電を付与する。
IF U > 0 && U < CHARANUM && NO:U == [[キャラ:아큐라(X)]] && EQUIP:U:악세서리 == 5349 && SKILLGAGE_H_GET(U, [[스킬:DARKNESS TRIGGER]]) == 1
	SETCOLOR 0x9933cd
	PRINTL 다크니스 트리거Ｘ 장비중　ダークネストリガーによる追撃時のダメージ上昇
	RESETCOLOR
ENDIF
PRINTL ※このスキルで敵を倒した場合、一体ごとにクードスが２０加算される

;사용조건
@SKILL_SPECIAL_ACTIONABLE_3628, ARG
;ターゲット=23が入るのはロック中のみ
;かつ実行時にターゲットが死んだりするとダメ
SIF ARG > 0 && CFLAG:ARG:타겟 == 23 && BLITZ_TARGET_JUDG(ARG) != 1
	RETURN 0
RETURN 1

;계승에 필요한 부위
@계승부위_3628, ARG
RETURN 0
