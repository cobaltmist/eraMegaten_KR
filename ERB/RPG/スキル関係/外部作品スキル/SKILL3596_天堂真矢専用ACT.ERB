;更新履歴
;001	2020/11/07		kamedakeisuke(ﾟдﾟ)			新規作成
;002	2022/02/12		kamedakeisuke(ﾟдﾟ)			クリティカル率に不具合があったので修正＋非CR時の基礎威力上昇
;003	2022/06/05		kamedakeisuke(ﾟдﾟ)			非CR時の基礎威力上昇、エラー避けのおまじない追加

;天堂真矢（ゼウス）専用スキル

;---
;CAスキルは意図的に強力にする
;使用条件がイベント後半まで進行＆星のティアラをアクセサリー欄に装備＆キラめきチャージ完了時に１発だけ、なので。
;入手の際の代償もあるし、それに見合った性能に。

;通常スキルに使用条件は無し
;だけどこちらも使えるようになるタイミングは同じなので多少は盛る
;---

;通用時はダメージ付きの列攻撃ナイスショート、コスト低め
;まあ普通に使ってたらフレーバー。先手取ってSHOCKできる環境下なら強いと思います

;クライマックスアクトはデカジャ・デクンダ＋単体大火力。
;達人バフもあるのでほかの子より威力は一段上になる
;（原作だと全体攻撃だけど、それだと強すぎ感もあるので、単体攻撃へ捏造）

;クライマックス・アクトは消費無し（そのかわり撃ったらクライマックスレヴュー終了

;Renameは『追討の斬撃』のほうで

;스킬명
@SKILL_NAME_3596,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RESULTS = 꿰뚫는 케라우노스
ELSE
	RESULTS = 추토의 참격
ENDIF

;　명중
@SKILL_HITRATE_3596, ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 150
ELSE
	RETURN 80
ENDIF

@SKILL_CRITICAL_RATE_3596, ARG
RETURN 5

;　위력
@SKILL_POWER_3596, ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 640
ELSE
	RETURN 360
ENDIF


;　최저공격횟수
@SKILL_MINATTACKNUMBER_3596,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3596,ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3596, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_3596,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	;消費無し（代わりにゲージをゼロに）
	RETURN 0
ELSE
	RETURN 18
ENDIF

;　상성
@SKILL_TYPE_3596,ARG
#LOCALSSIZE 5
#LOCALSIZE 4
;CR展開時、複合属性
;電撃・核熱・戦技のうち、最も効果のある相性で攻撃
IF SKILLGAGE_D_GET(ARG,3588)
	SIF ARG < 0 || 스킬속성표시중
		RETURN GET_TYPE_NUM("전격")
	SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
		RETURN GET_TYPE_NUM("전격")
	LOCAL = POS(CFLAG:ARG:타겟)
	SIF LOCAL < 0
		RETURN GET_TYPE_NUM("전격")
		
	LOCAL:2 = -300

	LOCALS = 전격
	LOCALS:1 = 전격
	LOCALS:2 = 핵열
	LOCALS:3 = 전술
	FOR LOCAL:1, 1, 4
		;判定用に数値を仕舞う。反射は-200扱い
		LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
		LOCAL:3 = !(HAVE_SKILL(ARG,[[스킬:영세라이도우]]) || HAVE_SKILL(ARG,2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
		LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
		SELECTCASE LOCAL:1
			CASE 3
				LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
			CASE 1 TO 2
				LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
			CASEELSE
		ENDSELECT
		IF LOCAL:3 > LOCAL:2
			LOCAL:2 = LOCAL:3
			LOCALS = %LOCALS:(LOCAL:1)%
		ENDIF
	NEXT
	PRINT 속성상성：
	PRINTS LOCALS
	PRINTL ！
	RETURN GET_TYPE_NUM(LOCALS)
	
;通常時は戦技
ELSE
	RETURN 3
ENDIF

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3596, ARG
RETURN 1
;EX

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3596, ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_3596
RETURN 1

;　사거리
@SKILL_RANGE_3596,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 3
ELSE
	RETURN 1
ENDIF

;　範囲　11체　2列　3전체　4　적아군전체
@SKILL_SPHERE_3596,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 1
ELSE
	RETURN 2
ENDIF

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3596
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_3596,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_3596
RETURN 6
;전격

;속도보정
@SKILL_SPEED_3596, ARG = -1
RETURN 0

;　구상의 표시와 그 타입
@SKILL_MESSAGE_3596,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3596
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3596
RETURN 15


;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3596,ARG
RETURN 0


;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3596,ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_3596,ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_3596,ARG
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_3596,ARG,ARG:1
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	ALIGNMENT CENTER
	SETCOLOR 0xffffff
	PRINTL 【ＣＬＩＭＡＸ　ＡＣＴ】
	RESETCOLOR
	ALIGNMENT LEFT

	;天堂真矢パワーアップイベント中かつボスが最終形態時のみ、ボスの不死フラグを剥がして攻撃
	IF GETBIT(이벤트플래그:93:18, 5) && (이벤트플래그:93:0 == 12 || 이벤트플래그:93:0 == 13) && (이벤트플래그:93:10 && [[キャラ:마야（무대소녀）]])
		CFLAG:(CHARANUM-1):불사신플래그 = 0
	ENDIF
	
	;デ・クンダ部分
	IF CFLAG:(ARG):PT플래그 == 0
		LOCAL:2 = 7
		LOCAL:3 = 17
	ELSE
		LOCAL:2 = 1
		LOCAL:3 = 7
	ENDIF

	FOR LOCAL,LOCAL:2,LOCAL:3
		SIF POS(LOCAL) < 0
			CONTINUE
		FOR LOCAL:1,0,6
			CFLAG:POS(LOCAL):(GET_BATTLESTATUS(LOCAL:1) + "강화") = MAX(0, CFLAG:POS(LOCAL):(GET_BATTLESTATUS(LOCAL:1) + "강화"))
		NEXT
	NEXT
	PRINTFORML \@ CFLAG:ARG:PT플래그 > 0 ? 아군 # 적 \@의 약화가 날아가버렸다！
	
	;デ・カジャ部分
	IF CFLAG:(ARG):PT플래그 == 0
		LOCAL:2 = 1
		LOCAL:3 = 7
	ELSE
		LOCAL:2 = 7
		LOCAL:3 = 17
	ENDIF
	FOR LOCAL,LOCAL:2,LOCAL:3
		SIF POS(LOCAL) < 0
			CONTINUE
		FOR LOCAL:1,0,6
			CFLAG:POS(LOCAL):(GET_BATTLESTATUS(LOCAL:1) + "강화") = MIN(0, CFLAG:POS(LOCAL):(GET_BATTLESTATUS(LOCAL:1) + "강화"))
		NEXT
	NEXT
	PRINTFORML \@ CFLAG:ARG:PT플래그 == 2 ? 적 # 아군 \@의 강화가 날아가버렸다！
	
	IF ARG:1 > 16
		CALL RANDOM_TARGET,ARG,3596
		ARG:1 = CFLAG:ARG:타겟
	ENDIF
	
	LOCAL = POS(ARG:1)
	CALL ATTACK_SINGLE,ARG,LOCAL,3596

	;天堂真矢パワーアップイベント中かつボスが最終形態時のみ、撃破時に天堂真矢が喋る
	IF GETBIT(이벤트플래그:93:18, 5) && (이벤트플래그:93:0 == 12 || 이벤트플래그:93:0 == 13) && (이벤트플래그:93:10 && [[キャラ:마야（무대소녀）]])
	
		;ボスのHPがぴったり1の場合、『すでに削り切ったけど、攻撃をミスってとどめを刺せていない』状況と見る
		IF BASE:(CHARANUM-1):ＨＰ == 1
			PRINTL
			SETCOLOR 0x33ffcc
			PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓
			PRINTFORML ┃%"놓칠까보냐아아앗ー！！",72,LEFT%┃
			PRINTFORML ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
			PRINTL
			ALIGNMENT CENTER
			PRINTFORML ┏━━━━━━━━━━┓
			PRINTFORML ┃　　 　1more！　　　┃
			PRINTFORMW ┗━━━━━━━━━━┛
			RESETCOLOR
			ALIGNMENT LEFT
			
			;テイクツー
			CFLAG:(CHARANUM-1):불사신플래그 = 0
			CFLAG:ARG:베어내기플래그 = 1
			
			CALL ATTACK_SINGLE,ARG,LOCAL,3596
			
		ENDIF
		
		;ボス撃破
		IF BASE:(CHARANUM-1):ＨＰ < 1
			ALIGNMENT CENTER
			PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━━━━━━━━┓
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　 죽었던 별들이, 별자리를 이룬다……？ 　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
			PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━━━━━━━━┓
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　사랑스러운 아스테리오스여, 나와 함께　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　하늘의 빛은 전부 별……　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　 별의 빛은, 모두가, 사랑！ 　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
			PRINTL 
			ALIGNMENT LEFT
			SETCOLOR 0xffffff
			PRINTL 　　　――일어서라 Star Divine！　몇 번이고 상처 입더라도――
			PRINTL 
			SETCOLOR 0xcbc6cc
			PRINTL 　　　　　　　　――무대가 있기에 살 수 있어――
			PRINTL 
			SETCOLOR 0xffffff
			PRINTL 　　　　　　　　――칼끝에 영광 있으라！――
			PRINTL
			
		ENDIF
	ENDIF

	ALIGNMENT CENTER
	SETCOLOR 0xffffff
	PRINTW 【ＣＬＩＭＡＸ　ＲＥＶＵＥ／ＦＩＮＩＳＨ】
	PRINTL
	RESETCOLOR
	ALIGNMENT LEFT
	;クライマックスレヴュー終了
	;CALL SKILLGAGE_D_SET,ARG,3588,0

;それ以外
ELSE
	CALL ATTACK_FIELD,ARG,ARG:1,3596
	
	;ナイスショート部分
	IF CFLAG:(ARG):PT플래그 == 0
		LOCAL:7 = 1
		LOCAL:8 = 4
	ELSE
		LOCAL:7 = 7
		LOCAL:8 = 12
	ENDIF

	FOR LOCAL,LOCAL:7,LOCAL:8
		LOCALS = 포지션{LOCAL}
		SIF FLAG:LOCALS < 0
			CONTINUE
		
		SIF CFLAG:(FLAG:LOCALS):상태이상 != GET_STATE_NUM("SHOCK")
			CONTINUE
		PRINTFORM 　　TARGET:[{LOCAL,2}] %CALLNAME:(FLAG:LOCALS),20,LEFT%　>>>>>>　
		CALL ATTACK_BADSTATE,ARG,FLAG:LOCALS,GET_STATE_NUM("DYING"),17,200,95
	NEXT
	
ENDIF





;====================================================
;실행내용 여기까지
;====================================================

;　 설명문 표시
@SKILL_EXPLAIN_3596
#LOCALSIZE 1
IF CFLAG:(CHARANUM-1):PT플래그 || U < 0 || U >= CHARANUM
	PRINTL 【통상시】
	PRINTL 적 1렬에 전술 상성의 대 데미지＋감전되어 있는 적을 즉사시킨다
	PRINTL 
	PRINTL 【클라이맥스・레뷰　발동시】
	PRINTL 적 1체에『전격・핵열・전술』복합 초특대 데미지＋전 아군 약체화 해제＋전 적군 강화 해제
ELSE
	;CR展開時
	IF SKILLGAGE_D_GET(U,3588)
		SETCOLOR 0xFFFFFF
		PRINTL 【ＣＬＩＭＡＸ　ＡＣＴ】
		RESETCOLOR
		PRINTL 하늘에서 반짝이는 뇌정으로 온갖 것을 산산조각으로 박살내는、대신 제우스의 필살기。
		PRINTL 적 1체에『전격・핵열・전술』복합 초특대 데미지＋모든 아군의 약체화를 해제하고、모든 적의 강화를 해제한다。
		;天堂真矢パワーアップイベント中かつボスが最終形態時のみ撃ち放題
		IF GETBIT(이벤트플래그:93:18, 5) && (이벤트플래그:93:0 == 12 || 이벤트플래그:93:0 == 13) && (이벤트플래그:93:10 && [[キャラ:마야（무대소녀）]])
			SETCOLOR 0xFFFFFF
			PRINTL 혼신의 일격으로 이 레뷰에 결착을！
			RESETCOLOR
		;通常
		ELSE
			PRINTL 사용하면 클라이맥스 레뷰는 종료된다
		ENDIF
	ELSE
		PRINTL 적 1렬에 전술 상성의 대 데미지＋감전되어 있는 적을 즉사시킨다
		PRINTL 클라이맥스 레뷰 발동 중에는 성능이 변화한다。
	ENDIF
ENDIF

@계승불능_3596
RETURN 1

;계승에 필요한 부위
@계승부위_3596,ARG
RETURN 1


;사용조건
@SKILL_SPECIAL_ACTIONABLE_3596,ARG
RETURN 1
;とくにない

