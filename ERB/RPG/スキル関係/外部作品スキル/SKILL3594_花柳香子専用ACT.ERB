;更新履歴
;001	2020/08/17		kamedakeisuke(ﾟдﾟ)			新規作成

;石動双葉（長靴をはいた猫）専用スキル

;---
;CAスキルは意図的に強力にする
;使用条件がイベント後半まで進行＆星のティアラをアクセサリー欄に装備＆キラめきチャージ完了時に１発だけ、なので。
;入手の際の代償もあるし、それに見合った性能に。

;通常スキルに使用条件は無し
;だけどこちらも使えるようになるタイミングは同じなので多少は盛る
;---


;Renameは『분기의합주』のほうで

;スキル名
@SKILL_NAME_3594,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RESULTS = 猫の恩返し
;それ以外
ELSE
	RESULTS = 분기의합주
ENDIF

;　명중
@SKILL_HITRATE_3594, ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 125
;通常時
ELSE
	RETURN 80
ENDIF

;@SKILL_CRITICAL_RATE_3594, ARG
;RETURN 20

;　威力
@SKILL_POWER_3594, ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	;3回攻撃
	RETURN 215
ELSE
	RETURN 290
ENDIF


;　최저공격횟수
@SKILL_MINATTACKNUMBER_3594,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	;3回攻撃
	RETURN 3
;通常時
ELSE
	RETURN 1
ENDIF

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3594,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	;3回攻撃
	RETURN 3
;通常時
ELSE
	RETURN 1
ENDIF

;　ＨＰ2を支払うかＭＰ3を支払うか
@SKILL_COSTTYPE_3594, ARG
RETURN 3
;消費なし＝1

;　コストの量
@SKILL_COST_3594,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	;消費無し（代わりにゲージをゼロに）
	RETURN 0
;通常時
ELSE
	RETURN 20
ENDIF

;　相性
@SKILL_TYPE_3594,ARG
#LOCALSSIZE 5
#LOCALSIZE 4
;CR展開時、複合属性
;전술・검격・打撃のうち、最も効果のある相性で攻撃
IF SKILLGAGE_D_GET(ARG,3588)
	SIF ARG < 0 || 스킬속성표시中
		RETURN GET_TYPE_NUM("전술")
	SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
		RETURN GET_TYPE_NUM("전술")
	LOCAL = POS(CFLAG:ARG:타겟)
	SIF LOCAL < 0
		RETURN GET_TYPE_NUM("전술")
		
	LOCAL:2 = -300
	;全部-400とかないとおもうけど、あったらあれなんで、飛具をいれとく
	LOCALS = 전술
	LOCALS:1 = 전술
	LOCALS:2 = 검격
	LOCALS:3 = 지변
	FOR LOCAL:1, 1, 4
		;判定用に数値を仕舞う。反射は-200扱い
		LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
		LOCAL:3 = !(HAVE_SKILL(ARG,[[스킬:영세라이도우]]) || HAVE_SKILL(ARG,2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
		LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
		SELECTCASE LOCAL:1
			CASE 1 TO 2
				LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
			CASE 3
				LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
			CASEELSE
		ENDSELECT
		IF LOCAL:3 > LOCAL:2
			LOCAL:2 = LOCAL:3
			LOCALS = %LOCALS:(LOCAL:1)%
		ENDIF
	NEXT
	PRINT 属性相性：
	PRINTS LOCALS
	PRINTL ！
	RETURN GET_TYPE_NUM(LOCALS)
	
;通常時は戦技
ELSE
	RETURN 3
ENDIF

;　1EXTRAか2MAGICか
@SKILL_DECIDE_TYPE_3594, ARG
RETURN 1
;EX

;　1物理데미지で計算するか2魔法데미지で計算するか
@SKILL_DAMAGETYPE_3594, ARG
RETURN 1


;　対象が1敵か2味方か3場全体か4特殊か
@SKILL_TARGET_3594
RETURN 1

;　사거리
@SKILL_RANGE_3594,ARG
RETURN 2

;　範囲　1単体　2列　3全体　4　敵味方全体
@SKILL_SPHERE_3594,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 1
;通常時
ELSE
	RETURN 2
ENDIF

;　 効果のタイプ　1攻撃、2回復、3状態変化、4特殊、5状態回復
@SKILL_EFECT_3594
RETURN 1

;　 特殊処理の有無、及び処理内容
@SKILL_SPECIALEFECT_3594,ARG
;ARG = 1だった場合は処理の有無を返します
SIF ARG == 1
	RETURN 0
;以下に特殊な処理を記述します

;계승타입
@SKILL_SUCCESSION_TYPE_3594
RETURN 3
;전술

;속도보정
@SKILL_SPEED_3594, ARG = -1
RETURN 0

;　口上の表示とそのタイプ
@SKILL_MESSAGE_3594,ARG


;戦闘時以外に使用できるか
@SKILL_ACTIONABLE_FIELD_3594
RETURN 0

;スキル変化？用ランク
@SKILL_RANK_3594
RETURN 15


;テンプレート用、追加効果シリーズ
;追加効果ステータス
@SKILL_ADDTIONAL_STATE_3594,ARG
;CR展開時
;IF SKILLGAGE_D_GET(ARG,3588)
;	RETURN GET_STATE_NUM("BIND")
;ELSE
;	RETURN GET_STATE_NUM("POISON")
;ENDIF
RETURN 0


;추가효과상성
@SKILL_ADDTIONAL_TYPE_3594,ARG
;CR展開時
;만능
;IF SKILLGAGE_D_GET(ARG,3588)
;	RETURN 17
;신경
;ELSE
;	RETURN 8
;ENDIF
RETURN 0

;追加効果基本付与確率
;確率高め
@SKILL_ADDTIONAL_BASE_RATE_3594,ARG
;RETURN 50
RETURN 0

;追加効果付与確率上限
;確率高め
@SKILL_ADDTIONAL_MAX_RATE_3594,ARG
;RETURN 80
RETURN 0

;====================================================
;スキル及び行動の実行
;====================================================
@ACTION_3594,ARG,ARG:1
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	ALIGNMENT CENTER
	SETCOLOR 0x815ca1
	PRINTL 【ＣＬＩＭＡＸ　ＡＣＴ】
	RESETCOLOR
	ALIGNMENT LEFT

	;パワーアップイベント中かつボスが最終形態時のみ、ボスの不死フラグを剥がして攻撃
	IF GETBIT(이벤트フラグ:93:15, 5) && (이벤트フラグ:93:0 == 12 || 이벤트フラグ:93:0 == 13) && (이벤트フラグ:93:10 && [[キャラ:후타바（무대소녀）]])
		CFLAG:(CHARANUM-1):不死身フラグ = 0
	ENDIF
	
	;타겟ロックオン
	LOCAL = POS(ARG:1)
	
	;敵の能力を下げる（原作より使い勝手を盛る）
	CFLAG:LOCAL:공격강화 = MAX(-16, CFLAG:LOCAL:공격강화 - 8)
	CFLAG:LOCAL:방어강화 = MAX(-16, CFLAG:LOCAL:방어강화 - 8)
	
	;ぶん殴る
	CALL ATTACK_SINGLE,ARG,LOCAL,3594
	
	;パワーアップイベント中かつボスが最終形態時のみ、撃破時に敵が喋る
	IF GETBIT(이벤트フラグ:93:15, 5) && (이벤트フラグ:93:0 == 12 || 이벤트フラグ:93:0 == 13) && (이벤트フラグ:93:10 && [[キャラ:후타바（무대소녀）]])

		;ボスのHPがぴったり1の場合、『すでに削り切ったけど、攻撃をミスってとどめを刺せていない』状況と見る
		IF BASE:(CHARANUM-1):ＨＰ == 1
			PRINTL
			SETCOLOR 0x33ffcc
			PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓
			PRINTFORML ┃%"おーっと手が滑った",72,LEFT%┃
			PRINTFORML ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
			PRINTL
			ALIGNMENT CENTER
			PRINTFORML ┏━━━━━━━━━━┓
			PRINTFORML ┃　　 　1more！　　　┃
			PRINTFORMW ┗━━━━━━━━━━┛
			RESETCOLOR
			ALIGNMENT LEFT
			
			;テイクツー
			CFLAG:(CHARANUM-1):不死身フラグ = 0
			CFLAG:ARG:베어내기플래그 = 1
			
			CALL ATTACK_SINGLE,ARG,LOCAL,3594
			
		ENDIF
		
		;ボス撃破
		IF BASE:(CHARANUM-1):ＨＰ < 1
			ALIGNMENT CENTER
			PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━━━━━━━━┓
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　覚えたぞ、その名！　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　忘れぬぞ、長靴のペロ！　　　　　　　　　┃
			PRINTL ┃　　　　　　　いずれまた会う日まで――さらば！　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
			PRINTL 
			ALIGNMENT LEFT
		ENDIF
	ENDIF

	ALIGNMENT CENTER
	SETCOLOR 0x815ca1
	PRINTW 【ＣＬＩＭＡＸ　ＲＥＶＵＥ／ＦＩＮＩＳＨ】
	PRINTL
	RESETCOLOR
	ALIGNMENT LEFT
	;クライマックスレヴュー終了
	;CALL SKILLGAGE_D_SET,ARG,3588,0

;それ以外
ELSE
	;相手をぶん殴る処理
	CALL ATTACK_FIELD,ARG,ARG:1,3594
	;自分の攻撃力を上げる処理
	CFLAG:ARG:공격강화 = MIN(32, CFLAG:ARG:공격강화 + 4)
ENDIF









;====================================================
;実行内容ここまで
;====================================================

;　 説明文の表示
@SKILL_EXPLAIN_3594
#LOCALSIZE 1
IF CFLAG:(CHARANUM-1):PT플래그 || U < 0 || U >= CHARANUM
	PRINTL 【通常時】
	PRINTL 敵一列に戦技相性の中데미지＋自身の攻撃力を一段階上げる
	PRINTL 
	PRINTL 【クライマックス・レヴュー　発動時】
	PRINTL 敵一体の攻撃・防御を大幅に低下＋『전술・검격・지변』特大데미지
ELSE
	;CR展開時
	IF SKILLGAGE_D_GET(U,3588)
		SETCOLOR 0x8ac4ec
		PRINTL 【ＣＬＩＭＡＸ　ＡＣＴ】
		RESETCOLOR
		PRINTL 鋭い爪で切り裂いたのち、大地ごと敵を粉砕する長靴をはいた猫の必殺技
		PRINTL 敵一体の攻撃・防御を大幅に低下＋『전술・검격・지변』３ヒット特大데미지
		;双葉パワーアップイベント中かつボスが最終形態時のみ、撃ち放題
		IF GETBIT(이벤트フラグ:93:15, 5) && (이벤트フラグ:93:0 == 12 || 이벤트フラグ:93:0 == 13) && (이벤트フラグ:93:10 && [[キャラ:후타바（무대소녀）]])
			SETCOLOR 0x8ac4ec
			PRINTL 渾身の一撃でこのレヴューに決着を！
			RESETCOLOR
		;通常
		ELSE
			PRINTL 使用するとクライマックスレヴューは終了する
		ENDIF
	ELSE
		PRINTL 敵一列に戦技相性の中데미지＋自身の攻撃力を一段階上げる
		PRINTL クライマックスレヴュー発動中は性能が変化する。
	ENDIF
ENDIF

@継承不能_3594
RETURN 1

;継承に必要な部位
@継承部位_3594,ARG
RETURN 1


;使用条件
@SKILL_SPECIAL_ACTIONABLE_3594,ARG
RETURN 1
;とくにない

