;更新履歴
;001	2020/09/06		kamedakeisuke(ﾟдﾟ)			新規作成
;002	2020/12/08		kamedakeisuke(ﾟдﾟ)			気絶の斬撃を列攻撃に変更
;003	2022/02/12		kamedakeisuke(ﾟдﾟ)			非CR時の基礎威力上昇
;003	2022/06/05		kamedakeisuke(ﾟдﾟ)			CR時・非CR時の基礎威力上昇、バステ付与率上昇、コスト上昇、エラー避けのおまじない追加

;愛城華恋（キング・アーサー）専用スキル

;---
;CAスキルは意図的に強力にする
;使用条件がイベント後半まで進行＆星のティアラをアクセサリー欄に装備＆キラめきチャージ完了時に１発だけ、なので。
;入手の際の代償もあるし、それに見合った性能に。

;通常スキルに使用条件は無し
;だけどこちらも使えるようになるタイミングは同じなので多少は盛る
;---

;クライマックスアクトは全体全回復付き複合属性全体ランダム攻撃（盛りすぎワロタ）
;通常時は列攻撃イナズマ突き
;クライマックス・アクトは消費無し（そのかわり撃ったらクライマックスレヴュー終了

;Renameは『気絶の斬撃』のほうで

;스킬명
@SKILL_NAME_3589,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RESULTS = 선택받은 왕
ELSE
	RESULTS = 기절의 참격
ENDIF

;　명중
@SKILL_HITRATE_3589, ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 125
ELSE
	RETURN 100
ENDIF

;高クリティカル
@SKILL_CRITICAL_RATE_3589, ARG
RETURN 20

;　위력
@SKILL_POWER_3589, ARG
;CR展開時　9回攻撃・合計648相当
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 72
ELSE
	RETURN 360
ENDIF


;　최저공격횟수
@SKILL_MINATTACKNUMBER_3589,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 9
ELSE
	RETURN 1
ENDIF

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3589,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 9
ELSE
	RETURN 1
ENDIF

;一体辺りの最大ＨＩＴ数
@SKILL_MAXATK_PER_3589,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 9
ELSE
	RETURN 1
ENDIF


;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3589, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_3589,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	;消費無し（代わりにゲージをゼロに）
	RETURN 0
ELSE
	RETURN 19
ENDIF

;　상성
@SKILL_TYPE_3589,ARG
#LOCALSSIZE 5
#LOCALSIZE 4
;CR展開時、複合属性
;剣撃・核熱・重力のうち、最も効果のある相性で攻撃
IF SKILLGAGE_D_GET(ARG,3588)
	SIF ARG < 0 || 스킬속성표시중
		RETURN GET_TYPE_NUM("검격")
	SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
		RETURN GET_TYPE_NUM("검격")
	LOCAL = POS(CFLAG:ARG:타겟)
	SIF LOCAL < 0
		RETURN GET_TYPE_NUM("검격")
		
	LOCAL:2 = -300
	;全部-400とかないとおもうけど、あったらあれなんで、剣撃をいれとく
	LOCALS = 검격
	LOCALS:1 = 검격
	LOCALS:2 = 핵열
	LOCALS:3 = 중력
	FOR LOCAL:1, 1, 4
		;判定用に数値を仕舞う。反射は-200扱い
		LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
		LOCAL:3 = !(HAVE_SKILL(ARG,[[스킬:영세라이도우]]) || HAVE_SKILL(ARG,2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
		LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
		SELECTCASE LOCAL:1
			CASE 1
				LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
			CASE 2 TO 3
				LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
			CASEELSE
		ENDSELECT
		IF LOCAL:3 > LOCAL:2
			LOCAL:2 = LOCAL:3
			LOCALS = %LOCALS:(LOCAL:1)%
		ENDIF
	NEXT
	PRINT 속성상성：
	PRINTS LOCALS
	PRINTL ！
	RETURN GET_TYPE_NUM(LOCALS)
	
;通常時は剣撃
ELSE
	RETURN 0
ENDIF

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3589, ARG
RETURN 1
;EX

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3589, ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_3589
RETURN 1

;　사거리
@SKILL_RANGE_3589,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 3
ELSE
	RETURN 1
ENDIF

;　範囲　11체　2列　3전체　4　적아군전체
@SKILL_SPHERE_3589,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 3
ELSE
	RETURN 2
ENDIF

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3589
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_3589,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_3589
RETURN 0
;검격

;속도보정
@SKILL_SPEED_3589, ARG = -1
RETURN 0

;　구상의 표시와 그 타입
@SKILL_MESSAGE_3589,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3589
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3589
RETURN 16


;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3589,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 0
ELSE
	RETURN GET_STATE_NUM("SHOCK")
ENDIF


;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3589,ARG
;CR展開時
;검격
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 0
;전격
ELSE
	RETURN 6
ENDIF

;추가효과 기본부여확률
;確率高め
@SKILL_ADDTIONAL_BASE_RATE_3589,ARG
;CR展開時
;검격
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 0
ELSE
	RETURN 80
ENDIF

;추가효과 부여확률상한
;確率高め
@SKILL_ADDTIONAL_MAX_RATE_3589,ARG
;CR展開時
;검격
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 0
ELSE
	RETURN 100
ENDIF

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_3589,ARG,ARG:1
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	ALIGNMENT CENTER
	SETCOLOR 0xff0033
	PRINTL 【ＣＬＩＭＡＸ　ＡＣＴ】
	RESETCOLOR
	ALIGNMENT LEFT

	;愛城パワーアップイベント中かつボスが最終形態時のみ、ボスの不死フラグを剥がして攻撃
	IF GETBIT(이벤트플래그:93:11, 5) && (이벤트플래그:93:0 == 12 || 이벤트플래그:93:0 == 13) && (이벤트플래그:93:10 && [[キャラ:카렌（무대소녀）]])
		CFLAG:(CHARANUM-1):불사신플래그 = 0
	ENDIF
	
	;何らかの事情によりターゲットが22未満だったら
	IF CFLAG:ARG:타겟 < 22
		;22（全体）をセット
		CFLAG:ARG:타겟 = 22
	ENDIF
	
	;란다ム攻撃処理
	LOCAL = 22
	CALL ATTACK_RAND, ARG, LOCAL, 3589, 1
	
	;味方全回復
	IF CFLAG:(ARG):PT플래그
		LOCAL:1 = 1
		LOCAL:2 = 7
	ELSE
		LOCAL:1 = 7
		LOCAL:2 = 17
	ENDIF
	FOR LOCAL,LOCAL:1,LOCAL:2
		SIF POS(LOCAL) < 0
			CONTINUE
		SIF GET_STATE(CFLAG:POS(LOCAL):상태이상) == "DYING"
			CONTINUE
		CALL ALL_HEAL, ARG, POS(LOCAL), 0
	NEXT
	
	;愛城パワーアップイベント中かつボスが最終形態時のみ、撃破時に敵が喋る
	IF GETBIT(이벤트플래그:93:11, 5) && (이벤트플래그:93:0 == 12 || 이벤트플래그:93:0 == 13) && (이벤트플래그:93:10 && [[キャラ:카렌（무대소녀）]])
		
		;ボスのHPがぴったり1の場合、『すでに削り切ったけど、攻撃をミスってとどめを刺せていない』状況と見る
		IF BASE:(CHARANUM-1):ＨＰ == 1
			PRINTL
			SETCOLOR 0x33ffcc
			PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓
			PRINTFORML ┃%"마지막 한 발짝, 부디 힘을 빌려줘, 모두들――！！",72,LEFT%┃
			PRINTFORML ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
			PRINTL
			ALIGNMENT CENTER
			PRINTFORML ┏━━━━━━━━━━┓
			PRINTFORML ┃　　 　1more！　　　┃
			PRINTFORMW ┗━━━━━━━━━━┛
			RESETCOLOR
			ALIGNMENT LEFT
			
			;テイクツー
			CFLAG:(CHARANUM-1):불사신플래그 = 0
			CFLAG:ARG:베어내기플래그 = 1
			
			CALL ATTACK_RAND, ARG, LOCAL, 3589, 1
			
		ENDIF
		
		;ボス撃破
		IF BASE:(CHARANUM-1):ＨＰ < 1
			ALIGNMENT CENTER
			PRINTL
			SETCOLOR 0x777777
			PRINTL The Star remembers it all.　　　　　별은 모든 것을 기억한다　　　　
			FORCEWAIT
			PRINTL
			PRINTW When Fury was Passion.　　　　　　　그것은 격앙이 정열이었을 때
			PRINTW When Curse was Faith. 　　　　　　　그것은 주박이 신앙이었을 때
			PRINTW When Escape was Bravery.　　　　　　그것은 도피가 용기였을 때
			PRINTW When Jelousy was Affection. 　　　　그것은 질투가 애정이었을 때
			PRINTW When Despair was Hope.　　　　　　　그것은 절망이 희망이었을 때
			PRINTW When Arrogance was Pride. 　　　　　그것은 오만이 긍지였을 때
			PRINTL
			PRINTW And it shall be bestowed upon you.　거머쥐세요　　　　　　
			PRINTW the Star which you　　　　　　　　　당신이 바랬던　　　　　
			PRINTW have longed for　　　　　　　　　　 그 별을　　　　　　　　
			PRINTL
			SETCOLOR 0xfdca57
			PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━━━━━━━━┓
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　이어졌구나, 별의 인연　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
			FORCEWAIT
			PRINTL 
			ALIGNMENT LEFT
			SETCOLOR 0xff0033
			PRINTL ――나의 소망이　이뤄질 때에는　　
			PRINTL 
			PRINTL 　　　　　믿어줘！
			PRINTL 
			PRINTL 　　아무도 불행하게　하지않아！――
			PRINTL 
			RESETCOLOR
			
		ENDIF
	ENDIF


	ALIGNMENT CENTER
	SETCOLOR 0xff0033
	PRINTW 【ＣＬＩＭＡＸ　ＲＥＶＵＥ／ＦＩＮＩＳＨ】
	PRINTL
	RESETCOLOR
	ALIGNMENT LEFT
	;クライマックスレヴュー終了
	;CALL SKILLGAGE_D_SET,ARG,3588,0

;それ以外
ELSE
	;単体攻撃処理
	;LOCAL = POS(ARG:1)
	;CALL ATTACK_SINGLE,ARG,LOCAL,3589
	;やっぱ一列攻撃に
	CALL ATTACK_FIELD,ARG,ARG:1,3589
ENDIF









;====================================================
;실행내용 여기까지
;====================================================

;　 설명문 표시
@SKILL_EXPLAIN_3589
#LOCALSIZE 1
IF CFLAG:(CHARANUM-1):PT플래그 || U < 0 || U >= CHARANUM
	PRINTL 【통상시】
	PRINTL 적 １열에 검격 상성의 대 데미지＋감전
	PRINTL 
	PRINTL 【클라이맥스・레뷰　발동시】
	PRINTL 랜덤한 대상에 높은 크리티컬률『검격・중력・핵열』복합 대 데미지＋아군의 ＨＰ 전부 회복。
ELSE
	;CR展開時
	IF SKILLGAGE_D_GET(U,3588)
		SETCOLOR 0xff0033
		PRINTL 【ＣＬＩＭＡＸ　ＡＣＴ】
		RESETCOLOR
		PRINTL 동료의 마음과 함께 시대를 일직선으로 달려나가는 아서왕의 필살기。
		PRINTL 랜덤한 대상에 높은 크리티컬률『검격・중력・핵열』복합 대 데미지＋아군의 ＨＰ 전부 회복。
		;愛城パワーアップイベント中かつボスが最終形態時のみ、撃ち放題
		IF GETBIT(이벤트플래그:93:11, 5) && (이벤트플래그:93:0 == 12 || 이벤트플래그:93:0 == 13) && (이벤트플래그:93:10 && [[キャラ:카렌（무대소녀）]])
			SETCOLOR 0xff0033
			PRINTL 혼신의 일격으로 이 레뷰에 결착을！
			RESETCOLOR
		;通常
		ELSE
			PRINTL 사용하면 클라이맥스 레뷰는 종료된다
		ENDIF
		
	;それ以外
	ELSE
		PRINTL 적 １열에 검격 상성의 대 데미지＋감전
		PRINTL 클라이맥스 레뷰 발동 중에는 성능이 변화한다。
	ENDIF
ENDIF

@계승불능_3589
RETURN 1

;계승에 필요한 부위
@계승부위_3589,ARG
RETURN 1


;사용조건
@SKILL_SPECIAL_ACTIONABLE_3589,ARG
RETURN 1
;とくにない

