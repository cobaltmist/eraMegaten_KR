;통상공격
;스킬のテン플레이트になっています

;스킬名
@SKILL_NAME_3540,ARG
RESULTS = 6태도・삼
IF ARG > -1 && TFLAG:전투중
	SELECTCASE SKILLGAGE_H_GET(ARG,3540)
	CASE 1
		RESULTS = 1의태도・나데시코
	CASE 2
		RESULTS = 2의태도・시라유리
	CASE 3
		RESULTS = 3의태도・보탄
	CASE 4
		RESULTS = 4의태도・츠바키
	CASE 5
		RESULTS = 5의태도・아사가오
	CASE 0,6
		RESULTS = 6의태도・유우가오
	ENDSELECT
	SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1
		RESULTS += "・도검변화"
ENDIF
;　명중
@SKILL_HITRATE_3540, ARG
IF ARG > -1
		SIF SKILLGAGE_H_GET(ARG,3540) == 2
			RETURN 200
		SIF SKILLGAGE_H_GET(ARG,3540) == 6 || SKILLGAGE_H_GET(ARG,3540) == 0
			RETURN 50
ENDIF
RETURN 100

;　위력
@SKILL_POWER_3540, ARG
IF ARG > -1
		SIF SKILLGAGE_H_GET(ARG,3540) == 2
			RETURN 300
		SIF SKILLGAGE_H_GET(ARG,3540) == 4
			RETURN 320
		SIF SKILLGAGE_H_GET(ARG,3540) == 5
			RETURN 70
		SIF SKILLGAGE_H_GET(ARG,3540) == 6 || SKILLGAGE_H_GET(ARG,3540) == 0
			RETURN 200
ENDIF
RETURN 350

@SKILL_CRITICAL_RATE_3540, ARG
SIF FLAG:행동순1 > -1 && SKILLGAGE_H_GET(FLAG:행동순1,3540) == 3
	RETURN 30
RETURN 5

;　최저공격횟수
@SKILL_MINATTACKNUMBER_3540,ARG
IF ARG > -1
	SIF SKILLGAGE_H_GET(ARG,3540) == 5
		RETURN 4
ENDIF
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3540,ARG
IF ARG > -1
	SIF SKILLGAGE_H_GET(ARG,3540) == 5
		RETURN 6
ENDIF
RETURN 1

;　ＨＰ2を支払うかＭＰ3を支払うか
@SKILL_COSTTYPE_3540, ARG
RETURN 1
;소비なし＝1

;　コストの量
@SKILL_COST_3540,ARG
IF ARG > -1
	SIF SKILLGAGE_H_GET(ARG,3540) == 6 || SKILLGAGE_H_GET(ARG,3540) == 0
		RETURN 25
ENDIF
RETURN 15

;　相性
@SKILL_TYPE_3540,ARG
RETURN 0

;　1EXTRAか2MAGICか
@SKILL_DECIDE_TYPE_3540, ARG
RETURN 1
;どちらでもない

;　1물리데미지で計算するか2마법데미지で計算するか
@SKILL_DAMAGETYPE_3540, ARG
RETURN 1


;　대상が1적か2아군か3場전체か4特殊か
@SKILL_TARGET_3540
RETURN 1

;　사거리
@SKILL_RANGE_3540,ARG
RETURN 1

;　범위　1１체　2１열　3전체　4　적아군 전체
@SKILL_SPHERE_3540,ARG
RETURN 1

;　 効果の타입　1공격、2회복、3상태변화、4特殊、5상태회복
@SKILL_EFECT_3540
RETURN 1

;　 特殊処理の有無、及び処理内容
@SKILL_SPECIALEFECT_3540,ARG
;ARG = 1だった場合は処理の有無を返します
SIF ARG == 1
	RETURN 0
;以下に特殊な処理を記述します

;계승타입
@SKILL_SUCCESSION_TYPE_3540
RETURN 22
;검격

;속도보정
@SKILL_SPEED_3540, ARG = -1
IF ARG > -1
	SIF SKILLGAGE_H_GET(ARG,3540) == 1
		RETURN 20
ENDIF
RETURN 0

;　구상の표시とその타입
@SKILL_MESSAGE_3540,ARG


;전투時이외に사용できるか
@SKILL_ACTIONABLE_FIELD_3540
RETURN 0

;스킬변화？用랭크
@SKILL_RANK_3540
RETURN 23

@SKILL_RANKUP_3540
RETURN 0

;テン플레이트用、추가효과시리즈
;추가효과스테이터스
@SKILL_ADDTIONAL_STATE_3540,ARG
RETURN 0

;추가효과상성
@SKILL_ADDTIONAL_TYPE_3540,ARG
RETURN 0

;추가효과기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_3540,ARG
RETURN 0

;추가효과부여확률上限
@SKILL_ADDTIONAL_MAX_RATE_3540,ARG
RETURN 0

;====================================================
;カウンター発動조건
;====================================================
@SKILL_COUNTER_ACTIONABLE_3540,ARG,ARG:1
;修羅
SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) != 0 && CFLAG:ARG:PT플래그
	RETURN 0
SIF !(CFLAG:ARG:방어플래그) && CFLAG:ARG:PT플래그
	RETURN 0
SIF !GETBIT(CFLAG:ARG:받음플래그,18)
	RETURN 0
SIF ARG:1 < 0 || GET_STATE(CFLAG:(ARG:1):상태이상) == "DYING"
	RETURN 0
SIF !RAND:2
	RETURN 0
CALL SKILLGAGE_H_SET(ARG,3540,4)
RETURN 1

;====================================================
;데미지発動時
;====================================================
@SKILL_DAMAGE_EFFECT_3540,ARG,ARG:1
SELECTCASE SKILLGAGE_H_GET(ARG,3540)
CASE 4
	CFLAG:(ARG:1):크리티컬강화 = MAX(-4, CFLAG:(ARG:1):크리티컬강화 - 4)
	PRINT  & 크리티컬율 다운
CASE 0,6
ENDSELECT
FLAG:확산플래그 = 1

LOCAL = SKILLGAGE_H_GET(ARG,3540)
SIF LOCAL == 0
	LOCAL = 6
SIF ARG != ARG:1
	CALL YUCHOGE_SET,ARG,LOCAL

;====================================================
;스킬及び行動の実行
;====================================================

@ACTION_3540,ARG,ARG:1
LOCAL = POS(ARG:1)

FLAG:확산플래그 = 0
SELECTCASE SKILLGAGE_H_GET(ARG,3540)
CASE 1,2,3,4,5
	CALL ATTACK_SINGLE,ARG,LOCAL,3540
CASE 0,6
	LOCAL:1 = 0
	IF (MAXBASE:LOCAL:검격 < 100 || MAXBASE:LOCAL:검격 == 999) && CFLAG:LOCAL:검격가드킬 < 1
		CFLAG:LOCAL:검격가드킬 = 1
		LOCAL:1 = 1
	ENDIF
	CALL ATTACK_SINGLE,ARG,LOCAL,3540
	SIF LOCAL:1
		CFLAG:LOCAL:검격가드킬 = 0
	IF CFLAG:LOCAL:현데미지 > 0
		CFLAG:LOCAL:검격무효화횟수 -= 3
		IF CFLAG:LOCAL:검격무효화횟수 < 0
			CFLAG:LOCAL:검격가드킬 = -CFLAG:LOCAL:검격무효화횟수
			PRINTFORML [{ARG:1}]%CALLNAME:LOCAL,20% >>>>> {-CFLAG:LOCAL:검격무효화횟수}턴、검격의 내성을 무효화！
			CFLAG:LOCAL:검격무효화횟수 = 0
		ELSE
			PRINTFORML [{ARG:1}]%CALLNAME:LOCAL,20% >>>>> 검격무효화횟수소거！
		ENDIF
	ENDIF
ENDSELECT
IF GET_STATE(CFLAG:LOCAL:상태이상) == "DYING"
	IF SKILLGAGE_H_GET(ARG,3540) == 0
		CALL YUCHOGE_SET,ARG,6
	ELSE
		CALL YUCHOGE_SET,ARG,SKILLGAGE_H_GET(ARG,3540)
	ENDIF
ENDIF
;修羅
SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
	CALL SKILLGAGE_H_SET(ARG,3540,0)
;羅刹
SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 1 && FLAG:확산플래그
	CALL SIX_TATI_CHANGE(ARG)
FLAG:확산플래그 = 0

;====================================================
;実行内容ここまで
;====================================================

;　 설명文の표시
@SKILL_EXPLAIN_3540
IF TFLAG:전투중 == 0 || U < 0 || U >= CHARANUM
	PRINTL 【수라】
	PRINTL 6의태도・유우가오　　적１체에 상성무시의 검격 상성의 중 데미지+검격내성을 파괴
	PRINTL 4의태도・츠바키　　　방어중 물리공격을 받으면 50％로 발동。검격 상성의 대 데미지+상대의 크리티컬률 저하
	PRINTL 【나찰】
	PRINTL 최초는 6의 태도・유우가오・도검변화지만、이 스킬을 사용하면 육태도검의 모든 스킬이 1～6의 태도검으로 랜덤하게 변한다
ELSE
	SELECTCASE SKILLGAGE_H_GET(U,3540)
	CASE 1
		PRINTL 적１체에 검격 상성의 대 데미지。발동이 빠르다
		PRINTL 데미지를 주면 유초화를 나데시코가 지키게 된다
	CASE 2
		PRINTL 적１체에 검격 상성의 대 데미지。명중이 높다
		PRINTL 데미지를 주면 유초화를 시라유리가 지키게 된다
	CASE 3
		PRINTL 적１체에 검격 상성의 대 데미지。크리티컬률이 높다
		PRINTL 데미지를 주면 유초화를 보탄이 지키게 된다
	CASE 4
		PRINTL 적１체에 검격 상성의 대 데미지+상대의 크리티컬률 저하
		PRINTL 데미지를 주면 유초화를 츠바키가 지키게 된다
	CASE 5
		PRINTL 적１체에 검격 상성의 4～6회 소 데미지。
		PRINTL 데미지를 주면 유초화를 아사가오가 지키게 된다
	CASE 0,6
		PRINTL 적１체에 내성 무시의 검격 상성 의 중 데미지+검격의 내성을 무효화한다。명중률이 낮다
		PRINTL 데미지를 주면 유초화를 유우가오가 지키게 된다
	ENDSELECT
	SIF SKILLGAGE_F_GET(U,[[스킬:분노폭발]]) == 1
		PRINTL 또한 모든 육태도검스킬을 바꾼다
ENDIF

;継承に필요한部位
@継承部位_3540,ARG
RETURN 1
;とくにない

@SKILL_ACTIONABLE_BATTLE_3540
RETURN 1

;사용조건
@SKILL_SPECIAL_ACTIONABLE_3540,ARG
RETURN 1
;とくにない

@SIX_TATI_CHANGE(ARG)
#DIM STOCK
#DIM LCOUNT

FOR LCOUNT,3538,3541
	IF HAVE_SKILL(ARG,LCOUNT)
		LOCAL = SKILLGAGE_H_GET(ARG,LCOUNT)
		IF LOCAL == 0
			SIF LCOUNT == 3538
				LOCAL = 1
			SIF LCOUNT == 3539
				LOCAL = 5
			SIF LCOUNT == 3540
				LOCAL = 6
		ENDIF
		LOCAL:1 = 0
		SIF LCOUNT == 3539
			LOCAL:1 = SKILLGAGE_H_GET(ARG,3538)
		WHILE 1
			STOCK = RAND:6 + 1
			SIF STOCK != LOCAL && STOCK != LOCAL:1
				BREAK
		WEND
		CALL SKILLGAGE_H_SET(ARG,LCOUNT,STOCK)
	ENDIF
NEXT
PRINTW 모든 육태도검스킬이 변화했다！

