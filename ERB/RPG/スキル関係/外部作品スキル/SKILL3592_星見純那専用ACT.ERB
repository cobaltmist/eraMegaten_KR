;更新履歴
;001	2020/08/17		kamedakeisuke(ﾟдﾟ)			新規作成
;002	2020/12/08		kamedakeisuke(ﾟдﾟ)			暫定的に状態異常付与率の数字を変えてみる（元の数字は最大"50-80"）
;003	2022/02/12		kamedakeisuke(ﾟдﾟ)			非CR時の基礎威力上昇
;003	2022/02/12		kamedakeisuke(ﾟдﾟ)			非CR時の基礎威力・状態異常付与率上昇、エラー避けのおまじない追加

;星見純那（ロビンフッド）専用スキル

;---
;CAスキルは意図的に強力にする
;使用条件がイベント後半まで進行＆星のティアラをアクセサリー欄に装備＆キラめきチャージ完了時に１発だけ、なので。
;入手の際の代償もあるし、それに見合った性能に。

;通常スキルに使用条件は無し
;だけどこちらも使えるようになるタイミングは同じなので多少は盛る
;---

;クライマックスアクトは万能バインド付き複合属性至高の魔弾（盛りすぎワロタ）
;通常時は毒の付いた飛具版デスバウンド
;クライマックス・アクトは消費無し（そのかわり撃ったらクライマックスレヴュー終了

;Renameは『猛毒の協奏曲』のほうで

;스킬명
@SKILL_NAME_3592,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RESULTS = 셔우드에 숨은 그림자
ELSE
	RESULTS = 맹독의 협주곡
ENDIF

;　명중
@SKILL_HITRATE_3592, ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 150
ELSE
	RETURN 80
ENDIF

;高クリティカル
@SKILL_CRITICAL_RATE_3592, ARG
RETURN 20

;　위력
@SKILL_POWER_3592, ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 640
ELSE
	RETURN 360
ENDIF


;　최저공격횟수
@SKILL_MINATTACKNUMBER_3592,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3592,ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3592, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_3592,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	;消費無し（代わりにゲージをゼロに）
	RETURN 0
ELSE
	RETURN 18
ENDIF

;　상성
@SKILL_TYPE_3592,ARG
#LOCALSSIZE 5
#LOCALSIZE 4
;CR展開時、複合属性
;飛具・疾風・核熱のうち、最も効果のある相性で攻撃
IF SKILLGAGE_D_GET(ARG,3588)
	SIF ARG < 0 || 스킬속성표시중
		RETURN GET_TYPE_NUM("비구")
	SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
		RETURN GET_TYPE_NUM("비구")
	LOCAL = POS(CFLAG:ARG:타겟)
	SIF LOCAL < 0
		RETURN GET_TYPE_NUM("비구")
		
	LOCAL:2 = -300
	;全部-400とかないとおもうけど、あったらあれなんで、飛具をいれとく
	LOCALS = 비구
	LOCALS:1 = 비구
	LOCALS:2 = 질풍
	LOCALS:3 = 핵열
	FOR LOCAL:1, 1, 4
		;判定用に数値を仕舞う。反射は-200扱い
		LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
		LOCAL:3 = !(HAVE_SKILL(ARG,[[스킬:영세라이도우]]) || HAVE_SKILL(ARG,2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
		LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
		SELECTCASE LOCAL:1
			CASE 1
				LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
			CASE 2 TO 3
				LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
			CASEELSE
		ENDSELECT
		IF LOCAL:3 > LOCAL:2
			LOCAL:2 = LOCAL:3
			LOCALS = %LOCALS:(LOCAL:1)%
		ENDIF
	NEXT
	PRINT 속성상성：
	PRINTS LOCALS
	PRINTL ！
	RETURN GET_TYPE_NUM(LOCALS)
	
;通常時は飛具
ELSE
	RETURN 1
ENDIF

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3592, ARG
RETURN 1
;EX

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3592, ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_3592
RETURN 1

;　사거리
@SKILL_RANGE_3592,ARG
RETURN 3

;　範囲　11체　2列　3전체　4　적아군전체
@SKILL_SPHERE_3592,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 1
ELSE
	RETURN 2
ENDIF

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3592
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_3592,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_3592
RETURN 1
;비구

;속도보정
@SKILL_SPEED_3592, ARG = -1
RETURN 0

;　구상의 표시와 그 타입
@SKILL_MESSAGE_3592,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3592
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3592
RETURN 16


;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3592,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN GET_STATE_NUM("BIND")
ELSE
	RETURN GET_STATE_NUM("POISON")
ENDIF


;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3592,ARG
;CR展開時
;만능
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 17
;신경
ELSE
	RETURN 8
ENDIF

;추가효과 기본부여확률
;雑魚100ﾊﾟｰ、運が良ければボスに刺さる、ぐらいで設計
@SKILL_ADDTIONAL_BASE_RATE_3592,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 100000
ELSE
	RETURN 80
ENDIF

;추가효과 부여확률상한
;運が良ければボスに刺さる、ぐらいで設計
@SKILL_ADDTIONAL_MAX_RATE_3592,ARG
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	RETURN 100
ELSE
	RETURN 100
ENDIF

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_3592,ARG,ARG:1
;CR展開時
IF SKILLGAGE_D_GET(ARG,3588)
	ALIGNMENT CENTER
	SETCOLOR 0x8ac4ec
	PRINTL 【ＣＬＩＭＡＸ　ＡＣＴ】
	RESETCOLOR
	ALIGNMENT LEFT

	;星見パワーアップイベント中かつボスが最終形態時のみ、ボスの不死フラグを剥がして攻撃
	IF GETBIT(이벤트플래그:93:13, 5) && (이벤트플래그:93:0 == 12 || 이벤트플래그:93:0 == 13) && (이벤트플래그:93:10 && [[キャラ:쥰나]])
		CFLAG:(CHARANUM-1):불사신플래그 = 0
	ENDIF
	
	IF ARG:1 > 16
		CALL RANDOM_TARGET,ARG,3592
		ARG:1 = CFLAG:ARG:타겟
	ENDIF
	
	LOCAL = POS(ARG:1)
	CALL ATTACK_SINGLE,ARG,LOCAL,3592

	;星見パワーアップイベント中かつボスが最終形態時のみ、撃破時に敵が喋る
	IF GETBIT(이벤트플래그:93:13, 5) && (이벤트플래그:93:0 == 12 || 이벤트플래그:93:0 == 13) && (이벤트플래그:93:10 && [[キャラ:쥰나]])
	
		;ボスのHPがぴったり1の場合、『すでに削り切ったけど、攻撃をミスってとどめを刺せていない』状況と見る
		IF BASE:(CHARANUM-1):ＨＰ == 1
			PRINTL
			SETCOLOR 0x33ffcc
			PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓
			PRINTFORML ┃%"밀어붙인다！",72,LEFT%┃
			PRINTFORML ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
			PRINTL
			ALIGNMENT CENTER
			PRINTFORML ┏━━━━━━━━━━┓
			PRINTFORML ┃　　 　1more！　　　┃
			PRINTFORMW ┗━━━━━━━━━━┛
			RESETCOLOR
			ALIGNMENT LEFT
			
			;テイクツー
			CFLAG:(CHARANUM-1):불사신플래그 = 0
			CFLAG:ARG:베어내기플래그 = 1
			
			CALL ATTACK_SINGLE,ARG,LOCAL,3592
			
		ENDIF
	
		;ボス撃破
		IF BASE:(CHARANUM-1):ＨＰ < 1
			ALIGNMENT CENTER
			PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━━━━━━━━┓
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　후…… 이 얼마나 아름다운 밤하늘인가　　　　　　　　┃
			PRINTL ┃　　별은, 브리튼의 새벽놀은, 이미 그대의 손 안에――　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
			PRINTL 
			ALIGNMENT LEFT
		ENDIF
	ENDIF

	ALIGNMENT CENTER
	SETCOLOR 0x8ac4ec
	PRINTW 【ＣＬＩＭＡＸ　ＲＥＶＵＥ／ＦＩＮＩＳＨ】
	PRINTL
	RESETCOLOR
	ALIGNMENT LEFT
	;クライマックスレヴュー終了
	;CALL SKILLGAGE_D_SET,ARG,3588,0

;それ以外
ELSE
	CALL ATTACK_FIELD,ARG,ARG:1,3592
ENDIF









;====================================================
;실행내용 여기까지
;====================================================

;　 설명문 표시
@SKILL_EXPLAIN_3592
#LOCALSIZE 1
IF CFLAG:(CHARANUM-1):PT플래그 || U < 0 || U >= CHARANUM
	PRINTL 【통상시】
	PRINTL 적 1열에 비구 상성의 대 데미지＋POISON
	PRINTL 
	PRINTL 【클라이맥스・레뷰　발동시】
	PRINTL 적 1체에 높은 크리티컬률『비구・질풍・핵열』복합 초특대 데미지＋만능 BIND。
ELSE
	;CR展開時
	IF SKILLGAGE_D_GET(U,3588)
		SETCOLOR 0x8ac4ec
		PRINTL 【ＣＬＩＭＡＸ　ＡＣＴ】
		RESETCOLOR
		PRINTL 소환한 숲 속에 적을 가두어 치명적인 일격으로 쳐부수는、로빈 후드의 필살기。
		PRINTL 적 1체에 높은 크리티컬률『비구・질풍・핵열』복합 초특대 데미지＋만능 BIND。
		;星見パワーアップイベント中かつボスが最終形態時のみ、撃ち放題
		IF GETBIT(이벤트플래그:93:13, 5) && (이벤트플래그:93:0 == 12 || 이벤트플래그:93:0 == 13) && (이벤트플래그:93:10 && [[キャラ:쥰나]])
			SETCOLOR 0x8ac4ec
			PRINTL 혼신의 일격으로 이 레뷰에 결착을！
			RESETCOLOR
		;通常
		ELSE
			PRINTL 사용하면 클라이맥스 레뷰는 종료된다
		ENDIF
	ELSE
		PRINTL 적 1열에 비구 속성의 대 데미지＋POISON
		PRINTL 클라이맥스 레뷰 발동 중에는 성능이 변화한다。
	ENDIF
ENDIF


@계승불능_3592
RETURN 1

;계승에 필요한 부위
@계승부위_3592,ARG
RETURN 1


;사용조건
@SKILL_SPECIAL_ACTIONABLE_3592,ARG
RETURN 1
;とくにない

