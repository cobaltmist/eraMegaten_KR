;통상공격
;スキルのテンプレートになっています

;스킬명
@SKILL_NAME_3539,ARG
RESULTS = 6태도・이
IF ARG > -1
	SELECTCASE SKILLGAGE_H_GET(ARG,3539)
	CASE 1
		RESULTS = 1의태도・나데시코
	CASE 2
		RESULTS = 2의태도・시라유리
	CASE 3
		RESULTS = 3의태도・보탄
	CASE 4
		RESULTS = 4의태도・츠바키
	CASE 0,5
		RESULTS = 5의태도・아사가오
	CASE 6
		RESULTS = 6의태도・유우가오
	ENDSELECT
ENDIF
;　명중
@SKILL_HITRATE_3539, ARG
IF ARG > -1
		SIF SKILLGAGE_H_GET(ARG,3539) == 2
			RETURN 200
		SIF SKILLGAGE_H_GET(ARG,3539) == 6
			RETURN 50
ENDIF
RETURN 100

;　위력
@SKILL_POWER_3539, ARG
IF ARG > -1
		SIF SKILLGAGE_H_GET(ARG,3539) == 2
			RETURN 300
		SIF SKILLGAGE_H_GET(ARG,3539) == 4
			RETURN 320
		SIF SKILLGAGE_H_GET(ARG,3539) == 5 || SKILLGAGE_H_GET(ARG,3539) == 0
			RETURN 70
		SIF SKILLGAGE_H_GET(ARG,3539) == 6
			RETURN 200
ENDIF
RETURN 350

@SKILL_CRITICAL_RATE_3539, ARG
SIF FLAG:행동순1 > -1 && SKILLGAGE_H_GET(FLAG:행동순1,3539) == 3
	RETURN 30
RETURN 5

;　최저공격횟수
@SKILL_MINATTACKNUMBER_3539,ARG
IF ARG > -1
	SIF SKILLGAGE_H_GET(ARG,3539) == 5 || SKILLGAGE_H_GET(ARG,3539) == 0
		RETURN 4
ENDIF
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3539,ARG
IF ARG > -1
	SIF SKILLGAGE_H_GET(ARG,3539) == 5 || SKILLGAGE_H_GET(ARG,3539) == 0
		RETURN 6
ENDIF
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3539, ARG
RETURN 1
;소비없음＝1

;　코스트량
@SKILL_COST_3539,ARG
IF ARG > -1
	SIF SKILLGAGE_H_GET(ARG,3539) == 6
		RETURN 25
ENDIF
RETURN 15

;　상성
@SKILL_TYPE_3539,ARG
RETURN 0

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3539, ARG
RETURN 1
;どちらでもない

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3539, ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_3539
RETURN 1

;　사거리
@SKILL_RANGE_3539,ARG
RETURN 1

;　범위　1１체　2１열　3전체　4　적아군 전체
@SKILL_SPHERE_3539,ARG
RETURN 1

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3539
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_3539,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_3539
RETURN 22
;검격

;속도보정
@SKILL_SPEED_3539, ARG = -1
IF ARG > -1
	SIF SKILLGAGE_H_GET(ARG,3539) == 1
		RETURN 20
ENDIF
RETURN 0

;　구상의 표시와 그 타입
@SKILL_MESSAGE_3539,ARG


;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3539
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3539
RETURN 23

@SKILL_RANKUP_3539
RETURN 0

;テンプレート用、追加効果シリーズ
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3539,ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3539,ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_3539,ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_3539,ARG
RETURN 0

;====================================================
;トリガー発動条件
;====================================================
@SKILL_TRIGGER_ACTIONABLE_3539,ARG,ARG:1
;修羅
SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) != 0 && CFLAG:ARG:PT플래그
	RETURN 0
;自分じゃなければNG
SIF ARG != ARG:1
	RETURN 0
;単体攻撃じゃなければNG
SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
	RETURN 0
;クリティカルを受けているキャラを探す
;死んでる可能性があるのでキャラ全体くまなく探す
FOR LOCAL,0,CHARANUM
	SIF GETBIT(CFLAG:LOCAL:받음플래그,21) && CFLAG:LOCAL:PT플래그 != CFLAG:ARG:PT플래그
		BREAK
	SIF LOCAL == (CHARANUM - 1)
		RETURN 0
NEXT
;50%
SIF RAND:2
	RETURN 0
LOCAL = POS(CFLAG:ARG:타겟)
SIF LOCAL < 0 || GET_STATE(CFLAG:LOCAL:상태이상) == "DYING"
	CALL RANDOM_TARGET,ARG,3539
SIF CFLAG:ARG:타겟 < 0 || POS(CFLAG:ARG:타겟) < 0 || GET_STATE(CFLAG:POS(CFLAG:ARG:타겟):상태이상) == "DYING"
	RETURN 0
CALL SKILLGAGE_H_SET(ARG,3539,3)
RESULT:1 = CFLAG:ARG:타겟
RETURN 1

;====================================================
;ダメージ発動時
;====================================================
@SKILL_DAMAGE_EFFECT_3539,ARG,ARG:1
SELECTCASE SKILLGAGE_H_GET(ARG,3539)
CASE 4
	CFLAG:(ARG:1):크리티컬강화 = MAX(-4, CFLAG:(ARG:1):크리티컬강화 - 4)
	PRINT  & 여크리티컬률 다운
CASE 6
	FLAG:확산플래그 = 1
ENDSELECT

LOCAL = SKILLGAGE_H_GET(ARG,3539)
SIF LOCAL == 0
	LOCAL = 5
SIF ARG != ARG:1
	CALL YUCHOGE_SET,ARG,LOCAL

;====================================================
;스킬 및 행동의 실행
;====================================================

@ACTION_3539,ARG,ARG:1
LOCAL = POS(ARG:1)

FLAG:확산플래그 = 0
SELECTCASE SKILLGAGE_H_GET(ARG,3539)
CASE 0,1,2,3,4,5
	CALL ATTACK_SINGLE,ARG,LOCAL,3539
CASE 6
	LOCAL:1 = 0
	IF (MAXBASE:LOCAL:검격 < 100 || MAXBASE:LOCAL:검격 == 999) && CFLAG:LOCAL:검격가드킬 < 1
		CFLAG:LOCAL:검격가드킬 = 1
		LOCAL:1 = 1
	ENDIF
	CALL ATTACK_SINGLE,ARG,LOCAL,3539
	SIF LOCAL:1
		CFLAG:LOCAL:검격가드킬 = 0
	IF CFLAG:LOCAL:현데미지 > 0
		CFLAG:LOCAL:검격무효화횟수 -= 3
		IF CFLAG:LOCAL:검격무효화횟수 < 0
			CFLAG:LOCAL:검격가드킬 = -CFLAG:LOCAL:검격무효화횟수
			PRINTFORML [{ARG:1}]%CALLNAME:LOCAL,20% >>>>> {-CFLAG:LOCAL:검격무효화횟수}턴、검격내성을 무효화！
			CFLAG:LOCAL:검격무효화횟수 = 0
		ELSE
			PRINTFORML [{ARG:1}]%CALLNAME:LOCAL,20% >>>>> 검격무효화횟수가 감소！
		ENDIF
	ENDIF
ENDSELECT
IF GET_STATE(CFLAG:LOCAL:상태이상) == "DYING"
	IF SKILLGAGE_H_GET(ARG,3539) == 0
		CALL YUCHOGE_SET,ARG,5
	ELSE
		CALL YUCHOGE_SET,ARG,LOCAL
	ENDIF
ENDIF
;修羅
SIF SKILLGAGE_F_GET(ARG,[[스킬:분노폭발]]) == 0
	CALL SKILLGAGE_H_SET(ARG,3539,0)

FLAG:확산플래그 = 0

;====================================================
;실행내용 여기까지
;====================================================

;　 설명문 표시
@SKILL_EXPLAIN_3539
IF TFLAG:전투중 == 0 || U < 0 || U >= CHARANUM
	PRINTL 【수라】
	PRINTL 5의태도・아사가오　　적 1체에 검격 상성의 4～6회 소 데미지。
	PRINTL 3의태도・보탄　　　　1체 공격으로 회심이 나오면 50％로 발동。검격 상성의 대 데미지。
	PRINTL 【나찰】
	PRINTL 최초는 5의태도・아사가오지만、특정한 스킬을 사용하면 1～6의태도로 랜덤으로 변한다
ELSE
	SELECTCASE SKILLGAGE_H_GET(U,3539)
	CASE 1
		PRINTL 적 1체에 검격 상성의 대 데미지。발동이 빠르다
		PRINTL 데미지를 주면 유우초카에 패랭이꽃이 핀다
	CASE 2
		PRINTL 적 1체에 검격 상성의 대 데미지。명중률이 높다
		PRINTL 데미지를 주면 유우초카에 흰 백합이 핀다
	CASE 3
		PRINTL 적 1체에 검격 상성의 대 데미지。크리티컬률이 높다
		PRINTL 데미지를 주면 유우초카에 모란이 핀다
	CASE 4
		PRINTL 적 1체에 검격 상성의 대 데미지+상대의 크리티컬률 저하
		PRINTL 데미지를 주면 유우초카에 동백꽃이 핀다
	CASE 0,5
		PRINTL 적 1체에 검격 상성의 4～6회 소 데미지。
		PRINTL 데미지를 주면 유우초카에 나팔꽃이 핀다
	CASE 6
		PRINTL 적 1체에 내성무시의 검격 상성의 중 데미지+검격내성을 무효화한다。명중률이 낮다
		PRINTL 데미지를 주면 유우초카에 박꽃이 핀다
	ENDSELECT
ENDIF

;계승에 필요한 부위
@계승부위_3539,ARG
RETURN 1
;とくにない

@SKILL_ACTIONABLE_BATTLE_3539
RETURN 1

;사용조건
@SKILL_SPECIAL_ACTIONABLE_3539,ARG
RETURN 1
;とくにない

