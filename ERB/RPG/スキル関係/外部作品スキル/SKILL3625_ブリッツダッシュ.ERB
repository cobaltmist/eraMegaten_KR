;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:SKILL3625_ブリッツダッシュ.ERB
;	Facility	:SKILL:3625 (ブリッツダッシュ)。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2021/03/15		木綿豆腐				新規作成
;	002		2021/03/21		木綿豆腐				ゲージ周りの処理を変更。次回更新分の用意もしておく
;	003		2021/03/30		木綿豆腐				無謀の証、無気力のお守りの処理を追加。ロックオン周りの処理を変更。
;	004		2021/04/27		木綿豆腐				無謀の証・無気力のお守り装備時のクードス表示を修正
;													敵撃破時にクードス加算処理を追加
;	005		2021/08/24		木綿豆腐				念のため継承できないようにした
;	006		2021/10/04		木綿豆腐				クードスがリセットされたメッセージに名前も出すようにした
;	007		2022/02/06		木綿豆腐				↑がPRINTLだったのでPRINTFORMLにした
;													あと被弾判定にミスがあったので直した
;	008		2022/02/26		木綿豆腐				↑の被弾判定が更にミスってたので直した
;	009		2022/05/12		木綿豆腐				挙動をなるべく原作に近くした（ロックしたらそいつに強制的に攻撃）
;													イクス2ルートに対応
;	010		2022/07/24		木綿豆腐				ＨＰが足りなくてもコマンド実行できたので修正
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;スキル名
@SKILL_NAME_3625, ARG
; 使用者指定なし
IF ARG == -1
	RESULTS = ブリッツダッシュ
ELSE
	IF CFLAG:ARG:809 == 1
		RESULTS = ブリッツダッシュ
	ELSE
		IF SKILL_CHANGE(ARG, [[キャラ:아큐라(X)]]) && CSTR:ARG:100 == "X2"
			;ロック中の攻撃
			IF BLITZ_TARGET_JUDG(ARG) == 1
				RESULTS = ロックオンホイール
			;非ロック中の攻撃
			ELSE
				RESULTS = ブレイクホイール
			ENDIF
		ELSE
			RESULTS = ディヴァイド
		ENDIF
	ENDIF
ENDIF

;　명중
@SKILL_HITRATE_3625, ARG
IF SKILL_CHANGE(ARG, [[キャラ:아큐라(X)]]) && CSTR:ARG:100 == "X2"
	;ロック中の攻撃
	IF BLITZ_TARGET_JUDG(ARG) == 1
		RETURN 150
	;非ロック中の攻撃
	ELSE
		RETURN 100
	ENDIF
ELSE
	RETURN 150
ENDIF

;　위력
@SKILL_POWER_3625, ARG
IF SKILL_CHANGE(ARG, [[キャラ:아큐라(X)]]) && CSTR:ARG:100 == "X2"
	;ロック中の攻撃
	IF BLITZ_TARGET_JUDG(ARG) == 1
		;攻撃回数期待値は(2+3+4+5)*0.25=3.5　なのでダメージ期待値は3.5*65=245
		;「これだけでも戦えなくはない」ぐらいのイメージ
		RETURN 70
	;非ロック中の攻撃
	ELSE
		;攻撃回数3　なのでダメージ期待値は3*100=300
		;X2はブレイクホイールで殴る接近戦仕様
		RETURN 100
	ENDIF
ELSE
	;ロック中の攻撃
	IF BLITZ_TARGET_JUDG(ARG) == 1
		;攻撃回数期待値は(2+3+4+5+6)*0.2=4　なのでダメージ期待値は4*65=260
		;「これだけでも戦えなくはない」ぐらいのイメージ
		RETURN 65
	;非ロック中の攻撃
	ELSE
		;攻撃回数期待値は(1+2+3+4+5)*0.2=3　なのでダメージ期待値は3*50=150
		;心許ないがロックして使うのが基本と思ってもらえれば
		RETURN 50
	ENDIF
ENDIF

;　クリティカル率
;　ヒット数多いのでクリティカルはしないさせない
;　-200でもたまにクリティカルしたのでこれで←0でいいんだってさ
;「ブレイクホイール」の場合だけクリティカルするように
@SKILL_CRITICAL_RATE_3625, ARG
IF SKILL_CHANGE(ARG, [[キャラ:아큐라(X)]]) && CSTR:ARG:100 == "X2"
	SIF BLITZ_TARGET_JUDG(ARG) != 1
		RETURN 5
	RETURN 0
ELSE
	RETURN 0
ENDIF

;　최저공격횟수
@SKILL_MINATTACKNUMBER_3625, ARG
IF SKILL_CHANGE(ARG, [[キャラ:아큐라(X)]]) && CSTR:ARG:100 == "X2"
	;ロック中の攻撃
	IF BLITZ_TARGET_JUDG(ARG) == 1
		RETURN 2
	;非ロック中の攻撃
	ELSE
		RETURN 3
	ENDIF
ELSE
	;ロック中の攻撃
	IF BLITZ_TARGET_JUDG(ARG) == 1
		RETURN 2
	;非ロック中の攻撃
	ELSE
		RETURN 1
	ENDIF
ENDIF

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3625, ARG
IF SKILL_CHANGE(ARG, [[キャラ:아큐라(X)]]) && CSTR:ARG:100 == "X2"
	;ロック中の攻撃
	IF BLITZ_TARGET_JUDG(ARG) == 1
		RETURN 5
	;非ロック中の攻撃
	ELSE
		RETURN 3
	ENDIF
ELSE
	;ロック中の攻撃
	IF BLITZ_TARGET_JUDG(ARG) == 1
		RETURN 6
	;非ロック中の攻撃
	ELSE
		RETURN 5
	ENDIF
ENDIF
;ロック中の攻撃
IF BLITZ_TARGET_JUDG(ARG) == 1
	RETURN 6
;非ロック中の攻撃
ELSE
	RETURN 5
ENDIF

;　2 ＨＰ지불　3 ＭＰ지불
;　あくまでＭＰを消費するのはＲｏＲｏの分
;　アキュラくん自身はＭＰを持たないという解釈でいく　←やっぱやめた
@SKILL_COSTTYPE_3625, ARG
SIF ARG < 0
	RETURN 3
IF CFLAG:ARG:809 == 1
	RETURN 2
ELSE
	RETURN 3
ENDIF
;소비없음＝1

;　코스트량
@SKILL_COST_3625, ARG
SIF ARG < 0
	RETURN 10
IF CFLAG:ARG:809 == 1
	;ボスとして出た時はHP消費0。敵かつ雑魚としては出ないでしょ
	SIF ARG >= 0 && CFLAG:ARG:보스플래그
		RETURN 0
	;HP3%消費　気軽に使ってくれるラインはどれくらいだ
	RETURN 3
ELSE
	RETURN 10
ENDIF

;　상성
@SKILL_TYPE_3625, ARG
RETURN GET_TYPE_NUM("만능")

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3625, ARG
RETURN 1
;EXTRA

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3625, ARG
RETURN 1

;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_3625
#DIM COPEN
COPEN = 0
SIF U > 0 && U < CHARANUM && NO:U == [[キャラ:아큐라(X)]]
	COPEN = U
SIF COPEN == 0 && FLAG:행동순1 > 0 && NO:(FLAG:행동순1) == [[キャラ:아큐라(X)]]
	COPEN = FLAG:행동순1
;ロック中の攻撃・ロック対象に強制
IF COPEN > 0 && BLITZ_TARGET_JUDG(COPEN) == 1
	RETURN 4
;非ロック中の攻撃・敵単体
ELSE
	RETURN 1
ENDIF

;　사거리
@SKILL_RANGE_3625, ARG
IF SKILL_CHANGE(ARG, [[キャラ:아큐라(X)]]) && CSTR:ARG:100 == "X2"
	RETURN 1
ELSE
	RETURN 3
ENDIF

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_3625, ARG
;ロック中の攻撃・ロック対象に強制
IF BLITZ_TARGET_JUDG(ARG) == 1
	RETURN 4
;非ロック中の攻撃・敵単体
ELSE
	RETURN 1
ENDIF

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3625
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_3625, ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_3625
RETURN GET_SUCCESSION_NUM("만능")

;속도보정
@SKILL_SPEED_3625, ARG = -1
RETURN 0

;　口上の表示とそのタイプ
@SKILL_MESSAGE_3625, ARG

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3625
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3625
RETURN 23

@계승불능_3625
RETURN 1

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3625, ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3625, ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_3625, ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_3625, ARG
RETURN 0

;====================================================
;行動によるスキルゲージ操作
;====================================================
@SKILLGAGE_CHARGE_3625, ARG, ARG:1
#LOCALSIZE 1
#DIM 加算クードス
#DIM 被弾回数
#DIM BLITZ_TARGET
;1桁は被弾回数
被弾回数 = SKILLGAGE_H_GET(ARG, [[스킬:블리츠대쉬]]) % 10
;2桁以上はターゲット
BLITZ_TARGET = SKILLGAGE_H_GET(ARG, [[스킬:블리츠대쉬]]) / 10

;死んだら
IF BASE:ARG:ＨＰ < 1
	;クードス没収
	CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 0
	被弾回数 = 0
	;どっちのSONG OF DIVAも終わり
	CALL SKILLGAGE_H_SET, ARG, [[스킬:SONG OF DIVA]], 0
	CALL SKILLGAGE_F_SET, ARG, [[스킬:SONG OF DIVA]], 0
	IF SKILLGAGE_H_GET(ARG, [[스킬:DARKNESS TRIGGER]]) == 1
		;ダークネストリガーも終わり（もう使えない）
		CALL SKILLGAGE_H_SET, ARG, [[스킬:DARKNESS TRIGGER]], 2
	ENDIF
ENDIF

SIF CFLAG:ARG:고유게이지D1 == 0
	CFLAG:ARG:고유게이지H2 = 0

;敵の数が最初に記録した数から変わったら
IF ENEMY_COUNT() != CFLAG:ARG:고유게이지H1
	;なおかつ敵の数が最初に記録した数より小さくて、自分の行動だったら
	IF ENEMY_COUNT() < CFLAG:ARG:고유게이지H1 && ARG == ARG:1
		;次のターン(D1=1の時)に敵を撃破できれば連続撃破と見なす
		CFLAG:ARG:고유게이지D1 = 2
	ENDIF
	CFLAG:ARG:고유게이지H1 = ENEMY_COUNT()
ENDIF

;モード・アウェイクニング中はクードス加算しない
SIF SKILLGAGE_H_GET(ARG, [[스킬:SONG OF DIVA]]) == 1
	RETURN 0
加算クードス = 0
;自分の行動の場合
IF ARG == ARG:1
	;SPスキルを使ったらゲージは溜めない
	IF CFLAG:ARG:입력행동 == [[스킬:X-ROSS DISASTER]]
		RETURN 0
	ENDIF
	;ダメージを受けた敵の数に応じてスキルゲージアップ
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:PT플래그 == CFLAG:ARG:PT플래그
			CONTINUE
		;ダークネストリガーによる自動攻撃では上げない
		SIF SKILLGAGE_D_GET(ARG, [[스킬:DARKNESS TRIGGER]]) > 0
			CONTINUE
		IF CFLAG:LOCAL:현데미지 > 0
			;20未満を割った時って0だよね多分
			加算クードス += MAX(1, CFLAG:LOCAL:현데미지 / 20)
		ELSE
			CONTINUE
		ENDIF
	NEXT
ENDIF
;攻撃を回避したらゲージが増える
;本来はそんな有情な機能はないが慈悲を与える
SIF (GETBIT(CFLAG:ARG:받음플래그,18) || GETBIT(CFLAG:ARG:받음플래그,19)) && !CFLAG:ARG:회피실패
	加算クードス += 10

IF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
ELSE
	;3回ダメージを受けた又はカウンター、トリガー以外の攻撃を受けたらクードス全没収！0ダメージでもダメです
	;これはアパシーではなくティミッドだ！！
	IF (CFLAG:ARG:현데미지 >= 0 || GETBIT(CFLAG:ARG:받음플래그,18) || GETBIT(CFLAG:ARG:받음플래그,19)) && CFLAG:ARG:회피실패
		;クードス1以上なら被弾判定
		IF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 0
			被弾回数 += 1
			IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
				IF 被弾回数 > 0
					PRINTFORML ＞%CALLNAME:ARG%의 쿠도스가 리셋되었다！
					;クードス没収
					CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 0
					被弾回数 = 0
				ENDIF
			ELSE
				IF 被弾回数 > 2
					PRINTFORML ＞%CALLNAME:ARG%의 쿠도스가 리셋되었다！
					;クードス没収
					CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 0
					被弾回数 = 0
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
	TIMES 加算クードス, 1.25
ELSEIF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
	TIMES 加算クードス, 0.5
ENDIF

;最後に加算クードス分を足す
CALL SKILLGAGE_F_ADD, ARG, [[스킬:블리츠대쉬]], 加算クードス

IF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
	;アパシーマンの上限は1200
	SIF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 1200
		CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 1200
ELSE
	;上限は99999
	SIF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 99999
		CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 99999
ENDIF

;不屈式SONG OF DIVA発動中(モード・アウェイクニング)　特に処理なし
IF SKILLGAGE_H_GET(ARG, [[스킬:SONG OF DIVA]]) > 0
;クードス式SONG OF DIVA発動中(モード・ディーヴァ)
ELSEIF SKILLGAGE_F_GET(ARG, [[스킬:SONG OF DIVA]]) > 0
	;クードス値が1000を下回ったら（まあ没収されて0になったらだよね）SONG OF DIVA終了
	IF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) < 1000
		CALL SKILLGAGE_F_SET, ARG, [[스킬:SONG OF DIVA]], 0
	ENDIF
;SONG OF DIVA未発
ELSE
	;クードス値が1000を超えてて
	IF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 999 && HAVE_SKILL(ARG, [[스킬:SONG OF DIVA]])
		;ダークネストリガー未使用(0)・強制解除後(2)ならSONG OF DIVA自動発動
		IF SKILLGAGE_H_GET(ARG, [[스킬:DARKNESS TRIGGER]]) != 1
			CALL SKILLGAGE_F_SET, ARG, [[스킬:SONG OF DIVA]], 1
			CALL ACTION_3626, ARG, ARG:1
		ENDIF
	ENDIF
ENDIF
CALL SKILLGAGE_H_SET, ARG, [[스킬:블리츠대쉬]], 被弾回数
CALL SKILLGAGE_H_ADD, ARG, [[스킬:블리츠대쉬]], BLITZ_TARGET * 10
RETURN 0

;====================================================
;追加コマンド
;====================================================
@SKILL_SPECIAL_COMMAND_3625, ARG, LOCAL
#LOCALSIZE 1
IF BLITZ_TARGET_JUDG(ARG) == 1
	PRINTFORML [{LOCAL}] ロック解除
	C:LOCAL = 13625
	LOCAL += 1
ELSE
	CFLAG:ARG:809 = 1
	CALL CHECK_COST, ARG, 3625
	IF RESULT
		PRINTFORML [{LOCAL}] ブリッツダッシュ
		C:LOCAL = 13625
		LOCAL += 1
	ENDIF
	CFLAG:ARG:809 = 0
ENDIF
RETURN LOCAL
;====================================================
;コマンド実行
;====================================================
@INPUT_COM_13625, ARG
#LOCALSIZE 2
#DIM 被弾回数
#DIM BLITZ_TARGET

被弾回数 = SKILLGAGE_H_GET(ARG, [[스킬:블리츠대쉬]]) % 10

IF BLITZ_TARGET_JUDG(ARG) == 1
	PRINTL ロックをリセットしますか？
	CALL INPUT_YN
	IF RESULT == 1
		PRINTL ロックをリセットしませんでした。
	ELSE
		PRINTL ロックをリセットしました。
		CALL SKILLGAGE_D_SET, ARG, [[스킬:블리츠대쉬]], 0
		CALL SKILLGAGE_H_SET, ARG, [[스킬:블리츠대쉬]], 被弾回数
		CFLAG:ARG:입력행동 = -1
		CFLAG:ARG:타겟 = 0
		CFLAG:ARG:ＲＥＰＥＡＴ행동 = CFLAG:ARG:입력행동
		CFLAG:ARG:ＲＥＰＥＡＴ타겟 = CFLAG:ARG:타겟
		CFLAG:ARG:방어플래그 = 1
	ENDIF
	RETURN -1
ELSE
	CFLAG:ARG:809 = 1
	CALL TRUE_SKILL_COST, ARG, 3625
	PRINTFORML ブリッツダッシュを使用しますか？(消費：{RESULT}ＨＰ)
	CALL INPUT_YN
	IF RESULT == 0
		$INPUT_LOOP
		LOCAL = CFLAG:ARG:입력행동
		CFLAG:ARG:입력행동 = [[스킬:블리츠대쉬]]
		CALL PAY_COST, ARG, CFLAG:ARG:입력행동
		DRAWLINE
		SIF FLAG:스킬속성표시설정 == 1
			CALL SKILL_EXPLAIN_PERFORMANCE, CFLAG:ARG:입력행동, ARG
		PRINTL コマンドから使用。
		PRINTL 空中を高速移動して相手に体当たりし、ロックを行う。ダメージは発生せず、相性によって無効化もされない。
		PRINTL ロックは敵一体にのみ行うことができ、既にロック済みの場合は一度解除する必要がある。
		PRINTL ロックした対象は専用スキルのターゲットとなり、性能が変化する。
		PRINTL ロックオン状態は５ターンの間持続する。
		PRINTL ＣＡＮＣＥＬで中断することも可能。
		CALLFORM SKILL_TYPE_3625, ARG
		CALL SHOW_NOW_FORMATION_E, 0, 2, RESULT
		PRINTL [0] ＣＡＮＣＥＬ
		INPUT
		IF (RESULT < 17 && RESULT > 6) && POS(RESULT) > -1
		ELSEIF RESULT == 0
			RETURN -1
		ELSE
			CLEARLINE 23
			SIF FLAG:스킬속성표시설정 == 1
				CLEARLINE 1
			GOTO INPUT_LOOP
		ENDIF
		BLITZ_TARGET = POS(RESULT) * 10
		SETCOLOR 0x33ffcc
		PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓
		PRINTFORM ┃[{CPOS(ARG),2}] %CALLNAME:ARG,21,LEFT%┃　┃
		CALLFORM SKILL_NAME_3625, ARG
		PRINTFORML %RESULTS,40,LEFT%┃
		PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
		CALL SKILLGAGE_H_SET, ARG, [[스킬:블리츠대쉬]], 被弾回数
		CALL SKILLGAGE_H_ADD, ARG, [[스킬:블리츠대쉬]], BLITZ_TARGET
		CALL SKILLGAGE_D_SET, ARG, [[스킬:블리츠대쉬]], 5
		SWAP TARGET, ARG
		CALL 구상호출, "BATTLE_MESSAGE", TARGET, TARGET
		SWAP TARGET, ARG
		RESETCOLOR
		LOCAL:1 = BLITZ_TARGET / 10
		PRINTFORMW 　　%CALLNAME:(LOCAL:1)%をロックした！
		CFLAG:ARG:입력행동 = LOCAL
		CFLAG:ARG:809 = 0
		SELECTCASE CFLAG:ARG:입력행동
		;専用技1=スピンチョッパーの攻撃先だけはロック対象とは限らない
		CASE [[스킬:블리츠대쉬]], [[스킬:스파크스텔라]] TO [[스킬:크로스랜서]], [[스킬:전용기2]] TO [[스킬:전용긴6]]
			CFLAG:ARG:타겟 = 23
			CFLAG:ARG:ＲＥＰＥＡＴ타겟 = CFLAG:ARG:타겟
		ENDSELECT
	ENDIF
ENDIF
RETURN -1

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_3625, ARG, ARG:1
#DIM ROCK_H
#DIM ループ用
#DIM 攻撃前敵カウント
#DIM 攻撃後敵カウント
#DIM 差分
#DIM 加算クードス

攻撃前敵カウント = 0
攻撃後敵カウント = 0
IF CFLAG:ARG:PT플래그 > 0
	FOR ループ用, 7, 17
		LOCALS = 포지션{ループ用}
		SIF FLAG:LOCALS == -1
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
			CONTINUE
		攻撃前敵カウント += 1
	NEXT
ENDIF

SIF SKILL_CHANGE(ARG, [[キャラ:아큐라(X)]]) && CSTR:ARG:100 == "X2" && CFLAG:ARG:고유게이지D1 == 0
	CFLAG:ARG:고유게이지H2 = 0

;ロック中かつロック対象が生存中なら強制的にロック対象へ攻撃
;ターゲットに23が入るのはロック中限定なので
IF ARG:1 == 23
	IF BLITZ_TARGET_JUDG(ARG) == 1
		ROCK_H = SKILLGAGE_H_GET(ARG, [[스킬:블리츠대쉬]]) / 10
		CALL ATTACK_SINGLE, ARG, ROCK_H, 3625
	ENDIF
ELSE
	CALL ATTACK_SINGLE, ARG, POS(ARG:1), 3625
ENDIF

IF CFLAG:ARG:PT플래그 > 0
	FOR ループ用, 7, 17
		LOCALS = 포지션{ループ用}
		SIF FLAG:LOCALS == -1
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
			CONTINUE
		攻撃後敵カウント += 1
	NEXT
ENDIF

差分 = 攻撃前敵カウント - 攻撃後敵カウント

IF 差分 > 0
	加算クードス = 0
	IF ARG:1 == 23
		加算クードス += 10
		IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
			TIMES 加算クードス, 1.25
		ELSEIF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
			TIMES 加算クードス, 0.5
		ENDIF
		PRINTFORMW AERIAL　Kudos + {加算クードス}
		CALL SKILLGAGE_F_ADD, ARG, [[스킬:블리츠대쉬]], 加算クードス
	ELSEIF SKILL_CHANGE(ARG, [[キャラ:아큐라(X)]]) && CSTR:ARG:100 == "X2" && CFLAG:ARG:고유게이지D1 > 0
		;連鎖数をH2で記録
		CFLAG:ARG:고유게이지H2 += 1
		加算クードス += 30
		IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
			TIMES 加算クードス, 1.25
		ELSEIF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
			TIMES 加算クードス, 0.5
		ENDIF
		PRINTFORMW QUICK BREAK　Kudos + {加算クードス}
		CALL SKILLGAGE_F_ADD, ARG, [[스킬:블리츠대쉬]], 加算クードス
	ENDIF
ENDIF
IF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
	;アパシーマンの上限は1200
	SIF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 1200
		CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 1200
ELSE
	;上限は99999
	SIF SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]]) > 99999
		CALL SKILLGAGE_F_SET, ARG, [[스킬:블리츠대쉬]], 99999
ENDIF
;====================================================
;戦闘中スキルゲージの可視化
;====================================================
@SKILLGAGE_DISPLAY_3625, ARG
#DIM ROCK_D
#DIM ROCK_H
#DIM ROCK_F
#DIM 被弾回数
ROCK_D = SKILLGAGE_D_GET(ARG, [[스킬:블리츠대쉬]])
ROCK_H = SKILLGAGE_H_GET(ARG, [[스킬:블리츠대쉬]]) / 10
ROCK_F = SKILLGAGE_F_GET(ARG, [[스킬:블리츠대쉬]])
被弾回数 = SKILLGAGE_H_GET(ARG, [[스킬:블리츠대쉬]]) % 10
IF ROCK_F > 0
	IF  EQUIP:ARG:악세서리 == [[아이템:무모함의증거]] || EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
		PRINTFORML Kudos {ROCK_F}
	ELSE
		PRINTFORM Kudos {ROCK_F} 
		;被弾回数表示
		SELECTCASE 被弾回数
		CASE 0
			PRINTL |||
		CASE 1
			PRINT ||
			SETCOLOR 0x666666
			PRINTL |
			RESETCOLOR
		CASE 2
			SETCOLOR 0xff0033
			PRINT |
			SETCOLOR 0x666666
			PRINTL ||
			RESETCOLOR
		ENDSELECT
	ENDIF
ELSE
	PRINTFORML Kudos {ROCK_F}
ENDIF

SETCOLOR 0xed1a72
SIF BLITZ_TARGET_JUDG(ARG) == 1
	PRINTFORML [POS.{CPOS(ROCK_H), 2}] %CALLNAME:(ROCK_H),20,LEFT%　ロックオン中(残り{ROCK_D}ターン)
RESETCOLOR
;====================================================
;실행내용 여기까지
;====================================================

;　 説明文の表示
@SKILL_EXPLAIN_3625
IF U > 0 && U < CHARANUM && NO:U == [[キャラ:아큐라(X)]] && CSTR:U:100 == "X2"
	PRINTL キーライフルを改造した武器・ブレイクホイールで攻撃する。
	PRINTL ＨＰを消費するコマンド「ブリッツダッシュ」によってロック済みの敵がいる場合、強制的にその対象へ攻撃する。
	PRINTL 
	PRINTL 攻撃対象がロックオン状態の場合…敵単体に２～５回、万能相性で小ダメージを与える。クリティカルしない。
	PRINTL 　　このスキルで敵を倒した場合、クードスが１０加算される。
	PRINTL 攻撃対象が非ロック状態の場合　…敵単体に３回、万能相性で小ダメージを与える。後列使用不可、対象前列のみ。
	PRINTL 　　前のターンに敵を倒しており、かつこのスキルで敵を倒した場合、クードスが３０加算される。
ELSE
	PRINTL 愛用のレーザー銃・ディヴァイドで射撃する。
	PRINTL ＨＰを消費するコマンド「ブリッツダッシュ」によってロック済みの敵がいる場合、強制的にその対象へ攻撃する。
	PRINTL 
	PRINTL 攻撃対象がロックオン状態の場合…敵単体に２～６回、万能相性で小ダメージを与える。
	PRINTL 　　このスキルで敵を倒した場合、クードスが１０加算される。
	PRINTL 攻撃対象が非ロック状態の場合　…敵単体に１～５回、万能相性で小ダメージを与える。
	PRINTL どちらの場合もクリティカルしない。
ENDIF

;계승에 필요한 부위
@계승부위_3625, ARG
RETURN 0

;사용조건
@SKILL_SPECIAL_ACTIONABLE_3625, ARG
;ターゲット=23が入るのはロック中のみ
;かつ実行時にターゲットが死んだりするとダメ
SIF ARG > 0 && CFLAG:ARG:타겟 == 23 && BLITZ_TARGET_JUDG(ARG) != 1
	RETURN 0
RETURN 1

;仕様改変によって必要になったので。
;要は「現在ブリッツダッシュのターゲットは生きているか」を聞いている
@BLITZ_TARGET_JUDG, ARG
#FUNCTION
#DIM ROCK_D
#DIM ROCK_H
ROCK_D = SKILLGAGE_D_GET(ARG, [[스킬:블리츠대쉬]])
ROCK_H = SKILLGAGE_H_GET(ARG, [[스킬:블리츠대쉬]]) / 10
;超個人用メモ
;ROCK_H			…ロックオン中のキャラのリスト番号
;CPOS(ROCK_H)	…ロックオン中のキャラのポジション
;IF ROCK_D > 0 && ROCK_H > -1 && GET_STATE(CFLAG:ROCK_H:상태이상) != "DYING" && CPOS(ROCK_H) > 0
IF ROCK_D > 0
	;なんか聞く必要がない気もする
	SIF !(ROCK_H > -1)
		RETURNF -1
	;これをやらないとCPOS(ROCK_H)でエラーになることが
	SIF ROCK_H >= CHARANUM
		RETURNF -5
	;ロック対象がPOS1～16のどっかにいなかったら-1
	SIF !(CPOS(ROCK_H) > 0)
		RETURNF -2
	;ロック対象が死んでたら-1
	SIF GET_STATE(CFLAG:ROCK_H:상태이상) == "DYING"
		RETURNF -3
	RETURNF 1
ENDIF
RETURNF -4

;戦闘開始時に行う処理　初期配置の敵の数をH1に記録
@SKILL_BATTLE_START_3625, ARG
CFLAG:ARG:고유게이지H1 = ENEMY_COUNT()
