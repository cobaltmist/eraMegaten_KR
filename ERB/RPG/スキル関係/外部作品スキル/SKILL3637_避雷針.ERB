;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:SKILL3637_避雷針.ERB
;	Facility	:SKILL:3637 (避雷針)。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2021/03/30		木綿豆腐				新規作成
;	002		2021/04/06		木綿豆腐				ロックが切れた後再度ロックする際に、リセットするか聞かれないようにした
;	003		2021/04/14		木綿豆腐				実行時のSKILL_TYPE呼び出しがおかしかったので修正
;	004		2021/07/10		木綿豆腐				表示する相性を電撃に変更（攻撃は雷撃鱗で実行するので）
;	005		2021/08/24		木綿豆腐				一度に三回まで実行できるようにした
;													念のため継承できないようにした
;	006		2022/05/12		木綿豆腐				ターゲット処理をより賢くした
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;スキル名
@SKILL_NAME_3637, ARG
RESULTS = 피뢰침

;　명중
@SKILL_HITRATE_3637, ARG
RETURN 100

;　위력
@SKILL_POWER_3637, ARG
RETURN 0

;　최저공격횟수
@SKILL_MINATTACKNUMBER_3637, ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3637, ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3637, ARG
RETURN 3
;消費なし＝1

;　코스트량
@SKILL_COST_3637, ARG
RETURN 0

;　상성
@SKILL_TYPE_3637, ARG
RETURN GET_TYPE_NUM("전격")

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3637, ARG
RETURN 0
;どちらでもない

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3637, ARG
RETURN 0

;　対象が1敵か2味方か3場全体か4特殊か
@SKILL_TARGET_3637
RETURN 1

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_3637, ARG
RETURN 1

;　사거리
@SKILL_RANGE_3637, ARG
RETURN 3

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복、6 자동효과
@SKILL_EFECT_3637
RETURN 1

;계승타입
@SKILL_SUCCESSION_TYPE_3637
RETURN 22
;何でもおｋ

@계승불능_3637
RETURN 1
;不可

;스킬변화？용 랭크
@SKILL_RANK_3637
RETURN 23
;변화불가

@SKILL_RANKUP_3637
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_3637, ARG, ARG:1
#LOCALSIZE 1
#DIM DART_H
LOCAL = POS(ARG:1)
CALL SKILLGAGE_H_ADD, ARG, [[스킬:피뢰침]], 1
DART_H = SKILLGAGE_H_GET(ARG, [[스킬:피뢰침]])
CFLAG:ARG:("고유게이지D"+TOSTR(DART_H)) = 5
CFLAG:ARG:("고유게이지H"+TOSTR(DART_H)) = LOCAL
PRINTFORMW 　　%CALLNAME:LOCAL%를 록했다！
;====================================================
;追加コマンド
;====================================================
@SKILL_SPECIAL_COMMAND_3637, ARG, LOCAL
#LOCALSIZE 1
PRINTFORML [{LOCAL}] 피뢰침
C:LOCAL = 13637
LOCAL += 1
RETURN LOCAL
;====================================================
;コマンド実行
;====================================================
@INPUT_COM_13637, ARG
#LOCALSIZE 1
#DIM DART_H
#DIM DART_TARGET
CALL TARGET_CHECK_3637, ARG
DART_H = SKILLGAGE_H_GET(ARG, [[스킬:피뢰침]])
IF DART_H == 3
	PRINTL 록 횟수가 상한에 도달했습니다。
	PRINTL 현재 록 중인 타겟을 리셋하고 피뢰침을 실행하시겠습니까？
	CALL INPUT_YN
	IF RESULT == 1
		PRINTL 록을 리셋하지 않았습니다。
		RETURN -1
	ELSE
		CALL SKILLGAGE_H_SET, ARG, [[스킬:피뢰침]], 0
	ENDIF
ENDIF
DO
	$INPUT_LOOP
	LOCAL = CFLAG:ARG:입력행동
	CFLAG:ARG:입력행동 = 3637
	DRAWLINE
	SIF FLAG:스킬속성표시설정 == 1
		CALL SKILL_EXPLAIN_PERFORMANCE, CFLAG:ARG:입력행동, ARG
	CALL SKILL_EXPLAIN_3637
	PRINTL ＣＡＮＣＥＬ로 중단하는 것도 가능。
	CALLFORM SKILL_TYPE_3637, ARG
	CALL SHOW_NOW_FORMATION_E, 0, 2, RESULT
	PRINTL [0] ＣＡＮＣＥＬ
	INPUT
	IF (RESULT < 17 && RESULT > 6) && POS(RESULT) > -1
	ELSEIF RESULT == 0
		RETURN -1
	ELSE
		CLEARLINE 23
		SIF FLAG:스킬속성표시설정 == 1
			CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	DART_TARGET = RESULT
	SETCOLOR 0x33ffcc
	PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓
	PRINTFORM ┃[{CPOS(ARG),2}] %CALLNAME:ARG,21,LEFT%┃　┃
	CALLFORM SKILL_NAME_3637, ARG
	PRINTFORML %RESULTS,40,LEFT%┃
	PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
	RESETCOLOR
	SWAP TARGET, ARG
	CALL ACTION_3637, TARGET, DART_TARGET
	CALL 구상호출, "BATTLE_MESSAGE", TARGET, TARGET
	SWAP TARGET, ARG
	CFLAG:ARG:입력행동 = LOCAL
	DART_H = SKILLGAGE_H_GET(ARG, [[스킬:피뢰침]])
;DART_H < 3が1(真)である限りDO～LOOPを繰り返す(つまりDART_H=3以上になれば終わるということ)
LOOP DART_H < 3
RETURN -1

;====================================================
;戦闘中スキルゲージの可視化
;====================================================
@SKILLGAGE_DISPLAY_3637, ARG
#LOCALSIZE 3
#DIM DART_H
#DIM ループ用1
#DIM ループ用2
#DIM ターゲット番号
#DIM ロック時間
#DIM ロック深度
#DIM ターゲットリスト, 4

ロック深度 = 0
VARSET ターゲットリスト,0
CALL TARGET_CHECK_3637, ARG
DART_H = SKILLGAGE_H_GET(ARG, [[스킬:피뢰침]])
SIF DART_H == 0
	RETURN
DART_H += 1
FOR ループ用1, 1, DART_H
	ターゲット番号 = CFLAG:ARG:("고유게이지H"+TOSTR(ループ用1))
	ロック時間 = CFLAG:ARG:("고유게이지D"+TOSTR(ループ用1))
	ロック深度 += 1
	FOR ループ用2, 1, DART_H
		SIF CFLAG:ARG:("고유게이지H"+TOSTR(ループ用1)) == CFLAG:ARG:("고유게이지H"+TOSTR(ループ用2)) && ループ用1 != ループ用2
			ロック深度 += 1
	NEXT
	SELECTCASE ロック深度
	CASE 1
		SETCOLOR 0x11afe0
	CASE 2
		SETCOLOR 0xd2ae40
	CASE 3
		SETCOLOR 0xcc365d
	ENDSELECT
	IF MATCH(ターゲットリスト, ターゲット番号) == 0
		PRINTFORML [POS.{CPOS(ターゲット番号), 2}] %CALLNAME:ターゲット番号,20,LEFT%　록온 중 (앞으로 {ロック時間}턴)　심도{ロック深度}
		ターゲットリスト:ループ用1 = ターゲット番号
	ENDIF
	RESETCOLOR
	ロック深度 = 0
NEXT

;====================================================
;ターゲットの生死をチェック
;====================================================
@TARGET_CHECK_3637, ARG
#DIM DART_H
#DIM ループ用1
#DIM ループ用2
#DIM ターゲット番号
#DIM ロック時間

DART_H = SKILLGAGE_H_GET(ARG, [[스킬:피뢰침]])
SIF DART_H == 0
	RETURN
DART_H += 1
FOR ループ用1, 1, DART_H
	ターゲット番号 = CFLAG:ARG:("고유게이지H"+TOSTR(ループ用1))
	ロック時間 = CFLAG:ARG:("고유게이지D"+TOSTR(ループ用1))
	IF ターゲット番号 > 0 && GET_STATE(CFLAG:ターゲット番号:상태이상) != "DYING" && CPOS(ターゲット番号) > 0 && ロック時間 > 0
	ELSE
		CFLAG:ARG:("고유게이지H"+TOSTR(ループ用1)) = 0
		CFLAG:ARG:("고유게이지D"+TOSTR(ループ用1)) = 0
		CALL SKILLGAGE_H_ADD, ARG, [[스킬:피뢰침]], -1
	ENDIF
NEXT
;====================================================
;실행내용 여기까지
;====================================================

;　 説明文の表示
@SKILL_EXPLAIN_3637
PRINTL 커맨드로 사용。
PRINTL 머리카락을 가공한 탄환을 상대에게 박아넣어、록을 한다。데미지는 발생하지 않으며、상성에 의한 무효화도 되지 않는다。
PRINTL 록은 최대 3회까지 할 수 있으며、동일한 상대에 록을 중첩해서 하는 것도 가능。
PRINTL 록 된 대상은 「뇌격린」의 타겟이 되며、록 심도가 많을수록 데미지가 증가한다。
PRINTL 록온 상태는 ５턴간 지속된다。

@SKILL_ACTIONABLE_BATTLE_3637
RETURN 0

;계승에 필요한 부위
@계승부위_3637, ARG
RETURN 0

