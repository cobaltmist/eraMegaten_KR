;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:SKILL3636_雷撃鱗.ERB
;	Facility	:SKILL:3636 (雷撃鱗)。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2021/03/30		木綿豆腐				신규작성
;	002		2021/04/27		木綿豆腐				무모함의증표・무기력의부적 장비 시의 쿠도스 표시를 수정
;													EXTRA에서 MAGIC으로 변경・적 격파 시에 쿠도스 가산 처리를 추가
;	003		2021/06/10		木綿豆腐				3록 시 위력을 지오다인에 상당하게 변경
;	004		2021/08/24		木綿豆腐				만일을 위해 계승 불가하게 했다
;	005		2021/10/04		木綿豆腐				쿠도스가 리셋되었다는 메세지에 이름도 나오도록 했다
;													덤으로 SP의 아이콘이 자작한 것으로 출력되도록 했다
;	006		2022/02/06		木綿豆腐				↑가 PRINTL이었으므로 PRINTFORML으로 했다
;													그리고 피탄판정에 미스가 있어서 고쳤다
;	007		2022/02/26		木綿豆腐				↑의 피탄 판정이 또 미스가 있어서 고쳤다
;	008		2022/05/12		木綿豆腐				타겟의 처리를 보다 스마트하게 했다
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;スキル名
@SKILL_NAME_3636, ARG
RESULTS = 뇌격린

;　명중
@SKILL_HITRATE_3636, ARG
RETURN 250

;　위력
@SKILL_POWER_3636, ARG
SIF FLAG:확산플래그 == 2
	RETURN 240
SIF FLAG:확산플래그 == 3
	RETURN 320
RETURN 160

;　최저공격횟수
@SKILL_MINATTACKNUMBER_3636, ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_3636, ARG
RETURN 1

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_3636, ARG
RETURN 3
;消費なし＝1

;　코스트량
@SKILL_COST_3636, ARG
RETURN 10

;　상성
@SKILL_TYPE_3636, ARG
RETURN GET_TYPE_NUM("전격")

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_3636, ARG
RETURN 2

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_3636, ARG
RETURN 2

;　対象が1敵か2味方か3場全体か4特殊か
@SKILL_TARGET_3636
RETURN 4

;　사거리
@SKILL_RANGE_3636, ARG
RETURN 3

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_3636, ARG
RETURN 1

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_3636
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_3636, ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_3636
RETURN GET_SUCCESSION_NUM("전격")

;속도보정
@SKILL_SPEED_3636, ARG = -1
RETURN 0

;　口上の表示とそのタイプ
@SKILL_MESSAGE_3636, ARG

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_3636
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_3636
RETURN 23

@계승불능_3636
RETURN 1

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_3636, ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_3636, ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_3636, ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_3636, ARG
RETURN 0

;====================================================
;行動によるスキルゲージ操作
;====================================================
@SKILLGAGE_CHARGE_3636, ARG, ARG:1
#LOCALSIZE 1
#DIM 加算クードス
SIF HAVE_SKILL(ARG, [[스킬:피뢰침]])
	CALL TARGET_CHECK_3637, ARG
;死んだら
IF BASE:ARG:ＨＰ < 1
	;クードス没収
	CALL SKILLGAGE_F_SET, ARG, [[스킬:뇌격린]], 0
	;被弾回数リセット
	CALL SKILLGAGE_H_SET, ARG, [[스킬:뇌격린]], 0
	;どっちのSONG OF DIVAも終わり
	CALL SKILLGAGE_H_SET, ARG, [[스킬:SONG OF DIVA]], 0
	CALL SKILLGAGE_F_SET, ARG, [[스킬:SONG OF DIVA]], 0
ENDIF
;SONG OF DIVA/輪廻中はクードス加算しない
SIF SKILLGAGE_H_GET(ARG, [[스킬:SONG OF DIVA]]) == 1
	RETURN 0

加算クードス = 0
;自分の行動の場合
IF ARG == ARG:1
	;SPスキルを使ったらゲージは溜めない
	IF RANGE(CFLAG:ARG:입력행동, 3904, 3908)
		RETURN 0
	ENDIF
	;ダメージを受けた敵の数に応じてスキルゲージアップ
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:PT플래그 == CFLAG:ARG:PT플래그
			CONTINUE
		IF CFLAG:LOCAL:현데미지 > 0
			;20未満を割った時って0だよね多分
			加算クードス += MAX(1, CFLAG:LOCAL:현데미지 / 20)
		ELSE
			CONTINUE
		ENDIF
	NEXT
ENDIF
;攻撃を回避したらゲージが増える
;本来はそんな有情な機能はないが慈悲を与える
SIF (GETBIT(CFLAG:ARG:받음플래그,18) || GETBIT(CFLAG:ARG:받음플래그,19)) && !CFLAG:ARG:회피실패
	加算クードス += 10
IF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
ELSE
	;3回ダメージを受けた又はカウンター、トリガー以外の攻撃を受けたらクードス全没収！0ダメージでもダメです
	;これはアパシーではなくティミッドだ！！
	IF (CFLAG:ARG:현데미지 >= 0 || GETBIT(CFLAG:ARG:받음플래그,18) || GETBIT(CFLAG:ARG:받음플래그,19)) && CFLAG:ARG:회피실패
		;クードス1以上なら被弾判定
		IF SKILLGAGE_F_GET(ARG, [[스킬:뇌격린]]) > 0
			;ゲージHを使わないスキルなので被弾回数として用いる
			CALL SKILLGAGE_H_ADD, ARG, [[스킬:뇌격린]], 1
			IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
				IF SKILLGAGE_H_GET(ARG, [[스킬:뇌격린]]) > 0
					PRINTFORML ＞%CALLNAME:ARG%의 쿠도스가 리셋되었다！
					;クードス没収
					CALL SKILLGAGE_F_SET, ARG, [[스킬:뇌격린]], 0
					;被弾回数リセット
					CALL SKILLGAGE_H_SET, ARG, [[스킬:뇌격린]], 0
				ENDIF
			ELSE
				IF SKILLGAGE_H_GET(ARG, [[스킬:뇌격린]]) > 2
					PRINTFORML ＞%CALLNAME:ARG%의 쿠도스가 리셋되었다！
					;クードス没収
					CALL SKILLGAGE_F_SET, ARG, [[스킬:뇌격린]], 0
					;被弾回数リセット
					CALL SKILLGAGE_H_SET, ARG, [[스킬:뇌격린]], 0
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
	TIMES 加算クードス, 1.25
ELSEIF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
	TIMES 加算クードス, 0.5
ENDIF

;最後に加算クードス分を足す
CALL SKILLGAGE_F_ADD, ARG, [[스킬:뇌격린]], 加算クードス

IF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
	;アパシーマンの上限は1200
	SIF SKILLGAGE_F_GET(ARG, [[스킬:뇌격린]]) > 1200
		CALL SKILLGAGE_F_SET, ARG, [[스킬:뇌격린]], 1200
ELSE
	;上限は99999
	SIF SKILLGAGE_F_GET(ARG, [[스킬:뇌격린]]) > 99999
		CALL SKILLGAGE_F_SET, ARG, [[스킬:뇌격린]], 99999
ENDIF

;不屈式SONG OF DIVA発動中(SONG OF DIVA/輪廻)　特に処理なし
IF SKILLGAGE_H_GET(ARG, [[스킬:SONG OF DIVA]]) > 0
;クードス式SONG OF DIVA発動中(SONG OF DIVA/藍の運命)
ELSEIF SKILLGAGE_F_GET(ARG, [[스킬:SONG OF DIVA]]) > 0
	;クードス値が1000を下回ったら（まあ没収されて0になったらだよね）SONG OF DIVA終了
	IF SKILLGAGE_F_GET(ARG, [[스킬:뇌격린]]) < 1000
		CALL SKILLGAGE_F_SET, ARG, [[스킬:SONG OF DIVA]], 0
	ENDIF
;SONG OF DIVA未発
ELSE
	;クードス値が1000を超えたら
	IF SKILLGAGE_F_GET(ARG, [[스킬:뇌격린]]) > 999 && HAVE_SKILL(ARG, [[스킬:SONG OF DIVA]])
		CALL SKILLGAGE_F_SET, ARG, [[스킬:SONG OF DIVA]], 1
		CALL ACTION_3626, ARG, ARG:1
	ENDIF
ENDIF
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_3636, ARG, ARG:1
#LOCALSIZE 3
#DIM DART_D
#DIM DART_H
#DIM TEMP
#DIM ループ用
#DIM ループ用1
#DIM ループ用2
#DIM 攻撃前敵カウント
#DIM 攻撃後敵カウント
#DIM 差分
#DIM 加算クードス
#DIM ターゲット番号
#DIM ターゲットリスト, 4

攻撃前敵カウント = 0
攻撃後敵カウント = 0
TEMP = 0
IF CFLAG:ARG:PT플래그 > 0
	FOR ループ用, 7, 17
		LOCALS = 포지션{ループ用}
		SIF FLAG:LOCALS == -1
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
			CONTINUE
		攻撃前敵カウント += 1
	NEXT
ENDIF

FLAG:확산플래그 = 0
TEMP = 0
VARSET ターゲットリスト,0
CALL TARGET_CHECK_3637, ARG
DART_H = SKILLGAGE_H_GET(ARG, [[스킬:피뢰침]])
SIF DART_H == 0
	RETURN
DART_H += 1
FOR ループ用1, 1, DART_H
	ターゲット番号 = CFLAG:ARG:("고유게이지H"+TOSTR(ループ用1))
	FLAG:확산플래그 += 1
	FOR ループ用2, 1, DART_H
		SIF CFLAG:ARG:("고유게이지H"+TOSTR(ループ用1)) == CFLAG:ARG:("고유게이지H"+TOSTR(ループ用2)) && ループ用1 != ループ用2
			FLAG:확산플래그 += 1
	NEXT
	IF MATCH(ターゲットリスト, ターゲット番号) == 0
		CALL ATTACK_SINGLE, ARG, ターゲット番号, 3636
		TEMP = FLAG:확산플래그
		FLAG:확산플래그 = 0
		ターゲットリスト:ループ用1 = ターゲット番号
	ENDIF
NEXT

IF CFLAG:ARG:PT플래그 > 0
	FOR ループ用, 7, 17
		LOCALS = 포지션{ループ用}
		SIF FLAG:LOCALS == -1
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):상태이상) == "DYING"
			CONTINUE
		攻撃後敵カウント += 1
	NEXT
ENDIF

差分 = 攻撃前敵カウント - 攻撃後敵カウント

SELECTCASE 差分
CASE 1
	;原作だと+10だけど慈悲を与える
	IF TEMP == 3
		加算クードス = 30
		IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
			TIMES 加算クードス, 1.25
		ELSEIF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
			TIMES 加算クードス, 0.5
		ENDIF
		PRINTFORMW FULL-STRIKE　Kudos + {加算クードス}
		CALL SKILLGAGE_F_ADD, ARG, [[스킬:뇌격린]], 加算クードス
	ENDIF
CASE 2
	加算クードス = 100
	IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
		TIMES 加算クードス, 1.25
	ELSEIF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
		TIMES 加算クードス, 0.5
	ENDIF
	PRINTFORMW DOUBLE　Kudos + {加算クードス}
	CALL SKILLGAGE_F_ADD, ARG, [[스킬:뇌격린]], 加算クードス
CASE 3
	加算クードス = 250
	IF EQUIP:ARG:악세서리 == [[아이템:무모함의증거]]
		TIMES 加算クードス, 1.25
	ELSEIF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
		TIMES 加算クードス, 0.5
	ENDIF
	PRINTFORMW TRIPLE　Kudos + {加算クードス}
	CALL SKILLGAGE_F_ADD, ARG, [[스킬:뇌격린]], 加算クードス
ENDSELECT
IF EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
	;アパシーマンの上限は1200
	SIF SKILLGAGE_F_GET(ARG, [[스킬:뇌격린]]) > 1200
		CALL SKILLGAGE_F_SET, ARG, [[스킬:뇌격린]], 1200
ELSE
	;上限は99999
	SIF SKILLGAGE_F_GET(ARG, [[스킬:뇌격린]]) > 99999
		CALL SKILLGAGE_F_SET, ARG, [[스킬:뇌격린]], 99999
ENDIF
;====================================================
;戦闘中スキルゲージの可視化
;====================================================
@SKILLGAGE_DISPLAY_3636, ARG
#DIM RGRN_H
#DIM RGRN_F
#DIM RGRN_D
RGRN_H = SKILLGAGE_H_GET(ARG, [[스킬:뇌격린]])
RGRN_F = SKILLGAGE_F_GET(ARG, [[스킬:뇌격린]])
RGRN_D = SKILLGAGE_D_GET(ARG, [[스킬:뇌격린]])
IF RGRN_F > 0
	IF  EQUIP:ARG:악세서리 == [[아이템:무모함의증거]] || EQUIP:ARG:악세서리 == [[아이템:무기력의부적]]
		PRINTFORML Kudos {RGRN_F}
	ELSE
		PRINTFORM Kudos {RGRN_F} 
		;被弾回数表示
		SELECTCASE RGRN_H
		CASE 0
			PRINTL |||
		CASE 1
			PRINT ||
			SETCOLOR 0x666666
			PRINTL |
			RESETCOLOR
		CASE 2
			SETCOLOR 0xff0033
			PRINT |
			SETCOLOR 0x666666
			PRINTL ||
			RESETCOLOR
		ENDSELECT
	ENDIF
ELSE
	PRINTFORML Kudos {RGRN_F}
ENDIF
;ゲージD4あたり1ポイント分とし、スキル側で12以上にならないようチェックする
SELECTCASE RGRN_D
CASE 0
	IF EXIST_PICTURE(NO:ARG, 6600)
		PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(4)%"
		PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(4)%"
		PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(4)%"
	ELSE
		SETCOLOR 0xf5fa68
		PRINT ◆◆◆
		RESETCOLOR
	ENDIF
	PRINTL 　ＳＰ：３
CASE 1, 2, 3, 4
	IF EXIST_PICTURE(NO:ARG, 6600)
		PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(4)%"
		PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(4)%"
		SELECTCASE RGRN_D
		CASE 1
			PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(3)%"
		CASE 2
			PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(2)%"
		CASE 3
			PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(1)%"
		CASE 4
			PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(0)%"
		ENDSELECT
	ELSE
		SETCOLOR 0xf5fa68
		PRINT ◆◆
		SETCOLOR 0xb4580d
		PRINT ◇
		RESETCOLOR
	ENDIF
	PRINT 　ＳＰ：２(회복까지 
	SETCOLOR 0xc3530b
	PRINTFORM {RGRN_D}
	RESETCOLOR
	PRINTL 턴)
CASE 5, 6, 7, 8
	IF EXIST_PICTURE(NO:ARG, 6600)
		PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(4)%"
		SELECTCASE RGRN_D
		CASE 5
			PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(3)%"
		CASE 6
			PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(2)%"
		CASE 7
			PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(1)%"
		CASE 8
			PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(0)%"
		ENDSELECT
		PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(0)%"
	ELSE
		SETCOLOR 0xf5fa68
		PRINT ◆
		SETCOLOR 0xb4580d
		PRINT ◇◇
		RESETCOLOR
	ENDIF
	PRINT 　ＳＰ：１(회복까지 
	SETCOLOR 0xc3530b
	PRINTFORM {RGRN_D - 4}
	RESETCOLOR
	PRINTL 턴)
CASE 9, 10, 11, 12
	IF EXIST_PICTURE(NO:ARG, 6600)
		SELECTCASE RGRN_D
		CASE 9
			PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(3)%"
		CASE 10
			PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(2)%"
		CASE 11
			PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(1)%"
		CASE 12
			PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(0)%"
		ENDSELECT
		PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(0)%"
		PRINT_IMG @"A%TOSTR(NO:ARG)%_%TOSTR(6600)%_%TOSTR(0)%"
	ELSE
		SETCOLOR 0xb4580d
		PRINT ◇◇◇
		RESETCOLOR
	ENDIF
	PRINT 　ＳＰ：０(회복까지 
	SETCOLOR 0xc3530b
	PRINTFORM {RGRN_D - 8}
	RESETCOLOR
	PRINTL 턴)
ENDSELECT
;====================================================
;실행내용 여기까지
;====================================================

;　 説明文の表示
@SKILL_EXPLAIN_3636
#DIM DKTG_F
;ステータス画面からはこっち表示
IF CFLAG:(CHARANUM-1):PT플래그 || U < 0 || U >= CHARANUM
	PRINTL 피뢰침을 맞은 적에게、“푸른 뇌정”에 의한 전격을 흘린다。
	PRINTL 
	PRINTL 피뢰침에 의해 LOCK ON 중의 상대에 대해、전격 상성으로 데미지
	PRINTL 데미지는 LOCK 심도에 따라 변화한다
	PRINTL ※1체도 LOCK ON 되어있지 않은 경우는 사용할 수 없다。
;戦闘中はこっち表示
ELSE
	PRINTL 피뢰침에 의해 LOCK ON 중의 상대에 대해、전격 상성으로 데미지를 준다。
	PRINTL 데미지는 LOCK 심도에 따라 변화한다。
ENDIF
PRINTL ※이 스킬로 LOCK 심도가 3이 된 적을 쓰러뜨렸을 경우、쿠도스가 ３０ 가산된다
PRINTL ※이 스킬로 적을 2체 동시에 쓰러뜨렸을 경우、쿠도스가 １００ 가산된다
PRINTL ※이 스킬로 적을 3체 동시에 쓰러뜨렸을 경우、쿠도스가 ２５０ 가산된다

;계승에 필요한 부위
@계승부위_3636, ARG
RETURN 0

;사용조건
@SKILL_SPECIAL_ACTIONABLE_3636, ARG
#DIM DART_H

CALL TARGET_CHECK_3637, ARG
DART_H = SKILLGAGE_H_GET(ARG, [[스킬:피뢰침]])
IF DART_H > 0
	RETURN 1
ELSE
	RETURN 0
ENDIF
