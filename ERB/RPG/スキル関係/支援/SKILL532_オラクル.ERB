; オラクル
; 時間をおいて発動するようなスキルの作り方わかんないし即時発動
; 内容は無印FESとPごちゃまぜで8種類。ただし、MP(=SP)回復は露骨にいやらしく確率を1%未満に下げておく
; 原作のような行動消費しない回復支援行動でもないし別にどうでもいいのかもしれないけど
; MP回復引くまでリロードゲーになるようならMP回復削除か……
;18/07/30一人一日一回ならもう少し確率上げても大丈夫でしょう
;これで回復引くまで粘るぐらいならレベル上げて出直して来いって話ですわ

;스킬명
@SKILL_NAME_532, ARG
RESULTS = 오라클

;　명중
@SKILL_HITRATE_532, ARG
RETURN 0

;　위력
@SKILL_POWER_532, ARG
RETURN 0

;　최저공격횟수
@SKILL_MINATTACKNUMBER_532, ARG
RETURN 0

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_532, ARG
RETURN 0

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_532, ARG
RETURN 3
;소비없음＝1

;　코스트량
@SKILL_COST_532, ARG
RETURN 64

;　상성
@SKILL_TYPE_532, ARG
RETURN GET_TYPE_NUM("만능")
;相性なし

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_532, ARG
RETURN 2
;MAGIC

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_532, ARG
RETURN 0

;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_532
RETURN 2

;　사거리
@SKILL_RANGE_532, ARG
RETURN 3

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_532, ARG
RETURN 3

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_532
RETURN 4

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_532, ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_532
RETURN 5

@계승불능_532
RETURN 1
;계승불가

;속도보정
@SKILL_SPEED_532, ARG = -1
RETURN 0

;　구상의 표시와 그 타입
@SKILL_MESSAGE_532, ARG

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_532
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_532
RETURN 0
;변화불가

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_532, ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_532, ARG
RETURN 0

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_532, ARG
RETURN 0

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_532, ARG
RETURN 0

;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_532, ARG, ARG:1
;ARG:0…実行者のCHARA Num　ARG:1…대상の포지션Num

;------------------------------------------------
; CFLAG:ARG:오라클플래그 사용した日付
; LOCAL:0 대상Chara Num
; LOCAL:1 効果種別
;	0.아군全員の상태이상を完治,		 20.00%
;	1.아군全員に全カジャ効果,		 20.00%
;	2.아군全員にクリティカル率上昇,	 20.00%
;	3.아군全員に기합+集中,			 20.00%
;	4.아군全員のHPを全쾌,			 10.00%
;	5.아군全員のHPを1に,			  7.00%
;	6.아군全員のMPを全쾌,			  2.00%
;	7.아군全員のHPMPを全쾌,			  1.00%
; LOCAL:2 一時計算用
; LOCAL:7 カウンタ変数
; LOCAL:8 範囲Un
; LOCAL:9 範囲Up
;------------------------------------------------
; 初期化処理
VARSET LOCAL, 0, 0, 10
CFLAG:ARG:오라클플래그 = DAY
LOCAL:1 = RAND:10000
SELECTCASE LOCAL:1
	CASE IS < 100
		LOCAL:1 = 7
	CASE IS < 300
		LOCAL:1 = 6
	CASE IS < 1000
		LOCAL:1 = 5
	CASE IS < 2000
		LOCAL:1 = 4
	CASE IS < 4000
		LOCAL:1 = 3
	CASE IS < 6000
		LOCAL:1 = 2
	CASE IS < 8000
		LOCAL:1 = 1
	CASE IS < 10000
		LOCAL:1 = 0
	CASEELSE
		; ここに来ることは無いはずだが……
		LOCAL:1 = 5
ENDSELECT
; 範囲설정
IF CFLAG:ARG:PT플래그 == 0
	LOCAL:8 = 7
	LOCAL:9 = 17
ELSE
	LOCAL:8 = 1
	LOCAL:9 = 7
ENDIF
; 오라클本処理
FOR LOCAL:7, LOCAL:8, LOCAL:9
	LOCAL = POS(LOCAL:7)
	; 空き포지션スキップ
	SIF LOCAL < 0
		CONTINUE
	; DYINGスキップ
	SIF GET_STATE(CFLAG:LOCAL:상태이상) == "DYING"
		CONTINUE
	; 分岐
	SELECTCASE LOCAL:1
		CASE 0
			; 암리타から流用
			CALL SKILL_CURE_STATE_532, CFLAG:LOCAL:상태이상
			IF RESULT == 1
				PRINTFORML 　　TARGET:[{LOCAL:7,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　상태회복！
				CFLAG:LOCAL:상태이상 = 0
				CFLAG:LOCAL:상태이상경과턴수 = 0
			ENDIF
		CASE 1
			; 히트라이저から流用
			CFLAG:LOCAL:공격강화 += 4
			SIF CFLAG:LOCAL:공격강화 > 32
				CFLAG:LOCAL:공격강화 = 32
			CFLAG:LOCAL:방어강화 += 4
			SIF CFLAG:LOCAL:방어강화 > 32
				CFLAG:LOCAL:방어강화 = 32
			CFLAG:LOCAL:마법위력강화 += 4
			SIF CFLAG:LOCAL:마법위력강화 > 32
				CFLAG:LOCAL:마법위력강화 = 32
			CFLAG:LOCAL:명중강화 += 4
			SIF CFLAG:LOCAL:명중강화 > 32
				CFLAG:LOCAL:명중강화 = 32
			CFLAG:LOCAL:회피강화 += 4
			SIF CFLAG:LOCAL:회피강화 > 32
				CFLAG:LOCAL:회피강화 = 32
		CASE 2
			; レボリューションから流用
			CFLAG:LOCAL:크리티컬강화 += 8
			SIF CFLAG:LOCAL:크리티컬강화 > 8
				CFLAG:LOCAL:크리티컬강화 = 8
		CASE 3
			; 気合、コンセントレイトから流用
			CFLAG:LOCAL:기합플래그 = 1
			CFLAG:LOCAL:집중플래그 = 1
		CASE 4
			; メ・ディアラハンから流用
			PRINTFORM 　　TARGET:[{LOCAL:7,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
			LOCAL:2 = MAXBASE:LOCAL:ＨＰ
			PRINTFORML HP {LOCAL:2 - BASE:LOCAL:ＨＰ}회복
			CALL VAR_HP, LOCAL, LOCAL:2, 3
		CASE 5
			; メ・ディアラハンから流用
			PRINTFORM 　　TARGET:[{LOCAL:7,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
			LOCAL:2 = BASE:LOCAL:ＨＰ - 1
			PRINTFORML HP {LOCAL:2}데미지
			CALL VAR_HP, LOCAL, -LOCAL:2, 3
		CASE 6
			; メ・ディアラハンから流用
			PRINTFORM 　　TARGET:[{LOCAL:7,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
			LOCAL:2 = MAXBASE:LOCAL:ＭＰ
			PRINTFORML MP {LOCAL:2 - BASE:LOCAL:ＭＰ}회복
			CALL VAR_MP, LOCAL, LOCAL:2, 3
		CASE 7
			; メ・ディアラハンから流用
			PRINTFORM 　　TARGET:[{LOCAL:7,2}] %CALLNAME:LOCAL,20,LEFT%　>>>>>>　
			LOCAL:2 = MAXBASE:LOCAL:ＨＰ
			PRINTFORML HP {LOCAL:2 - BASE:LOCAL:ＨＰ}회복
			CALL VAR_HP, LOCAL, LOCAL:2, 3
			LOCAL:2 = MAXBASE:LOCAL:ＭＰ
			PRINTFORML MP {LOCAL:2 - BASE:LOCAL:ＭＰ}회복
			CALL VAR_MP, LOCAL, LOCAL:2, 3
	ENDSELECT
NEXT
PRINTL  
PRINTFORM \@ CFLAG:ARG:PT플래그 == 0 ? 적 # 아군 \@전체
SELECTCASE LOCAL:1
	CASE 0
		PRINTFORMW 의 상태이상이 회복되었다！
	CASE 1
		PRINTFORMW 의 모든 능력이 높아졌다！
	CASE 2
		PRINTFORMW 의 크리티컬률이 대폭 높아졌다！
	CASE 3
		PRINTFORMW 에 기합・집중 효과 부여！
	CASE 4
		PRINTFORMW 의 HP가 전부 회복되었다！
	CASE 5
		PRINTFORMW 의 HP가 1이 되었다！
	CASE 6
		PRINTFORMW 의 MP가 전부 회복되었다！
	CASE 7
		PRINTFORMW 의 HP・MP가 완쾌！
ENDSELECT
WAIT
;====================================================
;실행내용 여기까지
;====================================================

;　 설명문 표시
@SKILL_EXPLAIN_532
PRINTL 효과는 랜덤。이른바 팔푼테

;사용조건
@SKILL_SPECIAL_ACTIONABLE_532, ARG
; その日に使用してないこと
; (闘技場やイベント、依頼で使用していたら探索では使用できないし逆も然り)
SIF CFLAG:ARG:오라클플래그 == DAY
	RETURN 0
RETURN 1


;어떤 배드스테이터스를 고칠수 있는가
@SKILL_CURE_STATE_532, ARG
SELECTCASE GET_STATE(ARG)
	CASE "GOOD", "DYING"
		RETURN 0
	CASEELSE
		RETURN 1
ENDSELECT

