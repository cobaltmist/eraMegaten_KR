;通常공격
;스킬のテンプレートになっています

;스킬名
@SKILL_NAME_0,ARG
IF ARG < 0
	RESULTS = ＡＴＴＡＣＫ
	RETURN 0
ENDIF
IF EQUIP:ARG:검 > 0 && CFLAG:ARG:악마변신 == 0
	RESULTS = ＳＷＯＲＤ
ELSEIF TALENT:ARG:식노 > 0 && CFLAG:ARG:악마변신 == 1
	RESULTS = ＨＵＮＴ
ELSEIF TALENT:ARG:소인체형 && (TALENT:ARG:남자 == 0 || TALENT:ARG:오토코노코)
	SIF EQUIP:(ARG):악세사리 == 8190
		RESULTS = 퓻퓻한다
	SELECTCASE ABL:ARG:공격상성
		CASE 2
			RESULTS = 찰싹찰싹때린다
		CASE 3
			RESULTS = 아작아작깨문다
		CASE 0
			RESULTS = 쿡쿡찌른다
		CASEELSE
			RESULTS = ＡＴＴＡＣＫ
	ENDSELECT
ELSEIF TALENT:ARG:유아체형 && (TALENT:ARG:남자 == 0 || TALENT:ARG:오토코노코)
	SIF EQUIP:(ARG):악세사리 == 8190
		RESULTS = 퓻퓻한다
	SELECTCASE ABL:ARG:공격상성
		CASE 2
			RESULTS = 투닥투닥때린다
		CASEELSE
			RESULTS = ＡＴＴＡＣＫ
	ENDSELECT
ELSE
	RESULTS = ＡＴＴＡＣＫ
ENDIF

;　명중
@SKILL_HITRATE_0, ARG = -1
LOCAL = 100
SIF ARG == -1
	RETURN 100
;IF ABL:ARG:종족 != 0 && NO:ARG != 3501 && NO:ARG != 3502
SIF EQUIP:(ARG):악세사리 == 8190
	RETURN LOCAL
SELECTCASE ABL:ARG:사거리
	CASE 2
		LOCAL -= 5
	CASE 3
		LOCAL -= 15
ENDSELECT
;ENDIF
RETURN LOCAL

;　威힘
@SKILL_POWER_0, ARG = -1
LOCAL = 100
SIF ARG == -1
	RETURN 100
;IF ABL:ARG:종족 != 0 && NO:ARG != 3501 && NO:ARG != 3502
SELECTCASE ABL:ARG:공격범위
	CASE 2
		LOCAL -= 15
	CASE 3
		LOCAL -= 30
ENDSELECT
SIF HAVE_SKILL(ARG, 2468)
	LOCAL = LOCAL * 7/10
;ENDIF
RETURN LOCAL

;　흡수率
@SKILL_HP_ABSORBER_0, ARG
IF HAVE_SKILL(ARG, 2468)
	SELECTCASE ABL:ARG:공격범위
		CASE 2
			RETURN 15
		CASE 3
			RETURN 10
	ENDSELECT
	RETURN 25
ENDIF
RETURN 0

;　최저공격횟수
@SKILL_MINATTACKNUMBER_0,ARG
RETURN ABL:ARG:최저공격횟수

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_0,ARG
SIF HAVE_SKILL(ARG, [[스킬:이신의 흔적]])
	RETURN ABL:ARG:최대공격횟수 + 1
RETURN ABL:ARG:최대공격횟수

;　ＨＰ2を支払うかＭＰ3を支払うか
@SKILL_COSTTYPE_0, ARG
RETURN 1
;소비なし＝1

;　コストの量
@SKILL_COST_0,ARG
RETURN 0

;　相性
@SKILL_TYPE_0,ARG
SIF ARG < 0
	RETURN 0
SIF EQUIP:(ARG):악세사리 == 8190
	RETURN 13
RETURN ABL:ARG:공격상성

;　1EXTRAか2MAGICか
@SKILL_DECIDE_TYPE_0
RETURN 0
;どちらでもない

;　1物理데미지で計算するか2魔法데미지で計算するか
@SKILL_DAMAGETYPE_0, ARG
RETURN 1


;　대상が1적か2아군か3場전체か4特殊か
@SKILL_TARGET_0
RETURN 1

;　사거리
@SKILL_RANGE_0,ARG
SIF EQUIP:(ARG):악세사리 == 8190
	RETURN 3
RETURN ABL:ARG:사거리

;　範囲　11체　21열　3전체　4　적아군전체
@SKILL_SPHERE_0,ARG
RETURN ABL:ARG:공격범위

;　 効果の타입　1공격、2회복、3상태変化、4特殊、5상태회복
@SKILL_EFECT_0
RETURN 1

;　 特殊処理の有無、及び処理内容
@SKILL_SPECIALEFECT_0,ARG
;ARG = 1だった場合は処理の有無を返します
SIF ARG == 1
	RETURN 0
;以下に特殊な処理を記述します

;계승타입
@SKILL_SUCCESSION_TYPE_0
RETURN 0
;継承不可

;속도보정
@SKILL_SPEED_0, ARG = -1
RETURN 0

;　구상の표시とその타입
@SKILL_MESSAGE_0,ARG

;전투時以外に사용できるか
@SKILL_ACTIONABLE_FIELD_0
RETURN 0

;스킬変化？用랭크
@SKILL_RANK_0
RETURN 0
;変化不可

;テンプレート用、추가효과シリーズ
;추가효과스테이터스
@SKILL_ADDTIONAL_STATE_0,ARG
RETURN ABL:ARG:추가효과

;추가효과상성
@SKILL_ADDTIONAL_TYPE_0,ARG
RETURN ABL:ARG:추가효과상성

;추가효과기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_0,ARG
RETURN ABL:ARG:추가효과명중률

;추가효과부여확률上限
@SKILL_ADDTIONAL_MAX_RATE_0,ARG
RETURN ABL:ARG:추가효과최대명중률
;====================================================
;스킬及び行動の実行
;====================================================
@ACTION_0,ARG,ARG:1
LOCAL = ARG:1
IF ARG:1 == 0
	PRINTW 잘못된 장소를 공격하려고 했습니다
	RETURN 0
ENDIF	
IF ARG:1 < 17 
	LOCAL = POS(ARG:1)
	CALL ATTACK_SINGLE,ARG,LOCAL,0,1
ELSE
	CALL ATTACK_FIELD,ARG,ARG:1,0,1
ENDIF
;인간のみ무기の熟練度上昇
IF ABL:ARG:종족 == 0 || ABL:ARG:종족 == 36
	TRYCCALLFORM 검타입_{EQUIP:ARG:검}
		IF RESULT > 0 && RESULT < 12
			CFLAG:ARG:(1049 + RESULT) += (1 + RAND:5) * EQUIP:ARG:악세사리 == 5207 ? 2 # 1
			CFLAG:ARG:(1049 + RESULT) = MIN(1000,CFLAG:ARG:(1049 + RESULT))
		ENDIF
	CATCH
	ENDCATCH
ENDIF	





;====================================================
;実行内容ここまで
;====================================================

;　 説明文の표시
@SKILL_EXPLAIN_0
PRINTL 무기나 육체 등을 구사하여 공격한다。

;사용조건
@SKILL_SPECIAL_ACTIONABLE_0,ARG
RETURN 1
;とくにない

