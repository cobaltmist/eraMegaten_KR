;通常공격
;스킬のテンプレートになっています

;스킬명
@SKILL_NAME_0,ARG
IF ARG < 0
	RESULTS = ＡＴＴＡＣＫ
	RETURN 0
ENDIF
IF EQUIP:ARG:검 > 0 && CFLAG:ARG:악마변신 == 0
	RESULTS = ＳＷＯＲＤ
ELSEIF TALENT:ARG:식노 > 0 && CFLAG:ARG:악마변신 == 1
	RESULTS = ＨＵＮＴ
ELSEIF TALENT:ARG:소인체형 && (TALENT:ARG:남자 == 0 || TALENT:ARG:오토코노코)
	SIF EQUIP:(ARG):악세서리 == 8190
		RESULTS = 퓻퓻한다
	SELECTCASE ABL:ARG:공격상성
		CASE 2
			RESULTS = 찰싹찰싹때린다
		CASE 3
			RESULTS = 아작아작깨문다
		CASE 0
			RESULTS = 쿡쿡찌른다
		CASEELSE
			RESULTS = ＡＴＴＡＣＫ
	ENDSELECT
ELSEIF TALENT:ARG:유아체형 && (TALENT:ARG:남자 == 0 || TALENT:ARG:오토코노코)
	SIF EQUIP:(ARG):악세서리 == 8190
		RESULTS = 퓻퓻한다
	SELECTCASE ABL:ARG:공격상성
		CASE 2
			RESULTS = 투닥투닥때린다
		CASEELSE
			RESULTS = ＡＴＴＡＣＫ
	ENDSELECT
ELSE
	RESULTS = ＡＴＴＡＣＫ
ENDIF

;　명중
@SKILL_HITRATE_0, ARG = -1
LOCAL = 100
SIF ARG == -1
	RETURN 100
;IF ABL:ARG:종족 != 0 && NO:ARG != 3501 && NO:ARG != 3502
SIF EQUIP:(ARG):악세서리 == 8190
	RETURN LOCAL
SELECTCASE ABL:ARG:사거리
	CASE 2
		LOCAL -= 5
	CASE 3
		LOCAL -= 15
ENDSELECT
;ENDIF
RETURN LOCAL

;　위력
@SKILL_POWER_0, ARG = -1
LOCAL = 100
SIF ARG == -1
	RETURN 100
;IF ABL:ARG:종족 != 0 && NO:ARG != 3501 && NO:ARG != 3502
SELECTCASE ABL:ARG:공격범위
	CASE 2
		LOCAL -= 15
	CASE 3
		LOCAL -= 30
ENDSELECT
SIF HAVE_SKILL(ARG, 2468)
	LOCAL = LOCAL * 7/10
;ENDIF
RETURN LOCAL

;　흡수率
@SKILL_HP_ABSORBER_0, ARG
IF HAVE_SKILL(ARG, 2468)
	SELECTCASE ABL:ARG:공격범위
		CASE 2
			RETURN 15
		CASE 3
			RETURN 10
	ENDSELECT
	RETURN 25
ENDIF
RETURN 0

;　최저공격횟수
@SKILL_MINATTACKNUMBER_0,ARG
RETURN ABL:ARG:최저공격횟수

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_0,ARG
SIF HAVE_SKILL(ARG, [[스킬:이신의흔적]])
	RETURN ABL:ARG:최대공격횟수 + 1
RETURN ABL:ARG:최대공격횟수

;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_0, ARG
RETURN 1
;소비없음＝1

;　코스트량
@SKILL_COST_0,ARG
RETURN 0

;　상성
@SKILL_TYPE_0,ARG
SIF ARG < 0
	RETURN 0
SIF EQUIP:(ARG):악세서리 == 8190
	RETURN 13
RETURN ABL:ARG:공격상성

;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_0
RETURN 0
;どちらでもない

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_0, ARG
RETURN 1


;　대상　1 적　2 아군　3 양쪽전체　4 특수
@SKILL_TARGET_0
RETURN 1

;　사거리
@SKILL_RANGE_0,ARG
SIF EQUIP:(ARG):악세서리 == 8190
	RETURN 3
RETURN ABL:ARG:사거리

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_0,ARG
RETURN ABL:ARG:공격범위

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_0
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_0,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
@SKILL_SUCCESSION_TYPE_0
RETURN 0
;계승불가

;속도보정
@SKILL_SPEED_0, ARG = -1
RETURN 0

;　구상의 표시와 그 타입
@SKILL_MESSAGE_0,ARG

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_0
RETURN 0

;스킬변화？용 랭크
@SKILL_RANK_0
RETURN 0
;변화불가

;템플릿용、추가효과 시리즈
;추가효과 스테이터스
@SKILL_ADDTIONAL_STATE_0,ARG
RETURN ABL:ARG:추가효과

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_0,ARG
RETURN ABL:ARG:추가효과상성

;추가효과 기본부여확률
@SKILL_ADDTIONAL_BASE_RATE_0,ARG
RETURN ABL:ARG:추가효과명중률

;추가효과 부여확률상한
@SKILL_ADDTIONAL_MAX_RATE_0,ARG
RETURN ABL:ARG:추가효과최대명중률
;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_0,ARG,ARG:1
LOCAL = ARG:1
IF ARG:1 == 0
	PRINTW 잘못된 장소를 공격하려고 했습니다
	RETURN 0
ENDIF	
IF ARG:1 < 17 
	LOCAL = POS(ARG:1)
	CALL ATTACK_SINGLE,ARG,LOCAL,0,1
ELSE
	CALL ATTACK_FIELD,ARG,ARG:1,0,1
ENDIF
;인간のみ무기の熟練度上昇
IF ABL:ARG:종족 == 0 || ABL:ARG:종족 == 36
	TRYCCALLFORM 검타입_{EQUIP:ARG:검}
		IF RESULT > 0 && RESULT < 12
			CFLAG:ARG:(1049 + RESULT) += (1 + RAND:5) * (EQUIP:ARG:악세서리 == 5207 ? 2 # 1)
			CFLAG:ARG:(1049 + RESULT) = MIN(1000,CFLAG:ARG:(1049 + RESULT))
		ENDIF
	CATCH
	ENDCATCH
ENDIF	





;====================================================
;실행내용 여기까지
;====================================================

;　 설명문 표시
@SKILL_EXPLAIN_0
PRINTL 무기나 육체 등을 구사하여 공격한다。

;사용조건
@SKILL_SPECIAL_ACTIONABLE_0,ARG
RETURN 1
;とくにない

