@EVENT_93_5

CUSTOMDRAWLINE =

	PRINTL
SETCOLOR 0xdc7b8c
	PRINTW 「노래에 춤에 정처없이 오가며 지금껏 걸어왔던 둘만의 길」
RESETCOLOR
SETCOLOR 0x815ca1
	PRINTW 「하지만 나도 찾아내고 말았지, 저 밤하늘로 솟아오른 하나의 길！
	PRINTW 　99기생, 이스루기 후타바！기합 넣고서, 냅다 달려가겠습니다！」
RESETCOLOR
SETCOLOR 0xdc7b8c
	PRINTW 「99기생, 하나야기 카오루코！마지막까지 어울려주셔야겠소！」
RESETCOLOR
	PRINTL
	PRINTW ……이미 전투는 시작되어 있었다.
	PRINTL
	PRINTW 「내가 이기면, 이번에는 카오루코가 나를 돌보도록 해주겠어！
	PRINTW 　과자도 사오고, 다리도 주무르고, 바이크로 학교까지 데려다주게 할테니까앗！」
	PRINTL
	PRINTW 이스루기 후타바, 할버드를 든 무대소녀.
	PRINTW 체격은 작지만, 허스키한 목소리와 함께 장대한 무기를 휘두르고 있었다.
	PRINTL
	PRINTW 「그라믄、나가 이김, 같이 교토 돌아가는 거라！」
	PRINTL
	PRINTW 하나야기 카오루코, 장도를 든 무대소녀.
	PRINTW 몹시 강한 교토말씨와 함께, 나기나타를 휘두르며 후타바의 격투에 어울리고 있다.
	PRINTL
	PRINTW 「할 수 있다면 해봐！
	PRINTW 　카오루코, 네 근성―― 내가 때려 바로잡아주마！」
	PRINTL
	PRINTW 두 사람은 전혀 이쪽을 보고 있지 않지만……。
	PRINTW 길이 났다는 건, 옆을 쳐도 된다는 뜻이리라.
	PRINTW 당신은 무기를 갖추고, 두 사람에게 덤벼들었다.
	PRINTL
	PRINTL 무대소녀 이스루기 후타바 가 나타났다！
	PRINTW 무대소녀 하나야기 카오루코 가 나타났다！
	PRINTL
SETCOLOR 0xdc7b8c
	PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
	PRINTFORML ┃후에？　　　　　　　　　　　　　　　　　　　┃
	PRINTFORMW ┗━━━━━━━━━━━━━━━━━━━━━━┛
SETCOLOR 0x815ca1
	PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
	PRINTFORML ┃카오루코！　위험해앳！　　　　　　　　　　　　┃
	PRINTFORML ┃……칫. 너ㅡ 무대장치는 아닌 것 같은데？┃
	PRINTFORML ┃도중참가도 아닌 모양이고　　　　　┃
	PRINTFORMW ┗━━━━━━━━━━━━━━━━━━━━━━┛
RESETCOLOR
	PRINTL

	FLAG:도주불가플래그 = 1
	FLAG:회화불능플래그 = 1
	FLAG:선제기습취소 = 1
	
	;※方바늘　타격・충격、ちょっと질풍・주살
	;最初：双葉はんを魔法でシメとけばＯＫ。即死だけ注意
	;中盤：데・카쟈持参して충격対策でＦＡ
	;最後：하이퍼카운터対策＆ぶぶ漬けが息切れしない
	;ぶぶ漬けは레벨の影響が大きいのでゲーム後半まで放っておくと苦労する
	
	;6戦눈　★★★★★
	IF 이벤트플래그:93:0 > 6
		LOCAL:99 = 6
		LOCAL:97 = 3
	;5戦눈　★★★★☆
	ELSEIF 이벤트플래그:93:0 > 5
		LOCAL:99 = 5
		LOCAL:97 = 3
	;4戦눈　★★★☆☆
	ELSEIF 이벤트플래그:93:0 > 4
		LOCAL:99 = 4
		LOCAL:97 = 2
	;3戦눈　★★★☆☆
	ELSEIF 이벤트플래그:93:0 > 3
		LOCAL:99 = 3
		LOCAL:97 = 2
	;2戦눈　★★☆☆☆
	ELSEIF 이벤트플래그:93:0 > 2
		LOCAL:99 = 2
		LOCAL:97 = 1
	;1戦눈　★☆☆☆☆
	ELSEIF 이벤트플래그:93:0 > 1
		LOCAL:99 = 1
		LOCAL:97 = 1
	ENDIF
	
	;레벨は自軍최대値（최저値15）
	LOCAL:1 = 0
	FOR LOCAL , 0 , CHARANUM
		SIF CFLAG:LOCAL:PT플래그 == 0 || TALENT:LOCAL:비전투원
			CONTINUE
		LOCAL:98 = MAX(LOCAL:98 , BASE:LOCAL:LV)
	NEXT
	SIF LOCAL:98 < 10
		LOCAL:98 = 10
	
		
	;- SET:후타바
		CALL SET_ENEMY,9,[[キャラ:후타바（무대소녀）]],LOCAL:98,1,LOCAL:97
		CALLNAME:(CHARANUM-1) = 후타바
		;CFLAG:(CHARANUM-1):ＨＰ보정 = MAXBASE:ARG:ＨＰ*3
		CFLAG:(CHARANUM-1):ＭＰ보정 += 5000
		CFLAG:(CHARANUM-1):행동횟수 = 2
		ABL:(CHARANUM-1):공격상성 = 0
		ABL:(CHARANUM-1):사거리 = 2
		CALL SYNC_STATUS,(CHARANUM-1)
		ABL:(CHARANUM-1):스킬1 = [[스킬:소닉펀치]]
		ABL:(CHARANUM-1):스킬2 = [[스킬:물어-뜯기]]
		ABL:(CHARANUM-1):스킬3 = [[스킬:마구날뛰기]]
		ABL:(CHARANUM-1):스킬4 = [[스킬:자애의맹반격（보스용）]]
		ABL:(CHARANUM-1):스킬6 = 0
		ABL:(CHARANUM-1):스킬7 = 0
		ABL:(CHARANUM-1):스킬8 = 0
		;小柄体系を外さないとポカポカ叩き始めるから……
		TALENT:(CHARANUM-1):유아체형 = 0
		;초인扱いに
		ABL:(CHARANUM-1):종족 = 43
		CSTR:(CHARANUM-1):종족명 = 무대소녀
		LOCAL = CHARANUM-1
		CALL HEALTH_CHARA,(CHARANUM-1)

		;強すぎるんで出禁
		;ABL:(CHARANUM-1):스킬4 = [[스킬:열풍격]]


	;- SET:카오루코
		CALL SET_ENEMY,14,[[キャラ:카오루코]],LOCAL:98,1,LOCAL:97
		;CFLAG:(CHARANUM-1):ＨＰ보정 = MAXBASE:ARG:ＨＰ*2
		;ぶぶ漬けはＭＰ보정無しで
		CFLAG:(CHARANUM-1):행동횟수 = 2
		ABL:(CHARANUM-1):공격상성 = 0
		ABL:(CHARANUM-1):사거리 = 2
		CALL SYNC_STATUS,(CHARANUM-1)
		ABL:(CHARANUM-1):스킬1 = [[스킬:용분의춤]]
		ABL:(CHARANUM-1):스킬2 = [[스킬:타루・카쟈]]
		ABL:(CHARANUM-1):스킬3 = [[스킬:갈라]]
		ABL:(CHARANUM-1):스킬4 = [[스킬:저주의언령]]
		ABL:(CHARANUM-1):스킬5 = [[스킬:마하・갈라]]
		ABL:(CHARANUM-1):스킬6 = [[스킬:기공(대)]]
		ABL:(CHARANUM-1):스킬7 = 0
		ABL:(CHARANUM-1):스킬8 = 0
		;초인扱いに
		ABL:(CHARANUM-1):종족 = 43
		CSTR:(CHARANUM-1):종족명 = 무대소녀
		LOCAL = CHARANUM-1
		CALL HEALTH_CHARA,(CHARANUM-1)

		;6戦눈　★★★★★
		IF 이벤트플래그:93:0 > 6
			;ギミックで対応
		;5戦눈　★★★★★
		ELSEIF 이벤트플래그:93:0 > 5
			;素
		;4戦눈　★★★★☆
		ELSEIF 이벤트플래그:93:0 > 4
			ABL:(CHARANUM-1):스킬1 = [[스킬:타루・카쟈]]
			ABL:(CHARANUM-1):스킬2 = [[스킬:라쿠・카쟈]]
			;행동횟수1
			CFLAG:(CHARANUM-2):행동횟수 = 1
			;행동횟수2
			CFLAG:(CHARANUM-1):행동횟수 = 2
		;3戦눈　★★★☆☆
		ELSEIF 이벤트플래그:93:0 > 3
			;행동횟수1
			CFLAG:(CHARANUM-2):행동횟수 = 1
		
			;행동횟수１・뒈져아이가・타루카쟈以外削除。ひたすら매도する京都人
			CFLAG:(CHARANUM-1):행동횟수 = 1
			ABL:(CHARANUM-1):스킬1 = [[스킬:저주의언령]]
			ABL:(CHARANUM-1):스킬2 = [[스킬:타루・카쟈]]
			ABL:(CHARANUM-1):스킬3 = 0
			ABL:(CHARANUM-1):스킬4 = 0
			ABL:(CHARANUM-1):스킬5 = 0
		;2戦눈　★★☆☆☆
		ELSEIF 이벤트플래그:93:0 > 2
			;행동횟수1・반격削除
			CFLAG:(CHARANUM-2):행동횟수 = 1
			ABL:(CHARANUM-2):스킬4 = 0
		
			;행동횟수１・死ねどす以外削除。ひたすら매도する京都人
			CFLAG:(CHARANUM-1):행동횟수 = 1
			ABL:(CHARANUM-1):스킬1 = [[스킬:저주의언령]]
			ABL:(CHARANUM-1):스킬2 = 0
			ABL:(CHARANUM-1):스킬3 = 0
			ABL:(CHARANUM-1):스킬4 = 0
			ABL:(CHARANUM-1):스킬5 = 0
			
		;1戦눈　★☆☆☆☆
		ELSEIF 이벤트플래그:93:0 > 1
			;행동횟수1・소닉펀치、물어-뜯기以外削除
			CFLAG:(CHARANUM-2):행동횟수 = 1
			ABL:(CHARANUM-2):스킬3 = 0
			ABL:(CHARANUM-2):스킬4 = 0
			ABL:(CHARANUM-2):스킬5 = 0
			
			;행동횟수１・스킬全消し。頑張って素殴りするぶぶ漬け
			CFLAG:(CHARANUM-1):행동횟수 = 1
			ABL:(CHARANUM-1):스킬1 = 0
			ABL:(CHARANUM-1):스킬2 = 0
			ABL:(CHARANUM-1):스킬3 = 0
			ABL:(CHARANUM-1):스킬4 = 0
			ABL:(CHARANUM-1):스킬5 = 0
		ENDIF
		
		CALL SYNC_STATUS,(CHARANUM-2)
		CALL SYNC_STATUS,(CHARANUM-1)
		CALL HEALTH_CHARA,(CHARANUM-2)
		CALL HEALTH_CHARA,(CHARANUM-1)
		
		;解析度MAX処理
		CALL INCREASE_ANALYZE([[キャラ:후타바（무대소녀）]],1000)
		CALL INCREASE_ANALYZE([[キャラ:카오루코]],1000)

	;ふたかお復活フラグ
	SETBIT 이벤트플래그:93:15, 1
	CALL BATTLE_START
	CLEARBIT 이벤트플래그:93:15, 1

	CUSTOMDRAWLINE =

	PRINTW 「제길, 미안해, 카오루코……」
	PRINTW 「당신！　우리 후타바 양에게 무슨 짓이고！
	PRINTW 　추접다, 손놀림이 추접다카이. 드러븐 손으로 만지지 마라！」
	;最初のver、ナチュラルに薄い本発言が出ておりましたが、
	;ぶぶ漬けはそんなこと言わない（戒め）
	;この場を借りてお詫びと訂正を致します。
	PRINTL
	PRINTW 소리치는 교토인을 달래면서, 당신은 두 사람에게 악마퇴치를 도울 것을 청했다.
	PRINTL
	PRINTW 「……후우, 당신 있재, 잠꼬대 엉간하네？
	PRINTW 　세이쇼는 이제 없다？　우리들이 반짝임의 잔향？
	PRINTW 　모지리같은 삼류소설, 참으로 고맙심더.
	PRINTW 　후타바 양, 이런 거 치아뿌고, 퍼뜩 결착내죠.」
	PRINTW 「……카오루코, 침착하게 들어줘.」
	PRINTW 「후에？」
	PRINTW 「나도, 너도…… 몸, 투명해지고 있어.」
	PRINTW 「교에에에에에에！？」
	PRINTL
	PRINTW 무대소녀　이스루기 후타바가 동료가 되었다.
	PRINTW 무대소녀　하나야기 카오루코가 동료가 되었다.
	PRINTL
	PRINTW 「이것도 저것도, 후타바 양이 잘못한 기라！
	PRINTW 　후타바 양이 나를 말리니께！」
	PRINTW 「――미안, 그럴지도 몰라.
	PRINTW 　거기서, 너를, 말리지 않았다면.
	PRINTW 　악마와 싸우는 건 나 뿐이었을지도 몰라.」
	PRINTW 「우……
	PRINTW 　뭐꼬, 그기.
	PRINTW 　그런 표정으로, 나를 악인으로 하고 싶은 기가！？」
	PRINTW 「정말, 미안해. 이럴 리는 없었어. 그도 그럴게 넌……」
	PRINTW 「치라 안카나！？」
	PRINTL
	PRINTW 당신은 어색한 듯 말이 없어진 두 사람을 데리고 사무소로 돌아왔다……
	PRINTL

	CALL ADD_NEW_COMPANION,5318,200
	IF RESULT > 0
		CFLAG:(RESULT:1):전투참가불가능 = -1
		CALLNAME:(RESULT:1) = 후타바
	ENDIF

	CALL ADD_NEW_COMPANION,5319,200
	SIF RESULT > 0
		CFLAG:(RESULT:1):전투참가불가능 = -1

	이벤트플래그:93:0 += 1
	SIF 이벤트플래그:93:5 == 0
		SETBIT 이벤트플래그:93:5, 1
	SIF 이벤트플래그:93:6 == 0
		SETBIT 이벤트플래그:93:6, 1
	CALL EVENT_93_TURN_END


@EVENT_BATTLE_ACTEND_EVENT_93_5
;最終戦かつぶぶ漬けが先に死んだ
;双葉はん행동횟수爆上げ
IF 이벤트플래그:93:0 > 6 && BASE:(CHARANUM-1):ＨＰ < 1 && 이벤트플래그:93:5 == 0
	PRINTFORML 
SETCOLOR 0x815ca1
	PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
	PRINTFORML ┃그 녀석은, 약속을 했어.　　　　　　　　　　┃
	PRINTFORML ┃나에게, 가장, 빛나는 것을,　　　　　┃
	PRINTFORML ┃가장 먼저 보여준다고. 그러니까――！　　　　　┃
	PRINTFORML ┗━━━━━━━━━━━━━━━━━━━━━━┛
RESETCOLOR
	PRINTFORMW ＞후타바의 행동횟수가 증가했다.
	CFLAG:(CHARANUM-2):행동횟수 = 3
	CSTR:(CHARANUM-2):사고패턴 = 激おこ双葉はん
	
	SETBIT 이벤트플래그:93:5, 1
	SETBIT 이벤트플래그:93:6, 1
;最終戦かつ双葉が先に死んだ
;花柳香子스킬추가＋ＭＰ爆上げ＆회복
;만능相性の상태이상を撒き散らすようになるので、死ぬ
ELSEIF 이벤트플래그:93:0 > 6 && BASE:(CHARANUM-2):ＨＰ < 1 && 이벤트플래그:93:6 == 0
	PRINTFORML 
SETCOLOR 0xdc7b8c
	PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
	PRINTFORML ┃후타바 양은…… 말해주었소.　　　　┃
	PRINTFORML ┃나가 가장 빛나는 모습을 가장 먼저 봐주겠다고┃
	PRINTFORML ┃――봐두그라. 내도, 무대소녀다.　　　┃
	PRINTFORML ┗━━━━━━━━━━━━━━━━━━━━━━┛
RESETCOLOR
	PRINTFORMW ＞카오루코의 분위기가 일변한다.
	PRINTFORMW ＞카오루코의 MP가 완전회복되었다！
	
	ABL:(CHARANUM-1):스킬1 = [[스킬:저주의언령]]
	ABL:(CHARANUM-1):스킬2 = [[스킬:저주의언령]]
	ABL:(CHARANUM-1):스킬3 = [[스킬:갈라]]
	ABL:(CHARANUM-1):스킬4 = [[스킬:저주의언령]]
	ABL:(CHARANUM-1):스킬5 = [[스킬:마하・갈라]]
	ABL:(CHARANUM-1):스킬6 = [[스킬:기공(대)]]
	
	CSTR:(CHARANUM-1):사고패턴 = ぶぶ漬けでもお食べやす
	
	ABL:(CHARANUM-1):가창기능 = 10
	CFLAG:(CHARANUM-1):행동횟수 = 3
	MAXBASE:(CHARANUM-1):ＭＰ = 99999
	BASE:(CHARANUM-1):ＭＰ = 99999
	SETBIT 이벤트플래그:93:5, 1
	SETBIT 이벤트플래그:93:6, 1
ENDIF


@SET_ACTION_激おこ双葉はん, ARG
#DIM ガード殺すマン
#DIM ループ用
;殺すマンゼロクリア
ガード殺すマン = 0

IF CFLAG:ARG:행동횟수 == 0
	FOR ループ用, 1, 7
		;ガードしている誰かを探してガード殺すマンを保存
		IF POS(ループ用) >= 0 && GET_STATE(CFLAG:POS(ループ用):상태이상) != "DYING" && CFLAG:POS(ループ用):방어플래그
			ガード殺すマン = ループ用
			BREAK
		ENDIF
	NEXT
	;誰もガードしていないのなら마구날뛰기
	IF ガード殺すマン == 0
		CFLAG:ARG:입력행동 = [[스킬:마구날뛰기]]
	;ガードしてたらそいつを殺す
	ELSE
		SETCOLOR 0x815ca1
		;みんな大好きバルバドス
		PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
		PRINTFORML ┃가드 같은 거…… 하는 게 아니야！　　　　　　┃
		PRINTFORMW ┗━━━━━━━━━━━━━━━━━━━━━━┛
		RESETCOLOR
		CFLAG:ARG:타겟 = ガード殺すマン
		CFLAG:ARG:입력행동 = [[스킬:방어부수기]]
	ENDIF
	
ELSEIF CFLAG:ARG:행동한횟수 == 1
	CFLAG:ARG:입력행동 = [[스킬:질풍격]]
	
ELSE
	IF CFLAG:ARG:행동한횟수 == 2 && RAND:2 == 0
		SETCOLOR 0x815ca1
		PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
		PRINTFORML ┃호흡을 가다듬을게！　　　　　　　　　　┃
		PRINTFORMW ┗━━━━━━━━━━━━━━━━━━━━━━┛
		RESETCOLOR
		CFLAG:ARG:입력행동 = [[스킬:기합]]
	ELSE
		CALL 랜덤행동 , ARG
	ENDIF
	
ENDIF


@SET_ACTION_ぶぶ漬けでもお食べやす, ARG
#DIM 不健康カウント
#DIM ループ用


;奇数ターンは질풍物理
IF CFLAG:ARG:행동한횟수 == 0 && FLAG:경과턴수 % 2 == 0
	SETCOLOR 0xdc7b8c
	PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
	PRINTFORML ┃자, 대담하게～♪　　　　　　　　　　　　　　┃
	PRINTFORMW ┗━━━━━━━━━━━━━━━━━━━━━━┛
	RESETCOLOR
	CFLAG:ARG:입력행동 = [[스킬:전용기2]]
;偶数ターンは텐타라후互換
ELSEIF CFLAG:ARG:행동한횟수 == 0
	SETCOLOR 0xdc7b8c
	PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
	PRINTFORML ┃자아, 우아하게…… 갈끼라예？　　　　　　　　┃
	PRINTFORMW ┗━━━━━━━━━━━━━━━━━━━━━━┛
	RESETCOLOR
	CFLAG:ARG:입력행동 = [[스킬:전용기1]]
	
	
ELSEIF CFLAG:ARG:행동한횟수 == 1
	CFLAG:ARG:입력행동 = [[스킬:바리언트댄스]]
	
ELSE
	不健康カウント = 0
	FOR ループ用, 1, 7
		;상태이상（死亡・魅了以外）をカウント
		IF POS(ループ用) >= 0 && GET_STATE(CFLAG:POS(ループ用):상태이상) != "DYING" && GET_STATE(CFLAG:POS(ループ用):상태이상) != "GOOD"
			不健康カウント ++
		ENDIF
	NEXT
	;バステを溜めると壊滅
	IF CFLAG:ARG:행동한횟수 == 2 && 不健康カウント > 3
		;전체確定魅了
		SETCOLOR 0xdc7b8c
		PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
		PRINTFORML ┃선 모습은 작약, 앉은 모습은 모란, 춤추는 모습은 월하미인……┃
		PRINTFORML ┃99기생, 하나야기 카오루코！　　　　　　　　　　　　┃
		PRINTFORML ┃마지막까지 어울려주셔야겠소！　　　　　　　　　┃
		PRINTFORML ┗━━━━━━━━━━━━━━━━━━━━━━┛
		RESETCOLOR
		PRINTFORMW ＞아차, 디버프를 너무 많이 쌓고 말았다！
		
		FOR ループ用, 1, 7
			;死亡者以外を強制的にCHARMへ
			IF POS(ループ用) >= 0 && GET_STATE(CFLAG:POS(ループ用):상태이상) != "DYING"
				;実際にHAPPYする処理
				PRINTFORML 　　TARGET:[{ループ用,2}] %CALLNAME:POS(ループ用),20,LEFT%　>>>>>>　ほな、『CHARM』どす%UNICODE(0x2661)%
				CFLAG:POS(ループ用):상태이상 = GET_STATE_NUM("CHARM")
				CFLAG:POS(ループ用):상태이상경과턴수 = 0
				CALL SYNC_STATUS,POS(ループ用)
			ENDIF
		NEXT
		WAIT
		
	ELSEIF CFLAG:ARG:행동한횟수 == 2 && RAND:10 == 0
		SETCOLOR 0xdc7b8c
		PRINTFORML ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
		PRINTFORML ┃내의 이름은 하나야기 카오루코… 머지않아 세계를 취할 여자라！┃
		PRINTFORMW ┗━━━━━━━━━━━━━━━━━━━━━━┛
		RESETCOLOR
		CFLAG:ARG:입력행동 = [[스킬:스마일차지]]
		IF 不健康カウント > 1
			PRINTFORML ＞어서 디버프를 해제하지 않으면……！
			FORCEWAIT
		ENDIF
	ELSE
		CALL 랜덤행동 , ARG
		IF 不健康カウント > 1
			PRINTFORML ＞어서 디버프를 해제하지 않으면……！
			FORCEWAIT
		ENDIF
	ENDIF
	
ENDIF

;깨달음書　적아군の판정は
;CFRAGのPT플래그
;で出来る
