;====================
;更新履歴
;2021/03/18　テキスト試作
;2022/06/09　ほぼ完成
;2022/06/11　Ver.1.0投稿
;====================
;関連フラグリスト
;====================
;이벤트플래그:55:0 本イベントの段階フラグ。
;이벤트플래그:55:1 イベント1「これが私の救世主様」を踏んだ日付。
;이벤트플래그:55:2 イベント2「審判の日」を踏んだ日付。
;이벤트플래그:55:3 イベント3「女々しい野郎共の宴」を踏んだ日付。
;이벤트플래그:55:4 戦闘フラグ。1以降、フラグ番号で何戦目かを管理。0が1戦目でいいやんけ？そうだね。
;이벤트플래그:55:5 ICANFLYフラグ。1がハードモード（火炎ガーキル状態）をはじめ、選択肢で何を選んだかを参照する。
;이벤트플래그:55:6 御使い戦のコメントの進行フラグ。
;이벤트플래그:55:7 御使い戦で御使いの討伐数カウント。カウントはターン終了時に増える。御使いの討伐数が増えると行動回数と攻撃回数が増える。
;이벤트플래그:55:8 めしあちゃんのデバフが御使いに効いているフラグ。ターン終了時に御使いがHAPPYだとオン
;이벤트플래그:55:9 イベント6「意外なタレコミ」での情報提供の有無。　0：イベント踏んでない　1：情報提供拒否　2：情報提供あり。あとイベントの最初のNPCの判定にも使ってる。
;이벤트플래그:55:10 時間切れアナウンスフラグ。日数経過が原因でイベントが失敗したときに失敗イベントアナウンスを出し、フラグ1になる。
;====================
;条件
;====================
@PLAYABLE_EVENT_55

;経験ED数が1以上ならゲーム開始時に発生する。発生させないまま日曜を迎えると消滅する。
IF 이벤트플래그:55:0 == 0 && DAY < 7 && FLAG:경험ED수 > 0
	RESULTS = 道との遭遇
	RETURN 1
ENDIF
;　1.「これが私の救世主様」以降、本シナリオのEDフラグが立っていない状態で51日目を迎えた場合
;　2.「最後の聖戦」発生当日にイベントを実行しないまま翌日を迎えた
;　3.イベントの失敗フラグを踏んだ　以上いずれかで出現
IF (DAY > 50 && 이벤트플래그:55:0 >= 1 && 이벤트플래그:55:0 < 5) || (DAY > 이벤트플래그:55:3 + 7 && 이벤트플래그:55:0 == 4) || 이벤트플래그:55:0 == 8 
	IF 이벤트플래그:55:10 == 0
		CLEARBIT FLAG:이벤트출현1, 55
		이벤트플래그:55:10 = 1
	ENDIF
	RESULTS = シスターめしあちゃん、夢の終わりに
	;이벤트플래그がONのままイベント発生アナウンスを出したいので、失敗アナウンスフラグが立ってないならCLEARBITしてアナウンスを出し、フラグを立てる。
	RETURN 1
;最後の聖戦のあと週をまたがずEDが発生。
ELSEIF 이벤트플래그:55:0 == 5
	RESULTS = 彼女はそういう神になる
	RETURN 1
	;本イベントの失敗フラグに踏み込んだ
;前イベント翌日から、「最後の聖戦」発生の前日までの期間で、「意外なタレコミ」を実行したことが無い
ELSEIF DAY < 이벤트플래그:55:3 + 7 && 이벤트플래그:55:0 == 4 && 이벤트플래그:55:9 == 0
	RESULTS = 意外なタレコミ
	RETURN 1
;以降、毎週日曜日（日付が7の倍数の日）にイベントが発生する。解決させないまま日曜が終わると消滅する。
ELSEIF 이벤트플래그:55:0 > 0 && DAY % 7 ==0
	IF 이벤트플래그:55:0 == 1 
		RESULTS = これが私の救世主様	
		RETURN 1
	;前イベントを踏んだ翌日以降
	ELSEIF DAY > 이벤트플래그:55:1 && 이벤트플래그:55:0 == 2 
		RESULTS = 審判の日
		RETURN 1
	;前イベントを踏んだ翌日以降	
	ELSEIF DAY > 이벤트플래그:55:2 && 이벤트플래그:55:0 == 3 
		RESULTS = 女々しい野郎共の宴
		RETURN 1
	;前イベントを踏んだ翌日以降
	ELSEIF DAY > 이벤트플래그:55:3 && 이벤트플래그:55:0 == 4 
		RESULTS = 最後の聖戦
		RETURN 1
	ENDIF
ELSE
	RETURN 0
ENDIF

RETURN 0

;====================
;内容
;====================
@SHOPCOM_EVENT_55
CLEARBIT FLAG:이벤트출현1, 55

IF 이벤트플래그:55:0 == 0
	CALL EVENT_55_0
ELSEIF 이벤트플래그:55:0 == 1
	CALL EVENT_55_1
ELSEIF (DAY > 50 && 이벤트플래그:55:0 >= 1 && 이벤트플래그:55:0 < 5) || (DAY > 이벤트플래그:55:3 + 7 && 이벤트플래그:55:0 == 4) || 이벤트플래그:55:0 == 8 
	CALL EVENT_55_9
ELSEIF DAY < 이벤트플래그:55:3 + 7 && 이벤트플래그:55:0 == 4 && 이벤트플래그:55:9 == 0
	CALL EVENT_55_6
ELSEIF 이벤트플래그:55:0 == 2
	CALL EVENT_55_2
ELSEIF 이벤트플래그:55:0 == 3
	CALL EVENT_55_3
ELSEIF 이벤트플래그:55:0 == 4
	CALL EVENT_55_4
ELSEIF 이벤트플래그:55:0 == 5
	CALL EVENT_55_5
ENDIF

;イベント出現告知
;#;PRINTL 入力
;#;INPUTS
;#;TRYCALLFORM SHOPCOM_EVENT_555_%RESULTS%
;#;RETURN 0
