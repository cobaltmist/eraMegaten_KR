;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;	002		2020/05/31		Jガン					보이콧판정の計算式を変えて구상が出やすいように
;	003		2021/01/04		Jガン					링케이지참가자も구상が出せるように
;	004		2021/09/11		木綿豆腐				リンケージ参加口上が出なくなっていたので修正
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

@전투행동대응충성도상승,ARG
	#DIMS CONST COLOUR = "붕괴","우애","영맹","우둔","교만","교활","냉정","잔학","현명","좀비","천사","타천사","허심"
	#DIM LOYALTY,1
	#DIM MOTIVE,1
	;LOYALTY      획득충성도
	;MOTIVE       충성도上昇の引き金となったTALENT
	LOYALTY = 0
	MOTIVE = 0

	;ボイコット설정がOFFの場合はセリフ処理のみ
	IF FLAG:전투시보이콧설정 ==  0
		;인간は処理されない
		SIF IS_HUMAN(ARG) == 1
			GOTO RISE_SKIPPED_OFF
		;기본動機となる소질ごとの판정
		FOR LOCAL,0,VARSIZE("COLOUR")
			SIF !TALENT:ARG:(COLOUR:LOCAL)
				CONTINUE
			CALLFORM COMBAT_HIGH_%COLOUR:LOCAL%,ARG,CFLAG:ARG:입력행동
			IF RESULT > LOYALTY || (RESULT <= 0 && LOYALTY <= 0)
				LOYALTY = RESULT
				MOTIVE = GETNUM(TALENT,COLOUR:LOCAL)
			ENDIF
		NEXT
		;계승타입と一致する스킬なら충성도+1(조마を除く)
		IF LOYALTY <= 0 && ABL:ARG:종족 != GET_RACE_NUM("조마")
			CALLFORM SKILL_SUCCESSION_TYPE_{CFLAG:ARG:입력행동}
			SIF TALENT:ARG:GET_SUCCESSION(RESULT)
				LOYALTY += 1
		ENDIF
		;메시지呼び出し
		$RISE_SKIPPED_OFF
		SWAP TARGET,FLAG:행동순1
		CALL 구상호출 , "BATTLE_MESSAGE" , TARGET , TARGET , MOTIVE
		SWAP TARGET,FLAG:행동순1
		;충성도増減
		TCVAR:ARG:획득충성도 = MAX(0,TCVAR:ARG:획득충성도+LOYALTY)
		
	;ボイコット설정が有効な場合
	ELSE
		;인간は処理されない
		SIF IS_HUMAN(ARG) == 1
			GOTO RISE_SKIPPED_ON

		;기본動機となる소질ごとの판정
		FOR LOCAL,0,VARSIZE("COLOUR")
			SIF !TALENT:ARG:(COLOUR:LOCAL)
				CONTINUE
			CALLFORM COMBAT_HIGH_%COLOUR:LOCAL%,ARG,CFLAG:ARG:입력행동
			IF RESULT > LOYALTY || (RESULT <= 0 && LOYALTY <= 0)
				LOYALTY = RESULT
				MOTIVE = GETNUM(TALENT,COLOUR:LOCAL)
			ENDIF
		NEXT

		;계승타입と一致する스킬なら충성도+1(조마を除く)
		IF LOYALTY <= 0 && ABL:ARG:종족 != GET_RACE_NUM("조마")
			CALLFORM SKILL_SUCCESSION_TYPE_{CFLAG:ARG:입력행동}
			SIF TALENT:ARG:GET_SUCCESSION(RESULT)
				LOYALTY += 1
		ENDIF

		$RISE_SKIPPED_ON

		;인스톨소프트《블랙컴퍼니》は충성도上昇を阻害
		SIF EQUIP:MASTER:블랙컴퍼니 == 1 && LOYALTY > 0
			LOYALTY *= TALENT:ARG:허심 ? -1 # -5
		;보이콧판정
		CFLAG:ARG:보이콧플래그 = 0
		IF LOYALTY <= 0 && IS_HUMAN(ARG) == 0
			;충성도上昇がない場合は보이콧판정
			SIF MOTIVE != 0
				CALL 보이콧판정, ARG
			;스킬コストに応じてペナルティ설정
			CALLFORM SKILL_COST_{CFLAG:ARG:입력행동},ARG
			LOYALTY -= MAX(1,RESULT) * (TALENT:ARG:허심 ? 1 # 5)
		ENDIF
		SWAP TARGET,FLAG:행동순1
		IF CFLAG:ARG:보이콧플래그 == 1
			CALL 구상호출 , "BATTLE_BOYCOTT_MESSAGE" , TARGET , TARGET , MOTIVE
		ELSE
			;충성도上昇に伴う메시지
			CALL 구상호출 , "BATTLE_MESSAGE" , TARGET , TARGET , MOTIVE
		ENDIF
		SWAP TARGET,FLAG:행동순1
		;난이도[EASY]이상で기력値増減
		SIF FLAG:전투시보이콧설정 > 1
			BASE:ARG:기력 = LIMIT(BASE:ARG:기력+LOYALTY,0,MAXBASE:ARG:기력)
		;충성도増減
		TCVAR:ARG:획득충성도 = MAX(0,TCVAR:ARG:획득충성도+LOYALTY)
	ENDIF
	
	;---- EDIT 003 MOD START -------------------------
	;---- EDIT 004 MOD START -------------------------
	;IF CFLAG:(FLAG:행동순1):보이콧플래그 == 1 && FLAG:전투시보이콧설정 > 2
	;FLAG:전투시보이콧설정=3から実際に行動しなくなるので
	IF (CFLAG:(FLAG:행동순1):보이콧플래그 == 0 && FLAG:전투시보이콧설정 > 2) || (FLAG:전투시보이콧설정 <= 2)
		TFLAG:전투이벤트 = 100
		;リンケージの場合、更にリンケージ参加者の口上も出す
		IF CFLAG:ARG:링케이지발동 && RANGE(CFLAG:ARG:입력행동,4000,4999)
			CALLFORM 참가인수_{CFLAG:ARG:입력행동}
			LOCAL:1 = RESULT + 1
			FOR LOCAL , 1 , LOCAL:1
				SIF RANGE(CFLAG:ARG:("링케이지참가자"+TOSTR(LOCAL)),0,CHARANUM) == 0
					CONTINUE
				CALL 구상호출 , "BATTLE_EVENT" , CFLAG:ARG:("링케이지참가자"+TOSTR(LOCAL)) , CFLAG:ARG:("링케이지참가자"+TOSTR(LOCAL)) , ARG
			NEXT
		ENDIF
	ENDIF
	;---- EDIT 004 MOD END ---------------------------
	;---- EDIT 003 MOD END ---------------------------

