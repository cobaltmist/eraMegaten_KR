;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:LINKAGE.ERB
;	Facility	:合体スキル用関数置き場
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;	002		2021/02/11		Rsp暇人 				ACTIONABLE_LINKAGEで使用されるD及びEの中身を参照型変数で保存するように。
;													SHOW_LINKAGEで銃攻撃リンケージを表示でき、未解析のスキルを？？？？で表示するように。現在のパーティ編成で使用可能なリンケージは水色で表示するように。
;	003		2021/01/20		Jガン					スキル参照のフラグなどを追加して、スキルの詳細が正しくなるように
;	004		2021/04/22		Rsp暇人 				キャラクタ選択画面から発動可能なリンケージ一覧を表示し選択可能に。リンケージ参加者から参加リンケージをキャンセルできるように。
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;=================================================
;링케이지사용가능판정
;================================================
@ACTIONABLE_LINKAGE,ARG,ARG:1,REF_LOCAL
#DIM REF REF_LOCAL, 0, 0, 0;MOD 002 ADD
;대상스킬がそもそも링케이지じゃなかったら帰る
;この処理はより上の階層に逃がす予定
TRYCCALLFORM LINKAGE_{ARG}
	SIF RESULT == 0
		RETURN 0
CATCH
	RETURN 0
ENDCATCH
;発動者が조건を満たしているか체크
CALLFORM 발동자조건_{ARG},ARG:1
SIF RESULT == 0
	RETURN 0

;참가者の人数を체크
CALLFORM 참가인수_{ARG}
LOCAL:1 = RESULT

;조건を満たす참가者をDの枠に放り込む
VARSET D , 0
FOR LOCAL, 1 , LOCAL:1+1
	FOR LOCAL:2 , 1 , 7
		SIF POS(LOCAL:2) == -1
			CONTINUE
		;発動者は참가者になれない
		SIF POS(LOCAL:2) == ARG:1
			CONTINUE
		FLAG:스킬우선참조인 = POS(LOCAL:2)	;ADD 003
		;---- EDIT 002 MOD START ---------------------------
		;---- EDIT 004 ADD START ---------------------------
		U = ARG:1
		;---- EDIT 004 ADD END ---------------------------
		CALLFORM 참가자조건_{ARG},POS(LOCAL:2),LOCAL
		SIF RESULT == 0
			CONTINUE
		;参加可能ならREF_LOCALをONにする
		SETBIT REF_LOCAL:(ARG-4000):0:LOCAL, LOCAL:2
		;入力できない状態の人はそもそも無理
		CALL INPUTABLE_CHARA , POS(LOCAL:2)
		SIF RESULT == 0
			CONTINUE
		;別のリンケージに参加中のキャラは弾く
		;---- EDIT 004 MOD START ---------------------------
		SIF CFLAG:POS(LOCAL:2):링케이지참가 || CFLAG:POS(LOCAL:2):링케이지발동
		;---- EDIT 004 MOD END ---------------------------
			CONTINUE
		SETBIT D:LOCAL ,LOCAL:2
	NEXT
NEXT
FLAG:스킬우선참조인 = -1	;ADD 003

;0があると駄目
LOCAL:2 = 1
FOR LOCAL, 1, LOCAL:1+1
	LOCAL:2 = MIN(REF_LOCAL:(ARG-4000):0:LOCAL, LOCAL:2)
NEXT
SIF !LOCAL:2
	RETURN 0
;---- EDIT 002 MOD END ---------------------------



;重複せずに全てが埋まる組み合わせがあるかを체크
;どうすんだこれ
VARSET E , 0

;まずLOCAL番눈の조건を満たせるキャラが一人しか居ない場合、참가者LOCAL番눈はそのキャラに決定
;FOR LOCAL , 1 , LOCAL:1+1
;	SIF C:LOCAL > 0
;		CONTINUE
;	FOR LOCAL:2 , 1 , 7
;		IF D:LOCAL = POWER(2,LOCAL:2)
;			C:LOCAL = POWER(2,LOCAL:2)
;			;모든DからLOCAL:2番눈のビットを삭제
;			FOR LOCAL:3 , 1 , LOCAL:1+1
;				D:(LOCAL:3) &= ~(POWER(2,LOCAL:2))
;			NEXT
;			GOTO LOOP
;		ENDIF
;	NEXT
;NEXT

FOR LOCAL:4 , 1 , 7
	$LOOP1
	;LOCAL番눈の조건をLOCAL:4人が満たしている場合、번호の若い順に当てはめ
	;当てはめたらまた一人から체크
	FOR LOCAL , 1 , LOCAL:1 + 1
		SIF E:LOCAL > 0
			CONTINUE
		;人数を판정
		LOCAL:3 = 0
		FOR LOCAL:2 , 1 , 7
			LOCAL:3 += GETBIT(D:LOCAL, LOCAL:2)
		NEXT
		;LOCAL:4人じゃなければ次へ
		SIF LOCAL:3 != LOCAL:4
			CONTINUE
		FOR LOCAL:2 , 1 , 7
			IF D:LOCAL & POWER(2,LOCAL:2)
				E:LOCAL = POWER(2,LOCAL:2)
				;---- EDIT 002 ADD START ---------------------------
				REF_LOCAL:(ARG-4000):1:LOCAL = LOCAL:2
				;---- EDIT 002 ADD END ---------------------------
				;全てのDからLOCAL:2番目のビットを削除
				FOR LOCAL:3 , 1 , LOCAL:1+1
					D:(LOCAL:3) &= ~(POWER(2,LOCAL:2))
				NEXT
				LOCAL:4 = 0
				GOTO LOOP1
			ENDIF
		NEXT
	NEXT
NEXT


;Eに0が残ってたら駄目

;---- EDIT 002 MOD START ---------------------------
SETBIT FLAG:(1202+ARG / 63), ARG % 63
CALL CHECK_ACTIONABLE,ARG:1,ARG
SIF MINARRAY(E,1,LOCAL:1+1) == 0 || RESULT == 0
	RETURN -1
;---- EDIT 002 MOD END ---------------------------
RETURN 1

;---- EDIT 004 MOD START ---------------------------
;=================================================
;リンケージ一覧表示
;  ARG=0 非戦闘中、閲覧のみ 1 戦闘中発動者固定で一覧表示 2 属性を無視して発動可能なリンケージを１열記して直接選択し発動可能
;================================================
@SHOW_LINKAGE, ARG
#LOCALSIZE 4
#LOCALSSIZE 1
#DIM TYPE, 1
#DIM PAGE, 1
#DIM LINE, 1
#DIM LIST, 100
#DIM LINK_LOCAL, 1000, 2, 7;MOD 002 ADD
;LINK_LOCAL:(スキル番号-4000):(0 参加資格があるキャラ全ての配置番号（Bit） 1 実際に成立しうる一例のキャラの配置番号):(0 発動可能者(Bit) 1-5リンケージ参加枠番号)
;LINK_LOCAL:(スキル番号-4000):1:0 は隊１열を前後に入れ替えた場合の発動可能者(Bit) 入れ替え後のPOSなので注意

;---- EDIT 002 MOD START ---------------------------
VARSET LINK_LOCAL, 0
;ARGが0の場合敵が0のため射程内に敵が居ない→攻撃系リンケージが成立するか判定できないためダミー作成
SIF !ARG
	CALL SET_ENEMY,7,0,5,0,0
FOR LOCAL:2, 0, 2
	;射程Ｓのリンケージが後１열に発動可能者がいるかチェックするため前後入れ替え
	CALL CHANGE_POS,1,6
	CALL CHANGE_POS,2,5
	CALL CHANGE_POS,3,4
	FOR LOCAL:1, 1, 7
		SIF POS(LOCAL:1) < 0
			CONTINUE
		FOR LOCAL,4000,5000
			;入れ替え時の参加候補者をリセット
			LINK_LOCAL:(LOCAL-4000):1:(LOCAL:1) = 0
			TRYCCALLFORM LINKAGE_{LOCAL}
			CATCH
				CONTINUE
			ENDCATCH
			CALL ACTIONABLE_LINKAGE,LOCAL,POS(LOCAL:1),LINK_LOCAL
			SIF RESULT > 0 && (!ARG || (!CFLAG:POS(LOCAL:1):링케이지참가 && !CFLAG:POS(LOCAL:1):링케이지발동))
				SETBIT LINK_LOCAL:(LOCAL-4000):(!LOCAL:2):0, LOCAL:1
	NEXT
NEXT
NEXT
;使用済みのダミーを削除
SIF !ARG
	CALL 캐릭터삭제, CHARANUM - 1
;---- EDIT 004 MOD END ---------------------------
;---- EDIT 002 MOD END ---------------------------

LINE = LINECOUNT
$START
CLEARLINE LINECOUNT -LINE
DRAWLINE
;---- EDIT 004 MOD START ---------------------------
IF ARG == 2
	;ARGが2の場合、属性問わず現在使用可能なリンケージスキルを全部１열記
	FOR LOCAL, 4000, 5000
		IF LINK_LOCAL:(LOCAL-4000):0:0 > 0
			TRYCCALLFORM SKILL_NAME_{LOCAL}
			CATCH
				CONTINUE
			ENDCATCH
			PRINTFORM [{LOCAL}] %RESULTS,20,LEFT%
			CALLFORM 참가인수_{LOCAL}
			FOR LOCAL:1, 1, RESULT + 2
				SIF LOCAL:1 > 1
					PRINT 　→　
				CALLFORM 참가조건표시_{LOCAL}, LOCAL:1
			NEXT
			RESETCOLOR
			PRINTL 
		ENDIF
	NEXT
ELSE
	;---- EDIT 004 MOD END ---------------------------
	FOR LOCAL, 4000 + TYPE * 40, 4000 + (TYPE+1) * 40
	;---- EDIT 002 MOD START ---------------------------
		;未解析の場合は？？？？で存在を示唆するように
		IF !GETBIT(FLAG:(1202+LOCAL / 63), LOCAL % 63)
			TRYCCALLFORM SKILL_NAME_{LOCAL}
			CATCH
				CONTINUE
			ENDCATCH
			SETCOLOR 0x404040
			PRINTPLAINFORM [{LOCAL}] %"？？？？",20,LEFT%
			CALLFORM 참가인수_{LOCAL}
			FOR LOCAL:1, 1, RESULT + 2
				SIF LOCAL:1 > 1
					PRINT 　→　
				CALLFORM 참가조건표시_{LOCAL}, LOCAL:1
			NEXT
			RESETCOLOR
		ELSE
			TRYCCALLFORM SKILL_NAME_{LOCAL}
			CATCH
				CONTINUE
			ENDCATCH
			SIF LINK_LOCAL:(LOCAL-4000):0:0 || (!ARG && LINK_LOCAL:(LOCAL-4000):1:0)
				SETCOLOR COLOR("aqua")
			PRINTFORM [{LOCAL}] %RESULTS,20,LEFT%
			CALLFORM 참가인수_{LOCAL}
			FOR LOCAL:1, 1, RESULT + 2
				SIF LOCAL:1 > 1
					PRINT 　→　
				CALLFORM 참가조건표시_{LOCAL}, LOCAL:1
			NEXT
			RESETCOLOR
		ENDIF
		PRINTL 
	NEXT
	;---- EDIT 002 MOD END ---------------------------
	DRAWLINE
	LOCAL:1 = 0
	FOR LOCAL, 0, 21
		SIF GET_SUCCESSION(LOCAL) == ""
			CONTINUE
		SIF LOCAL == TYPE
			SETCOLOR COLOR("aqua")
		PRINTFORM [{LOCAL,2}]%GET_SUCCESSION(LOCAL), 6, LEFT%
		SIF LOCAL:1 % 7 == 6
			PRINTL 
		LOCAL:1++
		RESETCOLOR
	NEXT
	;---- EDIT 002 MOD START ---------------------------
	PRINTFORM [21]%"건슬링거", 16, LEFT%
	PRINTL
ENDIF
;---- EDIT 002 MOD END ---------------------------
;---- EDIT 004 MOD END ---------------------------

DRAWLINE
PRINTL [99] 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 99
	RETURN
ELSEIF RESULT >= 4000 && RESULT <= 5000
	LOCAL = RESULT
	IF GETBIT(FLAG:(1202+LOCAL / 63), LOCAL % 63)
		TRYCCALLFORM SKILL_NAME_{LOCAL}
			;---- EDIT 004 MOD START ---------------------------
			PRINTFORM [{LOCAL}] %RESULTS,20,LEFT%　
			;説明文に書かれてないことがあるので射程・範囲を明記
			CALLFORM SKILL_RANGE_{LOCAL}
			PRINTFORM 사거리:%AUTO_SPLIT(" /Ｓ/Ｍ/Ｌ/ＬＬ", "/", RESULT)%　
			CALLFORM SKILL_SPHERE_{LOCAL}
			PRINTFORML 범위:%AUTO_SPLIT("특수/１체/１열/전체/적아군/특수", "/", RESULT)%　
			;---- EDIT 004 MOD END ---------------------------
			PRINT 발동자　
			CALLFORM SKILL_COSTTYPE_{LOCAL}
			IF RESULT == 2
				CALLFORM SKILL_COST_{LOCAL}
				PRINTFORM ＨＰ{RESULT,3}％　
			ELSE
				CALLFORM SKILL_COST_{LOCAL}
				PRINTFORM ＭＰ{RESULT,3}　　
			ENDIF
			CALLFORM 참가인수_{LOCAL}
			LOCAL:1 = RESULT + 1
			FOR LOCAL:2, 1, LOCAL:1
				PRINTFORM 참가자{LOCAL:2}　
				CALLFORM 링케이지코스트타입_{LOCAL}, LOCAL:2
				IF RESULT == 2
					CALLFORM 링케이지코스트_{LOCAL}, LOCAL:2
					PRINTFORM ＨＰ{RESULT,3}％　
				ELSE
					CALLFORM 링케이지코스트_{LOCAL}, LOCAL:2
					PRINTFORM ＭＰ{RESULT,3}　　
				ENDIF
				IF LOCAL:1 == LOCAL:2 - 1
					PRINTL 
				ELSEIF LOCAL:2 == 2
					PRINTL 
					PRINTFORM %" "* 27%
				ENDIF
			NEXT
			PRINTL 
			;참가者が決まってなかったりして落ちる스킬があるので…
			;SIF FLAG:스킬속성표시설정 == 1
			;	CALL SKILL_EXPLAIN_PERFORMANCE, LOCAL, -1
			TRYCALLFORM SKILL_EXPLAIN_{LOCAL}
			;---- EDIT 004 MOD START ---------------------------
			IF LINK_LOCAL:(LOCAL-4000):0:0 || LINK_LOCAL:(LOCAL-4000):1:0
				LOCAL:2 = 0
				PRINTL
				PRINTFORM 발동가능：
				FOR LOCAL:1, 1, 7
					IF GETBIT(LINK_LOCAL:(LOCAL-4000):0:0, LOCAL:1) && ARG == 2
						;ARGが2の場合のみコマンド化
						PRINTFORM [{LOCAL:1,2}] %CALLNAME:POS(LOCAL:1)%　
						SETBIT LOCAL:2, LOCAL:1
					ELSEIF GETBIT(LINK_LOCAL:(LOCAL-4000):0:0, LOCAL:1)
						;現在の編成で発動可
						PRINTPLAINFORM [{LOCAL:1,2}] %CALLNAME:POS(LOCAL:1)%　
						SETBIT LOCAL:2, LOCAL:1
					ENDIF
				NEXT
				FOR LOCAL:1, 1, 7
					IF GETBIT(LINK_LOCAL:(LOCAL-4000):1:0, LOCAL:1) && !GETBIT(LOCAL:2, 7-LOCAL:1)
						;後列のため使用不可
						SETCOLOR 0x808080
						PRINTPLAINFORM [{7-LOCAL:1,2}] %CALLNAME:POS(7-LOCAL:1)%　
						RESETCOLOR
					ENDIF
				NEXT
				PRINTL [0]ＲＥＴＵＲＮ
				PRINTL
				$INPUT_LOOP1
				CLEARLINE 1
				CALL INPUTINT(0,1,2,3,4,5,6)
				IF RESULT == 0
					GOTO START
				ELSEIF GETBIT(LINK_LOCAL:(LOCAL-4000):0:0, RESULT) && ARG == 2
					LOCAL:1 = RESULT
					CLEARLINE 1
					DRAWLINE
					CALL INPUT_COM_11, POS(LOCAL:1), LOCAL
					RESULT:2 = RESULT
					DEBUGPRINTFORML 발동자%CALLNAME:POS(LOCAL:1)%　발동번호{CFLAG:POS(LOCAL:1):입력행동} 방어 \@CFLAG:POS(LOCAL:1):방어플래그? ON # OFF \@
					RETURN POS(LOCAL:1)
				ELSE
					GOTO INPUT_LOOP1
				ENDIF
			ENDIF
			;---- EDIT 004 MOD END ---------------------------
			WAIT
		CATCH
		ENDCATCH
	ENDIF
;---- EDIT 002 MOD START ---------------------------
ELSEIF RESULT >= 0 && RESULT < 22
;---- EDIT 002 MOD END ---------------------------
	TYPE = RESULT
ENDIF
GOTO START

;---- EDIT 004 ADD START ---------------------------
;=================================================
;リンケージ参加キャンセル ARGはキャラ番号
;================================================
@LINKAGE_CANSELL(ARG)
#LOCALSIZE 1
#DIM LCOUNT, 2

SIF !INRANGE(ARG, 0, CHARANUM-1)
	RETURN 0

SIF CFLAG:ARG:링케이지참가 < 1
	RETURN 0
LOCAL = CFLAG:ARG:링케이지참가 - 1
TRYCCALLFORM SKILL_NAME_{CFLAG:LOCAL:입력행동}
	PRINTPLAINFORM %CALLNAME:ARG%は[{CPOS(LOCAL)}] %CALLNAME:LOCAL%のリンケージスキル：%RESULTS%に参加中です
CATCH
	PRINTPLAINFORM %CALLNAME:ARG%は[{CPOS(LOCAL)}] %CALLNAME:LOCAL%のリンケージスキルに参加中？です
ENDCATCH
PRINTL
PRINTL 참가를 취소하시겠습니까？ 
CALL INPUT_YN("네","아니오",2)
SIF RESULT
	RETURN 0
FOR LCOUNT, 1, 6
	IF CFLAG:LOCAL:("링케이지참가자" + TOSTR(LCOUNT)) > -1
		CFLAG:(CFLAG:LOCAL:("링케이지참가자" + TOSTR(LCOUNT))):링케이지참가 = 0
	ENDIF
NEXT
CFLAG:LOCAL:링케이지발동 = 0
CFLAG:LOCAL:ＲＥＰＥＡＴ행동 = -1
CFLAG:LOCAL:방어플래그 = 1
RETURN 1
RETURN 0
;---- EDIT 004 ADD END ---------------------------
