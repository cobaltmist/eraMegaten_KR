;===================================
;オルギア時の行動決定
;===================================
@SET_ACT_ORGY,ARG
$DECIDE_ACTION
VARSET LOCAL , 0
;타겟数확인
FOR LOCAL:50,0,10
	;敵側ポジションで存在しており、死んでないか
	IF POS(7+LOCAL:50) > -1 && CFLAG:(POS(7+LOCAL:50)):ステート != GET_STATE_NUM("DYING")
		FOR LOCAL:51,0,4
			;物理相性の確認
			SELECTCASE MAXBASE:(POS(7+LOCAL:50)):(GET_TYPE(LOCAL:51))
				;反射と吸収
				CASE IS < 0, 999
					;敵前衛
					SIF LOCAL:50 < 5
						LOCAL:(LOCAL:51 + 4) -= 1
					LOCAL:(LOCAL:51) -= 1
				;弱点
				CASE IS > 100
					;敵前衛
					IF LOCAL:50 < 5
						LOCAL:(LOCAL:51 + 4) += 3
						LOCAL:(LOCAL:51 + 75) = 1
					ENDIF
					LOCAL:(LOCAL:51) += 3
				;無効
				CASE 0
				;それ以外
				CASEELSE
					;敵前衛
					SIF LOCAL:50 < 5
						LOCAL:(LOCAL:51 + 4) += 1
					LOCAL:(LOCAL:51) += 1
			ENDSELECT
		NEXT
		SIF LOCAL:50 < 5
			LOCAL:53 += 1
		LOCAL:52 += 1
	ENDIF
NEXT
;PRINTFORML 적前衛{LOCAL:53}、적전체で{LOCAL:52}であります！
;PRINTFORMW {LOCAL:0} {LOCAL:1} {LOCAL:2} {LOCAL:3} {LOCAL:4} {LOCAL:5} {LOCAL:6} {LOCAL:7}
;스킬評価
;LOCAL:60-70

;ＡＴＴＡＣＫ出来るか
CALL CHECK_ACTIONABLE,ARG,0
IF RESULT == 1
	;最小攻撃回数
	CALL SKILL_MINATTACKNUMBER_0,ARG
	LOCAL:54 = RESULT
		;최대공격횟수
	CALL SKILL_MAXATTACKNUMBER_0,ARG
	LOCAL:55 = RESULT
	;範囲攻撃
	CALL SKILL_SPHERE_0,ARG
	SELECTCASE RESULT
		CASE 1
			IF LOCAL:75 > 0
				LOCAL:56 = 3
			ELSEIF LOCAL:4 > 0
				LOCAL:56 = 1
			ELSE
				LOCAL:56 = 0
			ENDIF
		CASE 2
			LOCAL:56 = LOCAL:4
		CASE 3
			LOCAL:56 = LOCAL:0
		CASEELSE
			LOCAL:56 = 0
	ENDSELECT
	;ＡＴＴＡＣＫの予想攻撃力
	LOCAL:60 = LOCAL:56 * 100 * MAXBASE:ARG:(GET_BATTLESTATUS(0)) * (LOCAL:54 + LOCAL:55) / 2
ELSE
	LOCAL:60 = 0
ENDIF

;ＧＵＮ出来るか
CALL CHECK_ACTIONABLE,ARG,2101
IF RESULT == 1
	;最小攻撃回数
	CALL SKILL_MINATTACKNUMBER_2101,ARG
	LOCAL:54 = RESULT
	;최대공격횟수
	CALL SKILL_MAXATTACKNUMBER_2101,ARG
	LOCAL:55 = RESULT
	;範囲攻撃
	CALL SKILL_SPHERE_2101,ARG
	SELECTCASE RESULT
		CASE 1
			IF LOCAL:76 > 0
				LOCAL:56 = 3
			ELSEIF LOCAL:5 > 0
				LOCAL:56 = 1
			ELSE
				LOCAL:56 = 0
			ENDIF
		CASE 2
		IF LOCAL:55 >= 3
			LOCAL:56 = LOCAL:5 / LOCAL:53
		ELSE
			LOCAL:56 = LOCAL:5
		ENDIF
		CASE 3
		IF LOCAL:55 >= 3
			LOCAL:56 = LOCAL:5 / LOCAL:52
		ELSE
			LOCAL:56 = LOCAL:1
		ENDIF
		CASEELSE
			LOCAL:56 = 0
	ENDSELECT
	;ＧＵＮの予想攻撃力
	LOCAL:61 = LOCAL:56 * 100 * MAXBASE:ARG:총공격 * (LOCAL:54 + LOCAL:55) / 2
ELSE
	LOCAL:61 = 0
ENDIF

FOR LOCAL:50, 0, (FLAG:이능자스킬수 + 21)
	IF LOCAL:50 <= 11
		LOCALS = 스킬{LOCAL:50+1}
		;PRINTFORML 　　　　　 今%LOCALS%の{ABL:ARG:LOCALS}を確認したであります！
	ELSE
		LOCALS = 장비스킬{LOCAL:50+1-12}
		;PRINTFORML 　　　　　 今%LOCALS%の{ABL:ARG:LOCALS}を確認したであります！
	ENDIF
	;装備スキルが無い
	IF LOCAL:50 > 11 && ABL:ARG:LOCALS <= 0
		;PRINTFORML 　　　　　 %LOCALS%は無いであります！終わり！
		BREAK
	ENDIF
	;スキルが無い
	IF ABL:ARG:LOCALS == 0
		;PRINTFORML 　　　　　 %LOCALS%は無いであります！
		CONTINUE
	ENDIF
	RESULT = 1
	;戦闘中に使えるか
	TRYCALLFORM SKILL_ACTIONABLE_BATTLE_{ABL:ARG:LOCALS}
	SIF RESULT == 0
		CONTINUE
	;対象が1敵か
	TRYCALLFORM SKILL_TARGET_{ABL:ARG:LOCALS}
	SIF RESULT != 1
		CONTINUE
	;効果のタイプが1攻撃か
	TRYCALLFORM SKILL_EFECT_{ABL:ARG:LOCALS}
	SIF RESULT != 1
		CONTINUE
	;スキル発動出来るか
	CALL CHECK_ACTIONABLE,ARG,ABL:ARG:LOCALS
	IF RESULT == 1
		;スキル相性
		CALLFORM SKILL_TYPE_{ABL:ARG:LOCALS},ARG
		LOCAL:71 = RESULT
		;物理４種以外ダメ
		SIF LOCAL:71 >= 4
			CONTINUE
		;最小攻撃回数
		CALLFORM SKILL_MINATTACKNUMBER_{ABL:ARG:LOCALS},ARG
		LOCAL:54 = RESULT
		;최대공격횟수
		CALLFORM SKILL_MAXATTACKNUMBER_{ABL:ARG:LOCALS},ARG
		LOCAL:55 = RESULT
		;範囲攻撃
		CALLFORM SKILL_SPHERE_{ABL:ARG:LOCALS},ARG
		SELECTCASE RESULT
			CASE 1
				IF LOCAL:(LOCAL:71 + 75) > 0
					LOCAL:56 = 3
				ELSEIF LOCAL:(LOCAL:71) > 0
					LOCAL:56 = 1
				ELSE
					LOCAL:56 = 0
				ENDIF
			CASE 2
				IF LOCAL:55 >= 3
					LOCAL:56 = LOCAL:(LOCAL:71 + 4) / LOCAL:53
				ELSE
					LOCAL:56 = LOCAL:(LOCAL:71 + 4)
				ENDIF
			CASE 3
				IF LOCAL:55 >= 3
					LOCAL:56 = LOCAL:(LOCAL:71) / LOCAL:52
				ELSE
					LOCAL:56 = LOCAL:(LOCAL:71)
				ENDIF
			CASEELSE
				LOCAL:56 = 0
		ENDSELECT
		;スキル威力
		CALLFORM SKILL_POWER_{ABL:ARG:LOCALS}, ARG
		LOCAL:57 = RESULT
		;スキルの予想攻撃力
		LOCAL:(LOCAL:50+62) = LOCAL:56 * LOCAL:57 * MAXBASE:ARG:(GET_BATTLESTATUS(0)) * (LOCAL:54 + LOCAL:55) / 2
	ENDIF
NEXT
;PRINTFORML スキル評価 AT.{LOCAL:60} GU.{LOCAL:61}  1.{LOCAL:62}  2.{LOCAL:63}  3.{LOCAL:64}  4.{LOCAL:65}  5.{LOCAL:66}
;PRINTFORML 　　　　　  6.{LOCAL:67}  7.{LOCAL:68}  8.{LOCAL:69}  9.{LOCAL:70} 10.{LOCAL:71} 11.{LOCAL:72} 12.{LOCAL:73}
;PRINTFORMW 　　　　　 S1.{LOCAL:74} S2.{LOCAL:75} S3.{LOCAL:76} S4.{LOCAL:77} S5.{LOCAL:78} S6.{LOCAL:79} S7.{LOCAL:80}
;ＡＴＴＡＣＫＧＵＮスキル1～12装備スキル1～21
FOR LOCAL:50 ,0, 35
	;予想攻撃力の最大値を取る
	IF LOCAL:58 < LOCAL:(LOCAL:50+60)
		LOCAL:58 = LOCAL:(LOCAL:50+60)
		;最も強そうな行動
		LOCAL:59 = LOCAL:50
	ENDIF
NEXT
SELECTCASE LOCAL:59
	CASE 0
		LOCAL:99 = 0
	CASE 1
		LOCAL:99 = 2101
	CASE 3 TO 13
		LOCALS = 스킬{LOCAL:59-1}
		LOCAL:99 = ABL:ARG:LOCALS
	CASEELSE
		LOCALS = 장비스킬{LOCAL:59-13}
		LOCAL:99 = ABL:ARG:LOCALS
ENDSELECT
		
;何もできなければ防御
IF LOCAL:58 == 0
	;PRINTFORMW 　　　　　 今回は防御するであります！
	CFLAG:ARG:입력행동 = -1
	CFLAG:ARG:방어플래그 = 1
	RETURN 0
ENDIF
;PRINTFORMW 　　　　　 今回は{LOCAL:59-1}番%LOCALS%の{LOCAL:99}を選択したであります！

CFLAG:ARG:타겟 = 0
CALLFORM SKILL_SPHERE_{LOCAL:99},ARG
IF RESULT == 1 && LOCAL:75+LOCAL:76+LOCAL:77+LOCAL:78
	CALLFORM SKILL_TYPE_{LOCAL:99},ARG
	FOR LOCAL:50,0,5
	IF POS(7+LOCAL:50) > -1 && CFLAG:(POS(7+LOCAL:50)):ステート != GET_STATE_NUM("DYING")
		IF MAXBASE:(POS(7+LOCAL:50)):(GET_TYPE(RESULT)) > 100
			CFLAG:ARG:타겟 = 7+LOCAL:50
		ENDIF
	ENDIF
	NEXT
ENDIF
SIF CFLAG:ARG:타겟 <= 0
	CALL RANDOM_TARGET,ARG,LOCAL:99
CFLAG:ARG:입력행동 = LOCAL:99

