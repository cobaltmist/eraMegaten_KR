;------------------------------
;조수が헤드셋を装着していると、アナウンスをしてくれる
;------------------------------
;LOCAL:1 シチュエーション１
;LOCAL:2 シチュエーション２
;LOCAL:3 적の초기数
;LOCAL:4 現在の적数
;LOCAL:5 現在の아군の数
;LOCAL:6 配置保存
;LOCAL:7 상태이상の아군の数
;LOCAL:8 HPが50%を切った아군の数
;LOCAL:9 HPが25%を切った아군の数
;LOCAL:10 死亡している아군の数
;LOCAL:11 初ターンのSUPORT関数二重呼び出し防止用
;LOCAL:12 대상キャラの番号取得用
;LOCAL:13 대상キャラの番号取得用２
;LOCAL:14 적の레벨取得用
;LOCAL:15 아군の레벨取得用
;LOCAL:16 不要だったので削除
;LOCAL:17 적레벨の平均値取得用
;LOCAL:18 아군레벨の平均値取得用
;LOCAL:19 아군の초기数
;LOCAL:20 인간の死亡を優先してアナウンスさせるためのカウンタ
;LOCAL:21 死んだ아군악마の数



@SUPORT(LOCAL:1, LOCAL:2)
SIF ASSI < 1
	RETURN -1
SIF EQUIP:ASSI:악세서리 != 5204 && EQUIP:ASSI:악세서리 != 8103
	RETURN -1
LOCAL:15 = 0
LOCAL:5 = 0
;조수がＰＴにいる場合はアナウンスできない
FOR LOCAL,1,7
	LOCALS = 포지션{LOCAL}
	SIF FLAG:LOCALS < 0
		CONTINUE
	SIF FLAG:LOCALS == ASSI
		RETURN -1
;아군情報取得
	LOCAL:15 += BASE:(FLAG:LOCALS):LV
	LOCAL:5 += 1
NEXT
LOCAL:18 = (LOCAL:15 / LOCAL:5)
LOCAL:7 = 0
LOCAL:8 = 0
LOCAL:9 = 0
LOCAL:10 = 0
;전투開始時
IF LOCAL:1 == 1
	LOCAL:14 = 0
	LOCAL:11 += 1
	LOCAL:3 = 0
	LOCAL:19 = 0
	FOR LOCAL,1,7
		LOCALS = 포지션{LOCAL}
		SIF FLAG:LOCALS < 0
			CONTINUE
		LOCAL:19 += 1
	NEXT
	FOR LOCAL,7,17
		LOCALS = 포지션{LOCAL}
		SIF FLAG:LOCALS < 0
			CONTINUE
		LOCAL:14 += BASE:(FLAG:LOCALS):LV
		LOCAL:3 += 1
	NEXT
	LOCAL:17 = LOCAL:14 / LOCAL:3
	PRINTL 
	DRAWLINE
	PRINTL
	;통상
	IF LOCAL:2 == 0
		;아군의 레벨이 적의 레벨보다 10 이상 높다
		IF LOCAL:18 - LOCAL:17 >= 10
			TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(0, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
			CATCH
				PRINTFORMW %CALLNAME:ASSI%「적 발견, 수는 {LOCAL:3}마리. 수준은 낮지만 긴장은 풀지 말아주세요」
			ENDCATCH
		;아군の레벨が적の레벨より１０以上低い
		ELSEIF LOCAL:17 - LOCAL:18 >= 10
			TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(1, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
			CATCH
				PRINTFORMW %CALLNAME:ASSI%「가, 강적입니다! 수는 {LOCAL:3}마리! 절대로 무리하진 말아주세요!」
			ENDCATCH
		;동레벨오버
		ELSE
			TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
			CATCH
				IF RAND:2 == 1
					PRINTFORML %CALLNAME:ASSI%「적 발견! 수는 {LOCAL:3}마리. 옵니다!」
					PRINTFORMW %CALLNAME:ASSI%「%CSTR:ASSI:12%, 힘내세요」
				ELSE
					PRINTFORMW %CALLNAME:ASSI%「적, 수는 {LOCAL:3}마리. 이길 수 있는 상대입니다!」
				ENDIF
			ENDCATCH
		ENDIF
	;선제공격
	ELSEIF LOCAL:2 == 1
		TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(3, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
		CATCH
			PRINTFORML %CALLNAME:ASSI%「적 발견! 수는 {LOCAL:3}마리. 아직 이쪽을 눈치채지 못했습니다」
			PRINTFORMW %CALLNAME:ASSI%「선제공격할 수 있어요, %CSTR:ASSI:12%」
		ENDCATCH
	;백어택
	ELSEIF LOCAL:2 == 2
		TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(4, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
		CATCH
				PRINTFORMW %CALLNAME:ASSI%「배후의 기습입니다, 버텨주세요!」
		ENDCATCH
	;백어택（하쿠타로우 발동）
	ELSEIF LOCAL:2 == 3
		TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(5, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
		CATCH
			PRINTFORML %CALLNAME:ASSI%「하쿠타로 발동, 적의 백 어택을 막았습니다」
			PRINTFORMW %CALLNAME:ASSI%「적의 수 {LOCAL:3}. 요격을!」
		ENDCATCH
		LOCAL:11 = 0
	;기습
	ELSEIF LOCAL:2 == 4
		TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(6, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
		CATCH
			PRINTFORML %CALLNAME:ASSI%「적의 기습입니다. 수는 {LOCAL:3}!」
			PRINTFORMW %CALLNAME:ASSI%「공격이 옵니다. 견뎌주세요!」
		ENDCATCH
	;기습（하쿠타로우 발동）
	ELSEIF LOCAL:2 == 5
		TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(7, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
		CATCH
			PRINTFORML %CALLNAME:ASSI%「하쿠타로 발동, 적의 기습을 막았습니다!」
			PRINTFORMW %CALLNAME:ASSI%「적의 수 {LOCAL:3}. 요격을!」
		ENDCATCH
		LOCAL:11 = 0
	ENDIF
ELSEIF LOCAL:1 == 2
	PRINTL 
	DRAWLINE
	PRINTL
	IF LOCAL:2 == 1
		;아군の레벨が적の레벨より１０以上高い
		IF LOCAL:18 - LOCAL:18 >= 10
			TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(8, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
			CATCH
				PRINTFORMW %CALLNAME:ASSI%「섬멸 완료, 수고하셨습니다!」
			ENDCATCH
		;아군の레벨が적の레벨より１０以上低い
		ELSEIF LOCAL:17 - LOCAL:18 >= 10
			TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(9, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
			CATCH
				PRINTFORMW %CALLNAME:ASSI%「대단해요, 저런 강적을 쓰러트리다니……!」
			ENDCATCH
		;同레벨오비
		ELSE
			TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(10, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
			CATCH
				PRINTFORML %CALLNAME:ASSI%「모든 적의 배제를 확인했습니다!」
				PRINTFORMW %CALLNAME:ASSI%「여러분, 수고하셨습니다」
			ENDCATCH
		ENDIF
	ELSEIF LOCAL:2 == 2
		TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(11, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
		CATCH
			PRINTFORML %CALLNAME:ASSI%「그럴 수가……전멸!?」
			PRINTFORMW %CALLNAME:ASSI%「정신차리세요 %CSTR:ASSI:12%, %CSTR:ASSI:12%!」
		ENDCATCH
	ENDIF
;전투중
ELSEIF LOCAL:1 == 3
	;初ターンフラグが立っていたらそれをへし折ってから何もせずに戻す
	IF LOCAL:11 > 0
		LOCAL:11 -= 1
		RETURN 0
	ENDIF
	PRINTL 
	DRAWLINE
	PRINTL
	;적の現在の数をLOCAL:4に収める
	LOCAL:4 = 0
	FOR LOCAL,7,17
		LOCALS = 포지션{LOCAL}
		SIF FLAG:LOCALS < 0
			CONTINUE
		LOCAL:4 += 1
	NEXT

	;人数差で優劣を報告　이상상태のメンバーを報告　ＨＰがやばいメンバーの報告
	LOCAL:6 = 1		;스테이터스체크関数に引き渡すための自陣포지션を収める変数
	FOR LOCAL,1, 7
		LOCALS = 포지션{LOCAL}
		SIF FLAG:LOCALS < 0
			CONTINUE
		CALL CHECK_STATUS(LOCAL:6)
		SELECTCASE RESULT
			;報告事項なし
			CASE 0
			;ＨＰが50%を切った
			CASE 1
				LOCAL:8 += 1
			;チャーム
			CASE 2
				LOCAL:7 += 1
			;スリープ
			CASE 3
				LOCAL:7 += 1
			;パニック
			CASE 4
				LOCAL:7 += 1
			;ハッピー
			CASE 5
				LOCAL:7 += 1
			;石化
			CASE 6
				LOCAL:7 += 1
			;麻痺
			CASE 7
				LOCAL:7 += 1
			;毒
			CASE 8
				LOCAL:7 += 1
			;ＨＰ25%を切った
			CASE 9
				LOCAL:9 += 1
			;死亡している
			CASE 10
				LOCAL:10 += 1
			;他の状態異常
			CASE 11
				LOCAL:7 += 1
		ENDSELECT
		LOCAL:6 += 1
	NEXT
	;사망한 아군이 있다
	IF LOCAL:10 > 0
		IF LOCAL:10 > 1 
				IF FLAG:도주불가플래그
					TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(12, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
					CATCH
						PRINTFORMW %CALLNAME:ASSI%「{LOCAL:10}명 전투 불능! 전멸하기 전에 어서 소생을!」
					ENDCATCH
				ELSE
					TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(13, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
					CATCH
						IF RAND:2 == 1
							PRINTFORMW %CALLNAME:ASSI%「{LOCAL:10}명 전투 불능。피해가 막심합니다、도주도 생각해주세요！」
						ELSE
							PRINTFORMW %CALLNAME:ASSI%「{LOCAL:5}명 중 {LOCAL:10}명 전투 불능! 즉시 소생해주세요!」
						ENDIF
					ENDCATCH
				ENDIF
		ELSE
			LOCAL:6 = 1		;스테이터스체크関数に引き渡すための自陣포지션を収める変数
			LOCAL:20 = 0	;0인간の아군は死んでいない,1인간の아군が死んでいる
			FOR LOCAL,1, 7
				LOCALS = 포지션{LOCAL}
				SIF FLAG:LOCALS < 0
					CONTINUE
				CALL CHECK_STATUS(LOCAL:6)
				IF RESULT == 10
					LOCAL:12 = FLAG:LOCALS
					IF LOCAL:12 == MASTER
						TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(14, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
						CATCH
							PRINTFORMW %CALLNAME:ASSI%「괜찮습니까 %CSTR:ASSI:12%!? 누군가 소생을!」
						ENDCATCH
					ELSE
						TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(15, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
						CATCH
							PRINTFORMW %CALLNAME:ASSI%「%CALLNAME:(LOCAL:12)% 전투 불능! 소생해주세요!」
						ENDCATCH
					ENDIF
					LOCAL:20 = 1
				ENDIF
				LOCAL:6 += 1
			NEXT
		ENDIF
	;COMPに戻っている場合カウントされないので、別で呼び出し。ただし인간優先
	ELSEIF LOCAL:19 - LOCAL:5 > 0
		TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(32, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
		CATCH
			PRINTFORMW %CALLNAME:ASSI%「전력 저하, 소환해서 보충해주세요!」
		ENDCATCH
	;ＨＰ가 25%대인 아군이 있다
	ELSEIF LOCAL:9 > 0
		IF LOCAL:9 > 1
			IF FLAG:도주불가플래그
				TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(16, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
				CATCH
					PRINTFORMW %CALLNAME:ASSI%「{LOCAL:9}명 빈사! 다음 공격이 오기 전에 회복을!」
				ENDCATCH
			ELSE
				TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(17, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
				CATCH
					PRINTFORMW %CALLNAME:ASSI%「{LOCAL:9}명 빈사입니다. 어서 회복해주세요」
				ENDCATCH
			ENDIF
		ELSE
			LOCAL:6 = 1		;스테이터스체크関数に引き渡すための自陣포지션を収める変数
			FOR LOCAL,1, 7
				LOCALS = 포지션{LOCAL}
				SIF FLAG:LOCALS < 0
					CONTINUE
				CALL CHECK_STATUS(LOCAL:6)
				IF RESULT == 9
					LOCAL:12 = FLAG:LOCALS
						IF LOCAL:12 == MASTER
							TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(18, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
							CATCH
								PRINTFORMW %CALLNAME:ASSI%「%조사처리(CSTR:ASSI:12,"가")% 빈사입니다, 회복해주세요!」
							ENDCATCH
						ELSE
							TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(19, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
							CATCH
								PRINTFORMW %CALLNAME:ASSI%「%조사처리(CALLNAME:(LOCAL:12),"가")% 빈사입니다, 회복해주세요!」
							ENDCATCH
						ENDIF
				ENDIF
				LOCAL:6 += 1
			NEXT
		ENDIF
	;상태이상의 아군이 있음
	ELSEIF LOCAL:7 > 0
		IF LOCAL:7 > 1
			TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(20, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
			CATCH
				PRINTFORMW %CALLNAME:ASSI%「아군 {LOCAL:7}명, 상태이상. 치료해주세요」
			ENDCATCH
		ELSE
		LOCAL:6 = 1		;스테이터스체크関数に引き渡すための自陣포지션を収める変数
		FOR LOCAL,1, 7
			LOCALS = 포지션{LOCAL}
			SIF FLAG:LOCALS < 0
				CONTINUE
			CALL CHECK_STATUS(LOCAL:6)
			LOCAL:12 = FLAG:LOCALS
			;독
			IF RESULT == 8
				TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(21, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
				CATCH
				IF LOCAL:12 == NO:MASTER
						PRINTFORML %CALLNAME:ASSI%「%CSTR:ASSI:12%, 괜찮으세요?」
						PRINTFORMW %CALLNAME:ASSI%「어서 독 상태를 치료해주세요!」
					ELSE
						PRINTFORMW %CALLNAME:ASSI%「%조사처리(CALLNAME:(LOCAL:12),"가")% 독 상태입니다, 치료를!」
					ENDIF
				ENDCATCH
			;마비
			ELSEIF RESULT == 7
				TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(22, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
				CATCH
					IF LOCAL:12 == NO:MASTER
						PRINTFORML %CALLNAME:ASSI%「%CSTR:ASSI:12%, 괜찮으세요？」
						PRINTFORMW %CALLNAME:ASSI%「누가 %CSTR:ASSI:12%의 마비를 해제해주세요!」
					ELSE
						PRINTFORMW %CALLNAME:ASSI%「%조사처리(CALLNAME:(LOCAL:12),"가")% 마비 상태입니다, 치료를!」
					ENDIF
				ENDCATCH
			;석화
			ELSEIF RESULT == 6
				TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(23, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
				CATCH
				IF LOCAL:12 == NO:MASTER
						PRINTFORML %CALLNAME:ASSI%「%조사처리(CSTR:ASSI:12,"가")% 석상으로!?」
						PRINTFORMW %CALLNAME:ASSI%「누가 %조사처리(CSTR:ASSI:12,"를")% 원래대로 되돌려 주세요!」
					ELSE
						PRINTFORMW %CALLNAME:ASSI%「%조사처리(CALLNAME:(LOCAL:12),"가")% 석화 상태입니다, 치료를!」
					ENDIF
				ENDCATCH
			;해피
			ELSEIF RESULT == 5
				TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(24, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
				CATCH
					IF LOCAL:12 == NO:MASTER
						PRINTFORML %CALLNAME:ASSI%「%조사처리(CSTR:ASSI:12,"가")% 행복한 꿈을 꿔서 움직일 수 없습니다」
						PRINTFORMW %CALLNAME:ASSI%「누가 현실로 되돌려 주세요!」
					ELSE
						PRINTFORMW %CALLNAME:ASSI%「%조사처리(CALLNAME:(LOCAL:12),"가")% 행복 상태입니다, 가능하다면 치료를!」
					ENDIF
				ENDCATCH
			;공포(패닉)
			ELSEIF RESULT == 4
				TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(25, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
				CATCH
					IF LOCAL:12 == NO:MASTER
						PRINTFORML %CALLNAME:ASSI%「%CSTR:ASSI:12%, 정신 차려주세요!」
						PRINTFORMW %CALLNAME:ASSI%「누가 %CSTR:ASSI:12%의 치료를!」
					ELSE
						PRINTFORMW %CALLNAME:ASSI%「%조사처리(CALLNAME:(LOCAL:12),"가")% 혼란 상태입니다, 아군을 때리기 전에 치료를!」
					ENDIF
				ENDCATCH
			;슬리프(수면)
			ELSEIF RESULT == 3
				TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(26, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
				CATCH
					IF LOCAL:12 == NO:MASTER
						PRINTFORML %CALLNAME:ASSI%「일어나주세요 %CSTR:ASSI:12%, 자고 있을 때가 아니에요!」
						PRINTFORMW %CALLNAME:ASSI%「누가 %조사처리(CSTR:ASSI:12,"를")% 흔들어 깨워주세요!」
					ELSE
						PRINTFORMW %CALLNAME:ASSI%「%조사처리(CALLNAME:(LOCAL:12),"가")% 자고 있습니다, 누가 깨워 주세요!」
					ENDIF
				ENDCATCH
			;매료
			ELSEIF RESULT == 2
				TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(27, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
				CATCH
					IF LOCAL:12 == NO:MASTER
						PRINTFORML %CALLNAME:ASSI%「%CSTR:ASSI:12%, 제 목소리가 들리시나요!?」
						PRINTFORMW %CALLNAME:ASSI%「누가 %CSTR:ASSI:12%의 매료를 치료해주세요!」
					ELSE
						PRINTFORMW %CALLNAME:ASSI%「%조사처리(CALLNAME:(LOCAL:12),"가")% 매료 상태입니다、치료를！」
					ENDIF
				ENDCATCH
			;その他のバステ
			ELSEIF RESULT == 11
				TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(21, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
				CATCH
				IF LOCAL:12 == NO:MASTER
						PRINTFORML %CALLNAME:ASSI%「%CSTR:ASSI:12%、괜찮습니까？」
					ELSE
						PRINTFORMW %CALLNAME:ASSI%「%조사처리(CALLNAME:(LOCAL:12),"가")% 매료 상태입니다, 치료를!」
					ENDIF
				ENDCATCH
			ENDIF
			LOCAL:6 += 1
		NEXT
		ENDIF
	;ＨＰ가 50%대인 아군이 있다
	ELSEIF LOCAL:8 > 0
		TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(28, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
		CATCH
			PRINTFORMW %CALLNAME:ASSI%「아군 {LOCAL:8}명, ＨＰ 감소. 큰일로 번지기 전에 회복해주세요」
		ENDCATCH
	;위험한 상태의 아군은 없다
	ELSE
		IF FLAG:도주불가플래그
			TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(29, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
			CATCH
				PRINTFORMW %CALLNAME:ASSI%「강적이지만 %조사처리(CSTR:ASSI:12,"라")%면 쓰러트릴 수 있습니다, 포기하지 말아주세요!」
			ENDCATCH
		ELSEIF (LOCAL:3 - LOCAL:4 > 0)
			TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(30, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
			CATCH
				IF RAND:4 == 1
					PRINTFORMW %CALLNAME:ASSI%「적 잔여{LOCAL:4}체、힘내주세요！」
				ELSEIF RAND:3 == 1
					PRINTFORMW %CALLNAME:ASSI%「그런 식입니다, 힘내주세요!」
				ELSEIF RAND:2 == 1
					PRINTFORMW %CALLNAME:ASSI%「적 {LOCAL:3}마리 중 {(LOCAL:3 - LOCAL:4)}마리 격파! 계속해서 격파를 노려주세요」
				ELSE
					PRINTFORMW %CALLNAME:ASSI%「적의 수는 줄어들고 있습니다. 이대로 공격을 계속해 주세요!」
				ENDIF
			ENDCATCH
		ELSE
			TRYCCALLFORM BATTLE_NAVI_K{NO:ASSI}(31, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:12)
			CATCH
				IF RAND:2 == 1
					PRINTFORMW %CALLNAME:ASSI%「적 남은 수 {LOCAL:4}, 힘내주세요!」
				ELSE
					PRINTFORMW %CALLNAME:ASSI%「적 세력 아직 건재, 방심하지 말아주세요!」
				ENDIF
			ENDCATCH
		ENDIF
	ENDIF
ENDIF
RETURN 0

;CHECK_STATUS
@CHECK_STATUS(LOCAL:1)
	LOCALS = 포지션{LOCAL:1}
	SIF FLAG:LOCALS < 0
		RETURN -1
	LOCAL:2 = NO:(LOCAL:1)
	LOCAL:1 = FLAG:LOCALS
	;死亡していたら報告
	IF CFLAG:(LOCAL:1):상태이상 == GET_STATE_NUM("DYING")
		RETURN 10
	;HPが25%を切ったら報告
	ELSEIF (BASE:(LOCAL:1):ＨＰ * 100 / MAXBASE:(LOCAL:1):ＨＰ) < 25
		RETURN 9
	;毒なら報告
	ELSEIF CFLAG:(LOCAL:1):상태이상 == GET_STATE_NUM("POISON")
		RETURN 8
	;麻痺なら報告
	ELSEIF CFLAG:(LOCAL:1):상태이상 == GET_STATE_NUM("PALYZE")
		RETURN 7
	;石化なら報告
	ELSEIF CFLAG:(LOCAL:1):상태이상 == GET_STATE_NUM("STONE")
		RETURN 6
	;ハッピーなら報告
	ELSEIF CFLAG:(LOCAL:1):상태이상 == GET_STATE_NUM("HAPPY")
		RETURN 5
	;パニックなら報告
	ELSEIF CFLAG:(LOCAL:1):상태이상 == GET_STATE_NUM("PANIC")
		RETURN 4
	;スリープなら報告
	ELSEIF CFLAG:(LOCAL:1):상태이상 == GET_STATE_NUM("SLEEP")
		RETURN 3
	;チャームなら報告
	ELSEIF CFLAG:(LOCAL:1):상태이상 == GET_STATE_NUM("CHARM")
		RETURN 2
	;HPが50%を切ったら報告
	ELSEIF (BASE:(LOCAL:1):ＨＰ * 100 / MAXBASE:(LOCAL:1):ＨＰ) < 50
		RETURN 1
	;それ以外のバステなら報告
	ELSEIF CFLAG:(LOCAL:1):상태이상 > 0
		RETURN 11
	;特に報告することなし
	ELSE
		RETURN 0
	ENDIF
RETURN -1
