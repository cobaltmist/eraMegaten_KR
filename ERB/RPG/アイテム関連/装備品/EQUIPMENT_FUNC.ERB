;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:EQUIPMENT_FUNC.ERB
;	Facility	:装備品関数。
;
;				 装備品処理で使用する共通関数を纏める。
;				 記載関数には先頭に"EQ_"を付ける。(EQUIPMENTの略)
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2011/10/23		旅人					新規作成
;	002		2015/10/02		魔晶拡張パッチ			EQ_GETNAME_魔晶装備に名称自由変更の処理を追加
;	003		2020/10/18		Jガン					装備確認の関数追加
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ EQ_장비부위_마정장비				EQ:장비부위(마정장비)
;	・ EQ_GETID_마정장비				EQ:아이템ID취득(마정장비)
;	・ EQ_GETNAME_마정장비_ITEM			EQ:명칭취득(마정장비)-ITEM由来
;	・ EQ_GETNAME_마정장비_CHARA		EQ:명칭취득(마정장비)-CHARA由来
;	・ EQ_GETNAME_마정장비				EQ:명칭취득(마정장비)
;
; *- 그 외
;
;	・ EQ_화면모드전환				EQ:화면모드전환
;	・ EQ_사거리명칭						EQ:사거리명칭
;
;===============================================================================

;=================================================
;   sub EQ_장비부위_마정장비
;=================================================
;   EQ:장비부위(마정장비)
;-------------------------------------------------
; Input:
;  ARGS					아이템No
; Output:
;  RETURNF				장비부위
;-------------------------------------------------
@EQ_장비부위_마정장비(ARG)
#FUNCTION
#DIM L_I
#DIM L_TMP
L_TMP=0

FOR L_I,0,7
	IF ARG < 2500 + L_I*500
		L_TMP = L_I
		BREAK
	ENDIF
NEXT
RETURNF L_TMP


;=================================================
;   sub EQ_GETID_마정장비
;=================================================
;   EQ:아이템ID취득(마정장비)
;-------------------------------------------------
;   아이템Noから마정장비のIDを취득する
;-------------------------------------------------
; Input:
;  ARGS					아이템No
; Output:
;  RETURNF				장비부위
;-------------------------------------------------
@EQ_GETID_마정장비(ARG)
#FUNCTION
#DIM L_POINT

;- EQ:장비부위(마정장비)
L_POINT = EQ_장비부위_마정장비(ARG)

RETURNF ARG - 2450 - L_POINT * 450


;=================================================
;   sub EQ_GETNAME_마정장비_ITEM
;=================================================
;   EQ:명칭취득(마정장비)-ITEM由来
;-------------------------------------------------
;   아이템Noから마정장비の명칭を返す
;-------------------------------------------------
; Input:
;  ARG					아이템No
; Output:
;  RETURNF				명칭
;-------------------------------------------------
@EQ_GETNAME_마정장비_ITEM(ARG)
#FUNCTIONS
#DIM L_TMP

L_TMP = EQ_GETID_마정장비(ARG)

RETURNF EQ_GETNAME_마정장비(L_TMP)


;=================================================
;   sub EQ_GETNAME_마정장비_CHARA
;=================================================
;   EQ:명칭취득(마정장비)-CHARA由来
;-------------------------------------------------
;   指定したキャラの장비부위に세트してある장비から마정장비の명칭を返す
;-------------------------------------------------
; Input:
;  ARG:0				キャラ
;  ARG:1				장비부위
; Output:
;  RETURNF				명칭
;-------------------------------------------------
@EQ_GETNAME_마정장비_CHARA(ARG:0, ARG:1)
#FUNCTIONS
#DIM L_TMP

;- 장비中の아이템Noを취득
L_TMP = EQUIP:ARG:(GET_EQUIP(ARG:1))

RETURNF EQ_GETNAME_마정장비_ITEM(L_TMP)


;=================================================
;   sub EQ_GETNAME_마정장비
;=================================================
;   EQ:명칭취득(마정장비)
;-------------------------------------------------
; Input:
;  ARG					데이터베이스位置
; Output:
;  RETURNF				명칭
;-------------------------------------------------
@EQ_GETNAME_마정장비(ARG)
#FUNCTIONS
	SIF DC:ARG:49 == 19
	RETURNF SAVESTR:(550+ARG)
RETURNF STR:(DC:ARG:49+100)


;-------------------------------------------------------------------------------
;   그 외
;-------------------------------------------------------------------------------

;=================================================
;   sub EQ_화면모드전환
;=================================================
;   EQ:화면모드전환
;-------------------------------------------------
@EQ_화면모드전환

;- 反転
FLAG:아이템리스트화면모드설정 = !FLAG:아이템리스트화면모드설정



;=================================================
;   sub EQ_사거리명칭
;=================================================
;   EQ:사거리명칭
;-------------------------------------------------
; Input:
;  ARG					사거리
; Output:
;  RETURNF				명칭
;-------------------------------------------------
@EQ_사거리명칭(ARG)
#FUNCTIONS

SELECTCASE ARG
	CASE 1
		RETURNF "Ｓ"
	CASE 2
		RETURNF "Ｍ"
	CASE 3
		RETURNF "Ｌ"

	CASEELSE
		;- 引っかからない場合はERROR
		RETURNF "E!"
ENDSELECT
;------- MOD 003 ADD START -----------------------
;=================================================
;   sub EQUIPMENT_CHECK
;=================================================
;   EQ:EQUIPMENT_CHECK(そのキャラが장비してるかの체크)
;-------------------------------------------------
; Input:
;  ARG					キャラ번호
;  ARG:1				장비번호
; Output:
;  RETURNF				0-장비してない
;						1-장비してる
;-------------------------------------------------
@EQUIPMENT_CHECK(ARG,ARG:1)
#FUNCTION
FOR LOCAL,10,17
	SIF EQUIP:ARG:LOCAL == ARG:1
		RETURNF 1
	SIF 개조장비(EQUIP:ARG:LOCAL) && ARG:1 == 베이스장비번호:개조장비번호(EQUIP:ARG:LOCAL)
		RETURNF 1
NEXT
RETURNF 0

;=================================================
;   sub EQUIPMENT_COUNT
;=================================================
;   EQ:EQUIPMENT_COUNT(장비してる人数の체크)
;-------------------------------------------------
; Input:
;  ARG					장비번호
; Output:
;  RETURNF				장비者の人数
;-------------------------------------------------
@EQUIPMENT_COUNT(ARG)
#FUNCTION
LOCAL:1 = 0
FOR LOCAL,0,CHARANUM
	SIF EQUIPMENT_CHECK(LOCAL,ARG)
		LOCAL:1 ++
NEXT
RETURNF LOCAL:1
;------- MOD 003 ADD END   -----------------------
