
;======================================================
;장비품의 데이터
;티타니아의원피스
;======================================================

@기본능력수정_3601,ARG,ARG:1 = -1

SELECTCASE ARG
ENDSELECT
RETURN 0

@전투능력수정_3601,ARG,ARG:1 = -1

SELECTCASE ARG
	CASE 2
		SIF ARG:1 == -1
			RETURN 70
		IF EQUIP:(ARG:1):머리 == 3201
			LOCAL = BASE:(ARG:1):LV / 3 +70
			RETURN LOCAL
		ELSE
			RETURN 70
		ENDIF
	CASE 3
		SIF ARG:1 == -1
			RETURN 25
		IF EQUIP:(ARG:1):머리 == 3201
			LOCAL = BASE:(ARG:1):LV / 4 + 25
			RETURN LOCAL
		ELSE
			RETURN 25
		ENDIF
	CASE 4
		SIF ARG:1 == -1
			RETURN 5
		LOCAL = BASE:(ARG:1):LV / 4 + 11
		RETURN LOCAL
	CASE 5
		SIF ARG:1 == -1
			RETURN 5
		LOCAL = BASE:(ARG:1):LV / 4 + 5
		RETURN LOCAL
ENDSELECT
RETURN 0

@방어상성_3601,ARG,ARG:1 = -1
SIF ARG:1 == -1
	RETURN 100
SIF EQUIP:(ARG:1):머리 != 3201
	RETURN 100
SELECTCASE ARG
	CASE 1
		RETURN 150
	CASE 2
		RETURN 150
	CASE 3
		RETURN 175
	CASE 4
		RETURN 50
	CASE 5
		RETURN 50
	CASE 6
		RETURN 50
	CASE 7
		RETURN 50
	CASE 8
		RETURN 0
	CASE 9
		RETURN 150
	CASE 12
		RETURN 50
	CASE 13
		RETURN 50
	CASE 14
		RETURN 50
	CASE 15
		RETURN 50
	CASE 16
		RETURN 50
	CASE 17
		RETURN 100
	CASE 18
		RETURN 75
ENDSELECT
RETURN 100

@공격상성_3601, ARG = -1
RETURN 2

@최저공격횟수_3601, ARG = -1

RETURN 1

@최대공격횟수_3601, ARG = -1
RETURN 1

@사거리_3601, ARG = -1
RETURN 1

@공격범위_3601, ARG = -1
RETURN 1

@장비부위_3601
RETURN GET_EQUIPNUM("몸통")

;髪飾り装備時にスキルを発揮　ドラウプニル装備で三分の魔脈が活泉に変化　ロイヤルポケット装備時に煌天の会心が静天の会心に変化
@장비추가스킬_3601,ARG,ARG:1 = -1
IF ARG:1 != -1 && ABL:(ARG:1):종족 == 36
	IF EQUIP:(ARG:1):머리 == 3201
		SELECTCASE ARG
			CASE 0
				IF EQUIP:(ARG:1):팔 == 4070
					RETURN 2402
				ELSE 
					RETURN 2405
				ENDIF
			CASE 1
				IF EQUIP:(ARG:1):총 == 2511
					RETURN 2424
				ELSE
					RETURN 2423
				ENDIF
		ENDSELECT
	ENDIF
ENDIF
RETURN 0

@필요레벨_3601
RETURN 1

@장비해설_3601
PRINTFORML 요정티타니아의 몸에 걸치는 심플하고 섬세한 원피스
PRINTFORML 티타니아의머리장식와 함께 착용하면 그 영험한 힘을 얻을 수 있다
SETCOLOR 0x87ceeb
PRINTL 《티타니아의머리장식》
RESETCOLOR
SETCOLOR 0xdc143c
PRINTL 반마인 추가스킬존재(세트효과)
PRINTL 《드라우프니르》《로열포켓》
RESETCOLOR

@장비조건_3601,ARG
SIF IS_LOOKSLIKE_MALE(ARG)
	RETURN 0
SIF BASE:ARG:LV < 51
	RETURN 0
RETURN 1

@장비조건설명_3601
PRINTL 장비조건：♀ LV51～

@구입조건_3601
RETURN 0

@인계가능불가능_3601
RETURN 1
