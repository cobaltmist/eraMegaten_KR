;======================================================
;장비품의 데이터
;히노카쿠츠치의검
;原作小説含めて、N島くん以外も使ってるし、装備制限自体はなしに
;ただ、N島くんが使うときは弓子のステに応じた基本ステ補正と自分と弓子のLVに応じて攻撃力・命中にボーナスをつけるように
;性能の計算は魔晶剣とほぼ同じで、持ち手のステータス
;この補正は弓子を陥落させている場合は好感度TOPのキャラにも発生する
;======================================================
@기본능력수정_2207,ARG,ARG:1 = -1
#LOCALSIZE 1
#LOCALSSIZE 1
LOCAL = GETCHARA([[キャラ:유미코]])
SIF LOCAL < 0
	RETURN 0
SIF ARG:1 < 0
	RETURN 0
SIF ARG:1 == GETCHARA([[キャラ:아케미]]) || (FAVORITE_1(LOCAL) == ARG:1 && 함락(LOCAL))
	RETURN BASE:LOCAL:GET_BASESTATUS(ARG) / 10
RETURN 0
@전투능력수정_2207,ARG,ARG:1 = -1
#LOCALSIZE 3
LOCAL = GETCHARA([[キャラ:유미코]])
LOCAL:1 = 0
LOCAL:2 = 40
;剣のレベル設定。弓子が陥落している場合、弓子のレベルの半分が加算される
SIF ARG:1 >=0 && LOCAL >= 0 && (NO:(ARG:1) == [[キャラ:아케미]] || 함락(LOCAL, ARG:1))
	LOCAL:2 += MIN(BASE:LOCAL:LV / 2, 40)
LOCAL:2 += 5
;レベルによる戦闘能力値
SELECTCASE GET_BATTLESTATUS(ARG)
	CASE "공격"
		LOCAL:1 = LOCAL:2 * 3/2
	CASE "명중"
		LOCAL:1 = LOCAL:2
	CASE "마법위력"
		LOCAL:1 = LOCAL:2 * 3/2
	CASE "마법효과"
		LOCAL:1 = LOCAL:2
ENDSELECT
;STATUSによる能力値　朱美ありは弓子陥落でステータスによって性能が加算される
IF ARG:1 >= 0 && (NO:(ARG:1) == [[キャラ:아케미]] || (LOCAL >=0 && 함락(LOCAL, ARG:1)))
	SELECTCASE GET_BATTLESTATUS(ARG)
		CASE "공격"
			LOCAL:1 +=  BASE:(ARG:1):힘*3/2 + BASE:(ARG:1):인내력 / 2
		CASE "명중"
			LOCAL:1 += BASE:(ARG:1):속도*3/2 + BASE:(ARG:1):운/2
		CASE "마법위력"
			LOCAL:1 += BASE:(ARG:1):마력*3/2 + BASE:(ARG:1):지혜 / 2
		CASE "마법효과"
			LOCAL:1 += BASE:(ARG:1):지혜 + BASE:(ARG:1):운/2 + BASE:(ARG:1):마력/2
	ENDSELECT
ENDIF
;剣仕様に変更
SELECTCASE GET_BATTLESTATUS(ARG)
	CASE "공격"
		TIMES LOCAL:1, 1.00
	CASE "명중"
		LOCAL:1 += 10
	CASE "마법위력"
		TIMES LOCAL:1, 0.80
	CASE "마법효과"
		TIMES LOCAL:1, 0.40
ENDSELECT
;最低でも40レベル剣の性能は保証される
SELECTCASE GET_BATTLESTATUS(ARG)
	CASE "공격"
		RETURN MAX(107, LOCAL:1)
	CASE "명중"
		RETURN MAX(94, LOCAL:1)
	CASE "마법위력"
		RETURN MAX(85, LOCAL:1)
	CASE "마법효과"
		RETURN MAX(33, LOCAL:1)
ENDSELECT
@방어상성_2207,ARG,ARG:1 = -1
SELECTCASE ARG
ENDSELECT
RETURN 100

@공격상성_2207, ARG = -1
RETURN GET_TYPE_NUM("화염")

@최저공격횟수_2207, ARG = -1
RETURN 1

@최대공격횟수_2207, ARG = -1
RETURN 1

@사거리_2207, ARG = -1
RETURN 1

@공격범위_2207, ARG = -1
RETURN 2

@장비부위_2207
RETURN GET_EQUIPNUM("검")

;-----------------------------
;- SHOP
;-----------------------------
@구입조건_2207
RETURN 0

@필요레벨_2207
RETURN 1

@장비조건_2207,ARG
RETURN 1

@장비조건설명_2207
PRINTL 특수조건:유미코와 관계가 깊은 자가 장비할 때에만 성능이 변화한다。

@장비해설_2207
PRINTL 이자나기와 이자나미의 사이에서 태어난 불의 신。
PRINTL 이자나미의 몸을 태워버려 결국 죽음에 이르게 한다。
PRINTL 격노한 이자나기에게 죽임을 당하고、그 사체로부터 수많은 신이 탄생하게 된다。
PRINTL 
PRINTL 원작 소설에서는、이자나미의 도움을 얻은 나카지마 아케미에 의해 검으로서 소환되었고、
PRINTL 여신전생 시리즈에서는 원작을 따라 검으로 취급한다。
;二行にまとめてもいいか迷う

RETURN 0


@검타입_2207
;1 = 도검　2 =레이피어　　3 = 활　　　　　　4 = 검　  5 = 창　　　6 = 도끼、둔기
;7 = 채찍　8 =투척(投具)　9 = 지팡이、제구　10= 곤봉　11= 권투구
RETURN 4