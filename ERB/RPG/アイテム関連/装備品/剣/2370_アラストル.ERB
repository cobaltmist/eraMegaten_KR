;======================================================
;장비품의 데이터
;아라스톨
;性能の計算は魔晶剣とほぼ同じで、悪魔狩人あなたのステータスの影響を受ける
;======================================================
@기본능력수정_2370,ARG,ARG:1 = -1
#LOCALSIZE 1
#LOCALSSIZE 1
LOCAL = GETCHARA(4999)
SIF LOCAL < 0
	RETURN 0
SIF ARG:1 < 0
	RETURN 0
IF ARG:1 == GETCHARA(4999)
	SELECTCASE ARG
		CASE 5
			SIF ARG == 5 && ARG:1 == GETCHARA(0) || ARG:1 == GETCHARA(4999) || (FAVORITE_1(LOCAL) == ARG:1 && 함락(LOCAL))
			RETURN BASE:LOCAL:GET_BASESTATUS(ARG) / 10 + 6
		CASEELSE
			SIF ARG:1 == GETCHARA(0)
				LOCAL = GETCHARA(0)
			SIF ARG:1 == GETCHARA(4999)
				LOCAL = GETCHARA(4999)
			SIF LOCAL < 0
				RETURN 0
			SIF ARG:1 < 0
				RETURN 0
			SIF ARG:1 == GETCHARA(0) || ARG:1 == GETCHARA(4999) || (FAVORITE_1(LOCAL) == ARG:1 && 함락(LOCAL))
				RETURN BASE:LOCAL:GET_BASESTATUS(ARG) / 10
	ENDSELECT
ENDIF
RETURN 0

@전투능력수정_2370,ARG,ARG:1 = -1
#LOCALSIZE 3
LOCAL = GETCHARA(4999)
LOCAL:1 = 0
LOCAL:2 = 40

;剣のレベル設定。あなたが陥落している場合、あなたのレベルの半分が加算される
SIF ARG:1 >= 0 && (NO:(ARG:1) == 0 || NO:(ARG:1) == 4999 || (NO:(ARG:1) == 4998 && (CFLAG:(ARG:1):당신의전공분야 == 7)) || (LOCAL >=0 && 함락(LOCAL, ARG:1)))
	LOCAL:2 += MIN(BASE:(ARG:1):LV / 2, 40)
LOCAL:2 += 5
;レベルによる戦闘能力値
SELECTCASE GET_BATTLESTATUS(ARG)
	CASE "공격"
		LOCAL:1 = LOCAL:2 * 3/2
	CASE "명중"
		LOCAL:1 = LOCAL:2
	CASE "마법위력"
		LOCAL:1 = LOCAL:2 * 3/2
	CASE "마법효과"
		LOCAL:1 = LOCAL:2
ENDSELECT

;STATUSによる能力値　あなた陥落でステータスによって性能が加算される
IF ARG:1 >= 0 && (NO:(ARG:1) == 0 || NO:(ARG:1) == 4999 || (NO:(ARG:1) == 4998 && (CFLAG:(ARG:1):당신의전공분야 == 7)) || (LOCAL >=0 && 함락(LOCAL, ARG:1)))
	SELECTCASE GET_BATTLESTATUS(ARG)
		CASE "공격"
			LOCAL:1 +=  BASE:(ARG:1):힘*3/2 + BASE:(ARG:1):인내력 / 2
		CASE "명중"
			LOCAL:1 += BASE:(ARG:1):속도*3/2 + BASE:(ARG:1):운/2
		CASE "마법위력"
			LOCAL:1 += BASE:(ARG:1):마력*3/2 + BASE:(ARG:1):지혜 / 2
		CASE "마법효과"
			LOCAL:1 += BASE:(ARG:1):지혜 + BASE:(ARG:1):운/2 + BASE:(ARG:1):마력/2
	ENDSELECT
ENDIF

;剣仕様に変更
SELECTCASE GET_BATTLESTATUS(ARG)
	CASE "공격"
		TIMES LOCAL:1, 1.10
	CASE "명중"
		LOCAL:1 += 10
	CASE "마법위력"
		TIMES LOCAL:1, 1.00
	CASE "마법효과"
		TIMES LOCAL:1, 0.30
ENDSELECT
;最低でも40レベル剣の性能は保証される
SELECTCASE GET_BATTLESTATUS(ARG)
	CASE "공격"
		RETURN MAX(107, LOCAL:1)
	CASE "명중"
		RETURN MAX(94, LOCAL:1)
	CASE "마법위력"
		RETURN MAX(95, LOCAL:1)
	CASE "마법효과"
		RETURN MAX(23, LOCAL:1)
ENDSELECT
@방어상성_2370,ARG,ARG:1 = -1
SELECTCASE ARG
ENDSELECT
RETURN 100

@공격상성_2370, ARG = -1
RETURN GET_TYPE_NUM("검격")

@최저공격횟수_2370, ARG = -1
RETURN 1

@최대공격횟수_2370, ARG = -1
RETURN 1

@사거리_2370, ARG = -1
RETURN 1

@공격범위_2370, ARG = -1
RETURN 1

@장비부위_2370
RETURN GET_EQUIPNUM("검")

@장비추가스킬_2370,ARG,ARG:1 = -1
IF ARG:1 > -1
	SELECTCASE ARG
		CASE 0
				RETURN [[스킬:라운드트립]]
		CASE 1
				RETURN [[스킬:에어하이크]]
	ENDSELECT
ENDIF
RETURN 0

;-----------------------------
;- SHOP
;-----------------------------
@구입조건_2370
RETURN 0

@필요레벨_2370
RETURN 1

@장비조건_2370,ARG
SIF (NO:ARG == 0 || NO:ARG == 4999) && (CFLAG:MASTER:당신의전공분야 == 7)
	RETURN 1
SIF NO:ARG == 4998 && (CFLAG:ARG:당신의전공분야 == 7)
	RETURN 1
SIF TALENT:ARG:석세서 == 0
	RETURN 0
SIF TALENT:ARG:무기 == 0
	RETURN 0
RETURN 1

@장비조건설명_2370
PRINTL 장비조건：악마사냥꾼 당신 전용

@장비해설_2370
PRINTL 뇌격을 머금고、가진 자에게 스피드와 비상 능력을 주는 검

@인계가능불가능_2370
RETURN 1

@검타입_2370
;1 = 도검　2 =레이피어　　3 = 활　　　　　　4 = 검　  5 = 창　　　6 = 도끼、둔기
;7 = 채찍　8 =투척(投具)　9 = 지팡이、제구　10= 곤봉　11= 권투구
RETURN 4

