;コロシアム
;とりあえず今の所전투のテスト用
;クリアフラグに던전フラグ:0:Xを사용


@SHOPCOMABLE_102
#DIM LCOUNT
RESULT:1 = 2
RESULTS = 콜로세움참가
SIF FLAG:콜로세움참가완료
	RETURN 0
;당신が臨月だと
SIF (TALENT:MASTER:임신 && CFLAG:MASTER:출산予定日 - 2 <= DAY)
	RETURN 0
;당신육아중だと
SIF TALENT:MASTER:육아중
	RETURN 0
;パーティに一人이상いるなら
LOCAL = 0
FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:PT플래그 == 2
		LOCAL += 1
NEXT
IF LOCAL > 0
	RETURN 1
ELSE
	RETURN 0
ENDIF


@SHOW_COLOSSEUM
CUSTOMDRAWLINE =

FOR LOCAL, 0, 999
	TFLAG:LOCAL = 0
	RESULTS = 
	TRYCCALLFORM PLAYABLE_COLOSSEUM_{LOCAL}
		IF RESULT
			TFLAG:LOCAL = 1
			IF RESULTS != ""
				PRINTFORML [{LOCAL}] - %RESULTS%
			ENDIF
		ENDIF
	CATCH
		
	ENDCATCH
NEXT

@SHOP_COM_102
CALL SHOW_COLOSSEUM
PRINTL [1000] 돌아간다

DRAWLINE
PRINTL 어느 시합에 출장합니까?

$INPUT_LOOP
INPUT
IF RESULT == 1000
	FLAG:SHOP커맨드 = 0
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= VARSIZE("TFLAG")
	GOTO INPUT_LOOP
ELSEIF TFLAG:RESULT || FLAG:DEBUG 
	FLAG:진행중콜로세움 = RESULT
	TRYCALLFORM COLOSSEUM_{RESULT}
	FOR LOCAL,0,CHARANUM
		SIF CFLAG:LOCAL:악마변신 == 1
			CALL ACTION_2310 , LOCAL , 0
	NEXT
	FLAG:진행중콜로세움 = 0
	FLAG:SHOP커맨드 = 0
	FLAG:콜로세움참가완료 = 1
	IF RESULT == 1
		VARSET TFLAG,-1
		BEGIN TURNEND
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

VARSET TFLAG,-1
RETURN 0


;====================================================================
;투기장패배
;====================================================================
@LOSE_COLOSSEUM
#LOCALSIZE 2
#LOCALSSIZE 1
#DIM LINE
#DIM 元TARGET
LOCALS = 0
FOR LOCAL, 1, 10
	RESULT = 0
	TRYCALLFORM CL_LOSE_ABLE_{LOCAL}
	SIF RESULT
		LOCALS += @"/{LOCAL}"
NEXT
TFLAG:투기장패배 = TOINT(RAND_SPLIT(LOCALS, "/"))
;入場者などの描写
CALLFORM CL_LOSE_START_{TFLAG:투기장패배}
CUSTOMDRAWLINE ・
元TARGET = TARGET
FOR LOCAL, 1, 7
	TARGET = POS(LOCAL)
	SIF TARGET < 0
		CONTINUE
	CALL KOJO_MESSAGE_LOSE_COLOSSEUM
	PRINTL 
	DRAWLINE
NEXT
TARGET = 元TARGET
CUSTOMDRAWLINE ・
;末路などの描写
CALLFORM CL_LOSE_END_{TFLAG:투기장패배}
CALL GAME_OVER

@MESSAGE_LOSE_COLOSSEUM
CALLFORM CL_LOSE_MAIN_{TFLAG:투기장패배}




