@SHOPCOMABLE_193
[IF_DEBUG]
	RESULTS = 스킬분포작성
	RETURN 1
[ENDIF]
IF FLAG:DEBUG
	RESULTS = 스킬분포작성
	RETURN 1
ENDIF
RETURN -1

@SHOP_COM_193
;LOCAL 스킬번호回し
;LOCAL:1 キャラクタ번호回し
;LOCAL:2 스킬検索用
PRINTL [0]남자 [1]여자　[2]양방　[3]캐릭터를 표시하지 않는다
CALL INPUTINT,0,1,2,3
LOCAL:4 = RESULT
PRINTL [0]스킬을 전부 표시 [1]표시하는 스킬 번호를 결정
CALL INPUTINT,0,1
IF RESULT == 0
	LOCAL:6 = 1
	LOCAL:7 = 3000
ELSE
	PRINTL 시작하는 스킬 번호 입력
	INPUT
	IF RESULT < 1
		PRINTL 번호 입력이 적기 때문에 처음부터 합니다
		LOCAL:6 = 1
	ELSEIF RESULT > 2999
		PRINTL 번호 입력이 많기 때문에 마지막 직전부터 합니다
		LOCAL:6 = 2999
	ELSEIF RESULT > 1 && RESULT < 3000
		LOCAL:6 = RESULT
	ELSE
		PRINTL 번호 입력이 부정확해서 처음부터 합니다
		LOCAL:6 = 1
	ENDIF
	PRINTL 종료하는 스킬 번호 입력
	INPUT
	IF RESULT <= LOCAL:6
		PRINTL 번호 입력이 적기 때문에 처음의 직후로 바꿉니다
		LOCAL:7 = LOCAL:6 + 1
	ELSEIF RESULT > 3000
		PRINTL 번호 입력이 많기 때문에 마지막까지 합니다
		LOCAL:7 = 3000
	ELSEIF RESULT > 1 && RESULT < 3000
		LOCAL:7 = RESULT
	ELSE
		PRINTL 번호 입력이 부정확해서 마지막까지 합니다
		LOCAL:7 = 3000
	ENDIF
ENDIF

PRINTL [0]단번에 표시 [1]4상성마다 멈춘다
CALL INPUTINT,0,1
LOCAL:5 = RESULT

CLEARLINE 5000
PRINTFORML ;-------------------------------
PRINTFORML ;%GAMEBASE_TITLE% {GAMEBASE_VERSION/1000},{GAMEBASE_VERSION % 1000}
GETTIME
PRINTFORML ;%RESULTS%
PRINTFORML ;-------------------------------
PRINTFORML ;-------------------------------
PRINTFORML ;この資料は資料フォルダ内にある
PRINTFORML ;SORT_DISTRIBUTION.ERBを이용して作成されています
PRINTFORML ;何か추가する際はコピペするのではなく、
PRINTFORML ;SORT_DISTRIBUTION.ERBを이용して출력してください
PRINTFORML ;-------------------------------
FOR LOCAL,LOCAL:6,LOCAL:7
	SIF LOCAL == 2101
		LOCAL = 2400
	TRYCCALLFORM SKILL_NAME_{LOCAL}
		;랭크0か、계승타입23の스킬は無視
		CALLFORM SKILL_RANK_{LOCAL}
		SIF RESULT == 0
			CONTINUE
		CALLFORM SKILL_SUCCESSION_TYPE_{LOCAL}
		SIF RESULT == 23
			CONTINUE
		
		PRINTFORML [{LOCAL}] %RESULTS%
		;性能を記述
		;========================================================================
		
		TRYCALLFORM SKILL_RANK_{LOCAL}		
		PRINTFORM 랭크:{RESULT,2}　
		RESULT = 0
		TRYCALLFORM SKILL_HITRATE_{LOCAL}
		PRINTFORM 명중:{RESULT,4}　
		RESULT = 0
		TRYCALLFORM SKILL_POWER_{LOCAL}
		PRINTFORM 위력:{RESULT,4}　
		RESULT = 0
		TRYCALLFORM SKILL_COSTTYPE_{LOCAL}
		LOCAL:1 = RESULT
		RESULT = 0
		TRYCALLFORM SKILL_COST_{LOCAL}
		PRINTFORML \@ LOCAL:1 == 2 ? 소비HP:{RESULT,3}％ # 소비MP:{RESULT,3} \@

		SELECTCASE LOCAL
			;罪아군の専用마법シリーズは타겟いねーと落ちるので
			CASE 1520, 1620, 1720 ,1923, 2025
				PRINTFORM 상성:특수　
			CASEELSE
				RESULT = -1
				TRYCALLFORM SKILL_TYPE_{LOCAL}
				IF RESULT == -1
					PRINTFORM 상성:특수　
				ELSE
					PRINTFORM 상성:%GET_TYPE(RESULT), 6%　
				ENDIF
		ENDSELECT
		TRYCALLFORM SKILL_DECIDE_TYPE_{LOCAL}
		PRINTFORM \@RESULT == 1 ? EX # MA\@　
		RESULT = 0
		TRYCALLFORM SKILL_SPHERE_{LOCAL}
		PRINT 범위:
		SELECTCASE RESULT
			CASE 1
				PRINTFORM １체　　
			CASE 2
				PRINTFORM 열　　　
			CASE 3
				PRINTFORM 전체　　
			CASE 4
				PRINTFORM 적아군　
			CASEELSE
				PRINT 없음　　
		ENDSELECT
		TRYCALLFORM SKILL_EFECT_{LOCAL}
		PRINT 종류:
		SELECTCASE RESULT
			CASE 1
				PRINTFORM 공격　　　　
			CASE 2
				PRINTFORM 회복　　　　
			CASE 3
				PRINTFORM 상태변화　　
			CASE 4
				PRINTFORM 특수　　　　
			CASE 5
				PRINTFORM 상태회복　　
			CASEELSE
				PRINTFORM 그외　　　
		ENDSELECT
		TRYCCALLFORM SKILL_MINATTACKNUMBER_{LOCAL},-1
			PRINTFORM 공격횟수:{RESULT}～
			CALLFORM SKILL_MAXATTACKNUMBER_{LOCAL},-1
			PRINTFORM {RESULT}
			TRYCCALLFORM SKILL_MAXATK_PER_{LOCAL},-1
				PRINTFORM 　１체 당 {RESULT}HIT까지
			CATCH
			ENDCATCH
		CATCH
		ENDCATCH
		PRINTL
		
		TRYCALLFORM SKILL_EXPLAIN_{LOCAL}
		PRINTL 
		;========================================================================
		IF LOCAL:4 != 3
			FOR LOCAL:1,1,4500
				;特定の종족を飛ばす処理
				SELECTCASE (LOCAL:1+99)/100
					CASE 0
						LOCAL:1 += 99
						CONTINUE
					CASE IS < 19
					CASE 40,42,44
					CASEELSE
						LOCAL:1 += 99
						CONTINUE
				ENDSELECT
				SIF EXISTCSV(LOCAL:1,0) == 0
					CONTINUE
				SIF CSVTALENT(LOCAL:1,GETNUM(TALENT,"남자"),0) == LOCAL:4
					CONTINUE
				SIF CSVCFLAG(LOCAL:1,GETNUM(CFLAG,"전서소환불가"),0) == 1
					CONTINUE
				
				LOCAL:3 = -1
				FOR LOCAL:2,1,FLAG:스킬수+1
					SIF CSVABL(LOCAL:1,GETNUM(ABL,"스킬"+ TOSTR(LOCAL:2)),0) == LOCAL
						LOCAL:3 = 0
					SIF LOCAL:3 > -1
						BREAK
				NEXT
				FOR LOCAL:2,1,21
					SIF CSVABL(LOCAL:1,GETNUM(ABL,"습득스킬"+ TOSTR(LOCAL:2)),0) == LOCAL
						LOCAL:3 = CSVABL(LOCAL:1,GETNUM(ABL,"습득LV"+ TOSTR(LOCAL:2)),0)
					SIF LOCAL:3 > -1
						BREAK
				NEXT
				IF LOCAL:3 == 0
					PRINTFORML 　　　[{LOCAL:1,4}] %CSVCALLNAME(LOCAL:1,0),20,LEFT% LV{CSVBASE(LOCAL:1,GETNUM(BASE,"LV"),0)}　　　초기습득
				ELSEIF LOCAL:3 > 0
					PRINTFORML 　　　[{LOCAL:1,4}] %CSVCALLNAME(LOCAL:1,0),20,LEFT% LV{CSVBASE(LOCAL:1,GETNUM(BASE,"LV"),0)}　　　습득LV:{LOCAL:3}
				ENDIF
				
			NEXT
		ENDIF
		PRINTL
	CATCH
	ENDCATCH
	IF LOCAL % 400 == 0 && LOCAL:5 == 1
		PRINTW
	ENDIF
NEXT
WAIT





