@SUPER_27
#LOCALSIZE 10
#LOCALSSIZE 10
;リール
#DIM REEL, 1
#DIM REEL1, 64
#DIM REEL2, 64
#DIM REEL3, 64
;スロット
;1 2 3
;4 5 6
;7 8 9
#DIM SLOT, 10
#DIM HOLD, 4
#DIM WIN, 1
#DIM CHALLENGE, 6
PRINTL 바니:
PRINTL 「아찔한 즐거움의 세계에 어서 오세요
PRINTW 　이 슬롯 머신은 조금 어려워요！」
PRINTL 「어떻게 할 거야？」
$INPUT_START
CALL INPUT_SELECT(1, "게임 스타트", 2, "어드바이스를 듣는다", 9, "밖으로 나간다")
IF RESULT > 1
	PRINTL 바니:
	IF RESULT == 2
		PRINTL 「슈퍼２７은、1게임 1코인
		PRINTL 　이 슬롯에는 ＨＯＬＤ게임과 
		PRINTW 　ＢＯＮＵＳ게임이 있어요」
		PRINTL 「ＨＯＬＤ게임에서는 멈춰 두고 싶은 
		PRINTL 　릴이 있으면 ＨＯＬＤ해 둘 수 있어 
		PRINTW 　왼쪽에서부터  [1]･[2]･[3]으로 멈춰요」
		PRINTL 「그렇지만、ＨＯＬＤ는 계속해서 할 수 없어요
		PRINTL 　２개의 심볼이 모이면 
		PRINTW 　그것을 멈추는 것이 좋아요」
		PRINTW 「앞으로는 스스로 궁리해 보세요！」
		PRINTL 「심볼이 센터라인에 늘어서면 
		PRINTW 　ＢＯＮＵＳ게임을 선택할 수 있어요」
		PRINTL 「ＢＯＮＵＳ는 １게임 ５코인
		PRINTL 　２７라인 게임이 돼
		PRINTW 　요컨데 이런 느낌이군요」
		PRINTL 
		PRINTL ◯◯◯　◯◯・　◯◯・　
		PRINTL ・・・　・・◯　・・・　
		PRINTL ・・・　・・・　・・◯　
		PRINTL 
		PRINTL ◯・◯　◯・・　◯・・　
		PRINTL ・◯・　・◯◯　・◯・　
		PRINTL ・・・　・・・　・・◯　
		PRINTL 
		PRINTL ◯・◯　◯・・　◯・・　
		PRINTL ・・・　・・◯　・・・　
		PRINTW ・◯・　・◯・　・◯◯　
		PRINTL 
		PRINTL ・◯◯　・◯・　・◯・　
		PRINTL ◯・・　◯・◯　◯・・　
		PRINTL ・・・　・・・　・・◯　
		PRINTL 
		PRINTL ・・◯　・・・　・・・　
		PRINTL ◯◯・　◯◯◯　◯◯・　
		PRINTL ・・・　・・・　・・◯　
		PRINTL 
		PRINTL ・・◯　・・・　・・・　
		PRINTL ◯・・　◯・◯　◯・・　
		PRINTW ・◯・　・◯・　・◯◯　
		PRINTL 
		PRINTL ・◯◯　・◯・　・◯・　
		PRINTL ・・・　・・◯　・・・　
		PRINTL ◯・・　◯・・　◯・◯　
		PRINTL 
		PRINTL ・・◯　・・・　・・・　
		PRINTL ・◯・　・◯◯　・◯・　
		PRINTL ◯・・　◯・・　◯・◯　
		PRINTL 
		PRINTL ・・◯　・・・　・・・　
		PRINTL ・・・　・・◯　・・・　
		PRINTW ◯◯・　◯◯・　◯◯◯　
		PRINTL 
		PRINTL 바니:
		PRINTW 「그렇지만、ＨＯＬＤ는 할 수 없어요」
		PRINTL 「ＢＯＮＵＳ에 들어갔을 때 맞은 것 이외의 
		PRINTL 　６개의 심볼이 챌린지 심볼이 되어」
		PRINTL 「릴 아래에 챌린지 심볼의 라이트가 나와
		PRINTW 　맞으면 라이트가 사라져 가요」
		PRINTL 「챌린지 심볼이 전부 사라질까
		PRINTL 　ＷＩＮ의 코인이 전부 사라질까
		PRINTW 　１０００코인정도에서 결정하세요」
		PRINTL 「ＪＰ심볼은 ＢＯＮＵＳ가 아니어도
		PRINTL 　상중하의 어디라도 정답
		PRINTW 　배당은 랜덤이니 기대해주세요！」
		PRINTL 「ＢＯＮＵＳ를 그만둘 때는 
		PRINTW 　ＴＡＫＥ ＳＣＯＲＥ 해！」
		PRINTW 「ＬＥＴ’Ｓ ＴＲＹ！！」
	ELSEIF RESULT  == 9
		PRINTFORMW 「당신의 코인은 {MONEY:3}매야。또 와줘！」
		REDRAW 1
		RETURN 1
	ENDIF
	PRINTL 바니:
	PRINTL 「어떻게 할 거야？」
	GOTO INPUT_START
ENDIF
VARSET LOCAL
;ライン作成 16周期*4でJP以外をふる
;REELに順番に振っていく
FOR LOCAL:2, 1, 4
	FOR LOCAL:1, 0, 4
		LOCAL:4 = RAND:4
		FOR LOCAL, 0, 16
			IF LOCAL:4 == 0
				SELECTCASE LOCAL
					;７　１００点
					CASE 0
						LOCAL:3 = 0
					;♀　５０点
					CASE 1
						LOCAL:3 = 1
					;♂　５０点
					CASE 2
						LOCAL:3 = 2
					;太陽　２０点
					CASE 3,4,5
						LOCAL:3 = 3
					;月　２０点
					CASE 6,7
						LOCAL:3 = 4
					;★　２０点
					CASE 8,9
						LOCAL:3 = 5
					;●　１０点
					CASE 10,11,12
						LOCAL:3 = 6
					;ダイア 5点
					CASE 13,14,15
						LOCAL:3 = 7
				ENDSELECT
			ELSEIF LOCAL:4 == 1
				SELECTCASE LOCAL
					;７　１００点
					CASE 0
						LOCAL:3 = 0
					;♀　５０点
					CASE 1,2
						LOCAL:3 = 1
					;♂　５０点
					CASE 3
						LOCAL:3 = 2
					;太陽　２０点
					CASE 4, 5
						LOCAL:3 = 3
					;月　２０点
					CASE 6, 7
						LOCAL:3 = 4
					;★　２０点
					CASE 8, 9, 10
						LOCAL:3 = 5
					;●　１０点
					CASE 10, 11, 12
						LOCAL:3 = 6
					;ダイア 5点
					CASE 13, 14, 15
						LOCAL:3 = 7
				ENDSELECT
			ELSEIF LOCAL:4 == 2
				SELECTCASE LOCAL
					;７　１００点
					CASE 0
						LOCAL:3 = 0
					;♀　５０点
					CASE 1
						LOCAL:3 = 1
					;♂　５０点
					CASE 2, 3
						LOCAL:3 = 2
					;太陽　２０点
					CASE 4, 5
						LOCAL:3 = 3
					;月　２０点
					CASE 6, 7, 8
						LOCAL:3 = 4
					;★　２０点
					CASE 10, 11
						LOCAL:3 = 5
					;●　１０点
					CASE 11, 12
						LOCAL:3 = 6
					;ダイア 5点
					CASE 13, 14, 15
						LOCAL:3 = 7
				ENDSELECT
			ELSE
				SELECTCASE LOCAL
					;７　１００点
					CASE -1
						LOCAL:3 = 0
					;♀　５０点
					CASE 0, 1
						LOCAL:3 = 1
					;♂　５０点
					CASE 2, 3
						LOCAL:3 = 2
					;太陽　２０点
					CASE 4, 5
						LOCAL:3 = 3
					;月　２０点
					CASE 6, 7
						LOCAL:3 = 4
					;★　２０点
					CASE 8, 9
						LOCAL:3 = 5
					;●　１０点
					CASE 10, 11, 12
						LOCAL:3 = 6
					;ダイア 5点
					CASE 13, 14, 15
						LOCAL:3 = 7
				ENDSELECT
			ENDIF
			SELECTCASE LOCAL:2
				CASE 1
					REEL1:(LOCAL + LOCAL:1 * 16) = LOCAL:3
				CASE 2
					REEL2:(LOCAL + LOCAL:1 * 16) = LOCAL:3
				CASE 3
					REEL3:(LOCAL + LOCAL:1 * 16) = LOCAL:3
			ENDSELECT
		NEXT
		;16個の中で入れ替え
		FOR LOCAL, LOCAL:1*16, LOCAL:1*16+16
			LOCAL:3 = RAND:16+LOCAL:1*16
			SELECTCASE LOCAL:2
				CASE 1
					SWAP REEL1:LOCAL, REEL1:(LOCAL:3)
				CASE 2
					SWAP REEL2:LOCAL, REEL2:(LOCAL:3)
				CASE 3
					SWAP REEL3:LOCAL, REEL3:(LOCAL:3)
			ENDSELECT
		NEXT
	NEXT
NEXT
;７が存在しないリールが誕生していた場合は７を作る
SIF !MATCH(REEL1, 0)
	REEL1:(RAND:4*16) = 0
SIF !MATCH(REEL2, 0)
	REEL2:(RAND:4*16) = 0
SIF !MATCH(REEL3, 0)
	REEL3:(RAND:4*16) = 0

;境界で同一のがきている場合は片方をJPにする
SIF REEL1 == REEL1:63
	REEL1:63 = 8
SIF REEL1:15 == REEL1:16
	REEL1:16 = 8
SIF REEL1:31 == REEL1:32
	REEL1:32 = 8
SIF REEL1:47 == REEL1:48
	REEL1:48 = 8
SIF REEL2 == REEL2:63
	REEL2:63 = 8
SIF REEL2:15 == REEL2:16
	REEL2:16 = 8
SIF REEL2:31 == REEL2:32
	REEL2:32 = 8
SIF REEL2:47 == REEL2:48
	REEL2:48 = 8
SIF REEL3 == REEL3:63
	REEL3:63 = 8
SIF REEL3:15 == REEL3:16
	REEL3:16 = 8
SIF REEL3:31 == REEL3:32
	REEL3:32 = 8
SIF REEL3:47 == REEL3:48
	REEL3:48 = 8
;JP作成(7は潰さない)
$JP_START
LOCAL = RAND:3*16+15
SIF REEL1:LOCAL != 7
	REEL1:LOCAL = 8
LOCAL = RAND:3*16+15
SIF REEL2:LOCAL != 7
	REEL2:LOCAL = 8
LOCAL = RAND:3*16+15
SIF REEL3:LOCAL != 7
	REEL3:LOCAL = 8
SIF MATCH(REEL1, 8) == 0 || MATCH(REEL2, 8) == 0 || MATCH(REEL3, 8) == 0
	GOTO JP_START
;リールの中身確認
;FOR LOCAL, 0, 64
;	PRINTFORM [{LOCAL}] 
;	CALL SLOT_SYMBOL, REEL1:LOCAL
;	CALL SLOT_SYMBOL, REEL2:LOCAL
;	CALL SLOT_SYMBOL, REEL3:LOCAL
;	PRINTL 
;NEXT
;WAIT
REEL = 0
SLOT = 0
SLOT:1 = REEL1:0
SLOT:4 = REEL1:1
SLOT:7 = REEL1:2
SLOT:2 = REEL2:0
SLOT:6 = REEL2:1
SLOT:8 = REEL2:2
SLOT:3 = REEL3:0
SLOT:6 = REEL3:1
SLOT:9 = REEL3:2
FONTSTYLE 3
PRINTL ＳＵＰＥＲ　２７
FONTSTYLE 0
;得点表示
CALL SLOT_SYMBOL, 8
PRINTL  　５～１００　MYSTERY
CALL SLOT_SYMBOL, 0
PRINT  １００　
CALL SLOT_SYMBOL, 1
PRINTL  　５０　
CALL SLOT_SYMBOL, 2
PRINT  　５０　
CALL SLOT_SYMBOL, 3
PRINTL  　２０　
CALL SLOT_SYMBOL, 4
PRINT  　２０　
CALL SLOT_SYMBOL, 5
PRINTL  　２０　
CALL SLOT_SYMBOL, 6
PRINT  　１０　
CALL SLOT_SYMBOL, 7
PRINTL  　　５　
LOCAL:9 = LINECOUNT
$START
CLEARLINE LINECOUNT - LOCAL:9
PRINTFORML COINS {MONEY:3} WIN {WIN}
PRINTL 
PRINTL 
PRINTL 
FOR LOCAL, 1, 10
	PRINT  
	CALL SLOT_SYMBOL, SLOT:LOCAL
	PRINT  
	SIF LOCAL % 3 == 0
		PRINTL 
NEXT
IF SLOT
	FOR LOCAL, 0, 6
		CALL SLOT_SYMBOL, CHALLENGE:LOCAL % 10, CHALLENGE:LOCAL / 10 * 2
		PRINT  
	NEXT
	PRINTL 
ELSE
	FOR LOCAL, 1, 4
		IF HOLD == 0 && WIN == 0
			IF HOLD:LOCAL
				SETCOLOR COLOR("aqua")
			ELSE
				SIF HOLD:1+HOLD:2+HOLD:3 == 2
					SETCOLOR COLOR("gray")
			ENDIF
		ELSE
			SETCOLOR COLOR("gray")
		ENDIF
		SIF LOCAL > 1
			PRINT  
		PRINTFORM [{LOCAL}]
		RESETCOLOR
	NEXT
	SIF HOLD || WIN
		SETCOLOR COLOR("gray")
	PRINTL  ＨＯＬＤ
ENDIF
RESETCOLOR
IF SLOT > 0
	PRINTL [0] ＳＴＡＲＴ（５ＣＯＩＮＳ）
	PRINTL [9] ＴＡＫＥ ＳＣＯＲＥ
ELSE
	PRINTL [0] ＳＴＡＲＴ(１ＣＯＩＮ)
	PRINTL [9] ＥＸＩＴ
ENDIF
CALL INPUTINT(0, 1, 2, 3, 9)
IF RESULT == 0
	IF SLOT
		WIN -= 5
	ELSE
		IF MONEY:3 < 1
			GOTO START
		ELSE
			MONEY:3 -= 1
			FLAG:잭팟 += 10
		ENDIF
	ENDIF
ELSEIF RESULT > 0 && RESULT <= 3 && WIN == 0
	SIF HOLD == 0 && (HOLD:1 + HOLD:2 + HOLD:3 < 2 || HOLD:RESULT)
		INVERTBIT HOLD:RESULT, 0
	PRINTFORML COINS {MONEY:3} WIN {WIN}
	PRINTL 
	PRINTL 
	PRINTL 
	GOTO START
ELSEIF RESULT == 9
	IF SLOT
		CALL MESSAGE_B(1, "ＴＡＫＥ "+TOSTR(WIN)+"ＣＯＩＮＳ!!")
		MONEY:3+= WIN
		WIN = 0
		PRINTFORML COINS {MONEY:3} WIN {WIN}
		GOTO START
	ELSE
		PRINTFORMW 「당신의 코인은 {MONEY:3}매야。또 와줘！」
		REDRAW 1
		RETURN 1
	ENDIF
ENDIF
HOLD = HOLD:1+HOLD:2+HOLD:3 ? 1 # 0
LOCAL:3 = 0
LOCAL = 0
REDRAW 0
WHILE 1
	CLEARLINE LINECOUNT - LOCAL:9
	PRINTFORML COINS {MONEY:3} WIN {WIN}
	PRINTL 
	PRINTL 
	PRINTL 
	FOR LOCAL:1, 0, 3
		SETCOLOR COLOR("aqua")
		PRINT  
		IF HOLD:1 == 0
			CALL SLOT_SYMBOL, REEL1:(REEL+LOCAL:1)
			SLOT:(LOCAL:1*3+1) = REEL1:(REEL+LOCAL:1)
		ELSE
			CALL SLOT_SYMBOL, SLOT:(LOCAL:1*3+1)
		ENDIF
		PRINT   
		IF HOLD:2 == 0
			CALL SLOT_SYMBOL, REEL2:(REEL + LOCAL:1)
			SLOT:(LOCAL:1*3+2) = REEL2:(REEL + LOCAL:1)
		ELSE
			CALL SLOT_SYMBOL, SLOT:(LOCAL:1*3+2)
		ENDIF
		PRINT   
		IF HOLD:3 == 0
			CALL SLOT_SYMBOL, REEL3:(REEL+LOCAL:1)
			SLOT:(LOCAL:1*3+3) = REEL3:(REEL+LOCAL:1)
		ELSE
			CALL SLOT_SYMBOL, SLOT:(LOCAL:1*3+3)
		ENDIF
		PRINTL  
		RESETCOLOR
	NEXT
	RESETCOLOR
	IF SLOT
		FOR LOCAL:2, 0, 6
			CALL SLOT_SYMBOL, CHALLENGE:(LOCAL:2) % 10, CHALLENGE:(LOCAL:2) / 10 * 2
			PRINT  
		NEXT
		PRINTL 
	ELSE
		FOR LOCAL:2, 1, 4
			IF HOLD == 0
				IF HOLD:(LOCAL:2) == 1
					SETCOLOR COLOR("aqua")
				ELSEIF HOLD:(LOCAL:2) == 2
					SETCOLOR COLOR("red")
				ELSE
					SETCOLOR COLOR("gray")
				ENDIF
			ELSE
				SETCOLOR COLOR("gray")
			ENDIF
			SIF LOCAL:2 > 1
				PRINT  
			PRINTFORM [{LOCAL:2}]
			RESETCOLOR
		NEXT
		SETCOLOR COLOR("gray")
		PRINTL  ＨＯＬＤ
	ENDIF
	SETCOLOR COLOR("gray")
	SIF LOCAL:3 == 0
		RESETCOLOR
	PRINTL [0] ＳＴＯＰ
	SETCOLOR COLOR("gray")
	PRINTL [9] ＥＸＩＴ
	RESETCOLOR
	SIF REEL-- == 0
		REEL = 61
	IF LOCAL:3 == 0
		TINPUT 1, 1, 0
		IF RESULT == 0
			LOCAL:3 = 1
			LOCAL = 5 + RAND:5
		ENDIF
	ELSE
		TWAIT 1, 0
		IF --LOCAL < 0
			HOLD:(LOCAL:3) = 2
			LOCAL = 5 + RAND:5
			SIF LOCAL:3++ == 3
				BREAK
		ENDIF
	ENDIF
WEND
REDRAW 1
;1 2 3
;4 5 6
;7 8 9
CLEARLINE LINECOUNT - LOCAL:9
IF SLOT == 0
	IF MATCH(SLOT, 8) == 3
		SELECTCASE RAND:8
			CASE 0
				WIN = 100
			CASE 1,2
				WIN = 50
			CASE 3,4,5
				WIN = 20
			CASE 6
				WIN = 10
			CASE 7
				WIN = 5
		ENDSELECT
		CALL MESSAGE_BL(1, "ＧＥＴ "+TOSTR(WIN)+"ＣＯＩＮＳ!!")
		LOCAL:1 = 0
		FOR LOCAL, 1, 10
			PRINT  
			CALL SLOT_SYMBOL, SLOT:LOCAL, 2 - SLOT:LOCAL == 8
			PRINT  
			IF SLOT:LOCAL != 8
				CHALLENGE:(LOCAL:1) = SLOT:LOCAL
				LOCAL:1 += 1
			ENDIF
			SIF LOCAL % 3 == 0
				PRINTL 
		NEXT
	ELSEIF GROUPMATCH(SLOT:4, SLOT:5, SLOT:6) == 2
		SLOT = SLOT:4
		SELECTCASE SLOT:4
			CASE 0
				WIN = 100
			CASE 1,2
				WIN = 50
			CASE 3,4,5
				WIN = 20
			CASE 6
				WIN = 10
			CASE 7
				WIN = 5
		ENDSELECT
		CHALLENGE = SLOT:1
		CHALLENGE:1 = SLOT:2
		CHALLENGE:2 = SLOT:3
		CHALLENGE:3 = SLOT:7
		CHALLENGE:4 = SLOT:8
		CHALLENGE:5 = SLOT:9
		CALL MESSAGE_BL(1, "ＧＥＴ "+TOSTR(WIN)+"ＣＯＩＮＳ!!")
		FOR LOCAL, 1, 10
			PRINT  
			CALL SLOT_SYMBOL, SLOT:LOCAL, 2 - (LOCAL >= 4 && LOCAL <= 6) 
			PRINT  
			SIF LOCAL % 3 == 0
				PRINTL 
		NEXT
		SETCOLOR COLOR("gray")
		PRINTL  [1]  [2]  [3]ＨＯＬＤ
		PRINTL [0] ＳＴＯＰ
		PRINTW [9] ＥＸＩＴ
		RESETCOLOR
		CALL MESSAGE_B(1, "ＢＯＮＵＳ ＧＡＭＥ ＳＴＡＲＴ!!")
		SLOT = 1
	ENDIF
ELSE
	LOCAL:3 = 0
	FOR LOCAL:2, 1, 8, 3
		FOR LOCAL:1, 2, 9, 3
			FOR LOCAL, 3, 10, 3
				IF GROUPMATCH(SLOT:LOCAL, SLOT:(LOCAL:1), SLOT:(LOCAL:2)) == 2
					LOCAL:4 = SLOT:LOCAL == 8 ? RAND:8 # SLOT:LOCAL
					SELECTCASE LOCAL:4
						CASE 0
							LOCAL:3 = 100
						CASE 1,2
							LOCAL:3 = 50
						CASE 3,4,5
							LOCAL:3 = 20
						CASE 6
							LOCAL:3 = 10
						CASE 7
							LOCAL:3 = 5
					ENDSELECT
					CALL MESSAGE_BL(1, "ＧＥＴ "+TOSTR(LOCAL:3)+"ＣＯＩＮＳ!!")
					FOR LOCAL:4, 1, 10
						PRINT  
						CALL SLOT_SYMBOL, SLOT:(LOCAL:4), 2 - (LOCAL:4 == LOCAL || LOCAL:4 == LOCAL:1 || LOCAL:4 == LOCAL:2)
						PRINT  
						SIF LOCAL:4 % 3 == 0
							PRINTL 
					NEXT
					;CHALLENGE権消失の判定
					FOR LOCAL:4, 0, 6
						SIF CHALLENGE:(LOCAL:4) >= 10
							CONTINUE
						SIF CHALLENGE:(LOCAL:4) != SLOT:LOCAL
							CONTINUE
						CHALLENGE:(LOCAL:4) += 10
						BREAK
					NEXT
					FOR LOCAL:4, 0, 6
						CALL SLOT_SYMBOL, CHALLENGE:(LOCAL:4) % 10, CHALLENGE:(LOCAL:4) / 10 * 2
						PRINT  
					NEXT
					PRINTL 
					SETCOLOR COLOR("gray")
					PRINTL [0] ＳＴＯＰ
					PRINTW [9] ＥＸＩＴ
					RESETCOLOR
					WIN += LOCAL:3
					CLEARLINE LINECOUNT - LOCAL:9
				ENDIF
			NEXT
		NEXT
	NEXT
	;終了判定
	IF MINARRAY(CHALLENGE) >= 10 || WIN >= 1000 || WIN <= 0
		CALL MESSAGE_B(1, "ＢＯＮＵＳ ＧＡＭＥ ＥＮＤ！")
		SIF WIN > 0
			CALL MESSAGE_B(1, "ＴＡＫＥ "+TOSTR(MIN(WIN,1000))+"ＣＯＩＮＳ!!")
		CALL MESSAGE_B(1, "ＨＯＬＤ ＧＡＭＥ ＳＴＡＲＴ!")
		MONEY:3 += LIMIT(WIN, 0, 1000)
		WIN = 0
		SLOT = 0
	ENDIF
ENDIF
HOLD:1 = 0
HOLD:2 = 0
HOLD:3 = 0
GOTO START
@SLOT_SYMBOL, ARG, ARG:1
GETCOLOR
SIF ARG:1 == 1
	SETCOLOR COLOR("red")
SIF ARG:1 == 2
	SETCOLOR COLOR("gray")
SELECTCASE ARG
	CASE 0
		SIF ARG:1 == 0
			SETCOLOR 0x8a2be2
		PRINTFORM ７
	CASE 1
		SIF ARG:1 == 0
			SETCOLOR 0xff69b4
		PRINTFORM ♀
	CASE 2
		SIF ARG:1 == 0
			SETCOLOR 0x00008b
		PRINTFORM ♂
	CASE 3
		;太陽
		SIF ARG:1 == 0
			SETCOLOR COLOR("red")
		PRINTFORM %UNICODE(0x2600)%
	CASE 4
		;三日月
		SIF ARG:1 == 0
			SETCOLOR 0xffff00
		PRINTFORM %UNICODE(0x263E)%
	CASE 5
		SIF ARG:1 == 0
			SETCOLOR 0xffa500
		PRINTFORM ★
	CASE 6
		SIF ARG:1 == 0
			SETCOLOR 0xf0f8ff
		PRINTFORM ●
	CASE 7
		SIF ARG:1 == 0
			SETCOLOR 0x008800
		PRINTFORM ◆
	CASE 8
		SIF ARG:1 == 0
			SETCOLOR COLOR("aqua")
		PRINTFORM JP
ENDSELECT
SETCOLOR RESULT
