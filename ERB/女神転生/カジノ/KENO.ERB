@KENO
#LOCALSIZE 10
#LOCALSSIZE 10
#DIM SPOT,64
#DIM PICKED,64
#DIM HIT,1
#DIM BET,1
#DIM WIN, 1
#DIM LINE, 1
#DIM MODE,1
#DIM LAST_PICK,1
#DIM PICKED_RESULT,64
MODE = 0
REDRAW 2
VARSET SPOT
VARSET PICKED
PRINTL 돌리:
PRINTL 「이야호！ 키노가 여기 있어
PRINTL 　어제 해도 키노、오늘 해도 키노
PRINTW 　바보자식！ 여기는 호객하는 곳이다ー」
PRINTL 「에이 익숙해 지라니깐」
$INPUT_START
CALL INPUT_SELECT(1, "게임 스타트", 2, "어드바이스를 듣는다", 3, "화면을 움직인다", 9, "밖으로 나간다")
IF RESULT > 1
	PRINTL 돌리:
	IF RESULT == 2
		PRINTL 「ＫＥＮＯ는 １부터 ６４까지의 수 중에서
		PRINTL 　１플레이로 １６의 수가 선택되면 
		PRINTW 　그것을 예상해서 걸면 되요」
		PRINTL 「걸 수 있는 수는 1에서 16까지 
		PRINTW 　몇 개 선택할지는 자유라구」
		PRINTL 「넘버 보드 의 숫자를 클릭하거나
		PRINTL 　숫자를 직접 입력하면 선택한 수가 물색(하늘색)이 될거야
		PRINTW 　캔슬도 똑같은 방법으로 해주세요」
		PRINTL 「수를 선택하면、오른쪽에 확률표가 나올거야
		PRINTL 　"１ x４"와 같이 나오면、선택한 수가 1개
		PRINTW 　맞는다면 코인４배라는 것이야」
		PRINTL 「다 선택했으면、[70] ＢＥＴ
		PRINTL 　코인을 걸어 줘」
		PRINTW 　ＭＡＸ１００매 까지 걸 수 있어」
		PRINTL 「[80]ＥＲＡＳＥ는 선택한 것을 전부 지운다
		PRINTW 　처음부터 다시 선택하고 싶은 때 사용해」
;		PRINTL 「[90]ＲＥＳＵＬＴ는 지금까지의 결과
;		PRINTL 　빨강은 많다、파랑은 적다、녹색은
;		PRINTW 　보통으로 나온 편。잘 보고 선택해 줘」
		PRINTL 「내기하려면 [0] 으로 스타트！
		PRINTL 　그만두고 싶을 때는 [99]
		PRINTW 　당 게임에 ＭＡＸ는 ９９９９９９매 라고！」
	ELSEIF RESULT == 3
		PRINTW 「EMUERA이 묘화에 사용하고 있는 GDI+의 문제로、색을 칠하면 어긋난다」
		PRINTL 「윈도우 상부에 있는 콘피그에서 표시 탭(표시タブ)을 선택해
		PRINTW 　묘화(描画)를 GDI+ 에서 GDI로 하면 어긋나지 않는다」
	ELSEIF RESULT  == 9
		PRINTL 「ＫＥＮＯ라고 쓰고 키노！ 케노라고 
		PRINTL 　읽고싶으면、마음대로 해
		PRINTW 　그럼、또ー」
		RETURN 1
	ENDIF
	PRINTL 돌리:
	PRINTL 「에이 익숙해 지라니깐」
	GOTO INPUT_START
ENDIF
DRAWLINE
FONTSTYLE 3
PRINTL ＫＥＮＯ
FONTSTYLE 0
DRAWLINE
LINE = LINECOUNT
WHILE 1
	CLEARLINE LINECOUNT - LINE
	;HIT数の計算
	HIT = 0
	FOR LOCAL,0,64
		SIF PICKED:LOCAL && SPOT:LOCAL
			HIT++
	NEXT
	FOR LOCAL, 0, 23
		SELECTCASE LOCAL
			CASE 0, 14
				PRINTFORM ┌─┬─┬─┬─┬─┬─┬─┬─┐
			CASE 1, 3 , 5, 7
				FOR LOCAL:1, LOCAL/2*8, LOCAL/2*8+8
					PRINT │
					IF SPOT:(LOCAL:1) && PICKED:(LOCAL:1)
						;ライム
						SETCOLOR 0x00ff00
						FONTSTYLE 8
					ELSEIF SPOT:(LOCAL:1)
						SETCOLOR COLOR("aqua")
						FONTSTYLE 8
					ELSEIF PICKED:(LOCAL:1)
						SETCOLOR COLOR("red")
					ELSE
						RESETCOLOR
					ENDIF
					IF !MODE
						PRINTBUTTON @"{LOCAL:1+1,2}", LOCAL:1+1
					ELSE
						PRINTFORM {LOCAL:1+1,2}
					ENDIF
					FONTSTYLE 0
					RESETCOLOR
				NEXT
				PRINT │
			CASE 15, 17 , 19, 21
				FOR LOCAL:1, (LOCAL/2-3)*8, (LOCAL/2-3)*8+8
					PRINT │
					IF SPOT:(LOCAL:1) && PICKED:(LOCAL:1)
						;ライム
						SETCOLOR 0x00ff00
						FONTSTYLE 8
					ELSEIF SPOT:(LOCAL:1)
						SETCOLOR COLOR("aqua")
						FONTSTYLE 8
					ELSEIF PICKED:(LOCAL:1)
						SETCOLOR COLOR("red")
					ELSE
						RESETCOLOR
					ENDIF
					IF !MODE
						PRINTBUTTON @"{LOCAL:1+1,2}", LOCAL:1+1
					ELSE
						PRINTFORM {LOCAL:1+1,2}
					ENDIF
					RESETCOLOR
					FONTSTYLE 0
				NEXT
				PRINT │
			CASE 2, 4, 6, 16, 18, 20
				PRINT ├─┼─┼─┼─┼─┼─┼─┼─┤
			CASE 8, 22
				PRINT └─┴─┴─┴─┴─┴─┴─┴─┘
			CASE 9
				PRINTFORM  ┌────┬────┬────┐ 
			CASE 10
				PRINTFORM  │　
				SETCOLOR COLOR("aqua")
				PRINT SPOT
				PRINTD 　│ 
				SETCOLOR COLOR("red")
				PRINT PICKED
				PRINTD  │ 
				SETCOLOR 0x00ff00
				PRINT CATCH
				RESETCOLOR
				PRINT 　│ 
			CASE 11
				PRINTFORM  ├────┼────┼────┤ 
			CASE 12
				PRINTFORM  │　%TOFULL(@"{MATCH(SPOT, 1)}"),4%　│　%TOFULL(@"{LIMIT(MODE-1, 0, 16)}"),4%　│　%TOFULL(@"{HIT}"),4%　│ 
			CASE 13
				PRINTFORM  └────┴────┴────┘ 
		ENDSELECT
		SELECTCASE LOCAL
			CASE 0,6
				PRINTFORML ┌────┬─────┐
			CASE 1
				PRINTL │BET 　　│COINS 　　│
			CASE 3
				PRINTFORML │{BET,8}│{MONEY:3,10}│
			CASE 4, 22
				PRINTL └────┴─────┘
			CASE 5
				PRINTL 
			CASE 7 
				PRINTL │CATCH 　│OOS 　　　│
			CASE 2, 8
				PRINTL ├────┼─────┤
			CASE IS < 22
				IF LOCAL == 21
					SIF HIT >= 12
						SETCOLOR COLOR("aqua")
					PRINTFORMD │ 
					PRINT １２up
					PRINTD  │
					PRINTFORM %"x"+@"{KENO_RESULT(MATCH(SPOT,1),12)}",10%
					PRINTDL │
				ELSE
					SIF HIT == LOCAL - 9
						SETCOLOR COLOR("aqua")
					PRINTFORMD │ 
					PRINTFORM %TOFULL(@"{LOCAL-9}"),4%   
					PRINTD │
					PRINTFORM %"x"+@"{KENO_RESULT(MATCH(SPOT,1),LOCAL-9)}",10%
					PRINTDL │
				ENDIF
				RESETCOLOR
		ENDSELECT
	NEXT
	IF !MODE
		SIF BET == 0 || BET >= MONEY:3 || !MATCH(SPOT,1)
			SETCOLOR COLOR("gray")
		PRINTL [ 0] ＳＴＡＲＴ
		RESETCOLOR
		PRINTL [70] ＢＥＴ
		PRINTL [80] ＥＲＡＳＥ
		PRINTL [99] ＥＸＩＴ
		RESETCOLOR
	ELSE
		
	ENDIF
	;セレクトモード
	IF !MODE
		INPUT
		SELECTCASE RESULT
			CASE 0
				IF BET > 0 && BET < MONEY:3 && MATCH(SPOT,1)
					MODE = 1
					MONEY:3 -= BET
					FLAG:잭팟 += BET
				ENDIF
			CASE IS < 65
				SPOT:(RESULT-1) = MATCH(SPOT,1) < 16 && SPOT:(RESULT-1) == 0 ? 1 # 0
			CASE 70
				PRINTL ＞내기에 걸 코인의 매수를 입력해 주세요
				CALL INPUT_MANY(0, MIN(MONEY:3, 100))
				BET = RESULT
			CASE 80
				VARSET SPOT
			CASE 99
				REDRAW 1
				RETURN 0
		ENDSELECT
	ELSE
		IF MODE == 1
			CALL MESSAGE_BL(1, "ＫＥＮＯ ＳＴＡＲＴ!!")
			PRINTL 
		ELSE
			IF SPOT:LAST_PICK
				SETCOLOR COLOR("aqua")
				CALL MESSAGE_BL(2, "ＰＩＣＫ "+TOFULL(@"{LAST_PICK+1}"), "ＣＡＴＣＨ!!")
				RESETCOLOR
			ELSE
				CALL MESSAGE_BL(1, TOFULL("ＰＩＣＫ ")+TOFULL(@"{LAST_PICK+1}"))
				PRINTL 
			ENDIF
		ENDIF
		REDRAW 2
		TWAIT 3000, 1
		DO 
			LAST_PICK = RAND:64 
		LOOP PICKED:LAST_PICK
		PICKED:LAST_PICK = 1
		PICKED_RESULT:LAST_PICK += 1
		IF MODE++ == 17
			CLEARLINE 4
			LOCAL = MIN(KENO_RESULT(MATCH(SPOT, 1), HIT) * BET, 999999)
			IF LOCAL > 0
				SETCOLOR COLOR("aqua")
				CALL MESSAGE_BL(2, "Ｃｏｎｇｒａｔｕｌａｔｉｏｎｓ！" ,@"ＧＥＴ %TOFULL(@"{LOCAL}")%ＣＯＩＮＳ！")
			ELSE
				CALL MESSAGE_BL(1, "ＧＥＴ ＮＯ ＣＯＩＮ")
				PRINTL 
			ENDIF
			REDRAW 2
			WAIT
			VARSET PICKED
			MONEY:3 += LOCAL
			MODE = 0
		ENDIF
	ENDIF
WEND
RESTART
;払い戻しテーブル
@KENO_RESULT, ARG, ARG:1
#FUNCTION
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE 0
				RETURNF 0
			CASE 1
				RETURNF 4
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE IS < 2
				RETURNF 0
			CASE 2
				RETURNF 20
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE IS < 2
				RETURNF 0
			CASE 2
				RETURNF 3
			CASE 3
				RETURNF 30
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE IS < 3
				RETURNF 0
			CASE 3
				RETURNF 10
			CASE 4
				RETURNF 200
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE IS < 3
				RETURNF 0
			CASE 3
				RETURNF 3
			CASE 4
				RETURNF 30
			CASE 5
				RETURNF 500
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE IS < 4
				RETURNF 0
			CASE 4
				RETURNF 10
			CASE 5
				RETURNF 200
			CASE 6
				RETURNF 2000
		ENDSELECT
	CASE 7
		SELECTCASE ARG:1
			CASE IS < 4
				RETURNF 0
			CASE 4
				RETURNF 3
			CASE 5
				RETURNF 50
			CASE 6
				RETURNF 500
			CASE 7
				RETURNF 5000
		ENDSELECT
	CASE 8
		SELECTCASE ARG:1
			CASE IS < 5
				RETURNF 0
			CASE 5
				RETURNF 50
			CASE 6
				RETURNF 200
			CASE 7
				RETURNF 2000
			CASE 8
				RETURNF 20000
		ENDSELECT
	CASE 9
		SELECTCASE ARG:1
			CASE IS < 5
				RETURNF 0
			CASE 5
				RETURNF 10
			CASE 6
				RETURNF 50
			CASE 7
				RETURNF 500
			CASE 8
				RETURNF 5000
			CASE 9
				RETURNF 50000
		ENDSELECT
	CASE 10
		SELECTCASE ARG:1
			CASE IS < 5
				RETURNF 0
			CASE 5
				RETURNF 5
			CASE 6
				RETURNF 20
			CASE 7
				RETURNF 200
			CASE 8
				RETURNF 2000
			CASE 9
				RETURNF 20000
			CASE 10
				RETURNF 100000
		ENDSELECT
	CASE 11
		SELECTCASE ARG:1
			CASE IS < 6
				RETURNF 0
			CASE 6
				RETURNF 5
			CASE 7
				RETURNF 100
			CASE 8
				RETURNF 1000
			CASE 9
				RETURNF 10000
			CASE 10,11
				RETURNF 100000
		ENDSELECT
	CASE 12
		SELECTCASE ARG:1
			CASE IS < 6
				RETURNF 0
			CASE 6
				RETURNF 5
			CASE 7
				RETURNF 30
			CASE 8
				RETURNF 300
			CASE 9
				RETURNF 3000
			CASE 10
				RETURNF 30000
			CASE 11,12
				RETURNF 100000
		ENDSELECT
	CASE 13
		SELECTCASE ARG:1
			CASE IS < 6
				RETURNF 0
			CASE 6
				RETURNF 5
			CASE 7
				RETURNF 10
			CASE 8
				RETURNF 100
			CASE 9
				RETURNF 1000
			CASE 10
				RETURNF 10000
			CASE 11,12,13
				RETURNF 100000
		ENDSELECT
	CASE 14
		SELECTCASE ARG:1
			CASE IS < 6
				RETURNF 0
			CASE 6
				RETURNF 3
			CASE 7
				RETURNF 10
			CASE 8
				RETURNF 50
			CASE 9
				RETURNF 500
			CASE 10
				RETURNF 5000
			CASE 11
				RETURNF 50000
			CASE 12,13,14
				RETURNF 100000
		ENDSELECT
	CASE 15
		SELECTCASE ARG:1
			CASE 0
				RETURNF 30
			CASE IS < 7
				RETURNF 0
			CASE 7
				RETURNF 3
			CASE 8
				RETURNF 30
			CASE 9
				RETURNF 100
			CASE 10
				RETURNF 1000
			CASE 11
				RETURNF 10000
			CASE 12,13,14,15
				RETURNF 100000
		ENDSELECT
	CASE 16
		SELECTCASE ARG:1
			CASE 0
				RETURNF 50
			CASE IS < 7
				RETURNF 0
			CASE 7
				RETURNF 3
			CASE 8
				RETURNF 20
			CASE 9
				RETURNF 50
			CASE 10
				RETURNF 500
			CASE 11
				RETURNF 5000
			CASE 12,13,14,15,16
				RETURNF 100000
		ENDSELECT
ENDSELECT
RETURNF 0

