@POKER
#LOCALSIZE 10
#LOCALSSIZE 10
;山札
#DIM DECK,53
;손札
#DIM HAND,5
#DIM HOLD,5
;勝ち枚数
#DIM WIN, 1
#DIM LINE,2
PRINTL 바니:
PRINTL 「아찔한 즐거움의 세계에 어서 오세요
PRINTW 　 이 포커에서는 배짱과 마지막까지 지켜보는 분별력을 시험 받아요！」
PRINTL 「어떻게 할 거야？」
$INPUT_START
CALL INPUT_SELECT(1, "게임 스타트", 2, "어드바이스를 듣는다", 3, "화면을 움직인다", 9, "밖으로 나간다")
IF RESULT > 1
	PRINTL 바니:
	IF RESULT == 2
		PRINTL 「포커 Ｈ&Ｌ에서는 게임에서 사용하는 코인을 
		PRINTL 　１코인과 ５코인과 １０코인 중에서 선택할 수 있어요」
		PRINTL 「이 포커에서는 ＰＯＫＥＲ와 
		PRINTW 　ＥＸＴＲＡ게임이 있어요」
		PRINTL 「ＰＯＫＥＲ에서는 남겨두고 싶은 
		PRINTL 　카드가 있으면 ＨＯＬＤ해 둘 수 있어
		PRINTW 　왼쪽부터 [1]･[2]･[3]･[4]･[5]로 남길 카드를 결정해」
		PRINTL 「결정하면 ＤＲＡＷ해！
		PRINTL 　버린 카드와 똑같은 매수만 카드를 가져올 수 있어
		PRINTW 　그래서 역이 생겼으면 코인을 GET！」
		PRINTW 「역을 가르쳐 줄께요！」
		CALL SHOW_CARD(1, 101, 5, 305, 211)
		PRINTL 바니:
		PRINTL 「２ ＰＡＩＲ는 이런 느낌으로 똑같은 수의 카드가 2매씩 있을 때 
		PRINTW 　간단하지만、그만큼 자주 나와요」
		CALL SHOW_CARD(1, 101, 201, 305, 211)
		PRINTL 바니:
		PRINTL 「３ ＯＦ ＡＫＩＮＤ는 이런 느낌으로 똑같은 수의 카드가 3매 있을 때 
		PRINTW 　일본은 ３ＣＡＲＤ라 부르는 경우가 많으려나」
		CALL SHOW_CARD(1, 101, 201, 301, 211)
		PRINTL 바니:
		PRINTL 「４ ＯＦ ＡＫＩＮＤ는 똑같은 수의 카드가 4매 있을 때 
		PRINTW 　３까지는 자주 볼 수 있어도、이 아이는 좀처럼 볼 수 없어요」
		CALL SHOW_CARD(1, 101, 201, 301, 9999)
		PRINTL 바니:
		PRINTL 「이것이 ５ ＯＦ ＡＫＩＮＤ、와일드 카드+４ ＯＦ ＡＫＩＮＤ에요
		PRINTL 　와일드 카드가 없는 포커는 할 수 없는 역이군요
		PRINTL 　와일드 카드라 하는 건 어떤 카드로도 될 수 있는 카드로 
		PRINTW 　ＪＯＫＥＲ가 사용되는 경우가 많아요」
		CALL SHOW_CARD(1, 101, 201, 105, 205)
		PRINTL 바니:
		PRINTL 「ＦＵＬＬ ＨＯＵＳＥ는 ３매와２매의 조합이에요
		PRINTW 　꽤 강하니까、만들 수 있으면 기쁘지요」
		CALL SHOW_CARD(1, 102, 103, 204, 305)
		CALL SHOW_CARD(10, 11, 212, 313, 301)
		CALL SHOW_CARD(112,113,201,202,303)
		PRINTL 바니:
		PRINTL 「「ＳＴＲＡＩＧＨＴ는 ５매의 카드가 연속으로 번호가 이어질 때、
		PRINTL 　제일 위는 물론이고 Ａ는 ２만이 아니라 K와도 이어지니 2번째에도 오를 수 있어요.(오르다 = 역이 만들어지다)
		PRINTW 　하지만、Ｋ-Ａ-２같이 연결되지 않기 때문에、3번재 것은 오를 수 없으니 주위해 줘」
		CALL SHOW_CARD(202, 205, 207, 209, 211)
		PRINTL 바니:
		PRINTL 「모든 카드의 모늬가 같으면 ＦＬＵＳＨ
		PRINTW 　알기 쉽지만、의외로 모이지 않아요」
		CALL SHOW_CARD(302, 303, 304, 305, 306)
		PRINTL 바니:
		PRINTL 「ＦＬＵＳＨ와 ＳＴＲＡＩＧＨＴ의 조건을 동시에 만족하면 
		PRINTW 　ＳＴＲＡＩＧＨＴ ＦＬＵＳＨ、후후、이름 그대로네」
		CALL SHOW_CARD(10, 11, 12, 13, 1)
		PRINTL 바니:
		PRINTL 「１０・Ｊ・Ｑ・Ｋ・Ａ의 조합으로 ＳＴＲＡＩＧＨＴ ＦＬＵＳＨ를 하면 
		PRINTW 　ＲＯＹＡＬ ＦＬＵＳＨ에요！ 이 게임 최강의 역！」
		PRINTL 「한층 더 이 １０매 일때 ＲＯＹＡＬ ＦＬＵＳＨ로、ＪＡＣＫ ＰＯＴ
		PRINTL  ＪＡＣＫ ＰＯＴ의 점수를 모두 가져가요！
		PRINTW  ＪＡＣＫ ＰＯＴ은 누군가가 코인을 넣을 대마다 성장해 나가니、성장해 있으면 대단한 일이 되어요」
		PRINTL 「다음은…그렇구나 조금 전에도 말했지만、ＪＯＫＥＲ는 
		PRINTW 　어떤 카드 대신으로도 쓸 수 있으니까 잘 사용해 줘」
		PRINTL 「그래서、역을 가지런히 하면、거기에 상응하는 코인을 받을 수 있지만 
		PRINTW 　여기서부터가 ＥＸＴＲＡ게임의 스타트에요」
		PRINTL 「ＥＸＴＲＡ게임은 ＨＩＧＨ ＆ ＬＯＷ
		PRINTL 　５매의 카드로부터 한 장을 선택해
		PRINTW 　그것이 선이 낸 카드보다 큰 숫자라면 더블 업！」
		PRINTL 「받을 수 있는 코인이 배가 되요
		PRINTL 　그리고、더블 업은 연속해서 할 수 있어서
		PRINTW 　계속 이겨나가면、２배、４배、８배…자꾸자꾸 코인이 증가해가요」
		PRINTL 「그렇지만、한 번이라도 지면 코인은 ０매가 되어서 
		PRINTL 　１매도 받을 수 없게 돼버려
		PRINTW 　패하기 전에 ＴＡＫＥ ＯＵＴ해 코인을 가지고 돌아가」
		PRINTW 「ＬＥＴ’Ｓ ＴＲＹ！！」
	ELSEIF RESULT == 3
		PRINTW 「EMUERA이 묘화에 사용하고 있는 GDI+의 문제로、색을 칠하면 어긋난다」
		PRINTL 「윈도우 상부에 있는 콘피그에서 표시 탭(표시タブ)을 선택해
		PRINTW 　묘화(描画)를 GDI+ 에서 GDI로 하면 어긋나지 않는다。신경 쓰이는 사람은 시험해 봐」
	ELSEIF RESULT  == 9
		PRINTFORMW 「당신의 코인은 {MONEY:3}매야。또 와줘！」
		REDRAW 1
		RETURN 1
	ENDIF
	PRINTL 바니:
	PRINTL 「어떻게 할 거야？」
	GOTO INPUT_START
ENDIF
REDRAW 0
FONTSTYLE 3
PRINTL ＰＯＫＥＲ
FONTSTYLE 0
DRAWLINE
PRINTFORML ┌────────┬───┬───┬───┐
PRINTFORML │%"OODS", 16,LEFT%│%"BET1",6%│%"BET5",6%│%"BET10",6%│
PRINTFORML ├────────┼───┼───┼───┤
PRINTFORML │%"ROYAL FLASH", 16,LEFT%│{300,6}│{1500,6}│%"ＪＰ",6%│
PRINTFORML │%"5 OF AKIND", 16,LEFT%│{100,6}│{500,6}│{1000,6}│
PRINTFORML │%"STRAIGHT FLASH", 16,LEFT%│{50,6}│{250,6}│{500,6}│
PRINTFORML │%"4 OF AKIND", 16,LEFT%│{30,6}│{150,6}│{300,6}│
PRINTFORML │%"FLASH", 16,LEFT%│{10,6}│{50,6}│{100,6}│
PRINTFORML │%"STRAIGHT", 16,LEFT%│{5,6}│{25,6}│{50,6}│
PRINTFORML │%"3 OF AKIND", 16,LEFT%│{3,6}│{15,6}│{30,6}│
PRINTFORML │%"2 PAIR", 16,LEFT%│{2,6}│{10,6}│{20,6}│
PRINTFORML │%"HIGH CARD", 16,LEFT%│{0,6}│{0,6}│{0,6}│
PRINTFORML ├────────┼───┴───┴───┤
PRINTFORML │%"JACK POT", 16,LEFT%│{FLAG:잭팟/100,17}COINS│
PRINTFORML └────────┴───────────┘
LINE = LINECOUNT
$START
VARSET HOLD
CLEARLINE LINECOUNT - LINE
PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
DRAWLINE
PRINTL 
PRINTL 
PRINTL 
PRINTL
PRINTL
DRAWLINE
CALL SHOW_CARD(0,0,0,0,0)
DRAWLINE
PRINTL ＢＥＴ [1] １ ＣＯＩＮ [2] ５ ＣＯＩＮＳ [3] １０ ＣＯＩＮＳ
PRINTL [9] ＥＸＩＴ
CALL INPUTINT(1,2,3,9)
IF RESULT == 9
	PRINTFORMW 「당신의 코인은 {MONEY:3}매야。또 와줘！」
	REDRAW 1
	RETURN 1
ELSEIF RESULT == 1
	LOCAL = 1
ELSEIF RESULT == 2
	LOCAL = 5
ELSE
	LOCAL = 10
ENDIF
SIF MONEY:3 < LOCAL
	GOTO START
WIN = LOCAL
MONEY:3 -= LOCAL
CLEARLINE LINECOUNT - LINE

FLAG:잭팟 += 10 * WIN
;山札の作成
;カードはスート(スペード0, クラブ1,ハート2,ダイア3)*100+2~13の数で등록する
FOR LOCAL, 0, 53
	DECK:LOCAL = (LOCAL/13 * 100)+LOCAL % 13+1
NEXT
;53枚눈の山札はJOKER(9999)にする
DECK:52 = 9999
;山札をシャッフルする
FOR LOCAL, 0, 53
	LOCAL:1 = RAND:53
	SWAP DECK:LOCAL, DECK:(LOCAL:1)
NEXT
;最初の손札を配る
FOR LOCAL, 0, 5
	HAND:LOCAL = DECK:LOCAL
NEXT
;山札から札を삭제
ARRAYREMOVE DECK, 0, 5
;손札の整列
DO 
	LOCAL:1 = 0
	FOR LOCAL, 0, 4
		IF HAND:LOCAL > HAND:(LOCAL+1)
			SWAP HAND:LOCAL, HAND:(LOCAL+1)
			LOCAL:1 = 1
		ENDIF
	NEXT
LOOP LOCAL:1
$CHOICE_HOLD
CLEARLINE LINECOUNT-LINE
PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
DRAWLINE
PRINTL 
PRINTL 
PRINTL 
PRINTL
PRINTL
DRAWLINE
CALL SHOW_CARD(HAND:0,HAND:1,HAND:2,HAND:3,HAND:4, HOLD*COLOR("aqua"), HOLD:1*COLOR("aqua"), HOLD:2*COLOR("aqua"), HOLD:3*COLOR("aqua"), HOLD:4*COLOR("aqua"))
DRAWLINE
FOR LOCAL, 0, 5
	IF HOLD:LOCAL
		SETCOLOR COLOR("aqua")
	ELSE
		SETCOLOR COLOR("gray")
	ENDIF
	PRINTFORM [{LOCAL+1}] 
	PRINT HELD　
NEXT
RESETCOLOR
PRINTL 
PRINTL [0] ＤＲＡＷ
CALL INPUTINT(0 ,1 ,2 ,3, 4, 5)
IF RESULT > 0
	INVERTBIT HOLD:(RESULT-1),0
	GOTO CHOICE_HOLD
ENDIF
IF MATCH(HOLD, 0)
	VARSET LOCAL, -1, 2, 7
	LOCAL:1 = 0
	FOR LOCAL, 0, 5
		IF !HOLD:LOCAL
			HAND:LOCAL = DECK:(LOCAL:1)
			LOCAL:(LOCAL:1+2) = DECK:(LOCAL:1)
			LOCAL:1++ 
		ENDIF
	NEXT
	;山札から札を삭제
	ARRAYREMOVE DECK, 0, MATCH(HOLD, 0)
	;손札の整列
	DO 
		LOCAL:1 = 0
		FOR LOCAL, 0, 4
			IF HAND:LOCAL > HAND:(LOCAL+1)
				SWAP HAND:LOCAL, HAND:(LOCAL+1)
				LOCAL:1 = 1
			ENDIF
		NEXT
	LOOP LOCAL:1
	CLEARLINE LINECOUNT-LINE
	PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
	DRAWLINE
	CALL MESSAGE_BL, 1, "ＤＲＡＷ ＣＡＲＤＳ"
	PRINTL
	PRINTL 
	;ひいたカードの提示
	DRAWLINE
	CALL SHOW_CARD, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6
	DRAWLINE
	SETCOLOR COLOR("gray")
	FOR LOCAL, 0, 5
		PRINTFORM [{LOCAL+1}] 
		PRINT HELD　
	NEXT
	RESETCOLOR
	PRINTL
	PRINTL [0] ＤＲＡＷ
	RESETCOLOR
	WAIT
ENDIF
CLEARLINE LINECOUNT-LINE
PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
DRAWLINE
;役の판정
;JOKERがある場合　HAND:4はJOKERになってるはずなので、HAND:3までのみをみる
IF MATCH(HAND,9999)
	;10-J-Q-K-Aのストレートは別판정しておく。それぞれが含まれているかを체크
	VARSET LOCAL,0, 1, 6
	FOR LOCAL, 0, 4
		SELECTCASE HAND:LOCAL % 100
			CASE 1
				LOCAL:1 = 1
			CASE 10
				LOCAL:2 = 1
			CASE 11	
				LOCAL:3 = 1
			CASE 12
				LOCAL:4 = 1
			CASE 13
				LOCAL:5 = 1
		ENDSELECT
	NEXT
	;이상의 カードの内４枚が含まれていれば、10-Aの並びのストレート
	LOCAL:8 = LOCAL:1+LOCAL:2+LOCAL:3+LOCAL:4+LOCAL:5
	VARSET LOCAL, 0, 1, 5
	;同一カード系の판정 LOCAL:1~4に1枚~4枚揃いのカードがあるごとに+1
	FOR LOCAL, 0, 5
		LOCAL:GROUPMATCH(HAND:LOCAL%100, HAND%100, HAND:1%100, HAND:2%100, HAND:3%100) += 1
	NEXT
	;最小と최대の差が４以下かつ４枚全てが別カードならストレート
	IF LOCAL:8 == 4 || (MAX(HAND%100, HAND:1%100, HAND:2%100, HAND:3%100) - MIN(HAND%100, HAND:1%100, HAND:2%100, HAND:3%100) <= 4 && LOCAL:1 == 4)
		;フラッシュの판정　カードの最小と최대の差が13미만ならフラッシュ
		IF MAX(HAND, HAND:1, HAND:2, HAND:3) - MIN(HAND, HAND:1, HAND:2, HAND:3) < 13
			;ロイヤルフラッシュの판정　上記で판정してるのを流用
			IF LOCAL:8 == 4
				LOCALS = ＲＯＹＡＬ ＦＬＵＳＨ！
				WIN *= 300
			;ストレートフラッシュ
			ELSE
				LOCALS = ＳＴＲＡＩＧＨＴ ＦＬＵＳＨ！
				WIN *= 50
			ENDIF
		;ストレート
		ELSE
			LOCALS = ＳＴＲＡＩＧＨＴ！
			WIN *= 5
		ENDIF
	ELSE
		;5 OF AKINDの판정
		IF LOCAL:4 == 4
			LOCALS = ５ ＯＦ ＡＫＩＮＤ！
			WIN *= 100
		;4 OF AKINDの판정
		ELSEIF LOCAL:3 == 3
			LOCALS = ４ ＯＦ ＡＫＩＮＤ！
			WIN *= 30
		;フルハウスの판정
		ELSEIF LOCAL:2 == 4
			LOCALS = ＦＵＬＬ ＨＯＵＳＥ！
			WIN *= 20
		ELSE
			;フラッシュの판정　カードの最小と최대の差が13미만ならフラッシュ
			IF MAX(HAND, HAND:1, HAND:2, HAND:3) - MIN(HAND, HAND:1, HAND:2, HAND:3) < 13
				LOCALS = ＦＬＵＳＨ！
				WIN *= 10
			ELSE
				;3 OF AKINDの판정
				IF LOCAL:2 == 2
					LOCALS = ３ ＯＦ ＡＫＩＮＤ！
					WIN *= 3
				;役なし
				ELSE
					LOCALS = ＨＩＧＨ ＣＡＲＤ
					WIN *= 0
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSE
	;10-J-Q-K-Aのストレートは別판정しておく。それぞれが含まれているかを체크
	VARSET LOCAL,0, 1, 6
	FOR LOCAL, 0, 5
		SELECTCASE HAND:LOCAL % 100
			CASE 1
				LOCAL:1 = 1
			CASE 10
				LOCAL:2 = 1
			CASE 11
				LOCAL:3 = 1
			CASE 12
				LOCAL:4 = 1
			CASE 13
				LOCAL:5 = 1
		ENDSELECT
	NEXT
	;이상의 カードの内５枚が含まれていれば、10-Aの並びのストレート
	LOCAL:8 = LOCAL:1+LOCAL:2+LOCAL:3+LOCAL:4+LOCAL:5
	VARSET LOCAL, 0, 1, 6
	;同一カード系の판정 LOCAL:1~4に1枚~4枚揃いのカードがあるごとに+1
	FOR LOCAL, 0, 5
		LOCAL:GROUPMATCH(HAND:LOCAL%100, HAND%100, HAND:1%100, HAND:2%100, HAND:3%100, HAND:4%100) += 1
	NEXT
	;最小と최대の差が4かつ、모든カードが別のカードであるならストレート
	IF LOCAL:8 == 5 || (MAX(HAND%100, HAND:1%100, HAND:2%100, HAND:3%100, HAND:4%100) - MIN(HAND%100, HAND:1%100, HAND:2%100, HAND:3%100, HAND:4%100) == 4 && LOCAL:1 == 5)
		;フラッシュの판정　カードの最小と최대の差が13미만ならフラッシュ
		IF MAX(HAND, HAND:1, HAND:2, HAND:3, HAND:4) - MIN(HAND, HAND:1, HAND:2, HAND:3, HAND:4) < 13
			;ロイヤルフラッシュの판정 上記판정の流用
			IF LOCAL:8 == 5
				LOCALS = ＲＯＹＡＬ ＦＬＵＳＨ！
				WIN *= 300
			;ストレートフラッシュ
			ELSE
				LOCALS = ＳＴＲＡＩＧＨＴ ＦＬＵＳＨ！
				WIN *= 50
			ENDIF
		;ストレート
		ELSE
			LOCALS = ＳＴＲＡＩＧＨＴ！
			WIN *= 5
		ENDIF
	ELSE
		;4 OF AKINDの판정
		IF LOCAL:4 == 4
			LOCALS = ４ ＯＦ ＡＫＩＮＤ！
			WIN *= 30
		;フルハウス
		ELSEIF LOCAL:3 == 3 && LOCAL:2 == 2
			LOCALS = ＦＵＬＬ ＨＯＵＳＥ！
			WIN *= 20
		ELSE
			;フラッシュの판정　カードの最小と최대の差が13미만ならフラッシュ
			IF MAX(HAND, HAND:1, HAND:2, HAND:3, HAND:4) - MIN(HAND, HAND:1, HAND:2, HAND:3, HAND:4) < 13
				LOCALS = ＦＬＵＳＨ！
				WIN *= 10
			ELSE
				;3 OF AKINDの판정
				IF LOCAL:3 == 3
					LOCALS = ３ ＯＦ ＡＫＩＮＤ！
					WIN *= 3
				;2ペアの판정。2枚ぞろいのカードは4枚あることに注意
				ELSEIF LOCAL:2 == 4
					LOCALS = ２ ＰＡＩＲ！
					WIN *= 2
				;役なし
				ELSE
					LOCALS = ＨＩＧＨ ＣＡＲＤ
					WIN *= 0
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF
IF WIN
	IF WIN == 3000
		CALL MESSAGE_BL, 3, LOCALS, "ＪＡＣＫ ＰＯＴ！！", "ＧＥＴ "+TOSTR(FLAG:잭팟/100)+"ＣＯＩＮＳ！！"
		WIN = 0
		MONEY:3 += FLAG:잭팟/100
		FLAG:잭팟 = 10000 * 100
	ELSE
		CALL MESSAGE_BL, 3, LOCALS, "ＧＥＴ "+TOSTR(WIN)+"ＣＯＩＮＳ！！", "ＥＸＴＲＡ ＧＡＭＥ ＳＴＡＲＴ！！"
	ENDIF
ELSE
	CALL MESSAGE_BL, 2, LOCALS, "ＹＯＵ ＬＯＳＥＤ！"
	PRINTL 
ENDIF
DRAWLINE
CALL SHOW_CARD, HAND, HAND:1, HAND:2, HAND:3, HAND:4
DRAWLINE
SETCOLOR COLOR("gray")
FOR LOCAL, 0, 5
	PRINTFORM [{LOCAL+1}] 
	PRINT HELD　
NEXT
RESETCOLOR
PRINTL
PRINTL [0] ＤＲＡＷ
RESETCOLOR
WAIT
SIF WIN == 0
	GOTO START
;Aは14にする
FOR LOCAL,0, 53
	SIF DECK:LOCAL % 100 == 1
		DECK:LOCAL += 13
NEXT
WHILE WIN
	;イカサマあり→降りやすいし、なにより필요한コイン枚数が原作ほど多くないしでサマなしの勝率50%に변경しておきます
	;親が最も強いカードである場合、子のカードのいずれかと入れ替わる
	;引き分けと山札の在庫を置いておけば、20%~100%が当分にでるので、これで勝率6割
	;異聞録の実測が70%なんで、10%ほど負けてるけど、そのかわりドロップのタイミングを優遇しておく
	;TAKE OUTはいつでも가능にしておきます
	;IF DECK % 100 > MAX(DECK:1 % 100, DECK:2 % 100 ,  DECK:3 % 100 , DECK:4 % 100, DECK:5 %100)
	;	LOCAL = RAND:5
	;	SWAP DECK, DECK:LOCAL
	;ENDIF
	CLEARLINE LINECOUNT - LINE
	PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
	DRAWLINE
	CALL SHOW_CARD , , , DECK
	DRAWLINE
	CALL SHOW_CARD , 0, 0, 0, 0, 0
	DRAWLINE
	PRINTL [1] RAIZE [2] RAIZE [3] RAIZE [4] RAIZE [5] RAIZE
	PRINTL [9] ＴＡＫＥ ＯＵＴ
	CALL INPUTINT(1,2,3,4,5,9)
	IF RESULT == 9
		CLEARLINE LINECOUNT - LINE
		PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
		DRAWLINE
		CALL MESSAGE_BL, 1, "ＴＡＫＥ "+TOSTR(WIN)+"ＣＯＩＮＳ！！"
		PRINTL 
		MONEY:3 += WIN
		WIN = 0
	ELSE
		VARSET LOCAL, 0, 1, 6
		LOCAL = RESULT
		CLEARLINE LINECOUNT - LINE
		PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
		DRAWLINE
		LOCAL:RESULT = COLOR("aqua")
		CALL SHOW_CARD , , , DECK,
		DRAWLINE
		CALL SHOW_CARD , DECK:1 , DECK:2, DECK:3, DECK:4, DECK:5, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5
		DRAWLINE
		FOR LOCAL:6, 1, 6
			SIF LOCAL:(LOCAL:6)
			SETCOLOR COLOR("aqua")
			PRINTFORM [{LOCAL:6}]
			PRINT  RAIZE 
			RESETCOLOR
		NEXT
		PRINTL 
		SETCOLOR COLOR("gray")
		PRINTL [9] ＴＡＫＥ ＯＵＴ
		RESETCOLOR
		WAIT
		CLEARLINE LINECOUNT - LINE
		PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
		DRAWLINE
		IF DECK % 100 < DECK:LOCAL % 100
			WIN *= 2
			CALL MESSAGE_BL, 2, "ＤＯＵＢＬＥ ＵＰ！", "ＧＥＴ "+TOSTR(WIN)+"ＣＯＩＮＳ！！"
		ELSEIF DECK % 100 == DECK:LOCAL % 100
			CALL MESSAGE_BL, 2, "ＤＲＡＷＮ　ＭＡＴＣＨ！", "ＲＥＴＲＹ"
		ELSE
			CALL MESSAGE_BL, 2, "ＹＯＵ ＬＯＳＥ", "ＥＸＴＲＡ ＧＡＭＥ ＥＮＤ"
			WIN = 0
		ENDIF
		;山札から札を삭제
		ARRAYREMOVE DECK, 0, 6
	ENDIF
	PRINTL 
	DRAWLINE
	PRINTL 
	PRINTL 
	PRINTL 
	PRINTL 
	PRINTL 
	DRAWLINE
	FOR LOCAL:6, 1, 6
		SIF LOCAL:(LOCAL:6)
			SETCOLOR COLOR("aqua")
		PRINTFORM [{LOCAL:6}]
		PRINT  RAIZE 
		RESETCOLOR
	NEXT
	PRINTL 
	SETCOLOR COLOR("gray")
	PRINTL [9] ＴＡＫＥ ＯＵＴ
	RESETCOLOR
	WAIT
	;山札が切れたら재생成
	IF MATCH(DECK, 0) > 46
		CALL MESSAGE_B, 2, "ＤＥＣＫ ＯＵＴ！", "ＭＡＫＥ ＮＥＷ ＤＥＣＫ！"
		;山札の作成
		FOR LOCAL, 0, 53
			DECK:LOCAL = (LOCAL/13 * 100)+LOCAL % 13+2
		NEXT
		;53枚눈の山札はJOKER(9999)にする
		DECK:52 = 9999
		;山札をシャッフルする
		FOR LOCAL, 0, 53
			LOCAL:1 = RAND:53
			SWAP DECK:LOCAL, DECK:(LOCAL:1)
		NEXT
	ENDIF
WEND
GOTO START
@SHOW_CARD, ARG = -1, ARG:1 = -1, ARG:2 = -1, ARG:3= -1, ARG:4= -1, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9
#LOCALSIZE 10
#LOCALSSIZE 10
GETCOLOR
LOCAL:9 = RESULT
VARSET LOCALS
FOR LOCAL, 0 ,5
	IF ARG:(LOCAL+5)
		SETCOLOR ARG:(LOCAL+5)
	ELSE
		SETCOLOR LOCAL:9
	ENDIF
	SELECTCASE ARG:LOCAL
		CASE 9999
			PRINT  J──┐　
		CASE 0
			PRINT  ┌-─┐　
		CASE -1
			PRINT 　　　　　
		CASEELSE
			PRINT  
			CALL SHOW_TRUMP_SUIT(ARG:LOCAL)
			PRINT ──┐　
	ENDSELECT
NEXT
PRINTL 
FOR LOCAL, 0 ,5
	IF ARG:(LOCAL+5)
		SETCOLOR ARG:(LOCAL+5)
	ELSE
		SETCOLOR LOCAL:9
	ENDIF
	SELECTCASE ARG:LOCAL
		CASE 9999
			PRINT  │o  │　
		CASE -1
			PRINT 　　　　　
		CASEELSE
			PRINT  │   │　
	ENDSELECT
NEXT
PRINTL 
FOR LOCAL, 0 ,5
	IF ARG:(LOCAL+5)
		SETCOLOR ARG:(LOCAL+5)
	ELSE
		SETCOLOR LOCAL:9
	ENDIF
	SELECTCASE ARG:LOCAL
		CASE 9999
			PRINT  │ k │　
		CASE 0
			PRINT  │
			SETCOLOR COLOR("red")
			PRINT DD
			SETCOLOR COLOR("aqua")
			PRINT S
			IF ARG:(LOCAL+5)
				SETCOLOR ARG:(LOCAL+5)
			ELSE
				SETCOLOR LOCAL:9
			ENDIF
			PRINT │　
		CASE -1
			PRINT 　　　　　
		CASEELSE
			PRINTFORM  │
			CALL SHOW_TRUMP_NUM(ARG:LOCAL)
			PRINTFORM  │　
	ENDSELECT
NEXT
PRINTL 
FOR LOCAL, 0 ,5
	IF ARG:(LOCAL+5)
		SETCOLOR ARG:(LOCAL+5)
	ELSE
		SETCOLOR LOCAL:9
	ENDIF
	SELECTCASE ARG:LOCAL
		CASE 9999
			PRINT  │  e│　
		CASE 0
			PRINT  │   │　
		CASE -1
			PRINT 　　　　　
		CASEELSE
			PRINT  │   │　
	ENDSELECT
NEXT
PRINTL 
FOR LOCAL, 0 ,5
	IF ARG:(LOCAL+5)
		SETCOLOR ARG:(LOCAL+5)
	ELSE
		SETCOLOR LOCAL:9
	ENDIF
	SELECTCASE ARG:LOCAL
		CASE 9999
			PRINT  └─-r 　
		CASE 0
			PRINT  └─-┘　
		CASE -1
			PRINT 　　　　　
		CASEELSE
			PRINTFORM  └──
			CALL SHOW_TRUMP_SUIT(ARG:LOCAL)
			PRINT 　
	ENDSELECT
NEXT
PRINTL 
SETCOLOR LOCAL:9
@SHOW_TRUMP_SUIT, ARG
#LOCALSIZE 10
#LOCALSSIZE 10
GETCOLOR
SIF ARG == 0
	PRINTFORM EMPTY
SIF ARG == 9999
	PRINTFORM JOKER
SELECTCASE ARG / 100
	;スペード
	CASE 0
		SETCOLOR 0x0000ff
		PRINTFORM %UNICODE(0x2660)%
	;クラブ
	CASE 1
		SETCOLOR 0x228b22
		PRINTFORM %UNICODE(0x2663)%
	;ハート
	CASE 2
		SETCOLOR 0xff69b4
		PRINTFORM %UNICODE(0x2665)%
	CASE 3
	;ダイヤ
		SETCOLOR COLOR("red")
		PRINTFORM %UNICODE(0x2666)%
ENDSELECT
SETCOLOR RESULT
@SHOW_TRUMP_NUM, ARG
#LOCALSIZE 10
#LOCALSSIZE 10
GETCOLOR
SIF ARG == 0
	PRINTFORM EMPTY
SIF ARG == 9999
	PRINTFORM JOKER
SELECTCASE ARG % 100
	CASE 1, 14
		SETCOLOR COLOR("red")
		PRINTFORM  A
	CASE 11
		PRINTFORM  J
	CASE 12
		SETCOLOR 0xff69b4
		PRINTFORM  Q
	CASE 13
		PRINTFORM  K
	CASE 10
		PRINT 10
	CASEELSE
		PRINTFORM  {ARG%100}
ENDSELECT
SETCOLOR RESULT
