
@장비강화, ARG, ARG:1 = -1
#DIM DYNAMIC FIRST_LINE
#DIM DYNAMIC SECOND_LINE
#DIM DYNAMIC THIRD_LINE
#DIM DYNAMIC LCOUNT, 2
#DIM DYNAMIC 대상
#DIM DYNAMIC 장비부위
#DIM DYNAMIC 장비번호
#DIMS DYNAMIC 장비명
#DIM DYNAMIC 지정
#DIM DYNAMIC 강화치
#DIM DYNAMIC 강화단계
#DIM DYNAMIC 강화불가
#DIM DYNAMIC 개념강화불가

#DIMS DYNAMIC 강화소재1
#DIMS DYNAMIC 강화소재10,2
#DIM DYNAMIC 강화소재개수1
#DIM DYNAMIC 강화소재개수10,2
#DIM DYNAMIC 강화단계_소재판별루프
#DIM DYNAMIC 개조장비
#DIM DYNAMIC 소비ＭＡＧ, 2
#DIM DYNAMIC 소비Ｐ, 2

대상 = ARG
지정 = -1

$START_B
FIRST_LINE = LINECOUNT
DRAWLINE
PRINTFORML %CALLNAME:대상%
	CALL 장비강화_CSTR, "강화수", 대상
PRINTFORML □장비강화상태      강화가능수：{MAX(50 - RESULT)}
IF RESULT >= 50
	강화불가 = 1
	SIF RESULT >= 90
		개념강화불가 = 1
ELSE
	강화불가 = 0
ENDIF

CALL 장비강화_CSTR, "표시", 대상
DRAWLINE

$START_C
SECOND_LINE = LINECOUNT
RESETCOLOR
IF 지정 == -1
	SIF 개념강화불가 == 1
		SETCOLOR COLOR("gray")
	PRINTL [ 0] 개념강화
	IF CSTR:대상:장비강화 != ""
		PRINTL [ 1] 능력강화
		PRINTL [ 2] 내성강화
		SIF 강화불가 == 1
			SETCOLOR COLOR("gray")
		PRINTL [ 3] 공격상성강화
		PRINTL [ 4] 추가효과부여
			RESETCOLOR
		PRINTL [ 8] 강화해제
	ENDIF
	PRINTL [99] 사용설명서
	PRINTL [ 9] 그만둔다
	;INPUT
	CALL 장비강화_INPUT,"강화선택",0,1,2,3,4,8,9,99
	지정 = RESULT
	SIF 개념강화불가 == 1 && 지정 < 8 && 지정 != 99
		지정 = 10
	SIF 강화불가 == 1 && (2 < 지정 && 지정 < 8) && 지정 != 99
		지정 = 10
ENDIF

$START_D
THIRD_LINE = LINECOUNT
DRAWLINE

SELECTCASE 지정
	CASE 0 TO 2
		CLEARLINE LINECOUNT - SECOND_LINE
		PRINTL 장비를 선택해주세요
		CALL SHOW_EQUIP_LIST_장비강화,대상
		IF RESULT == 48 || RESULT == 49
			IF RESULT == 48
				PRINTL 이 장비는 한 번 강화를 해제하지 않는 한 강화할 수 없습니다。
				PRINTW 또한、강화의 혜택을 받을 수도 없습니다。
			ENDIF
			CLEARLINE LINECOUNT - FIRST_LINE
			지정 = -1
			GOTO START_B
		ENDIF
		장비부위 = RESULT
		장비번호 = EQUIP:대상:GET_EQUIP(RESULT)
		IF 개조장비(장비번호)
			장비명 = %개조장비명칭(개조장비番号(장비번호))%
		ELSE
			장비명 = %ITEMNAME:장비번호%
		ENDIF
	CASE 3 TO 4
		장비부위 = GET_EQUIPNUM("검")
		장비번호 = EQUIP:대상:GET_EQUIP(장비부위)
	CASE 8

	CASE 9
		CALL SYNC_STATUS,대상
		;표시させるキャラを리셋（Q:*に표시するキャラの등록번호、LOCAL:2に人수）
		VARSET Q, -1
		A = 0
		FOR LOCAL, 0, CHARANUM
			SIF !(CFLAG:LOCAL:노역플래그 == 3 || CFLAG:LOCAL:이자리에없는플래그 || CFLAG:LOCAL:필터링플래그)
				Q:(A++) = LOCAL
		NEXT
		RETURN -1
	CASE 10
		PRINTL 강화가능한 숫자를 초과했습니다
		PRINTW 필요없는 강화를 해제해주세요
			CLEARLINE LINECOUNT - FIRST_LINE
			지정 = -1
			GOTO START_B
	CASE 99
		;GOTO MANUAL_TOP
ENDSELECT

IF 지정 != 99
	CALL 장비강화_전개, 대상, 장비번호, "강화단계", "LV"
	IF RESULT == 1 && 지정 > 0 && 지정 != 8
		PRINTW 장비의 개념을 강화해주세요
		SIF 지정 == 3 || 지정 == 4
			지정 = -1
		CLEARLINE LINECOUNT - THIRD_LINE
		GOTO START_D
	ELSEIF RESULT >= BASE:대상:LV && 지정 == 0
		IF RESULT == 99
			PRINTFORMW 이 이상 강화할 수 없습니다
		ELSE
			PRINTFORMW 강화하기에는 %CALLNAME:대상%의 레벨이 부족합니다
		ENDIF
		CLEARLINE LINECOUNT - THIRD_LINE
		GOTO START_D
	ELSEIF 개조장비(장비번호) && 지정 == 0
		IF RESULT >= 2
			PRINTW 이 이상 강화할 수 없습니다
			CLEARLINE LINECOUNT - THIRD_LINE
			GOTO START_D
		ELSE
			PRINTW 개조장비를 강화하는 경우、일부의 강화가 열화해버립니다
			PRINTL
			PRINTL 능력강화：불가
			PRINTL 내성강화：불가
			PRINTL
			PRINTL 개념강화를 실행하겠습니까？
			PRINTL
			CALL INPUT_YN("네", "아니오", 2)
			IF RESULT == 1
				CLEARLINE LINECOUNT - THIRD_LINE
				GOTO START_D
			ENDIF
		ENDIF
	ENDIF
ENDIF

SELECTCASE 지정
	CASE 0
		CLEARLINE LINECOUNT - SECOND_LINE
		$S_LIST
		DRAWLINE
		PRINTL □개념강화
		DRAWLINE
		CALL 장비강화_전개, 대상, 장비번호, "강화단계", "LV"
		강화단계 = RESULT
		개조장비 = 개조장비(장비번호)
		PRINTFORML <%장비명%>  Lv:{강화단계}
		CALL MAKE_SALERPGITEMLIST,장비부위
		LOCAL:2 = RESULT
		$PRINT_LIST
		IF 강화단계 < 99 
			DRAWLINE
			;소비ＭＡＧは１回강화の치段。소비ＭＡＧ:1は１０回강화の치段
			소비ＭＡＧ = MAX(1, 2 * (강화단계 - 1) * 강화단계 * (강화단계 + 1)*2 ) + 1000
			소비ＭＡＧ:1 = 0
			FOR LCOUNT, 0, MIN(10, 99 - 강화단계)
				소비ＭＡＧ:1 += MAX(1, 2 * (강화단계 - 1 + LCOUNT) * (강화단계 + LCOUNT) * (강화단계 + 1 + LCOUNT)*2 ) + 1000
			NEXT
			
			;そのままだと多すぎるので
			소비ＭＡＧ /= 9
			소비ＭＡＧ:1 /= 9
			소비Ｐ = 소비ＭＡＧ * 11 / 100
			소비Ｐ:1 = 소비ＭＡＧ:1 * 11 / 100
			
			VARSET 강화소재1
			VARSET 강화소재10
			VARSET 강화소재개수1
			VARSET 강화소재개수10
			VARSET LCOUNT
			
			강화단계_소재판별루프 = 강화단계
			
			FOR LCOUNT, 0, 10
				SELECTCASE 강화단계_소재판별루프
					CASE 21 TO 30
						강화소재10:(LCOUNT:1) = 오팔
					CASE 31 TO 40
						강화소재10:(LCOUNT:1) = 에메랄드
					CASE 41 TO 50
						강화소재10:(LCOUNT:1) = 터키석
					CASE 51 TO 60
						강화소재10:(LCOUNT:1) = 토파즈
					CASE 61 TO 70
						강화소재10:(LCOUNT:1) = 사파이어
					CASE 71 TO 80
						강화소재10:(LCOUNT:1) = 진주
					CASE 81 TO 90
						강화소재10:(LCOUNT:1) = 루비
					CASE 91 TO 98
						강화소재10:(LCOUNT:1) = 다이아몬드
					CASEELSE
				ENDSELECT

				;宝石で交換できる合成소재の売却額が目安。大体で良い
				;ただしルビーとダイヤモンドは入手しにくいので高めに見積もる
				SELECTCASE 강화소재10:(LCOUNT:1)
					CASE "다이아몬드"
						;35個
						;Lv98までなのでLv91以上は個別に設定
						SELECTCASE 강화단계_소재판별루프 % 10
							CASE 1, 2
								강화소재개수10:(LCOUNT:1) += 3
							CASE 3, 4
								강화소재개수10:(LCOUNT:1) += 4
							CASE 5 TO 7
								강화소재개수10:(LCOUNT:1) += 5
							CASE 8
								강화소재개수10:(LCOUNT:1) += 6
							CASEELSE
						ENDSELECT
					CASE "루비"
						;30個
						SELECTCASE 강화단계_소재판별루프 % 10
							CASE 1
								강화소재개수10:(LCOUNT:1) += 1
							CASE 2, 3
								강화소재개수10:(LCOUNT:1) += 2
							CASE 4 TO 7
								강화소재개수10:(LCOUNT:1) += 3
							CASE 8, 9
								강화소재개수10:(LCOUNT:1) += 4
							CASE 0
								강화소재개수10:(LCOUNT:1) += 5
							CASEELSE
						ENDSELECT
					CASE "토파즈", "진주"
						;25個
						SELECTCASE 강화단계_소재판별루프 % 10
							CASE 1, 2
								강화소재개수10:(LCOUNT:1) += 1
							CASE 3 TO 6
								강화소재개수10:(LCOUNT:1) += 2
							CASE 7, 8
								강화소재개수10:(LCOUNT:1) += 3
							CASE 9
								강화소재개수10:(LCOUNT:1) += 4
							CASE 0
								강화소재개수10:(LCOUNT:1) += 5
							CASEELSE
						ENDSELECT
					CASE "에메랄드"
						;20個
						SELECTCASE 강화단계_소재판별루프 % 10
							CASE 1 TO 4
								강화소재개수10:(LCOUNT:1) += 1
							CASE 5 TO 7
								강화소재개수10:(LCOUNT:1) += 2
							CASE 8 TO 9
								강화소재개수10:(LCOUNT:1) += 3
							CASE 0
								강화소재개수10:(LCOUNT:1) += 4
							CASEELSE
						ENDSELECT
					CASE "터키석", "사파이어"
						;15個
						SELECTCASE 강화단계_소재판별루프 % 10
							CASE 1 TO 6
								강화소재개수10:(LCOUNT:1) += 1
							CASE 7 TO 9
								강화소재개수10:(LCOUNT:1) += 2
							CASE 0
								강화소재개수10:(LCOUNT:1) += 3
							CASEELSE
						ENDSELECT
					CASE "오팔"
						;10個
						강화소재개수10:(LCOUNT:1) += 1
				ENDSELECT
				
				;１回강화用
				IF LCOUNT == 0
					강화소재1 = %강화소재10:(LCOUNT:1)%
					강화소재개수1 = 강화소재개수10:(LCOUNT:1)
				ENDIF
					
				강화단계_소재판별루프++
				SIF 강화단계_소재판별루프 % 10 == 1
					LCOUNT:1 = 1
			NEXT
			
			IF BASE:대상:ＭＡＧ >= 소비ＭＡＧ && MONEY >= 소비ＭＡＧ/10 && ((개조장비 != 1) || (개조장비 == 1 && 강화단계 < 2))
				LOCALS = [ + 1] 소비ＭＡＧ：{소비ＭＡＧ, 8} / {BASE:대상:ＭＡＧ}　　소비￥：{소비ＭＡＧ/10, 8} / {MONEY}
				PRINTBUTTON LOCALS, 11
			ELSE
				SETCOLOR COLOR("gray")
				PRINTPLAINFORM [ × ] 소비ＭＡＧ：{소비ＭＡＧ, 8} / {BASE:대상:ＭＡＧ}　　소비￥：{소비ＭＡＧ/10, 8} / {MONEY}
				RESETCOLOR
			ENDIF
			PRINTL
			
			IF BASE:대상:ＭＡＧ >= 소비ＭＡＧ:1 && MONEY >= 소비ＭＡＧ:1/10 && 개조장비 != 1
				LOCALS = [ +10] 소비ＭＡＧ：{소비ＭＡＧ:1, 8} / {BASE:대상:ＭＡＧ}　　소비￥：{소비ＭＡＧ:1/10, 8} / {MONEY}
				PRINTBUTTON LOCALS, 12
			ELSE
				SETCOLOR COLOR("gray")
				PRINTPLAINFORM [ × ] 소비ＭＡＧ：{소비ＭＡＧ:1, 8} / {BASE:대상:ＭＡＧ}　　소비￥：{소비ＭＡＧ:1/10, 8} / {MONEY}
				RESETCOLOR
			ENDIF
			PRINTL
			
			IF 개념강화포인트 > 소비Ｐ && ((개조장비 != 1) || (개조장비 == 1 && 강화단계 < 2))
				LOCALS = [ + 1] 소비포인트 ：{소비Ｐ, 8} / {개념강화포인트,8}
				PRINTBUTTON LOCALS, 13
			ELSE
				SETCOLOR COLOR("gray")
				PRINTPLAINFORM [ × ] 소비포인트 ：{소비Ｐ, 8} / {개념강화포인트,8}
				RESETCOLOR
			ENDIF
			PRINTL

			IF 개념강화포인트 > 소비Ｐ:1 && 개조장비 != 1
				LOCALS = [ +10] 소비포인트 ：{소비Ｐ:1, 8} / {개념강화포인트,8}
				PRINTBUTTON LOCALS, 14			
			ELSE
				SETCOLOR COLOR("gray")
				PRINTPLAINFORM [ × ] 소비포인트 ：{소비Ｐ:1, 8} / {개념강화포인트,8}
				RESETCOLOR
			ENDIF
			PRINTL
			
			IF 강화단계 > 20
				IF ITEM:강화소재1 >= 강화소재개수1 && ((개조장비 != 1) || (개조장비 == 1 && 강화단계 < 2))
					LOCALS = [ + 1] 소비소재  ：%강화소재1, 12, RIGHT% × {강화소재개수1, 2, RIGHT} / {ITEM:강화소재1, 2, RIGHT}
					PRINTBUTTON LOCALS, 15
				ELSE
						SETCOLOR COLOR("gray")
					PRINTPLAINFORM [ × ] 소비보석  ：%강화소재1, 12, RIGHT% × {강화소재개수1, 2, RIGHT} / {ITEM:강화소재1, 2, RIGHT}
					RESETCOLOR
				ENDIF
				PRINTL
				
				IF ITEM:강화소재10 >= 강화소재개수10 && 개조장비 != 1
					LOCALS = [ +10] 소비보석１：%강화소재10, 12, RIGHT% × {강화소재개수10, 2, RIGHT} / {ITEM:강화소재10, 2, RIGHT}
					PRINTBUTTON LOCALS, 16				
			ELSE
				SETCOLOR COLOR("gray")
					PRINTPLAINFORM [ × ] 소비보석１：%강화소재10, 12, RIGHT% × {강화소재개수10, 2, RIGHT} / {ITEM:강화소재10, 2, RIGHT}
					RESETCOLOR
				ENDIF
				PRINTL
				IF 강화소재개수10:1 > 0
					SIF ITEM:(강화소재10:1) < 강화소재개수10:1
						SETCOLOR COLOR("gray")
					PRINTFORML        소비보석２：%강화소재10:1, 12, RIGHT% × {강화소재개수10:1, 2, RIGHT} / {ITEM:(강화소재10:1), 2, RIGHT}
						RESETCOLOR
				ENDIF
			ELSE
				SETCOLOR COLOR("gray")
				PRINTL [ × ] 소비보석  ：Lv21이상부터사용가능
				RESETCOLOR
			ENDIF

			;デバッグ用処理。
			;IF FLAG:DEBUG
			;	PRINTL
			;	LOCALS = [ + 1] デバッグ用処理：強化＋１
			;	PRINTBUTTON LOCALS, 90
			;	PRINTL
			;	LOCALS = [ +10] デバッグ用処理：強化＋１０
			;	PRINTBUTTON LOCALS, 91
			;	PRINTL
			;ENDIF
		ENDIF
		DRAWLINE
		PRINTLC [0]개념강화로 돌아간다
		PRINTLC [1]메뉴로 돌아간다
		INPUT
		LOCAL:3 = RESULT
		SELECTCASE RESULT
			CASE 0
				CLEARLINE LINECOUNT - THIRD_LINE
				GOTO START_D
			CASE 0
				CLEARLINE LINECOUNT - FIRST_LINE
				지정 = -1
			GOTO START_B
		; ELSEIF RESULT == 7 && P > 0
		; 	P -= 1
		; 	GOTO PRINT_LIST
		; ELSEIF RESULT == 9 && P < LOCAL:2/20
		; 	P += 1
		; 	GOTO PRINT_LIST
		CASE 11, 13, 15
			CALL 장비강화_전개, 대상, 장비번호, "강화단계", "NEXT"
			강화치 = RESULT + 1
			;CALL 장비강화_전개, 대상, 0, "강화단계", "保存강화치"
			;강화치 = 5 * (강화단계) * (강화단계+1) * (강화단계+2) / 3 + 1
			SELECTCASE LOCAL:3
					CASE 11
						BASE:대상:ＭＡＧ -= 소비ＭＡＧ
						MONEY -= 소비ＭＡＧ / 10
					CASE 13
						개념강화포인트 -= 소비Ｐ
					CASE 15
						ITEM:강화소재1 -= 강화소재개수1
					CASEELSE
						PRINTW 에러. 예상치 못한 동작이 발생했습니다.
				ENDSELECT
			CASE 12, 14, 16
				CALL 장비강화_전개, 대상, 장비번호, "강화단계", "EXP"
				강화치 = 5 * (강화단계+9) * (강화단계+1+9) * (강화단계+2+9) / 3 + 1 - RESULT
				SELECTCASE LOCAL:3
					CASE 12
						BASE:대상:ＭＡＧ -= 소비ＭＡＧ:1
						MONEY -= 소비ＭＡＧ:1 / 10
					CASE 14
						개념강화포인트 -= 소비Ｐ:1
					CASE 16
						ITEM:강화소재10 -= 강화소재개수10
						SIF 강화소재개수10:1 > 0
							ITEM:(강화소재10:1) -= 강화소재개수10:1
					CASEELSE
						PRINTW 에러. 예상치 못한 동작이 발생했습니다.
				ENDSELECT
			;デバッグ用処理
		;	CASE 90
		;		IF FLAG:DEBUG
		;			CALL 장비강화_전개, 대상, 장비번호, "강화단계", "NEXT"
		;			강화치 = RESULT + 1
		;		ENDIF
			;デバッグ用処理
		;	CASE 91
		;		IF FLAG:DEBUG
		;			CALL 장비강화_전개, 대상, 장비번호, "강화단계", "EXP"
		;			강화치 = 5 * (강화단계+9) * (강화단계+1+9) * (강화단계+2+9) / 3 + 1 - RESULT
		;		ENDIF
			CASEELSE
				GOTO PRINT_LIST
		ENDSELECT
		SIF 개조장비(장비번호)
			CALL 장비강화_CSTR, "書き込み", 대상, 장비번호, "기록_개조장비베이스번호", "EXP", 강화치
		CALL 장비강화_CSTR, "書き込み", 대상, 장비번호, "강화단계", "EXP", 강화치
		CALL 장비강화_전개, 대상, 장비번호, "강화단계", "LV"
		IF 강화단계 < 2
			CALL 장비강화_전개, 대상, 장비번호, "강화단계", "NEXT"
			강화치 = RESULT + 1
			CALL 장비강화_CSTR, "書き込み", 대상, 장비번호, "강화단계", "EXP", 강화치
			CALL 장비강화_전개, 대상, 장비번호, "강화단계", "LV"
		ENDIF
		SIF 강화단계 < RESULT
			PRINTFORMW 강화단계  Lv:{강화단계} → Lv:{RESULT}
		IF RESULT >= BASE:대상:LV
			CLEARLINE LINECOUNT - FIRST_LINE
			GOTO START_B
		ENDIF
		GOTO S_LIST

	CASE 1
		CLEARLINE LINECOUNT - SECOND_LINE
		;PRINTL □능력강화
		$START_1
		DRAWLINE
		PRINTFORML □능력강화　　(현재ＭＡＧ：{BASE:ARG:ＭＡＧ})
		CALL 장비강화_능력강화,대상,장비번호
	CASE 2
		CLEARLINE LINECOUNT - SECOND_LINE
		;PRINTL □내성강화
		CALL 장비강화_내성강화,대상,장비번호
	CASE 3
		CLEARLINE LINECOUNT - SECOND_LINE
		IF 장비부위 == GET_EQUIPNUM("검")
			;PRINTL □공격상성강화
			CALL 장비강화_공격상성,대상,장비번호
			IF RESULT == -1
				CLEARLINE LINECOUNT - FIRST_LINE
				지정 = -1
				GOTO START_B
			ENDIF
		ENDIF
	CASE 4
		CLEARLINE LINECOUNT - SECOND_LINE
		IF 장비부위 == GET_EQUIPNUM("검")
			;PRINTL □추가효과부여
			CALL 장비강화_추가효과,대상,장비번호
			IF RESULT == -1
				CLEARLINE LINECOUNT - FIRST_LINE
				지정 = -1
				GOTO START_B
			ENDIF
		ENDIF
	CASE 8
		CLEARLINE LINECOUNT - SECOND_LINE
		;PRINTL □강화해제
		CALL 장비강화_강화해제,대상,장비번호
		IF RESULT == -1
			CLEARLINE LINECOUNT - FIRST_LINE
			지정 = -1
			GOTO START_B
		ENDIF
	CASE 99
		CLEARLINE LINECOUNT - SECOND_LINE
		FIRST_LINE = LINECOUNT
		PRINTL 마장술을 구입해주셔서 감사합니다。
		PRINTL 본서를 잘 읽고 올바른 사용방법으로 애용해 주십시오。
		SECOND_LINE = LINECOUNT
		DO
			PRINTL
			PRINTL [ 0] 강화방법
			PRINTL [ 1] 개념강화
			PRINTL [ 2] 능력강화
			PRINTL [ 3] 내성강화
			PRINTL [ 4] 공격상성강화
			PRINTL [ 5] 추가효과부여
			PRINTL [ 6] 강화해제
			PRINTL [ 7] 경쟁하는것에 대하여
			PRINTL [ 8] 그외
			PRINTL [99] 돌아간다
			INPUT
			DRAWLINE

			SELECTCASE RESULT
				CASE 0
					PRINTL   『마장술』을 통한 강화 방법은 다음과 같습니다。
					PRINTL
					PRINTL   1.『개념강화』에서 장비의 레벨을 높인다。
					PRINTL   2. 하의 몇 가지 강화를 행한다。
					PRINTL
					PRINTL    『능력강화』    전투 능력의 강화。장비의 레벨에 비례한다
					PRINTL    『내성강화』    방어상성의 강화。약점의 많음에 비례한다
					PRINTL    『공격상성강화』검에 공격상성을 추가하는 강화。악마 1체의 소비가 필요
					PRINTL    『추가효과부여』검에 상태이상 효과를 추가하는 강화。악마 1체의 소비가 필요
					PRINTL
					PRINTL   각 강화의 자세한 내용을 알고 싶다면 다른 항목을 보시기 바랍니다。
				CASE 1
					PRINTL 『개념강화』
					PRINTL 강화를 한 인간과 장비의 연결을 강화하는 것이 가능합니다。
					PRINTL 한 번 강화하면 결합이 이뤄져、그 강함을 레벨로 나타냅니다。
					PRINTL 장비의 레벨은、연결되어 있는 인간과 동일한 레벨까지 올리는 것이 가능합니다。
					PRINTL
					PRINTL 『개념강화』만으로는 성능이 변화하지 않습니다。그러나 별항에서 설명하는 강화를 실행하기 위해、한 번이라도 할 필요가 있습니다。
					PRINTL
					PRINTL 비고：연결은 장비의 종류에 대한 것입니다。
					PRINTL 예를 들어、당신이 『어택나이프』를 강화한 경우、새로 구매한 별도의 『어택나이프』를 장비할 때에도 강화가 적용됩니다。
					PRINTL
					PRINTL 강화에는 MAG와 금전이 필요합니다。전부는 아니지만、보석으로도 대용 가능합니다。
				CASE 2
					PRINTL 『능력강화』
					PRINTL 장비의 전투능력을 강화하는 것이 가능합니다。
					PRINTL 예를 들어 강화하는 장비가 검이라면、공격・명중・마법위력・마법효과 등、그 장비에 원래 갖추어져 있는 능력의 강화가 가능합니다。
					PRINTL
					PRINTL 능력강화용의 포인트를 분배하는 것으로 강화가 이뤄집니다。다만 장비에 따라 한계치가 있고、그 이상 분배하는 것은 불가능합니다。
					PRINTL 포인트의 한계치는、그 장비의 레벨을 올리는 것으로 증가합니다。
					PRINTL 단, 장비를 장비한 사람의 능력에 따라 성능이 변화하는 장비는 예외입니다。포인트와 한계치가 현격하게 감소합니다。
					PRINTL 또한 방어상성이 모두 통상이며 상태이상내성도 없는 방어구는 다른 방어구보다 한계치가 높아집니다。
					PRINTL
					PRINTL 비고：장비 레벨을 높이다 보면 원래 해당 장비가 갖추지 못한 능력을 강화시킬 수 있습니다。
					PRINTL       단, 그 능력의 한계치는 다른 것보다 매우 낮아지기 때문에 기대하진 않는게 좋습니다。
				CASE 3
					PRINTL 『내성강화』
					PRINTL 방어구의 내성을 강화하는 것이 가능합니다。
					PRINTL
					PRINTL 내성강화용의 포인트를 분배하는 것으로 강화가 이뤄집니다。
					PRINTL 포인트는 강화하는 장비의 본래의 내성에 따라 결정됩니다。약점이 많을수록 포인트가 많고、내성이 많을 수록 포인트는 적어집니다。
					PRINTL 또한、장비의 레벨을 10 올릴 때마다 5포인트가 가산됩니다。
					PRINTL
					PRINTL 본래의 내성이 "내성"또는 "약점"일 경우에만 강화할 수 있습니다。"통상"、"무효"、"흡수"、"반사"는 대상외입니다。
					PRINTL 본래의 내성이 "내성(100% 미만)"이면 "무효(0%)"가 될 때까지、"약점(100% 이상)"이라면 "통상(100%)"이 될 때 까지 강화가 가능합니다。
				CASE 4
					PRINTL 『공격상성강화』
					PRINTL 무기에 새로운 속성을 추가하는 강화입니다。본래의 속성과 추가한 속성의 복합속성이 됩니다。
					PRINTL 공격했을 때、속성에 더 많은 데미지를 입히는 쪽의 속성으로 변화하지만、추가한 속성으로 변하는지는 "확률"로 결정됩니다。
					PRINTL 일반 공격은 물론、원래 상성과 동일한 상성의 물리계 스킬을 사용하려고 하는 경우도 변화합니다.
					PRINTL 또한、총과 방어구는 이 강화를 할 수 없습니다。
					PRINTL
					PRINTL 강화에는 악마를 일체 소비합니다。
					PRINTL 기본적으로 그 악마의 "상성소질"이 추가되는 상성의 후보가 됩니다。
					PRINTL 기억하고 있는 스킬과 향후 레벨업에서 습득할 스킬 중에서 후보와 상성이 같고, 가장 등급이 높은 스킬의 상성이 추가되는 상성이 됩니다。
					PRINTL 상성과 "계승타입" 같은 상성의 스킬이 많을수록, 또 그 스킬의 등급이 높을수록 확률이 높아집니다。
					PRINTL 또한, 상성소질이 없는 경우는 소지스킬과 습득스킬 중 가장 등급이 높은 스킬의 상성이 추가됩니다。
					PRINTL
					PRINTL 주의: 만능, 회복, 지원, 방어, 자동 효과, 전투 시 이외에 사용할 수 있는 스킬은 제외됩니다。
				CASE 5
					PRINTL 『추가효과부여』
					PRINTL 무기에 상태이상의 추가효과를 추가하는 강화입니다。
					PRINTL 총 이외의 추가 효과가 없는 무기에 대해 이 강화를 할 수 있습니다。
					PRINTL
					PRINTL 재료로서 악마를 1체 소비합니다。
					PRINTL 그 악마가 기억하고 있는 스킬의 추가효과에 따라、부여하는 추가효과를 결정합니다。
					PRINTL 같은 추가 효과를 가진 스킬이 많을수록, 또한 해당 스킬의 등급이 높을수록 추가 효과 확률이 상승합니다。
					PRINTL
					PRINTL 또한、BOMB、STONE、DYING、FLY는 부여할 수 없습니다。양해바랍니다。
					PRINTL
					PRINTL 비고:목표하는 추가효과가 표시되지 않는 경우에는 악마가 기억하고 있는 다른 스킬을 소거해보십시오。반드시 표시될 것입니다。
				CASE 6
					PRINTL 『강화해제』
					PRINTL 마장술에 의해 시행된 강화를 해제합니다。
					PRINTL 마장술로 강화된 장비를 선택하면、그 장비의 "개념강화를 포함한" 모든 강화가 해제됩니다。
					PRINTL 이때, 장비의 레벨에 따라 개념강화용 포인트를 얻을 수 있습니다。
					PRINTL 그 포인트는 자신은 물론 다른 사람의 장비를 개념강화할 경우에도 사용할 수 있으니 꼭 활용하시기 바랍니다。
					PRINTL 
					PRINTL "강화가능수"가 한계에 이르렀을 경우、강화를 없애고 싶을 경우에 이 기능을 사용해주십시오。
					PRINTL 
					PRINTL 비고：능력강화와 내성강화는 할당한 포인트를 0으로 하면 강화를 하지 않은 취급으로 돌아갑니다。
				CASE 7
					PRINTL   현재、마장술과 병용했을 경우、경쟁을 일으키는것이 몇가지 확인되고 있습니다。
					;        という事にして併用不可にする。強くなり過ぎるので。COMPを58%も使って併用する人がいるかは知らん
					PRINTL   확인된것은 다음과 같습니다。주의해주세요。
					PRINTL
					PRINTL  『하모나이저』
					PRINTL   경쟁하므로 하모나이저가 기동중에는 마장술을 기동할 수 없습니다。
					PRINTL   마장술이 기동중인 경우에도 하모나이저는 기동할 수 없습니다。
					PRINTL
					PRINTL
					PRINTL  『드워프의대장간』
					PRINTL   드워프의 대장간에서 작성한『개조장비』를 『마장술』로 강화하는 경우、일부 강화가 제한됩니다。
					PRINTL
					PRINTL   개념강화    ：LV2까지 가능
					PRINTL   능력강화    ：불가능
					PRINTL   내성강화    ：불가능
					PRINTL   공격상성강화：가능
					PRINTL   추가효과부여：가능
				CASE 8
					PRINTL ・"강화가능수"는 강화의 종류에서 셉니다。
					PRINTL   예를 들어 "강화가능수"가 50일 때 하나의 방어구의 방어과 회피를 강화한 경우、"강화가능수"는 48이 됩니다。
					PRINTL
					PRINTL ・『공격상성강화』와 『추가효과부여』는 장비의 레벨의 영향을 거의 받지 않습니다。
					PRINTL   이 강화만을 사용하는 것이라면 、장비의 레벨은 1 올리는 것만으로 충분합니다。
					PRINTL
					PRINTL ・어떠한 이유로 장비의 원래 레벨이 사용자 레벨보다 높을 수 있습니다。
					PRINTL   그 경우는 장비의 레벨이 사용자의 레벨과 동등한 수치까치 내려갑니다。
					PRINTL   그러나 사용자의 레벨이 올라갈때마다 장비의 레벨도 원래 레벨까지 자동으로 올라가니 안심하시기 바랍니다。
				CASE 99
					BREAK
				CASEELSE
					CLEARLINE LINECOUNT - FIRST_LINE
					CONTINUE
			ENDSELECT
			PRINTL
			DRAWLINE
			WAIT
			CLEARLINE LINECOUNT - FIRST_LINE
		LOOP 1
		CLEARLINE LINECOUNT - FIRST_LINE
		지정 = -1
ENDSELECT
	CALL SYNC_STATUS,대상
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO START_B

@장비강화_INPUT,ARGS, ARG, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9, ARG:10
INPUT
SELECTCASE RESULT
CASE 0 TO ARG - 1, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9, ARG:10
	RETURN RESULT
ENDSELECT
RESTART

@장비강화_능력강화, ARG, ARG:1
#DIM DYNAMIC FIRST_LINE
#DIM DYNAMIC 대상
#DIM DYNAMIC 장비번호
#DIM DYNAMIC 장비부위
#DIM DYNAMIC 강화단계
#DIM DYNAMIC 기초합계치
#DIM DYNAMIC 기초능력, 8
#DIM DYNAMIC 강화포인트, 8
#DIM DYNAMIC 강화포인트합계
#DIM DYNAMIC 선택능력,1
#DIM DYNAMIC 선택강화,1
#DIM DYNAMIC 포인트, 1
#DIM DYNAMIC 할당종료
#DIM DYNAMIC 할당후, 8
#DIMS DYNAMIC 선택지
#DIM DYNAMIC LCOUNT

#DIM DYNAMIC 강화포인트_최대, 8
#DIM DYNAMIC 강화포인트합계_최대
#DIM DYNAMIC 강화수 = 0
#DIM DYNAMIC 변화량_Max, 8
#DIM DYNAMIC 변화량_Min, 8
#DIM DYNAMIC 성능변동, 8
#DIM DYNAMIC 성능변동판정
#DIM DYNAMIC 성능변동보정
#DIM DYNAMIC 내성無
#DIM DYNAMIC 기초능력割合
#DIM DYNAMIC 미배분_최대, 8
#DIM DYNAMIC 미배분, 8
;#DIM DYNAMIC 装備品_必要レベル

VARSET 포인트
VARSET 할당후
VARSET 기초합계치
VARSET 기초능력
VARSET 강화포인트
VARSET 할당종료
;	기초能力:0 = BASE:대상:공격
;	기초能力:1 = BASE:대상:명중
;	기초能力:2 = BASE:대상:방어
;	기초能力:3 = BASE:대상:회피
;	기초能力:4 = BASE:대상:마법위력
;	기초能力:5 = BASE:대상:마법효과
;	기초能力:6 = BASE:대상:총공격
;	기초能力:7 = BASE:대상:총명중
VARSET 강화포인트합계

VARSET 강화포인트_최대
VARSET 강화포인트합계_최대

대상 = ARG
장비번호 = ARG:1
기초합계치 = 0

CALL 장비강화_CSTR, "강화수", 대상
강화수 = RESULT
CALLFORM 장비부위_{장비번호}
장비부위 = RESULT
CALL 장비강화_전개, 대상, 장비번호, "강화단계", "LV"
강화단계 = RESULT
CALL 장비강화_성능변동판정, 대상, 장비번호, 장비부위
IF RESULT:8 > 0
	FOR LCOUNT, 0, 8
		성능변동:LCOUNT = RESULT:LCOUNT
	NEXT
	성능변동판정 = RESULT:8
	성능변동보정 = RESULT:9
ENDIF

FOR LCOUNT,0,FLAG:전투능력수
	CALLFORM 전투능력수정_{장비번호}, LCOUNT, -1
	기초능력:LCOUNT = RESULT
	기초합계치 += RESULT
NEXT
IF 장비부위 == GET_EQUIPNUM("총")
	CALLFORM 총공격_{장비번호}
	기초능력:6 = RESULT
	CALLFORM 총명중_{장비번호}
	기초능력:7 = RESULT
	기초합계치 += 기초능력:6 + 기초능력:7
ENDIF

FOR LCOUNT,0,FLAG:전투능력수 +2
	CALL 장비강화_전개, 대상, 장비번호, "전투능력수정", GET_BATTLESTATUS(LCOUNT),-1
	할당후:LCOUNT = RESULT
	할당종료 += RESULT
NEXT

;防具のみ
IF 장비부위 > 1 && 장비부위 < 6 && 성능변동판정 == 0
	내성無 = 0
	FOR LCOUNT, 0, FLAG:상성수
		CALLFORM 방어상성_{장비번호}, LCOUNT, 대상
		IF RESULT != 100
			내성無 = 2
			BREAK
		ENDIF
	NEXT
	IF 내성無 < 2
		RESULT = 0
		FOR LCOUNT, 0, FLAG:상태이상수
			TRYCALLFORM バステ내성_{장비번호}, LCOUNT, 대상
			IF RESULT != 0
				내성無 = 2
				BREAK
			ENDIF
		NEXT
	ENDIF
	SIF 내성無 == 0
		내성無 = 1
ENDIF

CALL 장비강화_강화포인트산출(장비번호, 장비부위, 강화단계)
포인트 = RESULT

SIF 기초합계치 < 1
	기초합계치 = 1
	
FOR LCOUNT, 0, FLAG:전투능력수 +2
	기초능력割合 = 기초능력:LCOUNT *100 / 기초합계치
;	IF 강화포인트:LCOUNT < 10
;		강화포인트:LCOUNT += 20
;	ELSEIF 강화포인트:LCOUNT < 20
;		강화포인트:LCOUNT += 10
;	ELSEIF 강화포인트:LCOUNT > 50
;		강화포인트:LCOUNT = 50 - (강화포인트:LCOUNT / 10)
;	ELSEIF 강화포인트:LCOUNT > 80
;		강화포인트:LCOUNT -= 20
;	ELSEIF 강화포인트:LCOUNT > 60
;		강화포인트:LCOUNT -= 10
;	ENDIF
;	RESULT = 0
;	CALLFORM 장비부위_{장비번호}
;	SIF RESULT == GET_EQUIPNUM("검") || RESULT == GET_EQUIPNUM("총")
;		강화포인트:LCOUNT = 50

	CALL 장비강화_강화포인트산출, 장비번호, 장비부위, 99
	강화포인트_최대:LCOUNT = RESULT * 기초능력割合 / 100
	CALL 장비강화_강화포인트제한, 99, 장비부위, GET_BATTLESTATUS(LCOUNT)
	;割り振る事ができる포인트の최대値。기초능력と振った포인트の합계値ではない
	IF 강화포인트_최대:LCOUNT + 기초능력:LCOUNT > RESULT
		미배분_최대:LCOUNT = 강화포인트_최대:LCOUNT
		강화포인트_최대:LCOUNT = RESULT - 기초능력:LCOUNT + MIN(20, 강화포인트_최대:LCOUNT/10)
		미배분_최대:LCOUNT -= 강화포인트_최대:LCOUNT
	ENDIF
	
	강화포인트:LCOUNT = 포인트 * 기초능력割合 / 100
	CALL 장비강화_강화포인트제한, 강화단계, 장비부위, GET_BATTLESTATUS(LCOUNT)
	;こっちは現段階
	SIF 강화포인트:LCOUNT + 기초능력:LCOUNT > RESULT
		강화포인트:LCOUNT = RESULT - 기초능력:LCOUNT + MIN(20, 강화포인트:LCOUNT/10)

	;自前の강화があるので魔装術での강화は控えめに
	IF 성능변동판정 == 1 || (성능변동판정 == 2 && 성능변동:LCOUNT == 1)
		강화포인트_최대:LCOUNT = 강화포인트_최대:LCOUNT * 성능변동보정 / 100
		강화포인트:LCOUNT = 강화포인트:LCOUNT * 성능변동보정 / 100
	ENDIF
	
	;何かしらボーナスがないと내성のない防具は装備しないだろうと思うので。+20%くらいがちょうど良い
	IF 내성無 == 1
		강화포인트_최대:LCOUNT += (기초능력:LCOUNT + 강화포인트_최대:LCOUNT) / 5
		강화포인트:LCOUNT += (기초능력:LCOUNT + 강화포인트:LCOUNT) / 5
	ENDIF
NEXT

;강화포인트の計算式的に偏った性能の装備が割を食っているので、尖がった性能がより尖るようにする
;防具は不可。내성無しや性能がゼロの部分へのボーナスがあるから강화が過剰になる
IF SUMARRAY(미배분_최대, 0) > 0 && 장비부위 < 2
	IF 성능변동판정 > 0
		VARSET 미배분_최대
	ELSE
		FOR LCOUNT, 0, FLAG:전투능력수 + 2
			IF 미배분_최대:LCOUNT > 0
				IF LCOUNT % 2 == 0
					LOCAL = LCOUNT + 1
				ELSE
					LOCAL = LCOUNT - 1
				ENDIF
				
				IF 기초능력:LOCAL == 0
					미배분_최대:LCOUNT = 0
				ELSE
					CALL 장비강화_강화포인트제한, 99, 장비부위, GET_BATTLESTATUS(LOCAL)
					LOCAL:1 = 100 - ((강화포인트_최대:LOCAL + 기초능력:LOCAL) * 100 / ((RESULT + 20)))
					SIF LOCAL:1 < 0
						LOCAL:1 = 0
					미배분_최대:LCOUNT = 미배분_최대:LCOUNT * LOCAL:1 / 100
					;銃は강화できる능력の수が半分なので
					SIF 장비부위 == GET_EQUIPNUM("총")
						미배분_최대:LCOUNT /= 2
					미배분:LCOUNT = 미배분_최대:LCOUNT * 강화단계 / 99
				;	디버그용
				;	IF 미배분_최대:LCOUNT > 0
				;		PRINTFORML %GET_BATTLESTATUS(LCOUNT)%_최대：{미배분_최대:LCOUNT}
				;		PRINTFORML %GET_BATTLESTATUS(LCOUNT)%     ：{미배분:LCOUNT}
				;	ENDIF
				ENDIF
			ELSE
				미배분_최대:LCOUNT = 0
			ENDIF
		NEXT
		;↑のFOR文内で加算するとLOCAL:1が正確に計算されないのでここで加算
		FOR LCOUNT, 0, FLAG:전투능력수 + 2
			IF 미배분_최대:LCOUNT > 0
				강화포인트_최대:LCOUNT += 미배분_최대:LCOUNT
				강화포인트:LCOUNT += 미배분:LCOUNT
			ENDIF
		NEXT
	ENDIF
ENDIF

강화포인트합계_최대 = SUMARRAY(강화포인트_최대, 0)
강화포인트합계 = SUMARRAY(강화포인트, 0)
포인트 -= 기초합계치

;防具の元々の性能がゼロの部分に포인트られる포인트の上限を設定
IF (포인트 - 강화포인트합계) > 0 && 장비부위 != GET_EQUIPNUM("검") && 장비부위 != GET_EQUIPNUM("총")
	FOR LCOUNT,0,FLAG:전투능력수 +2
		IF 기초능력:LCOUNT == 0 && (LCOUNT >= 2 && LCOUNT <= 5)
			강화포인트:LCOUNT += (포인트 - 강화포인트합계) / 4
			강화포인트합계_최대 += 강화포인트:LCOUNT
		ENDIF
	NEXT
ENDIF

포인트 = 강화포인트합계 * (50 + SQRT(강화단계 + 1) * 5) / 100

;全く강화できないのも寂しいので少しだけ강화できるように
SIF 강화포인트합계_최대 < 15 && 성능변동판정 == 0
	포인트 = (강화단계 + 1) * 15 / 100

;개조장비は강화불가。鍛冶屋での강화だけで、魔装術での능력강화の上限近くになる為
SIF 개조장비(장비번호)
	포인트 = 0

포인트 -= 할당종료

FIRST_LINE = LINECOUNT
REDRAW 0
$S_LOOP
DRAWLINE
IF 개조장비(장비번호)
	PRINTFORM <%개조장비명칭(개조장비番号(장비번호))%>
ELSE
	PRINTFORM <%ITEMNAME:장비번호%>
ENDIF
CALL 장비강화_전개, 대상, 장비번호, "강화단계", "보존LV"
IF 강화단계 == RESULT
	PRINTFORM   Lv:{강화단계,5,LEFT}
ELSE
	PRINTFORM   Lv:{강화단계} ({RESULT})
ENDIF
PRINTFORM    강화可能回수:{50 - 강화수}
IF 50 - 강화수 == 0
	SETCOLOR COLOR("yellow")
	PRINT  [한계]
	RESETCOLOR
ENDIF
PRINTL
DRAWLINE
IF 개조장비(장비번호)
	PRINTFORML 강화불가
ELSE
	PRINTFORML 나머지 포인트:{포인트}
ENDIF
DRAWLINE

선택지 = 
FOR LCOUNT,0,FLAG:전투능력수 +2
	IF 기초능력:LCOUNT || 강화포인트:LCOUNT
		변화량_Max:LCOUNT = LIMIT(강화포인트:LCOUNT - 할당후:LCOUNT, 1, MAX(포인트, 1))
		변화량_Min:LCOUNT = MAX(할당후:LCOUNT, 1) * -1
		PRINTFORM %GET_BATTLESTATUS(LCOUNT),8,LEFT%:{기초능력:LCOUNT, 3} + {할당후:LCOUNT, 3} = {기초능력:LCOUNT + 할당후:LCOUNT, 3}
		CALL BUTTON_강화포인트("[ +1]", 200+(LCOUNT*10), 할당후:LCOUNT, 포인트, 1, 강화포인트:LCOUNT, 대상, 강화수)
		SIF RESULT != -1
			선택지 += TOSTR( 200+(LCOUNT*10) ) + "/"
		CALL BUTTON_강화포인트("[ -1]", 201+(LCOUNT*10), 할당후:LCOUNT, 포인트, -1, 강화포인트:LCOUNT)
		SIF RESULT != -1
			선택지 += TOSTR( 201+(LCOUNT*10) ) + "/"
		CALL BUTTON_강화포인트("[+10]", 202+(LCOUNT*10), 할당후:LCOUNT, 포인트, 10, 강화포인트:LCOUNT, 대상, 강화수)
		SIF RESULT != -1
			선택지 += TOSTR( 202+(LCOUNT*10) ) + "/"
		CALL BUTTON_강화포인트("[-10]", 203+(LCOUNT*10), 할당후:LCOUNT, 포인트, -10, 강화포인트:LCOUNT)
		SIF RESULT != -1
			선택지 += TOSTR( 203+(LCOUNT*10) ) + "/"
		CALL BUTTON_강화포인트("[MAX]", 204+(LCOUNT*10), 할당후:LCOUNT, 포인트, 변화량_Max:LCOUNT, 강화포인트:LCOUNT, 대상, 강화수)
		SIF RESULT != -1
			선택지 += TOSTR( 204+(LCOUNT*10) ) + "/"
		CALL BUTTON_강화포인트("[MIN]", 205+(LCOUNT*10), 할당후:LCOUNT, 포인트, 변화량_Min:LCOUNT, 강화포인트:LCOUNT)
		SIF RESULT != -1
			선택지 += TOSTR( 205+(LCOUNT*10) ) + "/"
		IF 할당후:LCOUNT >= 강화포인트:LCOUNT
			SETCOLOR COLOR("AQUA")
			PRINT [한계]
			RESETCOLOR
		ENDIF
		PRINTL
	ENDIF
NEXT

DRAWLINE
PRINTFORML [0] 결정
	선택지 += "0/"
PRINTFORML [998] 포인트 재조정
	선택지 += "998/"
PRINTFORML [999] 돌아간다
	선택지 += "999/"
$INPUT_LOOP_1
;INPUT

	CALL 장비강화_INPUT_LIST,선택지

IF RESULT == 999
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 0
	REDRAW 1
	PRINTFORML 능력 강화를 마치시겠습니까？
	CALL INPUT_YN,"예","아니오"
	IF RESULT == 0
		FOR LCOUNT,0,FLAG:전투능력수 +2
			CALL 장비강화_CSTR, "書き込み", 대상, 장비번호, "능력강화", GET_BATTLESTATUS(LCOUNT), 할당후:LCOUNT
		NEXT
		RETURN 0
	ELSEIF RESULT == 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO S_LOOP
	ENDIF
ELSEIF RESULT == 998
	REDRAW 1
	PRINTFORML 포인트를 재분배하시겠습니까？
	CALL INPUT_YN,"예","아니오"
	IF RESULT == 0
		FOR LCOUNT,0,FLAG:전투능력수 +2
			할당후:LCOUNT = 0
			CALL 장비강화_CSTR, "書き込み", 대상, 장비번호, "능력강화", GET_BATTLESTATUS(LCOUNT), 할당후:LCOUNT
		NEXT
		RESTART
	ELSEIF RESULT == 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO S_LOOP
	ENDIF
ELSEIF RESULT != -1
	선택능력 = (RESULT - 200) / 10
	선택강화 = (RESULT - 200) % 10
	SELECTCASE 선택강화
		CASE 0
			선택강화 = 1
		CASE 1
			선택강화 = -1
		CASE 2
			선택강화 = 10
		CASE 3
			선택강화 = -10
		CASE 4
			선택강화 = 변화량_Max:선택능력
		CASE 5
			선택강화 = 변화량_Min:선택능력
		CASEELSE
			CLEARLINE LINECOUNT - FIRST_LINE
			GOTO S_LOOP
	ENDSELECT
	IF !INRANGE(선택능력, 0, 8)
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO S_LOOP
	ENDIF
	SIF 할당후:선택능력 == 0
		강화수 += 1
	할당후:선택능력 += 선택강화
	SIF 할당후:선택능력 == 0
		강화수 -= 1
	포인트 -= 선택강화
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
GOTO S_LOOP

@장비강화_강화포인트제한, ARG, ARG:1, ARGS
;ARG 강화단계레벨  ARG:1 부위  ARGS 능력
;実際はここで出した한계치にいくらか足した수値が上限になる
#DIM DYNAMIC 한계치
#DIM DYNAMIC 장비부위
#DIMS DYNAMIC 지정능력

한계치 = ARG
장비부위 = ARG:1
지정능력 = %ARGS%

SELECTCASE 장비부위
	CASE GET_EQUIPNUM("검")
		SELECTCASE 지정능력
			CASE "공격"
			TIMES 한계치, 3.5
			SIF 한계치 > 260
				한계치 = 260 + (한계치-260)/6
			CASE "명중"
				TIMES 한계치, 3.5
				SIF 한계치 > 250
					한계치 = 250 + (한계치-250)/6
			CASE "마법위력"
				TIMES 한계치, 3.5
				SIF 한계치 > 260
					한계치 = 260 + (한계치-260)/6
			CASE "마법효과"
				TIMES 한계치, 3.0
				SIF 한계치 > 200
					한계치 = 200 + (한계치-200)/6
			CASEELSE
				TIMES 한계치, 1.0
		ENDSELECT
CASE GET_EQUIPNUM("총")
		SELECTCASE 지정능력
			CASE "총공격"
				TIMES 한계치, 4.0
				SIF 한계치 > 260
					한계치 = 260 + (한계치-260) / 6
			CASE "총명중"
				TIMES 한계치, 3.5
				SIF 한계치 > 220
					한계치 = 220 + (한계치-220)/6
			CASEELSE
				TIMES 한계치, 0.5
		ENDSELECT
	CASE GET_EQUIPNUM("머리")
		SELECTCASE 지정능력
			CASE "방어"
				TIMES 한계치, 0.8
				SIF 한계치 > 40
					한계치 = 40 + (한계치-40)/4
			CASE "회피"
				TIMES 한계치, 0.5
				SIF 한계치 > 20
					한계치 = 20 + (한계치-20)/4
			CASE "마법효과"
				TIMES 한계치, 0.85
				SIF 한계치 > 55
					한계치 = 55 + (한계치-55)/4
			CASEELSE
				TIMES 한계치, 0.5
		ENDSELECT
	CASE GET_EQUIPNUM("몸통")
		SELECTCASE 지정능력
			CASE "방어"
				TIMES 한계치, 1.7
				SIF 한계치 > 100
					한계치 = 100 + (한계치-100)/6
			CASE "회피"
				TIMES 한계치, 0.9
				SIF 한계치 > 40
					한계치 = 40 + (한계치-40)/6
			CASE "마법효과"
				TIMES 한계치, 0.6
				SIF 한계치 > 40
					한계치 = 40 + (한계치-40)/6
			CASEELSE
				TIMES 한계치, 0.5
		ENDSELECT
	CASE GET_EQUIPNUM("팔")
		SELECTCASE 지정능력
			CASE "방어"
				TIMES 한계치, 0.9
				SIF 한계치 > 55
					한계치 = 55 + (한계치-55)/4
			CASE "회피"
				TIMES 한계치, 0.35
				SIF 한계치 > 20
					한계치 = 20 + (한계치-20)/4
			CASE "마법효과"
				TIMES 한계치, 0.3
				SIF 한계치 > 20
					한계치 = 20 + (한계치-20)/4
			CASEELSE
				TIMES 한계치 , 0.3
		ENDSELECT
	CASE GET_EQUIPNUM("발")
		SELECTCASE 지정능력
			CASE "방어"
				TIMES 한계치, 0.8
				SIF 한계치 > 45
					한계치 = 45 + (한계치-50)/4
			CASE "회피"
				TIMES 한계치, 1.0
				SIF 한계치 > 50
					한계치 = 50 + (한계치-50)/4
			CASE "마법효과"
				TIMES 한계치 , 0.7
				SIF 한계치 > 40
					한계치 = 40 + (한계치-40)/4
			CASEELSE
				TIMES 한계치, 0.5
		ENDSELECT
	CASEELSE
		한계치 = 0
ENDSELECT

RETURN 한계치

@장비강화_내성강화,ARG,ARG:1
#DIM DYNAMIC FIRST_LINE
#DIM DYNAMIC SECOND_LINE
#DIM DYNAMIC 대상
#DIM DYNAMIC 장비번호
#DIM DYNAMIC 강화단계
#DIM DYNAMIC 내성합계
#DIM DYNAMIC 약점합계
#DIM DYNAMIC 현재치
#DIM DYNAMIC 기초치
#DIM DYNAMIC 강화치
#DIMS DYNAMIC 상성
#DIM DYNAMIC 강화포인트
#DIM DYNAMIC 선택강화
#DIM DYNAMIC 포인트
#DIM DYNAMIC 할당종료
#DIM DYNAMIC 할당후
#DIMS DYNAMIC 선택지
#DIM DYNAMIC 강화수 = 0
#DIM DYNAMIC 변화량_Max
#DIM DYNAMIC 변화량_Min

VARSET 포인트
VARSET 할당후
VARSET 강화포인트
VARSET 할당종료
VARSET 내성합계
VARSET 약점합계

대상 = ARG
장비번호 = ARG:1

CALL 장비강화_CSTR, "강화수", 대상
강화수 = RESULT

FOR LOCAL, 0, FLAG:상성수 - 1
	CALLFORM 방어상성_{장비번호},LOCAL,대상
	SELECTCASE RESULT
		CASE 999
			내성합계 += 15 
		CASE IS < 0
			내성합계 += 15 
		CASE 0
			내성합계 += 10 
		CASE IS > 199
			약점합계 += 45
		CASE IS > 149
			약점합계 += 25
		CASE IS > 100
			약점합계 += 15
		CASE IS <= 50
			내성합계 += 5 
		CASE IS < 100
			내성합계 += 3 
	ENDSELECT
NEXT

FIRST_LINE = LINECOUNT
REDRAW 0
	$START_3
	IF (약점합계 + 내성합계) == 0
		PRINTW 강화できる내성がありません
		RETURN
	ENDIF
	포인트 = (약점합계 - 내성합계) * 5 / 5
	CALL 장비강화_전개, 대상, 장비번호, "강화단계", "LV"
	강화단계 = RESULT
	포인트 += 강화단계 / 5 * 5 /2
	SIF 강화단계 == 99
		포인트 += 5

	;개조장비は강화불가。鍛冶屋と併用できるとゲームバランスが崩れそうなので
	SIF 개조장비(장비번호)
		포인트 = 0
		
	DRAWLINE
	;現在상성を표시
	PRINTL □전체상성
	FOR LOCAL, 0, FLAG:상성수 - 1
		PRINTFORM %GET_TYPE(LOCAL)% 
		현재치 = MAXBASE:대상:GET_TYPE(LOCAL)
		CALL SET_AISYOU_COLOR(현재치)
		PRINTFORM \@ 현재치 < 0 ? 흡 # 　 \@
		PRINTFORM \@ 현재치 != 999 ? {ABS(현재치), 3}％ # %" "%반사 \@　
		PRINTFORM \@ LOCAL % 6 == 5 ? \n # \@
		RESETCOLOR
	NEXT

	PRINTL
	DRAWLINE
	PRINTFORML □내성강화  <%ITEMNAME:장비번호%>  (Lv：{강화단계})
	DRAWLINE
	PRINTL 어느 내성을 변경하시겠습니까？
	DRAWLINE
	PRINTFORML 강화가능횟수:{50 - 강화수}
	IF 50 - 강화수 == 0
		SETCOLOR COLOR("yellow")
		PRINT  [한계]
		RESETCOLOR
	ENDIF
	DRAWLINE
	LOCAL:1 = 0
	선택지 = 
	FOR LOCAL, 0, FLAG:상성수 - 1
		CALLFORM 방어상성_{장비번호},LOCAL,대상
		기초치 = RESULT
		SIF 기초치 == 100 ;|| 기초치 <= 0 || 기초치 == 999
			CONTINUE
		CALL 장비강화_전개, 대상, 장비번호, "방어상성", GET_TYPE(LOCAL), RESULT
		포인트 -= 기초치 - RESULT
		현재치 = RESULT
		IF 기초치 <= 0 || 기초치 == 999
			PRINTFORM [XX]%GET_TYPE(LOCAL)%
		ELSE
			PRINTFORM [{LOCAL, 2}]%GET_TYPE(LOCAL)%
			선택지 += TOSTR(LOCAL) + "/"
		ENDIF
		LOCALS =   
		SIF (RESULT - 기초치) != 0
			LOCALS = ◇
		PRINTFORM %LOCALS%
		RESETCOLOR
		CALL SET_AISYOU_COLOR(RESULT)
		PRINTFORM \@ 현재치 < 0 ? 흡 #    \@
		PRINTFORM \@ 현재치 != 999 ? {ABS(현재치), 3}％ # %" "%반사 \@  
	PRINTL
		RESETCOLOR
	NEXT
	PRINTL
	PRINTL [998] 포인트 재분배
	선택지 += "998/"
	PRINTL [100]돌아간다
	선택지 += "100/"
	$INPUT_LOOP_3
	;INPUT
	CALL 장비강화_INPUT_LIST,선택지
	IF RESULT == 100
		RETURN
	ELSEIF RESULT == 998
		REDRAW 1
		PRINTFORML 포인트를 재분배하시겠습니까？
		CALL INPUT_YN,"예","아니오"
		IF RESULT == 0
			FOR LOCAL, 0, FLAG:상성수 - 1
				CALLFORM 방어상성_{장비번호}, LOCAL, 대상
				LOCAL:1 = RESULT
				SIF LOCAL:1 == 100 || LOCAL:1 <= 0 || LOCAL:1 == 999
					CONTINUE
				CALL 장비강화_전개, 대상, 장비번호, "방어상성", GET_TYPE(LOCAL), RESULT
				IF (RESULT - LOCAL:1) != 0
					CALL 장비강화_CSTR, "書き込み", 대상, 장비번호, "내성강화", GET_TYPE(LOCAL), 0
				ENDIF
			NEXT
		ENDIF
		CALL SYNC_STATUS,대상
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO START_3
	ELSEIF RESULT < 0 || RESULT >= FLAG:상성수-1
		GOTO INPUT_LOOP_3
	ENDIF
		상성 = %GET_TYPE(RESULT)%
		CALLFORM 방어상성_{장비번호},RESULT,대상
		기초치 = RESULT
		CALL 장비강화_전개, 대상, 장비번호, "방어상성", 상성
		할당후 = RESULT
		;SIF 할당후 < 997
		;	할당후 = -할당후
		포인트 = 포인트/5*5
	DRAWLINE
SECOND_LINE = LINECOUNT
REDRAW 0
$START_2
	PRINTFORML %상성%의 방어상성을 어떻게 하시겠습니까？　보정치:{포인트}
	PRINTL
	PRINTFORM %상성%:{기초치, 3} - {할당후, 3} = 
	CALL SET_AISYOU_COLOR, 기초치 - 할당후
	PRINTFORM {기초치 - 할당후, 3}
	RESETCOLOR
	PRINTFORM     

	IF 기초치 > 100 && 기초치 < 999
		강화포인트 = 기초치 - 100
		SIF 강화포인트 < 5
			강화포인트 = 5
	ELSEIF 기초치 < 100 && 기초치 > 0
		강화포인트 = 기초치
	ELSE
		강화포인트 = 0
	ENDIF
	선택지 = 
	IF 기초치 > 0 && 기초치 < 999
		변화량_Max = LIMIT(강화포인트 - 할당후, 5, MAX(포인트, 5))
		변화량_Min = MAX(할당후, 5) * -1
		CALL BUTTON_강화포인트("[ +5]", 101, 할당후, 포인트,   5, 강화포인트, 대상, 강화수)
		SIF RESULT != -1
			선택지 += TOSTR(101) + "/"
		CALL BUTTON_강화포인트("[ -5]", 102, 할당후, 포인트,  -5, 강화포인트)
		SIF RESULT != -1
			선택지 += TOSTR(102) + "/"
		CALL BUTTON_강화포인트("[+25]", 103, 할당후, 포인트,  25, 강화포인트, 대상, 강화수)
		SIF RESULT != -1
			선택지 += TOSTR(103) + "/"
		CALL BUTTON_강화포인트("[-25]", 104, 할당후, 포인트, -25, 강화포인트)
		SIF RESULT != -1
			선택지 += TOSTR(104) + "/"
		CALL BUTTON_강화포인트("[+50]", 105, 할당후, 포인트,  50, 강화포인트, 대상, 강화수)
		SIF RESULT != -1
			선택지 += TOSTR(105) + "/"
		CALL BUTTON_강화포인트("[-50]", 106, 할당후, 포인트, -50, 강화포인트)
		SIF RESULT != -1
			선택지 += TOSTR(106) + "/"
		CALL BUTTON_강화포인트("[MAX]", 107, 할당후, 포인트, 변화량_Max, 강화포인트, 대상, 강화수)
		SIF RESULT != -1
			선택지 += TOSTR(107) + "/"
		CALL BUTTON_강화포인트("[MIN]", 108, 할당후, 포인트, 변화량_Min, 강화포인트)
		SIF RESULT != -1
			선택지 += TOSTR(108) + "/"
		IF 할당후 >= 강화포인트
			SETCOLOR COLOR("AQUA")
			PRINT [한계]
			RESETCOLOR
		ENDIF
	ENDIF
	PRINTL

DRAWLINE
PRINTFORML [0] 결정
	선택지 += "0/"
PRINTFORML [999] 돌아간다
	선택지 += "999/"
$INPUT_LOOP_1
;INPUT

	CALL 장비강화_INPUT_LIST,선택지

IF RESULT == 999
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO START_3
ELSEIF RESULT == 0
	REDRAW 1
	CALL 장비강화_CSTR, "書き込み", 대상, 장비번호, "내성강화", 상성, 할당후
	CALL SYNC_STATUS,대상
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO START_3
ELSEIF INRANGE(RESULT, 101, 108)
	선택강화 = RESULT - 100
	SELECTCASE 선택강화
		CASE 0
			선택강화 = 0
			강화치 = 0
		CASE 1
			선택강화 = 5
			강화치 = 5
		CASE 2
			선택강화 = -5
			강화치 = -5
		CASE 3
			선택강화 = 25
			강화치 = 25
		CASE 4
			선택강화 = -25
			강화치 = -25
		CASE 5
			선택강화 = 50
			강화치 = 50
		CASE 6
			선택강화 = -50
			강화치 = -50
		CASE 7
			선택강화 = 변화량_Max
			강화치 = 변화량_Max
		CASE 8
			선택강화 = 변화량_Min
			강화치 = 변화량_Min
		CASEELSE
			GOTO INPUT_LOOP_3
	ENDSELECT
	SIF 할당후 == 0
		강화수 += 1
	할당후 += 강화치
	SIF 할당후 == 0
		강화수 -= 1
	포인트 -= 선택강화
ENDIF
CLEARLINE LINECOUNT - SECOND_LINE
GOTO START_2


@장비강화_공격상성,ARG,ARG:1
#DIM DYNAMIC FIRST_LINE
#DIM DYNAMIC SECOND_LINE
#DIM DYNAMIC 대상
#DIM DYNAMIC 장비번호
#DIM DYNAMIC 기초치
#DIM DYNAMIC 소재악마
#DIMS DYNAMIC 부여상성
#DIM DYNAMIC 부여확률
#DIMS DYNAMIC 장비명
#DIM DYNAMIC SKILL_RANK
#DIM DYNAMIC LCOUNT

대상 = ARG
장비번호 = ARG:1

FIRST_LINE = LINECOUNT
$START_1

	PRINTL □공격상성강화    (공격시 확률로 공격 상성을 변화시킨다)
	RESULT = 0
	TRYCALLFORM 공격상성_{장비번호}
	PRINTFORM   공격상성 [%GET_TYPE(RESULT),4%]
	기초치 = RESULT
	CALL 장비강화_전개, 대상, 장비번호, "공격상성", "부여상성"
	IF RESULT > -1
		PRINTFORM   ／  속성부여 [%GET_TYPE(RESULT),4%]
		CALL 장비강화_전개, 대상, 장비번호, "공격상성", "부여확률"
		PRINTFORM   부여확률 [{RESULT,2}]　
	ENDIF
	PRINTL
	DRAWLINE

SECOND_LINE = LINECOUNT
$START_2

	;キャラリストを표시・선택
	CALL SHOW_CHARA_LIST_장비강화,"공격상성",대상
	SIF RESULT == -1
		RETURN -1
	소재악마 = RESULT
	CALL 적정상성_강화用,RESULT
	IF 기초치 == RESULT
		PRINTW 원래의 상성과 같습니다
		CLEARLINE LINECOUNT - SECOND_LINE
		GOTO START_2
	ENDIF
	DRAWLINE
	PRINTFORML 마정변화:%CALLNAME:소재악마%
	PRINTL

	부여상성 = %GET_TYPE(RESULT)%
	LOCAL:1 = 0
	FOR LCOUNT, 1, FLAG:스킬수 + 1
		LOCALS = 스킬{LCOUNT}
		SIF ABL:소재악마:LOCALS == 0
			CONTINUE
		CALLFORM SKILL_SUCCESSION_TYPE_{ABL:소재악마:LOCALS}
		SIF GET_SUCCESSION(RESULT) != 부여상성
			CONTINUE
		CALLFORM SKILL_RANK_{ABL:소재악마:LOCALS}
		SIF RESULT > 4
			LOCAL:1 += 1
		SIF RESULT > 9
			LOCAL:1 += 1
		LOCAL:1 += 1
		SKILL_RANK = MIN(RESULT / 5 + 1, 3)
		CALLFORM SKILL_NAME_{ABL:소재악마:LOCALS}
		SETCOLOR COLOR("yellow")
		PRINTFORM %"★"*SKILL_RANK, 6, LEFT%
		RESETCOLOR
		PRINTFORML :%RESULTS%
	NEXT
	부여확률 = 5
	FOR LCOUNT, 0, LOCAL:1
		SELECTCASE LCOUNT
			CASE 0 TO 1
				부여확률 += 15
			CASE 2 TO 6
				부여확률 += 10
			CASE 7 TO 9
				부여확률 += 5
			CASEELSE
		ENDSELECT
	NEXT

	PRINTL
	PRINTFORM 변화강도:
	SETCOLOR COLOR("yellow")
	PRINTFORM %"★" * MIN(LOCAL:1, 10)%
	RESETCOLOR
	
	IF LOCAL:1 >= 10
		SETCOLOR COLOR("yellow")
		PRINT (MAX)
		RESETCOLOR
	ELSE
		PRINTFORM ({LOCAL:1})
	ENDIF
	
	PRINTL
DRAWLINE

IF 개조장비(장비번호)
	장비명 = %개조장비명칭(개조장비番号(장비번호))%
ELSE
	장비명 = %ITEMNAME:장비번호%
ENDIF

PRINTFORML %장비명%에 [%부여상성%]속성({부여확률, 3}％)을 부여하시겠습니까？
CALL INPUT_YN,"Yes","No",2
	IF RESULT == 1
		PRINTL
		CLEARLINE LINECOUNT - SECOND_LINE
		GOTO START_2
	ENDIF
CALL 장비강화_CSTR, "書き込み", 대상, 장비번호, "공격상성", 부여상성, 부여확률
CALL SYNC_STATUS,대상

PRINTFORMW %장비명%に%CALLNAME:소재악마%が融合した！

CALL FUSION_LETTER,소재악마,1
;合体소재の악마を消去
CALL 캐릭터삭제, 소재악마
	RETURN -1


@장비강화_추가효과,ARG,ARG:1
#DIM DYNAMIC FIRST_LINE
#DIM DYNAMIC SECOND_LINE
#DIM DYNAMIC 대상
#DIM DYNAMIC 장비번호
#DIM DYNAMIC 소재악마
#DIM DYNAMIC 부여ステート
#DIM DYNAMIC 기본명중
#DIM DYNAMIC 추가효과스테이터스
#DIMS DYNAMIC 장비명
#DIM DYNAMIC 스킬평가, 4
#DIM DYNAMIC 변화강도

#DIMS DYNAMIC 테스트

대상 = ARG
장비번호 = ARG:1

FIRST_LINE = LINECOUNT
$START_1

	PRINTL □추가효과부여
	RESULT = 0
	TRYCALLFORM 추가효과_{장비번호}
	추가효과스테이터스 = RESULT
	IF GET_STATE(추가효과스테이터스) == "GOOD"
		CALL 장비강화_전개, 대상, 장비번호, "추가효과", "ステート"
		IF GET_STATE(RESULT) != "GOOD"
			PRINTFORM   추가효과 [%GET_STATE(RESULT)%]
			CALL 장비강화_전개, 대상, 장비번호, "추가효과", "効果상성"
			PRINTFORM   상성 [%GET_TYPE(RESULT),4%]
			CALL 장비강화_전개, 대상, 장비번호, "추가효과", "기본명중"
			PRINTFORM   기본명중 [{RESULT,4}]
			CALL 장비강화_전개, 대상, 장비번호, "추가효과", "최대명중"
			PRINTFORM   최대명중 [{RESULT,4}]
		ELSE
			PRINTFORM   추가효과 [없음]
		ENDIF
	ELSE
		PRINTFORM   추가효과 [%GET_STATE(추가효과스테이터스)%]
		RESULT = 0
		TRYCALLFORM 추가효과상성_{장비번호}
		PRINTFORM   상성 [%GET_TYPE(RESULT),4%]
		RESULT = 0
		TRYCALLFORM 추가효과명중률_{장비번호}
		PRINTFORM   기본명중 [{RESULT,4}]
		RESULT = 0
		TRYCALLFORM 추가효과최대명중률_{장비번호}
		PRINTFORM   최대명중 [{RESULT,4}]
		PRINTL
		DRAWLINE
		IF GET_STATE(추가효과스테이터스) != "GOOD"
			PRINTW もともと추가효과を持っています
			RETURN -1
		ENDIF
	ENDIF
	PRINTL
	DRAWLINE

SECOND_LINE = LINECOUNT
$START_2

		;キャラリストを표시・선택
		CALL SHOW_CHARA_LIST_장비강화,"추가효과",대상
		SIF RESULT == -1
			RETURN -1
		소재악마 = RESULT
		CALL 소지스킬추가효과_강화用,RESULT
		부여ステート = RESULT

	DRAWLINE
	PRINTFORML 마정변화:%CALLNAME:소재악마%
	PRINTL
;	LOCAL:1 = 0
	변화강도 = 0
	FOR LOCAL, 1, FLAG:스킬수 + 1
		LOCALS = 스킬{LOCAL}
		SIF ABL:소재악마:LOCALS == 0
			CONTINUE

		TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ABL:소재악마:LOCALS}
			SIF RESULT != 부여ステート
				CONTINUE
		CATCH
			CONTINUE
		ENDCATCH
		
		CALLFORM SKILL_ADDTIONAL_BASE_RATE_{ABL:소재악마:LOCALS}, 소재악마
		SELECTCASE RESULT
			CASE IS < 31
				스킬평가 = 1
			CASE IS < 50
				스킬평가 = 2
			CASEELSE
				스킬평가 = 3
		ENDSELECT
		CALLFORM SKILL_ADDTIONAL_MAX_RATE_{ABL:소재악마:LOCALS}, 소재악마
		SELECTCASE RESULT
			CASE IS < 71
				스킬평가:1 = 1
			CASE IS < 95
				스킬평가:1 = 2
			CASEELSE
				스킬평가:1 = 3
		ENDSELECT
		CALLFORM SKILL_RANK_{ABL:소재악마:LOCALS}
		SELECTCASE RESULT
			CASE IS < 6
				스킬평가:2 = 1
			CASE IS < 11
				스킬평가:2 = 2
			CASEELSE
				스킬평가:2 = 3
		ENDSELECT
		
		스킬평가:3 = (스킬평가 + 스킬평가:1 + 스킬평가:2) / 3
		변화강도 += 스킬평가:3
		CALLFORM SKILL_NAME_{ABL:소재악마:LOCALS}, 소재악마
		SETCOLOR COLOR("yellow")
		PRINTFORM %"★" * 스킬평가:3, 6, LEFT%
		RESETCOLOR
		PRINTFORML :%RESULTS%
	NEXT

	기본명중 = MIN (10 + (5 * 변화강도), 35)
	PRINTL
	PRINTFORM 변화강도:
	SETCOLOR COLOR("yellow")
	PRINTFORM %"★" * MIN(변화강도, 5)%
	RESETCOLOR
	IF 변화강도 > 4
		SETCOLOR COLOR("yellow")
		PRINT (MAX)
		RESETCOLOR
	ELSE
		PRINTFORM ({변화강도})
	ENDIF
	PRINTL
	DRAWLINE

;추가효과스테이터스
;추가효과_{장비번호},ARG
;RETURN GET_STATE_NUM("DYING")

;추가효과상성
;추가효과상성_{장비번호},ARG
;RETURN GET_TYPE_NUM("만능")

;추가효과기본付与確率
;추가효과명중률_{장비번호},ARG
;RETURN 20
;	GET_STATE_NUM("FREEZE")	20%
;	GET_STATE_NUM("SHOCK")	20%
;	POISON 	２０％
;	SLEEP 	２５％
;	PANIC 	２５％
;	HAPPY 	３５％
;	BIND 	２５％
;	CHARM 	２０％

;추가효과付与確率上限
;추가효과최대명중률_{장비번호},ARG
;RETURN 95
IF 개조장비(장비번호)
	장비명 =  %개조장비명칭(개조장비番号(장비번호))%
ELSE
	장비명 = %ITEMNAME:장비번호%
ENDIF

PRINTFORML %장비명%에[%GET_STATE(부여ステート)%]({기본명중, 3}％)을 부여하시겠습니까？
CALL INPUT_YN,"Yes","No",2
	IF RESULT == 1
		PRINTL 
		CLEARLINE LINECOUNT - SECOND_LINE
		GOTO START_2
	ENDIF
	CALL 장비강화_CSTR, "書き込み", 대상, 장비번호, "추가효과", GET_STATE(부여ステート), 기본명중
	CALL SYNC_STATUS,대상

PRINTFORMW %장비명%에 %조사처리(CALLNAME:소재악마, "가")% 융합했다！ 

CALL FUSION_LETTER,소재악마,1

;合体소재の악마を消去
CALL 캐릭터삭제, 소재악마
RETURN -1


@BUTTON_강화포인트(문자열, 번호, 현재치, 포인트, 변화량, 제한, 대상 = -1, 강화수 = 0)
#DIMS DYNAMIC 문자열
#DIM DYNAMIC 번호
#DIM DYNAMIC 현재치
#DIM DYNAMIC 포인트
#DIM DYNAMIC 변화량
#DIM DYNAMIC 제한
#DIM DYNAMIC 대상
#DIM DYNAMIC 강화수

IF !((강화수 >= 50 && 현재치 < 1) || 변화량 > 포인트 || 변화량 + 현재치 > 제한 || 변화량 + 현재치 < 0)
	PRINTBUTTON 문자열, 번호
ELSE
	SETCOLOR COLOR("gray")
	PRINTPLAINFORM %문자열%
	RESETCOLOR
	RETURN -1
ENDIF

@장비강화_강화해제,ARG,ARG:1 = -1
#DIM DYNAMIC FIRST_LINE
#DIM DYNAMIC 대상
#DIM DYNAMIC 장비번호

대상 = ARG
장비번호 = ARG:1

LOCALS = 
$START_B
FIRST_LINE = LINECOUNT
PRINTL 강화를 해제할 장비을(를) 선택하십시오
DRAWLINE
PRINTFORML %CALLNAME:대상%
PRINTL □장비강화상태
PRINTL ▼장비중
RESULTS = 
CALL 장비강화_CSTR, "표시", 대상,0,"선택"
LOCALS = %RESULTS%
PRINTL ▼비장비
RESULTS = 
CALL 장비강화_CSTR, "표시：不장비", 대상,0,"선택"
LOCALS += RESULTS
PRINTL
PRINTL [9999] 그만둔다
LOCALS += "9999/"
;INPUT
CALL 장비강화_INPUT_LIST,LOCALS
SIF RESULT == 9999
	RETURN -1
LOCAL = RESULT
SIF RESULT == -1
	RETURN -1
	
장비번호 = LOCAL
	
IF 개조장비(장비번호)
	PRINTFORM %개조장비명칭(개조장비番号(장비번호))%
ELSE
	PRINTFORM %ITEMNAME:장비번호%
ENDIF
PRINTL 의 강화상태를 전부 해제합니다
CALL INPUT_YN,"예","아니오"
IF RESULT == 1
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO START_B
ENDIF

IF 개조장비(장비번호)
	PRINTFORM [%개조장비명칭(개조장비番号(장비번호))%]
ELSE
	PRINTFORM [%ITEMNAME:장비번호%]
ENDIF
PRINTW の강화を해제
CALL 장비강화_CSTR, "해제", 대상, 장비번호
RETURN
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO START_B

@SHOW_CHARA_LIST_장비강화,ARGS,ARG,ARG:1
#LOCALSIZE 5
#DIMS DYNAMIC 지정
#DIM DYNAMIC 강화단계
#DIM DYNAMIC 스킬상성
#DIM DYNAMIC 스킬평가, 3
#DIM DYNAMIC 확률표시
#DIM DYNAMIC LCOUNT,2

지정 = %ARGS%
강화단계 = ARG:1

LOCAL:3 = CURRENTREDRAW(), RESULT
SIF LOCAL:3 != 2
	REDRAW 2
P = 0
;표시させるキャラを抽出（Q:*に표시するキャラの登録번호、LOCAL:2に人수）
VARSET Q, -1
A = 0
IF 지정 == "공격상성"
	FOR LOCAL, 0, CHARANUM
		SIF ABL:LOCAL:종족 == 0 || ABL:LOCAL:종족 > 44
			CONTINUE
		SIF 강화단계 > BASE:LOCAL:LV
			CONTINUE
		SIF 계약(LOCAL) > 0
			CONTINUE
		SIF !(CFLAG:LOCAL:노역플래그 == 3 || CFLAG:LOCAL:이자리에없는플래그 || CFLAG:LOCAL:합체불가 || CFLAG:LOCAL:필터링플래그)
			Q:(A++) = LOCAL
	NEXT
ELSEIF 지정 == "추가효과"
	FOR LOCAL, 0, CHARANUM
		SIF ABL:LOCAL:종족 == 0 || ABL:LOCAL:종족 > 44
			CONTINUE
		SIF 강화단계 > BASE:LOCAL:LV
			CONTINUE
		SIF 계약(LOCAL) > 0
			CONTINUE
		CALL 소지스킬추가효과_강화用,LOCAL
		SIF GET_STATE(RESULT) == "GOOD" || GET_STATE(RESULT) == "BOMB" || GET_STATE(RESULT) == "STONE" || GET_STATE(RESULT) == "DYING" || GET_STATE(RESULT) == "FLY"
			CONTINUE
		SIF !(CFLAG:LOCAL:노역플래그 == 3 || CFLAG:LOCAL:이자리에없는플래그 || CFLAG:LOCAL:합체불가 || CFLAG:LOCAL:필터링플래그)
			Q:(A++) = LOCAL
	NEXT
ELSE
	FOR LOCAL, 0, CHARANUM
		SIF ABL:LOCAL:종족 != 0 || CFLAG:LOCAL:전투참가불가능
			CONTINUE
		SIF 계약(LOCAL) > 0
			CONTINUE
		SIF !(CFLAG:LOCAL:노역플래그 == 3 || CFLAG:LOCAL:이자리에없는플래그 || CFLAG:LOCAL:필터링플래그)
			Q:(A++) = LOCAL
	NEXT
ENDIF

LOCAL:1 = LINECOUNT, A
DO
	CLEARLINE LINECOUNT - LOCAL:1
	DRAWLINE
	PRINTFORML 캐릭터를 선택하세요　＜page.{P + 1}/{(LOCAL:2 - 1) / リスト표시数() + 1}＞
	DRAWLINE
;	CALL SHOW_CHARA_LIST

LOCAL:3 = 0
FOR LCOUNT, 0, リスト표시数()
	글자색변경 = 0
	LOCAL = Q:(LCOUNT + P * リスト표시数())
	SIF LOCAL < 0 || CFLAG:LOCAL:이자리에없는플래그 == 1
		CONTINUE
	
	CALL ARRANGE_SETCOLOR, LOCAL
	CALL ARRANGE_TARGETSIGN, LOCAL
	CALL ARRANGE_CHARALIST, LOCAL

	IF 지정 == "공격상성"
		CALL 적정상성_강화用, LOCAL
		PRINTFORM 　[%GET_TYPE(RESULT)%] 
		스킬상성 = RESULT
		스킬평가 = 0
		FOR LCOUNT:1, 1, FLAG:스킬수 + 1
			LOCALS = 스킬{LCOUNT:1}
			SIF ABL:LOCAL:LOCALS == 0
				CONTINUE
			CALLFORM SKILL_SUCCESSION_TYPE_{ABL:LOCAL:LOCALS}
			SIF GET_SUCCESSION(RESULT) != GET_TYPE(스킬상성)
				CONTINUE
			CALLFORM SKILL_RANK_{ABL:LOCAL:LOCALS}
			SIF RESULT > 4
				스킬평가 += 1
			SIF RESULT > 9
				스킬평가 += 1
			스킬평가 += 1
		NEXT
;		PRINTFORM ({MIN (10 + (15 * 강화:2), 95)})    
		확률표시 = 5
		FOR LCOUNT:1, 0, 스킬평가
			SELECTCASE LCOUNT:1
				CASE 0 TO 1
					확률표시 += 15
				CASE 2 TO 6
					확률표시 += 10
				CASE 7 TO 9
					확률표시 += 5
				CASEELSE
			ENDSELECT
		NEXT
		PRINTFORM ({확률표시, 3})  
	ELSEIF 지정 == "추가효과"
		CALL 소지스킬추가효과_강화用, LOCAL
		PRINTFORM 　[%GET_STATE(RESULT),6%] 
		스킬상성 = RESULT
		확률표시 = 0
		FOR LCOUNT:1, 1, FLAG:스킬수 + 1
			LOCALS = 스킬{LCOUNT:1}
			SIF ABL:LOCAL:LOCALS == 0
				CONTINUE
			TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ABL:LOCAL:LOCALS}
				SIF RESULT != 스킬상성
					CONTINUE
			CATCH
				CONTINUE
			ENDCATCH
			CALLFORM SKILL_ADDTIONAL_BASE_RATE_{ABL:LOCAL:LOCALS}, LOCAL
			SELECTCASE RESULT
				CASE IS < 31
					스킬평가 = 1
				CASE IS < 50
					스킬평가 = 2
				CASEELSE
					스킬평가 = 3
			ENDSELECT
			CALLFORM SKILL_ADDTIONAL_MAX_RATE_{ABL:LOCAL:LOCALS}, LOCAL
			SELECTCASE RESULT
				CASE IS < 71
					스킬평가:1 = 1
				CASE IS < 95
					스킬평가:1 = 2
				CASEELSE
					스킬평가:1 = 3
			ENDSELECT
			CALLFORM SKILL_RANK_{ABL:LOCAL:LOCALS}
			SELECTCASE RESULT
				CASE IS < 6
					스킬평가:2 = 1
				CASE IS < 11
					스킬평가:2 = 2
				CASEELSE
					스킬평가:2 = 3
			ENDSELECT
			확률표시 += (스킬평가 + 스킬평가:1 + 스킬평가:2) / 3
		NEXT
		PRINTFORM ({MIN (10 + (5 * 확률표시), 35), 3})    
	ENDIF

	PRINTFORM LV:{BASE:LOCAL:LV,3} 
	CALL ARRANGE_FALLTALENT, LOCAL
	CALL ARRANGE_SEX, LOCAL
	PRINTFORM 　충성도:{BASE:LOCAL:충성도,8}
	PRINTFORM 　MAG:{BASE:LOCAL:ＭＡＧ,8}/{MAXBASE:LOCAL:ＭＡＧ,8}
	RESETCOLOR

	PRINTL 
	RESETCOLOR
NEXT
RESETCOLOR

	PRINTFORM \@ P && P == (LOCAL:2 - 1) / リスト표시数() ? %"\n" * MAX(0, リスト표시数() + 3 + LOCAL:1 - LINECOUNT)% # \@
	DRAWLINE
	PRINTFORMLC \@ P > 0 ? [1007]이전 페이지 # %" " * 16% \@
	PRINTLC [1000]돌아간다
	PRINTFORMLC \@ P < (LOCAL:2 - 1) / リスト표시数() ? [1009]다음 페이지 # %" " * 16% \@
	DO
		INPUT
		LOCAL = RESULT == 1000 || (RESULT == 1007 && P > 0) || (RESULT == 1009 && P < (LOCAL:2 - 1) / リスト표시数())
		LOCAL |= MATCH(Q, RESULT, 0, LOCAL:2)
		SIF !LOCAL
			CLEARLINE 1
	LOOP !LOCAL
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0 TO CHARANUM - 1
			RETURN LOCAL
		CASE 1007
			P--
		CASE 1009
			P++
	ENDSELECT
LOOP LOCAL != 1000
SIF LOCAL:3 != 2
	REDRAW LOCAL:3
SIF LOCAL == 1000
	RETURN -1
RETURN LOCAL

@SHOW_EQUIP_LIST_장비강화,ARG
PRINTL      <EQUIP>
LOCALS:1 = 
	FOR LOCAL , 0 , 7
		IF ITEMNAME:(EQUIP:ARG:GET_EQUIP(LOCAL)) == ""
			PRINTFORML [XX] %"[" + GET_EQUIP(LOCAL) + "]",14,LEFT% ＥＭＰＴＹ
		ELSEIF 마정장비(EQUIP:ARG:GET_EQUIP(LOCAL))
			PRINTFORML [XX] %"[" + GET_EQUIP(LOCAL) + "]",14,LEFT% %EQ_GETNAME_마정장비_CHARA(ARG, LOCAL)%
		ELSEIF GET_EQUIP(LOCAL) == "악세사리"
			PRINTFORML [XX] %"[" + GET_EQUIP(LOCAL) + "]",14,LEFT% %ITEMNAME:(EQUIP:ARG:GET_EQUIP(LOCAL)),14,LEFT%
		ELSE
			CALL 장비강화_전개, ARG, EQUIP:ARG:GET_EQUIP(LOCAL), "강화단계", "LV"
			LOCALS = 
			SIF RESULT > 1
				LOCALS = Lv:%TOSTR(RESULT)%
			IF 개조장비(EQUIP:ARG:GET_EQUIP(LOCAL))
				CALL 장비강화_전개, ARG, EQUIP:ARG:GET_EQUIP(LOCAL), "기록_개조장비베이스번호", ""
				IF RESULT > 0 && 베이스장비번호:(개조장비番号(EQUIP:ARG:GET_EQUIP(LOCAL))) != RESULT
					SETCOLOR 255, 50, 50
					PRINTFORML [98] %"[" + GET_EQUIP(LOCAL) + "]",14,LEFT% %개조장비명칭(개조장비番号(EQUIP:ARG:GET_EQUIP(LOCAL))),16,LEFT%  %LOCALS% (강화불가)
					RESETCOLOR
				ELSEIF GET_EQUIP(LOCAL) != "검"
					PRINTFORML [XX] %"[" + GET_EQUIP(LOCAL) + "]",14,LEFT% %개조장비명칭(개조장비番号(EQUIP:ARG:GET_EQUIP(LOCAL))),16,LEFT%  %LOCALS%
				ELSE
					PRINTFORML [{50 + LOCAL}] %"[" + GET_EQUIP(LOCAL) + "]",14,LEFT% %개조장비명칭(개조장비番号(EQUIP:ARG:GET_EQUIP(LOCAL))),16,LEFT%  %LOCALS%
				ENDIF
			ELSE
				PRINTFORML [{50 + LOCAL}] %"[" + GET_EQUIP(LOCAL) + "]",14,LEFT% %ITEMNAME:(EQUIP:ARG:GET_EQUIP(LOCAL)),16,LEFT%  %LOCALS%
			ENDIF
			LOCALS:1 += TOSTR(50 + LOCAL) + "/"
		ENDIF
	NEXT
	PRINTL [99] 그만둔다
	LOCALS:1 += "99/"
;INPUT
CALL 장비강화_INPUT_LIST,LOCALS:1
SIF RESULT == -1
	RETURN 49
LOCAL = RESULT - 50
RETURN LOCAL


;-------------------------------------------------
;   ARGS 선택지のリスト문자열
;-------------------------------------------------
@장비강화_INPUT_LIST,ARGS
;#DIM LCOUNT, 2
;#DIM DYNAMIC 전개, 10
	STRFIND ARGS, "/"
	SIF RESULT == -1
		RETURN -1
;	VARSET LOCALS
;	;記録の分解
;	SPLIT ARGS, "/", LOCALS
;	;記録は100まで
;	FOR LCOUNT, 0, 100
;		SIF LOCALS:LCOUNT == ""
;			BREAK
;		전개:LCOUNT = TOINT(LOCALS:LCOUNT)
;	NEXT
;PRINTFORML %ARGS%
INPUT
SIF RESULT == 98
	RETURN 98
LOCAL = RESULT
LOCALS '= "/" + TOSTR(RESULT) + "/"
LOCALS:1 '= "/" + ARGS

;PRINTFORML %LOCALS%　%LOCALS:1%

STRFIND LOCALS:1, LOCALS
SIF RESULT != -1
	RETURN LOCAL
RESTART

;-------------------------------------------------
;   적정상성
;-------------------------------------------------
;  ARG:0 대상者　ARGS:0 지정
;1.소지している스킬を見る
;2.습득스킬を見る。ただし一度습득した스킬は見ない（습득の際に0が入るようになっている）
;3.これまで見た스킬の상성と상성素質が同じならば적정상성フラグオン
;4.３の中で一番스킬のランクが高い상성を返す。
;  적정상성がない場合は소지している스킬と습득스킬の中で一番ランクが高い스킬の상성を返す
;-------------------------------------------------
@적정상성_강화用,ARG,ARGS = ""
#DIM DYNAMIC 대상
#DIMS DYNAMIC 지정
#DIM DYNAMIC 소지상성,18
#DIM DYNAMIC 적정상성,18
#DIM DYNAMIC 최고랭크,18
#DIM DYNAMIC LCOUNT, 4
대상 = ARG
지정 = %ARGS%
	VARSET 소지상성
	VARSET 적정상성
	VARSET 최고랭크
	; VARSET LOCAL
	; VARSET RESULT

;	DRAWLINE
	;소지스킬
	FOR LCOUNT, 1, FLAG:스킬수 + 1
		LOCALS = 스킬{LCOUNT}
		SIF ABL:대상:LOCALS == 0
			CONTINUE
		TRYCCALLFORM SKILL_ACTIONABLE_BATTLE_{ABL:대상:LOCALS}
			CONTINUE
		CATCH
		ENDCATCH
		CALLFORM SKILL_SUCCESSION_TYPE_{ABL:대상:LOCALS}
		SIF RESULT == 4 || RESULT == 5 || RESULT == 6 || RESULT == 20 || RESULT == 22
			CONTINUE
		CALLFORM SKILL_TYPE_{ABL:대상:LOCALS}
		LCOUNT:1 = RESULT
		소지상성:(LCOUNT:1) += 1
		CALLFORM SKILL_RANK_{ABL:대상:LOCALS}
		SIF 최고랭크:(LCOUNT:1) < RESULT
			최고랭크:(LCOUNT:1) = RESULT
	NEXT
;	PRINTL
	IF 지정 != "소지限定"
		;습득스킬
		FOR LCOUNT,1,21
			LOCALS = 습득스킬{LCOUNT}
	;		LOCALS:1 = 적정LV{LCOUNT}
	;		SIF BASE:LCOUNT:LV >= 습득LV{LCOUNT}
	;			CONTINUE
			SIF ABL:대상:LOCALS == 0
				BREAK
	;		CALLFORM SKILL_NAME_{ABL:대상:LOCALS}
	;		PRINTFORMLC %RESULTS%
			TRYCCALLFORM SKILL_ACTIONABLE_BATTLE_{ABL:대상:LOCALS}
				CONTINUE
			CATCH
			ENDCATCH
			CALLFORM SKILL_SUCCESSION_TYPE_{ABL:대상:LOCALS}
			SIF RESULT == 4 || RESULT == 5 || RESULT == 6 || RESULT == 22
				CONTINUE
			CALLFORM SKILL_TYPE_{ABL:대상:LOCALS}
			LCOUNT:1 = RESULT
			소지상성:(LCOUNT:1) += 1
			CALLFORM SKILL_RANK_{ABL:대상:LOCALS}
			SIF 최고랭크:(LCOUNT:1) < RESULT
				최고랭크:(LCOUNT:1) = RESULT
	;		SIF LCOUNT % 4 == 0
	;			PRINTL
		NEXT
	ENDIF
	IF CFLAG:대상:초기링크악마
		;초기스킬
		FOR LCOUNT, 1, FLAG:스킬수 + 1
			LOCALS = 초기변신악마스킬{LCOUNT}
			SIF ABL:대상:LOCALS == 0
				CONTINUE
;			CALLFORM SKILL_NAME_{ABL:대상:LOCALS}
;			PRINTFORMLC %RESULTS%
			TRYCCALLFORM SKILL_ACTIONABLE_BATTLE_{ABL:대상:LOCALS}
				CONTINUE
			CATCH
			ENDCATCH
			CALLFORM SKILL_SUCCESSION_TYPE_{ABL:대상:LOCALS}
;			SIF RESULT == 4 || RESULT == 5 || RESULT == 6 || RESULT == 22
;				CONTINUE
			CALLFORM SKILL_TYPE_{ABL:대상:LOCALS}
			LCOUNT:1 = RESULT
			소지상성:(LCOUNT:1) += 1
			CALLFORM SKILL_RANK_{ABL:대상:LOCALS}
			SIF 최고랭크:(LCOUNT:1) < RESULT
				최고랭크:(LCOUNT:1) = RESULT
		NEXT
		PRINTL
		IF 지정 != "소지스킬"
			;습득스킬
			FOR LCOUNT,1,21
				LOCALS = 초기변신악마습득스킬{LCOUNT}
	;			LOCALS:1 = 습득LV{LOCAL}
	;			SIF BASE:LOCAL:LV >= 초기변신악마습득LV{LOCAL}
	;				CONTINUE
				SIF ABL:대상:LOCALS == 0
					BREAK
	;			CALLFORM SKILL_NAME_{ABL:대상:LOCALS}
	;			PRINTFORMLC %RESULTS%
				TRYCCALLFORM SKILL_ACTIONABLE_BATTLE_{ABL:대상:LOCALS}
					CONTINUE
				CATCH
				ENDCATCH
				CALLFORM SKILL_SUCCESSION_TYPE_{ABL:대상:LOCALS}
				SIF RESULT == 4 || RESULT == 5 || RESULT == 6 || RESULT == 22
					CONTINUE
				CALLFORM SKILL_TYPE_{ABL:대상:LOCALS}
				LCOUNT:1 = RESULT
				소지상성:(LCOUNT:1) += 1
				CALLFORM SKILL_RANK_{ABL:대상:LOCALS}
				SIF 최고랭크:(LCOUNT:1) < RESULT
					최고랭크:(LCOUNT:1) = RESULT
				SIF LCOUNT % 4 == 0
					PRINTL
			NEXT
		ENDIF
	ENDIF
;	PRINTW 
;	DRAWLINE

;	PRINTL □소지상성
	FOR LCOUNT, 0, FLAG:상성수 - 1
		IF 소지상성:LCOUNT > 0
;			PRINTFORM %GET_TYPE(LCOUNT)% 
;			PRINTFORM = {소지상성:LCOUNT} 
			IF TALENT:대상:GET_TYPE(LCOUNT) 
;				PRINTFORM 적정　
				적정상성:LCOUNT = 1
			ELSE
;				PRINTFORM 　　　
			ENDIF
;			PRINTFORM \@ LOCAL % 6 == 5 ? \n # \@
		ENDIF
	NEXT
;	PRINTW 
;	DRAWLINE

IF SUMARRAY(적정상성, 0, 18) == 1
	FOR LCOUNT, 0, FLAG:상성수 - 1
		IF 적정상성:LCOUNT > 0
			VARSET 적정상성
			적정상성 = LCOUNT
			BREAK
		ENDIF
	NEXT
ELSEIF SUMARRAY(적정상성, 0, 18)
	FOR LCOUNT, 0, FLAG:상성수 - 1
		IF 적정상성:LCOUNT > 0
			IF LCOUNT:3 == 최고랭크:LCOUNT
				SIF LCOUNT:2 > 소지상성:LCOUNT
					CONTINUE
			ENDIF
			IF 최고랭크:LCOUNT >= LCOUNT:3
				LCOUNT:1 = LCOUNT
				LCOUNT:2 = 소지상성:LCOUNT
				LCOUNT:3 = 최고랭크:LCOUNT 
			ENDIF
		ENDIF
	NEXT
	VARSET 적정상성
	적정상성 = LCOUNT:1
ELSE
	FOR LCOUNT, 0, FLAG:상성수 - 1
		IF 소지상성:LCOUNT > 0
			IF LCOUNT:2 == 소지상성:LCOUNT
				SIF LCOUNT:3 > 최고랭크:LCOUNT
					CONTINUE
			ENDIF
			IF 소지상성:LCOUNT >= LCOUNT:2
				LCOUNT:1 = LCOUNT
				LCOUNT:2 = 소지상성:LCOUNT
				LCOUNT:3 = 최고랭크:LCOUNT 
			ENDIF
		ENDIF
	NEXT
	VARSET 적정상성
	적정상성 = LCOUNT:1
ENDIF
RETURN 적정상성

;-------------------------------------------------
;   소지스테이터스추가효과
;-------------------------------------------------
;  ARG:0 대상者　ARGS:0 지정
;-------------------------------------------------
@소지스킬추가효과_강화用,ARG
#DIM DYNAMIC 대상
#DIMS DYNAMIC 지정
#DIM DYNAMIC 소지ステート,21
#DIM DYNAMIC 적정ステート
#DIM DYNAMIC 최고랭크,21
#DIM DYNAMIC LCOUNT,4

대상		= ARG
;지정		= %ARGS%
	VARSET 소지ステート
	VARSET 적정ステート
	VARSET 최고랭크
	; VARSET LOCAL

	;소지스킬
	FOR LCOUNT, 1, FLAG:스킬수 + 1
		LOCALS = 스킬{LCOUNT}
		SIF ABL:대상:LOCALS == 0
			CONTINUE
		TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ABL:대상:LOCALS}
			LCOUNT:1 = RESULT
		CATCH
			LCOUNT:1 = 0
		ENDCATCH
			SIF GET_STATE(RESULT) == "GOOD" || GET_STATE(RESULT) == "BOMB" || GET_STATE(RESULT) == "STONE" || GET_STATE(RESULT) == "DYING" || GET_STATE(RESULT) == "FLY"
				CONTINUE
		소지ステート:(LCOUNT:1) += 1
		CALLFORM SKILL_RANK_{ABL:대상:LOCALS}
		SIF 최고랭크:(LCOUNT:1) < RESULT
			최고랭크:(LCOUNT:1) = RESULT
	NEXT
	IF CFLAG:대상:초기링크악마
		;소지스킬
		FOR LCOUNT,1,12
			LOCALS = 초기변신악마스킬{LCOUNT}
			SIF ABL:대상:LOCALS == 0
				CONTINUE
			LCOUNT:1 = 0
			TRYCALLFORM SKILL_ADDTIONAL_STATE_{ABL:대상:LOCALS}
			LCOUNT:1 = RESULT
			SIF GET_STATE(RESULT) == "GOOD" || GET_STATE(RESULT) == "BOMB" || GET_STATE(RESULT) == "STONE" || GET_STATE(RESULT) == "DYING" || GET_STATE(RESULT) == "FLY"
					CONTINUE
			소지ステート:RESULT += 1
			CALLFORM SKILL_RANK_{ABL:대상:LOCALS}
			SIF 최고랭크:(LCOUNT:1) < RESULT
				최고랭크:(LCOUNT:1) = RESULT
		NEXT
		PRINTL
	ENDIF

IF SUMARRAY(소지ステート, 1, (FLAG:상태이상수)) == 1
	FOR LCOUNT, 1, FLAG:상태이상수
		IF 소지ステート:LCOUNT > 0
			VARSET 적정ステート
			적정ステート = LCOUNT
			BREAK
		ENDIF
	NEXT
ELSEIF SUMARRAY(소지ステート, 1, (FLAG:상태이상수)) > 0
	FOR LCOUNT, 1, FLAG:상태이상수
		IF 소지ステート:LCOUNT > 0
			IF LCOUNT:3 == 최고랭크:LCOUNT
				SIF LCOUNT:2 > 소지ステート:LCOUNT
					CONTINUE
			ENDIF
			IF 최고랭크:LCOUNT >= LCOUNT:3
				LCOUNT:1 = LCOUNT
				LCOUNT:2 = 소지ステート:LCOUNT
				LCOUNT:3 = 최고랭크:LCOUNT 
			ENDIF
		ENDIF
	NEXT
	VARSET 적정ステート
	적정ステート = LCOUNT:1
ELSE
	VARSET 적정ステート
	적정ステート = 0
ENDIF
RETURN 적정ステート

;====================================================
;장비강화
;====================================================
@장비강화_CSTR, ARGS, ARG, ARG:1, ARGS:1, ARGS:2, ARG:2 = -1
;ARG キャラ번호　ARG:1 장비번호　ARGS:1 分類　ARGS:2 지정　ARG:2 강화치
#DIM DYNAMIC LCOUNT, 2
#DIM DYNAMIC MEMO, 10
#DIMS DYNAMIC MEMOS, 11

#DIMS DYNAMIC 강화전개, 10
#DIMS DYNAMIC 장비번호
#DIMS DYNAMIC 分類
#DIMS DYNAMIC 지정
#DIM DYNAMIC 강화치
#DIMS DYNAMIC 선택지

#DIMS DYNAMIC 기록_개조장비베이스
#DIMS DYNAMIC 대상_개조장비베이스
#DIM DYNAMIC 기록_개조장비베이스_번호
#DIM DYNAMIC 대상_개조장비베이스_번호
#DIM DYNAMIC 능력강화치
#DIM DYNAMIC 포인트

SIF ARG < 0
	RETURN
IF !SOFT_ON("마장술")
	SIF ARGS != "全해제：능력のみ"
	RETURN
ENDIF	

SELECTCASE ARGS
CASE "강화수"
	SIF CSTR:ARG:장비강화 == ""
		RETURN 
	VARSET LOCALS
	;記録の分解
	SPLIT CSTR:ARG:장비강화, "/", LOCALS
	MEMO:9 = 0
	;記録は100まで
	FOR LCOUNT, 0, 100
		SIF LOCALS:LCOUNT == ""
			BREAK
		MEMO:9 += 1
	NEXT
	RETURN MEMO:9

CASE "해제"
	SIF CSTR:ARG:장비강화 == ""
		RETURN
	VARSET LOCALS
	VARSET 포인트
	;分解
	SPLIT CSTR:ARG:장비강화, "/", LOCALS

	;지정記録
	IF MEMOS == ""
			MEMOS = %TOSTR(ARG:1)%
			MEMOS:1 = %ARGS:1%
	;		MEMOS:2 = %ARGS:2%
	;		MEMOS:3 = %TOSTR(ARG:2)%
	ENDIF
	MEMOS:9 = 

	;100まで
	FOR LCOUNT, 0, 100
		SIF LOCALS:LCOUNT == ""
			BREAK
		VARSET 강화전개
		SPLIT LOCALS:LCOUNT, ":", 강화전개
		장비번호 = %강화전개:0%
	;	分類 = %강화전개:1%
	;	지정 = %강화전개:2%
	;	강화치 = TOINT(강화전개:3)

		;지정と同じものを
		IF MEMOS == 장비번호
				IF 포인트 == 0
					CALL 장비강화_전개, ARG, TOINT(장비번호), "강화단계", "LV"
					FOR LCOUNT:1, 1, RESULT
						포인트 += MAX(1, 2 * (LCOUNT:1 - 1) * LCOUNT:1 * (LCOUNT:1 + 1) * 2) + 1000
					NEXT
					;(강화に使った金銭+10%)/4
					;+10%はＭＡＧの分
					포인트 = 포인트 / 9 * 11 / 100 / 4
					PRINTFORMW これにより개념강화で使用できる포인트を {포인트} 入手しました
					개념강화포인트 += 포인트
				ENDIF
			CONTINUE
		ELSE
			MEMOS:9 = %MEMOS:9%%LOCALS:LCOUNT%/
		ENDIF
	NEXT

	CSTR:ARG:장비강화 = %MEMOS:9%
	;PRINTFORML 장비강화 = %CSTR:ARG:장비강화%

CASE "全해제：능력のみ"
	SIF CSTR:ARG:장비강화 == ""
		RETURN
	VARSET LOCALS
	;分解
	SPLIT CSTR:ARG:장비강화, "/", LOCALS

	;지정記録
	IF MEMOS == ""
			MEMOS = %TOSTR(ARG:1)%
			MEMOS:1 = %ARGS:1%
	;		MEMOS:2 = %ARGS:2%
	;		MEMOS:3 = %TOSTR(ARG:2)%
	ENDIF
	MEMOS:9 = 

	;100まで
	FOR LCOUNT, 0, 100
		SIF LOCALS:LCOUNT == ""
			BREAK
		VARSET 강화전개
		SPLIT LOCALS:LCOUNT, ":", 강화전개
		장비번호 = %강화전개:0%
		分類 = %강화전개:1%
	;	지정 = %강화전개:2%
	;	강화치 = TOINT(강화전개:3)

		;지정
		IF (分類 == "능력강화" || 分類 == "내성강화")
			CONTINUE
		ELSE
			MEMOS:9 = %MEMOS:9%%LOCALS:LCOUNT%/
		ENDIF
	NEXT

	CSTR:ARG:장비강화 = %MEMOS:9%
	;PRINTFORML 장비강화 = %CSTR:ARG:장비강화%

CASE "書き込み"
	SIF ARG:2 == -1
		RETURN
	VARSET LOCALS
	;分解
	SPLIT CSTR:ARG:장비강화, "/", LOCALS
	;지정記録
	IF MEMOS == ""
			MEMOS = %TOSTR(ARG:1)%
			MEMOS:1 = %ARGS:1%
			MEMOS:2 = %ARGS:2%
			MEMOS:3 = %TOSTR(ARG:2)%
			SIF 개조장비(ARG:1)
				MEMOS:10 = %TOSTR(베이스장비번호:(개조장비番号(ARG:1)))%
	ENDIF
	MEMOS:9 = 
	기록_개조장비베이스 = 

	;100まで
	FOR LCOUNT, 0, 100
		SIF LOCALS:LCOUNT == ""
			BREAK
		VARSET 강화전개
		SPLIT LOCALS:LCOUNT, ":", 강화전개
		장비번호 = %강화전개:0%
		分類 = %강화전개:1%
		지정 = %강화전개:2%
		SIF MEMOS:1 == "공격상성" || MEMOS:1 == "추가효과"
			지정 = %MEMOS:2%
		강화치 = TOINT(강화전개:3)
		SIF 개조장비(ARG:1)
			기록_개조장비베이스 = %강화전개:4%

		;지정と同じものを
		;장비번호:分類:지정:강화치 となっている
		IF MEMOS != "" && MEMOS == 장비번호 && MEMOS:1 == 分類 && MEMOS:2 == 지정
			MEMO = ARG:2
			SIF 分類 == "강화단계" && ARG:2 != 0
				MEMO = 강화치 + ARG:2
			IF MEMO == 0 
				;MEMOS초기化
				MEMOS = 
				CONTINUE
			ENDIF
			;개조장비は장비번호が同じでもベース장비が違う事があるので
			IF MEMOS:10 != "" && MEMOS:10 == 기록_개조장비베이스
				LOCALS:LCOUNT = %MEMOS%:%MEMOS:1%:%MEMOS:2%:%TOSTR(MEMO)%:%MEMOS:10%
			ELSE
				LOCALS:LCOUNT = %MEMOS%:%MEMOS:1%:%MEMOS:2%:%TOSTR(MEMO)%
			ENDIF
			;MEMOSは초기化
			MEMOS = 
			;記録する문자열を再構築
			MEMOS:9 = %LOCALS:LCOUNT%/%MEMOS:9%
		ELSE
			MEMOS:9 = %MEMOS:9%%LOCALS:LCOUNT%/
		ENDIF
	NEXT

	;初ならMEMOSは入っている
	IF 개조장비(ARG:1) && MEMOS != "" && !(ARG:2 == 0)
		MEMOS:9 = %MEMOS%:%MEMOS:1%:%MEMOS:2%:%TOSTR(ARG:2)%:%MEMOS:10%/%MEMOS:9%
	ELSEIF MEMOS != "" && !(ARG:2 == 0)
		MEMOS:9 = %MEMOS%:%MEMOS:1%:%MEMOS:2%:%TOSTR(ARG:2)%/%MEMOS:9%
	ENDIF

	CSTR:ARG:장비강화 = %MEMOS:9%
	;PRINTFORML 장비강화 = %CSTR:ARG:장비강화%

CASE "표시"
	SIF CSTR:ARG:장비강화 == ""
		RETURN
	FOR LOCAL:1 , 0 , 7
		SIF EQUIP:ARG:GET_EQUIP(LOCAL:1) < 2000
			CONTINUE
		CALL 장비강화_전개, ARG, EQUIP:ARG:GET_EQUIP(LOCAL:1), "강화단계", "LV"
		강화치 = RESULT
		IF 강화치 > 1
			IF 개조장비(EQUIP:ARG:GET_EQUIP(LOCAL:1))
				CALL 장비강화_전개, ARG, EQUIP:ARG:GET_EQUIP(LOCAL:1), "기록_개조장비베이스번호", ""
				대상_개조장비베이스_번호 = 베이스장비번호:(개조장비番号(EQUIP:ARG:GET_EQUIP(LOCAL:1)))
				LOCALS = ◇%개조장비명칭(개조장비番号(EQUIP:ARG:GET_EQUIP(LOCAL:1))), 16, LEFT%
				SIF RESULT > 0 && 대상_개조장비베이스_번호 != RESULT
					LOCALS += "(강화불가)"
			ELSE
				LOCALS = ◇%ITEMNAME:(EQUIP:ARG:GET_EQUIP(LOCAL:1)),16,LEFT%
			ENDIF
			IF ARGS:1 == "선택"
			;	PRINTBUTTON LOCALS, EQUIP:ARG:GET_EQUIP(LOCAL:1)
				PRINTFORM [{EQUIP:ARG:GET_EQUIP(LOCAL:1)}]
				PRINTFORM   %LOCALS%
				PRINTFORML   Lv:{강화치}
				선택지 += TOSTR(EQUIP:ARG:GET_EQUIP(LOCAL:1)) + "/"
			ELSE
			;	PRINTFORML ◇%ITEMNAME:(EQUIP:ARG:GET_EQUIP(LOCAL:1)),16,LEFT%　Lv:{강화치}
				PRINTFORML   %LOCALS%  Lv:{강화치}
			ENDIF
		ENDIF
		MEMO = 0
		MEMO:1 = 0
		MEMO:2 = 0
		MEMO:3 = 0

		CALL 장비강화_전개, ARG, EQUIP:ARG:GET_EQUIP(LOCAL:1), "공격상성", "부여상성"
		IF RESULT > -1
			SIF ARGS:1 == "선택"
				PRINT         
			PRINT   <공격상성>      
			PRINTFORM   속성부여  %GET_TYPE(RESULT),4%    
			CALL 장비강화_전개, ARG, EQUIP:ARG:GET_EQUIP(LOCAL:1), "공격상성", "부여확률"
			PRINTFORM 부여확률  {RESULT,2}％  
			PRINTL
		ENDIF
		CALL 장비강화_전개, ARG, EQUIP:ARG:GET_EQUIP(LOCAL:1), "추가효과", "ステート"
		IF GET_STATE(RESULT) != "GOOD"
			SIF ARGS:1 == "선택"
				PRINT         
			PRINT   <추가효과>      
			PRINTFORM   %GET_STATE(RESULT),16,LEFT%  
			CALL 장비강화_전개, ARG, EQUIP:ARG:GET_EQUIP(LOCAL:1), "추가효과", "기본명중"
			PRINTFORM 부여확률  {RESULT,2}％  
			PRINTL
		ENDIF

		FOR LOCAL,0,FLAG:전투능력수 +2
			CALL 장비강화_전개, ARG, EQUIP:ARG:GET_EQUIP(LOCAL:1), "전투능력수정", GET_BATTLESTATUS(LOCAL)
			IF RESULT != 0
				IF MEMO:1 == 0
					SIF ARGS:1 == "선택"
						PRINT         
					PRINT   <능력강화>      
					MEMO:1 = 1
				ENDIF
				PRINTFORM   %GET_BATTLESTATUS(LOCAL),4,LEFT%  
				SIF RESULT < 0
					SETCOLOR 0xEE1010
				PRINTFORM {RESULT,4}  
				RESETCOLOR
			ENDIF
		NEXT
		SIF MEMO:1 != 0
			PRINTL
		FOR LOCAL,0,FLAG:상성수
			;SIF (TALENT:ARG:페르소나구사자 || ABL:ARG:종족 == 36)
			;	SETCOLOR COLOR("gray")
			CALL 장비강화_전개, ARG, EQUIP:ARG:GET_EQUIP(LOCAL:1), "방어상성", GET_TYPE(LOCAL)
			IF RESULT != 0
				IF MEMO:2 == 0
					SIF ARGS:1 == "선택"
						PRINT         
					PRINT   <내성강화>      
					MEMO:2 = 1
				ENDIF
				PRINTFORM   %GET_TYPE(LOCAL),4,LEFT%
				SETCOLOR COLOR("LIGHT-GRAY")
				IF RESULT == 999
					PRINT   반사  
				ELSE
					PRINTFORM  %\@(RESULT < 0) ? 吸 #   \@%{ABS(RESULT),3}  
				ENDIF
				RESETCOLOR
			ENDIF
		NEXT
		SIF MEMO:2 != 0
			PRINTL
	NEXT
	SIF ARGS:1 == "선택"
		RESULTS = %선택지%

CASE "표시：不장비"
	SIF CSTR:ARG:장비강화 == ""
		RETURN
	VARSET LOCALS
	;分解
	SPLIT CSTR:ARG:장비강화, "/", LOCALS
	MEMOS:9 = 
	;100まで
	FOR LCOUNT, 0, 100
		SIF LOCALS:LCOUNT == ""
			BREAK
		VARSET 강화전개
		SPLIT LOCALS:LCOUNT, ":", 강화전개
		장비번호 = %강화전개:0%
		分類 = %강화전개:1%
		지정 = %강화전개:2%
		강화치 = TOINT(강화전개:3)
		IF 개조장비(TOINT(장비번호))
			대상_개조장비베이스 = %TOSTR(베이스장비번호:(개조장비番号(TOINT(장비번호))))%
			기록_개조장비베이스 = %강화전개:4%
		ENDIF

		TRYCCALLFORM 장비부위_%장비번호%
		CATCH
			CONTINUE
		ENDCATCH
		SIF EQUIP:ARG:GET_EQUIP(RESULT) == TOINT(장비번호)
			CONTINUE
		IF (장비번호 != "") && (分類 == "강화단계") && (지정 == "EXP") && (강화치 > 0)
			CALL 장비강화_전개, ARG, TOINT(장비번호), "강화단계", "LV"
			강화치 = RESULT
			IF 개조장비(TOINT(장비번호))
				MEMOS:0 = ◇%개조장비명칭(개조장비番号(TOINT(장비번호))), 16, LEFT%
				SIF 기록_개조장비베이스 != "" && 대상_개조장비베이스 != 기록_개조장비베이스
					MEMOS:0 += "(강화불가)"
			ELSE
				MEMOS:0 = ◇%ITEMNAME:(TOINT(장비번호)),16,LEFT%
			ENDIF
			IF ARGS:1 == "선택"
			;	PRINTBUTTON MEMOS:0, TOINT(장비번호)
				PRINTFORM [{TOINT(장비번호)}]
				PRINTFORM   %MEMOS:0%
				PRINTFORML   Lv:{강화치}
				선택지 += 장비번호 + "/"
			ELSE
				;PRINTFORML ◇%ITEMNAME:(TOINT(장비번호)),16,LEFT%  Lv:{강화치}
				PRINTFORML   %MEMOS:0%  Lv:{강화치}
			ENDIF
			MEMO:1 = 0
			MEMO:2 = 0

			CALL 장비강화_전개, ARG, TOINT(장비번호), "공격상성", "부여상성"
			IF RESULT > -1
				SIF ARGS:1 == "선택"
					PRINT         
				PRINT   <공격상성>      
				PRINTFORM   속성부여  %GET_TYPE(RESULT),4%    
				CALL 장비강화_전개, ARG, TOINT(장비번호), "공격상성", "부여확률"
				PRINTFORM 부여확률  {RESULT,2}％  
				PRINTL
			ENDIF
			CALL 장비강화_전개, ARG, TOINT(장비번호), "추가효과", "ステート"
			IF GET_STATE(RESULT) != "GOOD"
				SIF ARGS:1 == "선택"
					PRINT         
				PRINT   <추가효과>      
				PRINTFORM   %GET_STATE(RESULT),16,LEFT%  
				CALL 장비강화_전개, ARG, TOINT(장비번호), "추가효과", "기본명중"
				PRINTFORM 부여확률  {RESULT,2}％  
				PRINTL
			ENDIF

			FOR LOCAL,0,FLAG:전투능력수 +2
				CALL 장비강화_전개, ARG, TOINT(장비번호), "전투능력수정", GET_BATTLESTATUS(LOCAL)
				IF RESULT != 0
					IF MEMO:1 == 0
						SIF ARGS:1 == "선택"
							PRINT         
						PRINT   <능력강화>      
						MEMO:1 = 1
					ENDIF
					PRINTFORM   %GET_BATTLESTATUS(LOCAL),4,LEFT%  
					SIF RESULT < 0
						SETCOLOR 0xEE1010
					PRINTFORM {RESULT,4}  
					RESETCOLOR
				ENDIF
			NEXT
			SIF MEMO:1 != 0
				PRINTL
			FOR LOCAL,0,FLAG:상성수
				CALL 장비강화_전개, ARG, TOINT(장비번호), "방어상성", GET_TYPE(LOCAL)
				IF RESULT != 0
					IF MEMO:2 == 0
						SIF ARGS:1 == "선택"
							PRINT         
						PRINT   <내성강화>      
						MEMO:2 = 1
					ENDIF
					PRINTFORM   %GET_TYPE(LOCAL),4,LEFT%
					SETCOLOR COLOR("LIGHT-GRAY")
					IF RESULT == 999
						PRINT   반사  
					ELSE
						PRINTFORM  %\@(RESULT < 0) ? 吸 #   \@%{ABS(RESULT),3}  
					ENDIF
					RESETCOLOR
				ENDIF
			NEXT
			SIF MEMO:2 != 0
				PRINTL
		ENDIF
	NEXT
	SIF ARGS:1 == "선택"
		RESULTS = %선택지%

CASE "해설표시"
	SIF CSTR:ARG:장비강화 == ""
		RETURN
	CALL 장비강화_전개, ARG, ARG:1, "강화단계", "LV"
	SIF RESULT <= 1
		RETURN
	강화치 = RESULT
	PRINTFORML   ◇ %ITEMNAME:(ARG:1),16,LEFT%  Lv:{강화치}

	IF ARG:1 >= 2000 && ARG:1 < 2500
		CALL 장비강화_전개, ARG, ARG:1, "공격상성", "부여상성"
		IF RESULT > -1
			PRINT     <공격상성>  
			PRINTFORM   속성부여  %GET_TYPE(RESULT),4%    
			CALL 장비강화_전개, ARG, ARG:1, "공격상성", "부여확률"
			PRINTFORM 부여확률  {RESULT,2}％  
			PRINTL
		ENDIF
		CALL 장비강화_전개, ARG, ARG:1, "추가효과", "ステート"
		IF GET_STATE(RESULT) != "GOOD"
			PRINT     <추가효과>  
			PRINTFORM   %GET_STATE(RESULT),16,LEFT%  
			CALL 장비강화_전개, ARG, ARG:1, "추가효과", "기본명중"
			PRINTFORM 부여확률  {RESULT,2}％  
			PRINTL
		ENDIF
	ENDIF

	MEMO:1 = 0
	MEMO:2 = 0
	FOR LOCAL,0,FLAG:전투능력수 +2
		CALL 장비강화_전개, ARG, ARG:1, "전투능력수정", GET_BATTLESTATUS(LOCAL)
		IF RESULT != 0
			능력강화치 = RESULT
			IF MEMO:1 == 0
				PRINT     <능력강화>
				MEMO:1 = 1
			ENDIF
			PRINTFORM   %GET_BATTLESTATUS(LOCAL),4,LEFT%
			SIF RESULT < 0
				SETCOLOR 0xEE1010
				PRINTFORM   {능력강화치,4}
			RESETCOLOR
		ENDIF
	NEXT
	SIF MEMO:1 != 0
		PRINTL
	FOR LOCAL,0,FLAG:상성수
		CALL 장비강화_전개, ARG, ARG:1, "방어상성", GET_TYPE(LOCAL)
		IF RESULT != 0
			IF MEMO:2 == 0
				PRINT     <내성강화>  
				MEMO:2 = 1
			ENDIF
			PRINTFORM   %GET_TYPE(LOCAL),4,LEFT%
			CALL SET_AISYOU_COLOR, RESULT
			IF RESULT == 999
				PRINT   반사  
			ELSE
				PRINTFORM  %\@(RESULT < 0) ? 吸 #   \@%{ABS(RESULT),3}  
			ENDIF
			RESETCOLOR
		ENDIF
	NEXT
	SIF MEMO:2 != 0
		PRINTL
ENDSELECT

@장비강화_전개, ARG, ARG:1, ARGS:1, ARGS:2, ARG:2 = -1
;ARG キャラ번호  ARG:1 장비번호  ARGS:1 分類  ARGS:2 지정  ARG:2 장비기본치(これにプラス)
#DIM DYNAMIC LCOUNT, 2
#DIM DYNAMIC MEMO, 10
#DIMS DYNAMIC MEMOS, 10

#DIMS DYNAMIC 강화전개, 10
#DIM DYNAMIC キャラ번호
#DIMS DYNAMIC 장비번호
#DIMS DYNAMIC 分類
#DIMS DYNAMIC 지정
#DIM DYNAMIC 강화치
#DIM DYNAMIC 능력강화치,10
;0 공격  1 명중  2 방어  3 회피  4 마법위력  5 마법효과

#DIM DYNAMIC 장비기본치
#DIMS DYNAMIC 기록_개조장비베이스
#DIMS DYNAMIC 대상_개조장비베이스

SIF ARG < 0
	RETURN

IF !SOFT_ON("마장술") || SOFT_ON("하모나이저")
	SELECTCASE ARGS:1
		CASE "전투능력수정"
			SIF ARG:1 == 0
				RETURN
			SIF ARG:2 != -1
				RETURN ARG:2
			SIF ARG:2 == -1
				RETURN
		CASE "방어상성"
			SIF ARG:1 == 0
				RETURN 100
			SIF ARG:2 != -1
				RETURN ARG:2
			SIF ARG:2 == -1
				RETURN 100
		CASE "공격상성"
			RETURN
		CASE "추가효과"
			RETURN
	ENDSELECT
ELSE
	SIF CSTR:ARG:장비강화 == "" && ARGS:1 != "강화단계"
		RETURN ARG:2
	SELECTCASE ARGS:1
		CASE "전투능력수정"
			SIF ARG:1 == 0
				RETURN
		CASE "방어상성"
			SIF ARG:1 == 0
				RETURN 100
			SIF ARG:2 == 100
				RETURN 100
		CASE "공격상성"
			SIF ARG:1 == 0
				RETURN
		CASE "추가효과"
			SIF ARG:1 == 0
				RETURN
	ENDSELECT
ENDIF

;디버그용
;PRINTFORML {ARG}/{ARG:1}/%ARGS:1%/%ARGS:2%/{ARG:2}
キャラ번호 = ARG

SELECTCASE ARGS:1
	CASE "전투능력수정"
		장비기본치 = ARG:2

		SIF ARG:1 == 0
			RETURN 0
		SIF CSTR:キャラ번호:장비강화 == "" && ARG:2 != -1
			RETURN ARG:2
		SIF CSTR:キャラ번호:장비강화 == "" && ARG:2 == -1
			RETURN 0

		VARSET LOCALS
		;記録の分解
		SPLIT CSTR:キャラ번호:장비강화, "/", LOCALS

		;지정の記録
		IF MEMOS == ""
			MEMOS = %TOSTR(ARG:1)%
		;	MEMOS:1 = %ARGS:1%
			MEMOS:1 = 능력강화
			MEMOS:2 = %ARGS:2%
			MEMOS:3 = %TOSTR(ARG:2)%
		ENDIF
		
		;100まで
		FOR LCOUNT, 0, 100
			SIF LOCALS:LCOUNT == ""
				BREAK
			VARSET 강화전개
			SPLIT LOCALS:LCOUNT, ":", 강화전개
			장비번호 = %강화전개:0%
			分類 = %강화전개:1%
			지정 = %강화전개:2%
			강화치 = TOINT(강화전개:3)

			;지정と同じものを
			IF MEMOS != "" && MEMOS == 장비번호 && MEMOS:1 == 分類 && MEMOS:2 == 지정
				;장비번호:分類:지정:강화치 となっているので抜き出しす
				SIF 개조장비(TOINT(장비번호))
					CONTINUE
				SIF 장비기본치 != -1
					RETURN 장비기본치 + 강화치
				RETURN 강화치
			ENDIF
		NEXT
		SIF 장비기본치 != -1
			RETURN 장비기본치
		RETURN 0

	CASE "방어상성"
		SIF ARG:1 == 0
			RETURN 100
		SIF CSTR:キャラ번호:장비강화 == "" && ARG:2 != -1
			RETURN ARG:2
		SIF CSTR:キャラ번호:장비강화 == "" && ARG:2 == -1
			RETURN 100
		VARSET LOCALS
		;分解
		SPLIT CSTR:キャラ번호:장비강화, "/", LOCALS
		;지정の記録
		IF MEMOS == ""
				MEMOS = %TOSTR(ARG:1)%
			;	MEMOS:1 = %ARGS:1%
				MEMOS:1 = 내성강화
				MEMOS:2 = %ARGS:2%
				MEMOS:3 = %TOSTR(ARG:2)%
		ENDIF
		
		;記録は100まで
		FOR LCOUNT, 0, 100
			SIF LOCALS:LCOUNT == ""
				BREAK
			VARSET 강화전개
			SPLIT LOCALS:LCOUNT, ":", 강화전개
			장비번호 = %강화전개:0%
			分類 = %강화전개:1%
			지정 = %강화전개:2%
			강화치 = TOINT(강화전개:3)

			;지정と同じものを
			IF MEMOS != "" && MEMOS == 장비번호 && MEMOS:1 == 分類 && MEMOS:2 == 지정
				SIF 개조장비(TOINT(장비번호))
					CONTINUE
				SIF 강화치 == 999
					RETURN 강화치
				SIF ARG:2 > 0 
					RETURN ARG:2 - 강화치
				RETURN 강화치
			ENDIF
		NEXT
				SIF ARG:2 != -1
					RETURN ARG:2
				RETURN 0

	CASE "공격상성"
	;ARG キャラ번호　ARG:1 장비번호　ARGS:1 分類　ARGS:2 지정　ARG:2 장비기본치
		SIF ARG:1 == 0
			RETURN 0
		VARSET LOCALS
		;分解
		SPLIT CSTR:キャラ번호:장비강화, "/", LOCALS
		;지정の記録
		IF MEMOS == ""
				MEMOS = %TOSTR(ARG:1)%
				MEMOS:1 = %ARGS:1%
				MEMOS:2 = %ARGS:2%
			;	지정 = %ARGS:2%
		ENDIF
		
		FOR LCOUNT, 0, 100
			SIF LOCALS:LCOUNT == ""
				BREAK
			VARSET 강화전개
			SPLIT LOCALS:LCOUNT, ":", 강화전개
			장비번호 = %강화전개:0%
			分類 = %강화전개:1%
			지정 = %강화전개:2%
			강화치 = TOINT(강화전개:3)

			;지정と同じものを
			IF MEMOS != "" && MEMOS == 장비번호 && MEMOS:1 == 分類 ;&& MEMOS:2 == 지정
				IF 개조장비(TOINT(장비번호))
					기록_개조장비베이스 = %강화전개:4%
					대상_개조장비베이스 = %TOSTR(베이스장비번호:(개조장비番号(TOINT(장비번호))))%
					SIF 기록_개조장비베이스 != "" && 대상_개조장비베이스 != 기록_개조장비베이스
						CONTINUE
				ENDIF
				IF MEMOS:2 == "부여상성"
					RETURN GET_TYPE_NUM(지정)
				ElSEIF MEMOS:2 == "부여확률"
					RETURN 강화치
				ElSEIF MEMOS:2 == "속성부여"
					IF 강화치 >= RAND:100
						RETURN 1
					ENDIF
					RETURN 0
				ENDIF
			ENDIF
		NEXT
				IF MEMOS:2 == "부여상성"
					RETURN -1
				ElSEIF MEMOS:2 == "부여확률"
					RETURN 0
				ENDIF
				SIF ARGS:2 != ""
						RETURN 0
				RETURN -1

	CASE "추가효과"
		;	FREEZE	20%
		;	SHOCK	20%
		;	POISON 	20%
		;	SLEEP 	25%
		;	PANIC 	25%
		;	HAPPY 	35%
		;	BIND 	25%
		;	CHARM 	20%
		SIF ARG:1 == 0
			RETURN 0
		VARSET LOCALS
		;分解
		SPLIT CSTR:キャラ번호:장비강화, "/", LOCALS
		;지정の記録
		IF MEMOS == ""
				MEMOS = %TOSTR(ARG:1)%
				MEMOS:1 = %ARGS:1%
			;	MEMOS:1 = 추가효과
				MEMOS:2 = %ARGS:2%
			;	지정 = %ARGS:2%
		ENDIF
		
		FOR LCOUNT, 0, 100
			SIF LOCALS:LCOUNT == ""
				BREAK
			VARSET 강화전개
			SPLIT LOCALS:LCOUNT, ":", 강화전개
			장비번호 = %강화전개:0%
			分類 = %강화전개:1%
			지정 = %강화전개:2%
			강화치 = TOINT(강화전개:3)

			;지정と同じものを
			IF MEMOS != "" && MEMOS == 장비번호 && MEMOS:1 == 分類 ;&& MEMOS:2 == 지정
				IF 개조장비(TOINT(장비번호))
					기록_개조장비베이스 = %강화전개:4%
					대상_개조장비베이스 = %TOSTR(베이스장비번호:(개조장비番号(TOINT(장비번호))))%
					SIF 기록_개조장비베이스 != "" && 대상_개조장비베이스 != 기록_개조장비베이스
						CONTINUE
				ENDIF
				IF MEMOS:2 == "ステート"
					RETURN GET_STATE_NUM(지정)
				ElSEIF MEMOS:2 == "効果상성"
					IF 지정 == "FLAME"
						RETURN GET_TYPE_NUM("화염")
					ELSEIF 지정 == "FREEZE"
						RETURN GET_TYPE_NUM("빙결")
					ELSEIF 지정 == "SHOCK"
						RETURN GET_TYPE_NUM("전격")
					ELSEIF 지정 == "SLIP"
						RETURN GET_TYPE_NUM("충격")
					ELSEIF 지정 == "POISON" || 지정 == "BIND" || 지정 == "CLOSE" || 지정 == "PLYZE"
						RETURN GET_TYPE_NUM("신경")
					ELSEIF 지정 == "SLEEP" || 지정 == "PANIC" || 지정 == "HAPPY" || 지정 == "CHARM"
						RETURN GET_TYPE_NUM("정신")
					ELSEIF 지정 == "BRAND"
						RETURN GET_TYPE_NUM("파마")
					ELSEIF 지정 == "CURSE"
						RETURN GET_TYPE_NUM("주살")
					ENDIF
				ElSEIF MEMOS:2 == "기본명중"
					RETURN 강화치
				ElSEIF MEMOS:2 == "최대명중"
						RETURN 95
				ENDIF
			ENDIF
		NEXT
				SIF ARGS:2 != ""
						RETURN 0
				RETURN 0

	CASE "강화단계"
		SIF CSTR:キャラ번호:장비강화 == ""
			RETURN 1
		VARSET LOCALS
		;分解
		SPLIT CSTR:キャラ번호:장비강화, "/", LOCALS

		;지정記録
		IF MEMOS == ""
				MEMOS = %TOSTR(ARG:1)%
			;	MEMOS:1 = %ARGS:1%
				MEMOS:1 = 강화단계
				MEMOS:2 = %ARGS:2%
				MEMOS:3 = %TOSTR(ARG:2)%
		ENDIF
		;100まで
		FOR LCOUNT, 0, 100
			SIF LOCALS:LCOUNT == ""
				BREAK
			VARSET 강화전개
			SPLIT LOCALS:LCOUNT, ":", 강화전개
			장비번호 = %강화전개:0%
			分類 = %강화전개:1%
			지정 = %강화전개:2%
			강화치 = TOINT(강화전개:3)

			;지정と同じものを
			IF MEMOS != "" && MEMOS == 장비번호 && MEMOS:1 == 分類 ;&& MEMOS:2 == 지정
				SIF 지정 == "EXP" && MEMOS:2 == 지정
					RETURN 강화치
				SIF 지정 == "保存강화치" && MEMOS:2 == 지정
					RETURN 강화치
				IF MEMOS:2 == "LV" || MEMOS:2 == "NEXT" || MEMOS:2 == "보존LV"
					MEMO = 0
					MEMO:1 = 0
					WHILE 강화치 > MEMO:1
						MEMO += 1
						MEMO:1 = 5 * MEMO * (MEMO+1) * (MEMO+2) / 3
					;	PRINTFORML Lv：{MEMO}　　NEXT:{MEMO:1}/EXP:{강화치}
					SIF MEMO >= 99
							BREAK
					WEND
					SIF MEMOS:2 == "LV"
						RETURN MIN(BASE:キャラ번호:LV, MEMO)
					SIF MEMOS:2 == "NEXT"
						RETURN MEMO:1 - 강화치
					SIF MEMOS:2 == "보존LV"
						RETURN MEMO
				ENDIF
			ENDIF
		NEXT
			RETURN 1
	CASE "기록_개조장비베이스번호"
		SIF CSTR:キャラ번호:장비강화 == ""
			RETURN 0
		VARSET LOCALS
		;分解
		SPLIT CSTR:キャラ번호:장비강화, "/", LOCALS

		;지정記録
		IF MEMOS == ""
			MEMOS = %TOSTR(ARG:1)%
			MEMOS:1 = 기록_개조장비베이스번호
		ENDIF

		FOR LCOUNT, 0, 100
			SIF LOCALS:LCOUNT == ""
				BREAK
			VARSET 강화전개
			SPLIT LOCALS:LCOUNT, ":", 강화전개
			장비번호 = %강화전개:0%
			分類 = %강화전개:1%
			기록_개조장비베이스 = %강화전개:4%

			SIF MEMOS != "" && MEMOS == 장비번호 && MEMOS:1 == 分類 && 기록_개조장비베이스 != ""
				RETURN TOINT(기록_개조장비베이스)
		NEXT
		RETURN 0
	CASEELSE
ENDSELECT

@장비강화_강화포인트산출, ARG, ARG:1, ARG:2
;ARG 장비번호。ARGS:1 장비부위。ARG:2 강화단계
#DIM DYNAMIC 장비번호
#DIM DYNAMIC 장비부위
#DIM DYNAMIC 포인트
#DIM DYNAMIC 강화단계
#DIM DYNAMIC 공격횟수합계
#DIM DYNAMIC RANDOMTARGET
#DIM DYNAMIC LCOUNT

장비번호 = ARG
장비부위 = ARG:1
강화단계 = ARG:2

IF 장비부위 == GET_EQUIPNUM("검")
	포인트 = 강화단계 * 7
	CALLFORM 공격범위_{장비번호}
	LOCALS = %GET_SPHERE(RESULT)%
	IF LOCALS == "一列"
		TIMES 포인트 , 0.85
	ELSEIF LOCALS == "전체"
		TIMES 포인트 , 0.75
	ENDIF
ELSEIF 장비부위 == GET_EQUIPNUM("총")
	CALLFORM 공격범위_{장비번호}
	LOCALS = %GET_SPHERE(RESULT)%
	CALLFORM 최대공격횟수_{장비번호}
	LOCAL = RESULT
	CALLFORM 최대공격횟수_{장비번호}
	;LOCAL:1 = (LOCAL + RESULT) / 2
	공격횟수합계 = LOCAL + RESULT
	TRYCCALLFORM GUN_RANDOMTARGET_{장비번호}
		RANDOMTARGET = 1
	CATCH
	ENDCATCH
	IF RANDOMTARGET == 1
		포인트 = 강화단계 * 6
		ELSEIF LOCALS == "１체"
		포인트 = 강화단계 * 6
	ELSEIF LOCALS == "一列"
		포인트 = 강화단계 * 5
	ELSEIF LOCALS == "전체"
		포인트 = 강화단계 * 4
	ENDIF
	;SIF LOCAL:1 > 1
	;	포인트 = 포인트 / (LOCAL:1 / 2) * 120 /100
	;공격횟수합계が奇수の装備の計算の為に포인트と공격횟수합계を10倍にする
	SIF 공격횟수합계 > 2
		포인트 = 포인트 * 10 / (공격횟수합계 * 10 / 2) * 120 / 100
	CALLFORM 특수탄창_{장비번호}
	SIF RESULT > 2
		포인트 -= RESULT * 10
ELSEIF 장비부위 == GET_EQUIPNUM("머리")
	포인트 = 강화단계 * 14 / 10
ELSEIF 장비부위 == GET_EQUIPNUM("몸통")
	포인트 = 강화단계 * 2
ELSEIF 장비부위 == GET_EQUIPNUM("팔")
	포인트 = 강화단계 * 16 / 10
ELSEIF 장비부위 == GET_EQUIPNUM("발")
	포인트 = 강화단계 * 15 / 10
ELSE
	
ENDIF
RETURN 포인트

@장비강화_성능변동판정, ARG, ARG:1, ARG:2
;性能が変化する装備は後々増えるかもしれないのでまとめて管理する
#DIM DYNAMIC 대상
#DIM DYNAMIC 장비번호
#DIM DYNAMIC 장비부위
#DIM DYNAMIC 성능비교, 3
#DIM DYNAMIC 성능변동, 8
#DIM DYNAMIC 성능변동판정
#DIM DYNAMIC 성능변동_보정
#DIM DYNAMIC LCOUNT, 2

대상 = ARG
장비번호 = ARG:1
장비부위 = ARG:2

;まず特定の装備と一緒に装備していると性能が変動する装備の判定
;基本的にLVに比例して上がるもの
;アイテム名の直接지정とかやりたくないけど、これくらいしか方法が思いつかない
;他に良い方法があったら起用したい
IF 장비부위 == GET_EQUIPNUM("검")
ELSEIF 장비부위 == GET_EQUIPNUM("총")
ELSEIF 장비부위 == GET_EQUIPNUM("머리")
ELSEIF 장비부위 == GET_EQUIPNUM("몸통")
	SELECTCASE 장비번호
		CASE [[아이템:치유하는백의]]
			성능변동판정 = 1
		CASE [[아이템:티타니아의원피스]]
			성능변동판정 = 1
		CASE [[아이템:프레이아의날개옷]]
			성능변동판정 = 1
		CASE [[아이템:윗치레이디원피스]]
			성능변동판정 = 1
		CASE [[아이템:네코마타수트]]
			성능변동판정 = 1
		CASE [[아이템:메이드원피스]]
			성능변동판정 = 1
		CASE [[아이템:쿠훌린갑옷]]
			성능변동판정 = 1
		CASE [[아이템:데빌서바이버]]
			성능변동판정 = 1
		CASE [[아이템:프로스트수트]]
			성능변동판정 = 1
		CASE [[아이템:롱기누스아머]]
			성능변동판정 = 1
	ENDSELECT
ELSEIF 장비부위 == GET_EQUIPNUM("팔")
	SELECTCASE 장비번호
		CASE [[아이템:모래시계방패]]
			;LVに比例するものではないけれど、魔装術側での강화が重なると強くなり過ぎるので
			성능변동판정 = 1
	ENDSELECT
ELSEIF 장비부위 == GET_EQUIPNUM("발")
ELSE
ENDIF

SIF 성능변동판정 == 1
	GOTO 성능변동판정

;特殊な条件なく性能が変動する装備はこれで判別できる、筈
ADDCHARA 대상
FOR LCOUNT, 0, FLAG:전투능력수 + 2
	CALLFORM 전투능력수정_{장비번호}, LCOUNT, -1
	성능비교:0 = RESULT
	BASE:(CHARANUM - 1):LV = BASE:대상:LV + 50
	CALLFORM 전투능력수정_{장비번호}, LCOUNT, CHARANUM - 1
	성능비교:1 = RESULT
	BASE:(CHARANUM - 1):LV = BASE:대상:LV + 100
	FOR LCOUNT:1, 1, 7
		BASE:(CHARANUM - 1):(30 + LCOUNT:1) += 5
	NEXT
	CALLFORM 전투능력수정_{장비번호}, LCOUNT, CHARANUM - 1
	성능비교:2 = RESULT
	
	IF (성능비교:0 != 성능비교:1) && (성능비교:1 != 성능비교:2)
		성능변동:LCOUNT = 1
	ELSE
		성능변동:LCOUNT = 0
	ENDIF
NEXT
CALL 캐릭터삭제, CHARANUM - 1

SIF SUMARRAY(성능변동, 0, FLAG:전투능력수 + 2) > 0
	성능변동판정 = 2

IF 성능변동판정 > 0
	$성능변동판정
	
	;補整치が違うのは강화の上限が違う為
	IF 장비부위 == GET_EQUIPNUM("검") || 장비부위 == GET_EQUIPNUM("총")
		성능변동_보정 = 25
	ELSE
		성능변동_보정 = 50
	ENDIF
ENDIF

RETURN 성능변동:0, 성능변동:1, 성능변동:2, 성능변동:3, 성능변동:4, 성능변동:5, 성능변동:6, 성능변동:7, 성능변동판정, 성능변동_보정
