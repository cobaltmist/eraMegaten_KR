@무기수련 ,ARG
#LOCALSIZE 1
FOR LOCAL,0,11
	IF TALENT:ARG:(250 + LOCAL)
		CFLAG:ARG:(1050 + LOCAL) = 1000
	ENDIF
NEXT
;IF (ARG == 0 && 무기소질수(ARG) > 10) || (ARG > 0 && 무기소질수(ARG) > 1)
;	PRINTW これ以上素質を持てません
;	RETURN 0
;ENDIF

$INPUT_LOOP
DRAWLINE
PRINTL □무기수련
PRINTFORML [ 1]숙련도취득
PRINTFORML [ 2]무기소질부여
SIF 무기소질수(ARG) > 0
	PRINTFORML [ 3]무기소질형변경
PRINTL 
PRINTFORML [99]돌아간다
INPUT

SELECTCASE RESULT
	CASE 1
		DRAWLINE
		PRINTL □숙련도취득　★：초기소질
		PRINTFORML [ 1]도　　　　숙련도:{CFLAG:ARG:(1050),4,LEFT} \@CSVTALENT(NO:ARG,(250),0) ? ★#　\@
		PRINTFORML [ 2]레이피어　숙련도:{CFLAG:ARG:(1051),4,LEFT} \@CSVTALENT(NO:ARG,(251),0) ? ★#　\@
		PRINTFORML [ 3]활　　　　숙련도:{CFLAG:ARG:(1052),4,LEFT} \@CSVTALENT(NO:ARG,(252),0) ? ★#　\@
		PRINTFORML [ 4]검　　　　숙련도:{CFLAG:ARG:(1053),4,LEFT} \@CSVTALENT(NO:ARG,(253),0) ? ★#　\@
		PRINTFORML [ 5]창　　　　숙련도:{CFLAG:ARG:(1054),4,LEFT} \@CSVTALENT(NO:ARG,(254),0) ? ★#　\@
		PRINTFORML [ 6]도끼、둔기　숙련도:{CFLAG:ARG:(1055),4,LEFT} \@CSVTALENT(NO:ARG,(255),0) ? ★#　\@
		PRINTFORML [ 7]채찍　　　　숙련도:{CFLAG:ARG:(1056),4,LEFT} \@CSVTALENT(NO:ARG,(256),0) ? ★#　\@
		PRINTFORML [ 8]투척기　　숙련도:{CFLAG:ARG:(1057),4,LEFT} \@CSVTALENT(NO:ARG,(257),0) ? ★#　\@
		PRINTFORML [ 9]지팡이、제구　숙련도:{CFLAG:ARG:(1058),4,LEFT} \@CSVTALENT(NO:ARG,(258),0) ? ★#　\@
		PRINTFORML [10]곤봉　　　　숙련도:{CFLAG:ARG:(1059),4,LEFT} \@CSVTALENT(NO:ARG,(259),0) ? ★#　\@
		PRINTFORML [11]권갑　　숙련도:{CFLAG:ARG:(1060),4,LEFT} \@CSVTALENT(NO:ARG,(260),0) ? ★#　\@
		PRINTL 
		PRINTFORML [99]돌아간다
		INPUT
		SIF RESULT > 0 && RESULT < 12
			CALL WEAPON_TRAINING,ARG,RESULT
	CASE 2
		CALL 무기소질부여,ARG
	CASE 3
		IF 무기소질수(ARG) > 0
			DRAWLINE
			PRINTL □무기소질형변경
			PRINTL 변경하는 소질을 고르세요
			FOR LOCAL,1,12
				IF TALENT:ARG:(249 + LOCAL) > 0
					PRINTFORML [{LOCAL,2}]%TALENTNAME:(249 + LOCAL),16,LEFT%　현재의 형:%WEAPON_TYPE_NAME(LOCAL,TALENT:ARG:(249 + LOCAL))%
				ENDIF
			NEXT
			PRINTL 
			PRINTFORML [99]돌아간다
			INPUT
			SIF RESULT > 0 && RESULT < 12 && TALENT:ARG:(249 + RESULT)
				CALL WEAPON_TYPE_CHANGE,ARG,RESULT
		ENDIF
	CASE 99
		RETURN 0
ENDSELECT

GOTO INPUT_LOOP

@무기소질부여,ARG
#LOCALSIZE 21
#DIM LCOUNT
#DIM 変数A
変数A = 0
;VARSET Q,0
IF (ARG == MASTER && 무기소질수(ARG) > 10) || (ARG > 0 && 무기소질수(ARG) > 1)
	PRINTW 이 이상 소질을 가질 수 없습니다
	RETURN 0
ENDIF

PRINTL □무기소질부여
PRINTL 조건:숙련도1000이상　당신이외에는 한 개 까지만
PRINT 현재:
FOR LCOUNT,0,11
	IF TALENT:ARG:(250 + LCOUNT)
		SIF 変数A > 0
			PRINT 、
		PRINTFORM %조사처리(TALENTNAME:(250 + LCOUNT),"를")%
		変数A += 1
	ENDIF
NEXT
IF 変数A > 0
	PRINTL 취득
ELSE
	PRINTL 소질없음
ENDIF
FOR LCOUNT,0,11
	SIF TALENT:ARG:(250 + LCOUNT)
		CONTINUE
	IF CFLAG:ARG:(1050+LCOUNT) > 999 || CSVTALENT(NO:ARG,250 + LCOUNT,0)
		SELECTCASE LCOUNT
			CASE 0
				PRINTFORML [{LCOUNT}]　도술刀術
			CASE 1
				PRINTFORML [{LCOUNT}]　팬서
			CASE 2
				PRINTFORML [{LCOUNT}]　궁술
			CASE 3
				PRINTFORML [{LCOUNT}]　검술
			CASE 4
				PRINTFORML [{LCOUNT}]　창술
			CASE 5
				PRINTFORML [{LCOUNT}]　도끼술
			CASE 6
				PRINTFORML [{LCOUNT}]　채찍술
			CASE 7
				PRINTFORML [{LCOUNT}]　투척술
			CASE 8
				PRINTFORML [{LCOUNT}]　제사
			CASE 9
				PRINTFORML [{LCOUNT}]　봉술
			CASE 10
				PRINTFORML [{LCOUNT}]　마샬아츠
		ENDSELECT
		LOCAL:LCOUNT = 1
		LOCAL:20 = 1
	ENDIF
NEXT

IF LOCAL:20 == 0
	PRINTFORMW 조건을 충족한 항목이 없습니다
	RETURN 0
ENDIF
PRINTFORML [20]　돌아간다
INPUT
SIF RESULT == 20
	RETURN 0
IF LOCAL:RESULT
;	SELECTCASE RESULT
;		CASE 0
;			PRINT 도술刀術
;		CASE 1
;			PRINT 팬서
;		CASE 2
;			PRINT 궁술
;		CASE 3
;			PRINT 검술
;		CASE 4
;			PRINT 창술
;		CASE 5
;			PRINT 도끼술
;		CASE 6
;			PRINT 채찍술
;		CASE 7
;			PRINT 투척술
;		CASE 8
;			PRINT 제사
;		CASE 9
;			PRINT 봉술
;		CASE 10
;			PRINT 마샬아츠
;	ENDSELECT
;	PRINTW を取得した
	IF MASTER != ARG 
		FOR LOCAL, 0, 11
			IF LOCAL == RESULT
				PRINTFORML %TALENTNAME:(250 + RESULT)%を취득했다
				TALENT:ARG:(250 + RESULT) = 1
				CONTINUE
			ELSEIF TALENT:ARG:(250 + LOCAL) == 1
				PRINTFORML %TALENTNAME:(250 + LOCAL)%을 잃었다
			ENDIF
			TALENT:ARG:(250 + LOCAL) = 0
		NEXT
		PRINTFORMW 
	ELSE
		PRINTFORML %TALENTNAME:(250 + RESULT)%を취득했다
		TALENT:ARG:(250 + RESULT) = 1
		PRINTFORMW 
	ENDIF
ENDIF

@WEAPON_TRAINING,ARG,ARG:1
#LOCALSIZE 1
#LOCALSSIZE 1
$INPUT_LOOP
IF TALENT:ARG:(249 + ARG:1) || CSVTALENT(NO:ARG,(250 + ARG:1),0)
	PRINTW 이미 보유한 소질입니다
	CFLAG:ARG:(1049+ARG:1) = 1000
	RETURN 0
ENDIF
IF CFLAG:ARG:(1049+ARG:1) > 999
	PRINTW 숙련도가 1000 이상이 되었습니다
	CFLAG:ARG:(1049+ARG:1) = 1000
	RETURN 0
ENDIF
DRAWLINE
PRINT □
SELECTCASE ARG:1
	CASE 1
		LOCALS = 도
	CASE 2
		LOCALS = 레이피어
	CASE 3
		LOCALS = 활
	CASE 4
		LOCALS = 검
	CASE 5
		LOCALS = 창
	CASE 6
		LOCALS = 도끼、둔기
	CASE 7
		LOCALS = 채찍
	CASE 8
		LOCALS = 투척기
	CASE 9
		LOCALS = 지팡이、제구
	CASE 10
		LOCALS = 곤봉
	CASE 11
		LOCALS = 권갑
ENDSELECT
PRINTFORML %LOCALS% 숙련도:{CFLAG:ARG:(1049+ARG:1)}
PRINTFORML [ 1]수련
PRINTFORML [ 2]습득의 구슬 변환
PRINTL 
PRINTFORML [99]돌아간다
INPUT

SELECTCASE RESULT
	CASE 1
		CALL WEAPON_TRAINING2,ARG,ARG:1
	CASE 2
		DRAWLINE
		PRINTFORML %LOCALS%의 숙련도를 얼마나 올립니까？
		PRINTFORML ※습득의 구슬 100개당 숙련도 1
		PRINTFORML 습득의 구슬:{JUEL:ARG:습득}　({JUEL:ARG:습득/100}까지)　사용가능
		SIF JUEL:ARG:습득 >= 100
			PRINTFORML [1]
		SIF JUEL:ARG:습득 >= 500
			PRINTFORML [5]
		SIF JUEL:ARG:습득 >= 1000
			PRINTFORML [10]
		SIF JUEL:ARG:습득 >= 5000
			PRINTFORML [50]
		SIF JUEL:ARG:습득 >= 10000
			PRINTFORML [100]
		PRINTFORML [0]
		INPUT
		IF RESULT > (JUEL:ARG:습득 / 100)
			PRINTFORML 수치가 너무 큽니다
		ELSEIF RESULT > 0
			LOCAL = RESULT
			PRINTFORML 습득의 구슬:{JUEL:ARG:습득,6}　→　{JUEL:ARG:습득 - (LOCAL*100)}
			PRINTFORML 숙련도　:{CFLAG:ARG:(1049+ARG:1),6}　→　{CFLAG:ARG:(1049+ARG:1) + LOCAL}
			PRINTFORML 괜찮겠습니까？
			CALL INPUT_YN,"네","아니오"
			IF RESULT == 0
				JUEL:ARG:습득 -= LOCAL*100
				CFLAG:ARG:(1049+ARG:1) += LOCAL
			ENDIF
		ENDIF
	CASE 99
		RETURN 0
ENDSELECT

GOTO INPUT_LOOP

@WEAPON_TRAINING2,ARG,ARG:1
#LOCALSIZE 6
#LOCALSSIZE 1
#DIM 教授役 = -1
#DIM 上昇量
#DIM 仮횟수A
#DIM 仮횟수B
#DIM 수련횟수
;LOCAL = -1
$INPUT_LOOP
IF BASE:ARG:체력 < 500
	PRINTW 체력이 적어 수련할 수 없습니다
	教授役 = -1
	RETURN 0
ENDIF
IF CFLAG:ARG:(1049+ARG:1) > 999
	;PRINTW 熟練度が1000以上になりました
	;CFLAG:ARG:(1049+ARG:1) = 1000
	教授役 = -1
	RETURN 0
ENDIF
IF 教授役 > -1
	IF BASE:教授役:체력 < 250
		PRINTL 교수의 체력이 적어 교수할 수 없습니다
		教授役 = -1
		LOCAL = -1
	ENDIF
ENDIF
DRAWLINE
PRINT □
SELECTCASE ARG:1
	CASE 1
		LOCALS = 도
	CASE 2
		LOCALS = 레이피어
	CASE 3
		LOCALS = 활
	CASE 4
		LOCALS = 검
	CASE 5
		LOCALS = 창
	CASE 6
		LOCALS = 도끼、둔기
	CASE 7
		LOCALS = 채찍
	CASE 8
		LOCALS = 투척기
	CASE 9
		LOCALS = 지팡이、제구
	CASE 10
		LOCALS = 곤봉
	CASE 11
		LOCALS = 권갑
ENDSELECT
PRINTFORML %LOCALS% 수련
PRINTFORML 숙련도:{CFLAG:ARG:(1049+ARG:1)}
PRINTFORML 체력　:{BASE:ARG:체력}
PRINTFORM [ 1]교수선택
PRINTFORM 　교수:
IF 教授役 > -1
	PRINTFORML %CALLNAME:教授役%
ELSE
	PRINTL 없음
ENDIF
PRINTFORML [ 2]수련실행　　　　　체력500소비
PRINTFORML [ 3]체력500까지 수련 연속실행
PRINTL 
PRINTFORML [99]돌아간다
INPUT

SELECTCASE RESULT
	CASE 1
		CALL 교수선택,ARG:1
		教授役 = RESULT
	CASE 2
		上昇量 = 40 + RAND:21
		SIF 教授役 > -1
			TIMES 上昇量,1.50
		PRINTL □수련실행
		PRINTFORML ○%CALLNAME:ARG%
		PRINTFORML 체력　:{BASE:ARG:체력,4}　→　{BASE:ARG:체력 - 500}
		PRINTFORML 숙련도:{CFLAG:ARG:(1049+ARG:1),4}　→　{CFLAG:ARG:(1049+ARG:1) + 上昇量}
		BASE:ARG:체력 -= 500
		CFLAG:ARG:(1049+ARG:1) += 上昇量
		IF 教授役 > -1
			PRINTFORML ○%CALLNAME:教授役%
			PRINTFORML 체력　:{BASE:教授役:체력,4}　→　{BASE:教授役:체력 - 250}
			BASE:教授役:체력 -= 250
		ENDIF
		SIF CFLAG:ARG:(1049+ARG:1) > 1000
			CFLAG:ARG:(1049+ARG:1) = 1000
		PRINTW
	CASE 3
		上昇量 = 0
		仮횟수A = (BASE:ARG:체력 / 500) - 1
		수련횟수 = 仮횟수A
		IF 教授役 > -1
			仮횟수B = (BASE:教授役:체력 / 250) - 1
			수련횟수 = MIN(仮횟수A, 仮횟수B)
		ENDIF
		FOR LOCAL:5, 0, 수련횟수
			上昇量 += (40 + RAND:21)
		NEXT
		SIF 教授役 > -1
			TIMES 上昇量,1.50
		PRINTL □수련실행
		PRINTFORML ○%CALLNAME:ARG%
		PRINTFORML 실행수:{수련횟수}
		PRINTFORML 체력　:{BASE:ARG:체력,4}　→　{BASE:ARG:체력 - (500 * 수련횟수)}
		PRINTFORML 숙련도:{CFLAG:ARG:(1049+ARG:1),4}　→　{CFLAG:ARG:(1049+ARG:1) + 上昇量}
		BASE:ARG:체력 -= (500 * 수련횟수)
		CFLAG:ARG:(1049+ARG:1) += 上昇量
		SIF CFLAG:ARG:(1049+ARG:1) > 1000
			CFLAG:ARG:(1049+ARG:1) = 1000
		IF 教授役 > -1
			PRINTFORML ○%CALLNAME:教授役%
			PRINTFORML 체력　:{BASE:教授役:체력,4}　→　{BASE:教授役:체력 - (250 * 수련횟수)}
			BASE:教授役:체력 -= (250 * 수련횟수)
		ENDIF
		PRINTW
	CASE 99
		教授役 = -1
		RETURN 0
ENDSELECT

GOTO INPUT_LOOP


@교수선택,ARG
#LOCALSIZE 1
#DIM LCOUNT,200
PRINTL □교수선택
PRINTL 조건:무기소질 소지、함락이거나 충성도5000이거나 당신、수련1회당 체력250소비
;VARSET Q,0
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:PT플래그 == 0
		CONTINUE
	SIF CFLAG:LOCAL:이자리에없는플래그
		CONTINUE
	SIF CFLAG:LOCAL:노역플래그 == 3
		CONTINUE
	SIF !TALENT:LOCAL:(249 + ARG)
		CONTINUE
	SIF !함락(LOCAL) && BASE:LOCAL:충성도 < 5000 && LOCAL != MASTER
		CONTINUE
	SIF BASE:LOCAL:체력 < 250
		CONTINUE
	LCOUNT:LOCAL = 1
	PRINTFORML [{LOCAL,3}]　%CALLNAME:LOCAL%　체력　:{BASE:LOCAL:체력}
NEXT
PRINTFORML [999]　없음
INPUT
SIF RESULT == 999
	RETURN -1
SIF LCOUNT:RESULT == 1
	RETURN RESULT
RETURN -1

@무기소질수,ARG
#FUNCTION
#LOCALSIZE 2

LOCAL:1 = 0
FOR LOCAL,250,261
	SIF TALENT:ARG:LOCAL
		LOCAL:1++
NEXT

RETURNF LOCAL:1

@WEAPON_TYPE_CHANGE,ARG,ARG:1
#DIM LCOUNT
LCOUNT = 0
DRAWLINE
PRINTFORML □형변경　%TALENTNAME:(249 + ARG:1)%
PRINTL 어떤 형으로 하시겠습니까？
SELECTCASE ARG:1
	CASE 1
		;刀
		PRINTFORML [ 1]%WEAPON_TYPE_NAME(ARG:1,1)%　무기의 강함에 따라 힘、속도、운이 올라간다
		SETBIT LCOUNT,1
		PRINTFORML [ 2]%WEAPON_TYPE_NAME(ARG:1,2)%　전투 시작 시 1/3 확률로 베어내기상태로
		SETBIT LCOUNT,2
	CASE 2
		;레이피어
		PRINTFORML [ 1]%WEAPON_TYPE_NAME(ARG:1,1)%　무기의 강함에 따라 지혜、속도、운이 올라간다
		SETBIT LCOUNT,1
		PRINTFORML [ 2]%WEAPON_TYPE_NAME(ARG:1,2)%　크리티컬 데미지가 11\% 올라간다
		SETBIT LCOUNT,2
	CASE 3
		;弓
		PRINTFORML [ 1]%WEAPON_TYPE_NAME(ARG:1,1)%　무기의 강함에 따라 힘、마력、속도가 올라간다
		SETBIT LCOUNT,1
		PRINTFORML [ 2]%WEAPON_TYPE_NAME(ARG:1,2)%　약점 데미지가 11\% 올라간다
		SETBIT LCOUNT,2
	CASE 4
		;검
		PRINTFORML [ 1]%WEAPON_TYPE_NAME(ARG:1,1)%　무기 무기의 강함에 따라 힘、인내력、속도、운이 올라간다
		SETBIT LCOUNT,1
		PRINTFORML [ 2]%WEAPON_TYPE_NAME(ARG:1,2)%　크리티컬과 약점 데미지가 8\%씩 상승한다）
		SETBIT LCOUNT,2
	CASE 5
		;槍
		PRINTFORML [ 1]%WEAPON_TYPE_NAME(ARG:1,1)%　무기 무기의 강함에 따라 힘、인내력、운이 올라간다
		SETBIT LCOUNT,1
		PRINTFORML [ 2]%WEAPON_TYPE_NAME(ARG:1,2)%　후열로부터의 물리 공격이 3\% 올라, 후열의 물리 공격 위력이 떨어지지 않게 되다
		SETBIT LCOUNT,2
	CASE 6
		;斧、鈍器
		PRINTFORML [ 1]%WEAPON_TYPE_NAME(ARG:1,1)%　무기 무기의 강함에 따라 힘、인내력이 올라간다
		SETBIT LCOUNT,1
		PRINTFORML [ 2]%WEAPON_TYPE_NAME(ARG:1,2)%　전투 시작 시 1/3 확률로 기합상태로
		SETBIT LCOUNT,2
	CASE 7
		;채찍
		PRINTFORML [ 1]%WEAPON_TYPE_NAME(ARG:1,1)%　무기 무기의 강함에 따라 지혜、마력、인내력이 올라간다
		SETBIT LCOUNT,1
		PRINTFORML [ 2]%WEAPON_TYPE_NAME(ARG:1,2)%　마법공격의 피격데미지를 0.85배 낮춘다
		SETBIT LCOUNT,2
	CASE 8
		;投具
		PRINTFORML [ 1]%WEAPON_TYPE_NAME(ARG:1,1)%　무기 무기의 강함에 따라 지혜、마력、속도가 올라간다
		SETBIT LCOUNT,1
		PRINTFORML [ 2]%WEAPON_TYPE_NAME(ARG:1,2)%　후열로부터의 마법 공격이 3\% 올라, 후열의 마법 공격 위력이 떨어지지 않게 되다
		SETBIT LCOUNT,2
	CASE 9
		;杖、祭具
		PRINTFORML [ 1]%WEAPON_TYPE_NAME(ARG:1,1)%　무기 무기의 강함에 따라 지혜、마력이 올라간다
		SETBIT LCOUNT,1
		PRINTFORML [ 2]%WEAPON_TYPE_NAME(ARG:1,2)%　전투 시작 시1/3 확률로 컨센트레이트상태로
		SETBIT LCOUNT,2
	CASE 10
		;棍
		PRINTFORML [ 1]%WEAPON_TYPE_NAME(ARG:1,1)%　무기 무기의 강함에 따라 지혜、인내력、속도が 올라간다
		SETBIT LCOUNT,1
		PRINTFORML [ 2]%WEAPON_TYPE_NAME(ARG:1,2)%　모든 공격의 피격데미지를 0.91배 낮춘다
		SETBIT LCOUNT,2
	CASE 11
		;拳闘具
		PRINTFORML [ 1]%WEAPON_TYPE_NAME(ARG:1,1)%　무기 무기의 강함에 따라 힘、인내력、속도が 올라간다
		SETBIT LCOUNT,1
		PRINTFORML [ 2]%WEAPON_TYPE_NAME(ARG:1,2)%　물리공격의 피격데미지를 0.85배 낮춘다
		SETBIT LCOUNT,2
ENDSELECT
PRINTL
PRINTFORML [99]돌아간다

INPUT
SIF RESULT > 63 || RESULT < 0
	RETURN
IF GETBIT(LCOUNT,RESULT)
	PRINTFORMW %TALENTNAME:(249 + ARG:1)%　%WEAPON_TYPE_NAME(ARG:1,RESULT)%로 변경했습니다
	TALENT:ARG:(249 + ARG:1) = RESULT
ENDIF

@WEAPON_TYPE_NAME(ARG,ARG:1)
#FUNCTIONS
SIF ARG:1 < 1
	RETURNF ""
SELECTCASE ARG
	CASE 1
	;도술刀術
	
	CASE 2
	;팬서
	
	CASE 3
	;궁술
	
	CASE 4
	;검술
	
	CASE 5
	;창술
	
	CASE 6
	;도끼술
	
	CASE 7
	;채찍술
	
	CASE 8
	;투척술
	
	CASE 9
	;제사
	
	CASE 10
	;봉술
	
	CASE 11
	;마샬아츠
	
	CASEELSE
		RETURNF ""
ENDSELECT
SIF ARG:1 == 1
	RETURNF "제 1형"
SIF ARG:1 == 2
	RETURNF "제 2형"
RETURNF ""
