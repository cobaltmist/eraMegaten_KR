
;================================================================
;魔晶杖（通常版）

;魔匠の工房版より、こちらは成長が早く上限が低い。追加スキルは１つまで。
;================================================================
@MAKE_MASHO_STAFF
#DIM DYNAMIC MATERIAL,2
#DIM DYNAMIC STAFF
#DIM DYNAMIC ATTRIBUTE,26
#DIM DYNAMIC MASHO_TOTAL,50
#DIM DYNAMIC MASHO_COUNT
#DIM DYNAMIC LINE
#DIM DYNAMIC BATTLESTATUS_FOCUS,4
#DIM DYNAMIC BATTLESTATUS_HIGH
#DIM DYNAMIC BATTLESTATUS_LOW
#DIM DYNAMIC FOCUS_COUNT
#DIM DYNAMIC PAGE
#DIM DYNAMIC BASE_STATUS,7
#DIM DYNAMIC BAD_STATE_BONUS
#DIM DYNAMIC LEVEL_POINT
#DIM DYNAMIC SKILL_COUNT_MAX
#DIM DYNAMIC SKILL_COUNT,3
#DIM DYNAMIC NON_CHOICE,15
#DIM DYNAMIC USE_CARD,16
#DIM DYNAMIC INPUT_RESULT
#DIMS DYNAMIC LSKILL
#DIM DYNAMIC LCOUNT,3

;USE_CARD 0-14:スキル番号 15:追加スキル１のスキルカードの消費枚数

$START_MAKE

FLAG:합체예정악마1 = -1
MATERIAL = -1
MATERIAL:1 = -1

;キャラリストを表示・選択
CALL INPUT_CHARA_LIST("소체로 쓸 악마를 선택해주십시오", "CASTING_MASHO")
SIF RESULT == 1000
	RETURN 0
MATERIAL = RESULT

STAFF = -1
;空いている合体杖枠を参照
FOR LCOUNT, 0, 50
	IF DC:LCOUNT:49 == 0
		STAFF = LCOUNT
		BREAK
	ENDIF
NEXT
IF STAFF == -1
	PRINTW 이 이상 마정 지팡이를 가질 수 없습니다
	RETURN 0
ENDIF

DO
	LINE = LINECOUNT
	DRAWLINE
	PRINTL 작성할 마정 지팡이의 형태를 선택해주세요
	DRAWLINE
	PRINTL [0] 마법사      （소체의 지혜・마력이 증가、다른 능력이 감소합니다）
	PRINTL [1] 상태이상공격（소체의 지혜・속도가 증가、다른 능력이 감소합니다）
	PRINTL [2] 형태없음    （소체의 기본능력 그대로 작성합니다）
	DRAWLINE
	PRINTL [99]  캔슬
	INPUT
	IF RESULT == 0
		BATTLESTATUS_FOCUS:2 = 3
		BATTLESTATUS_FOCUS:3 = 2
		BREAK
	ELSEIF RESULT == 1
		BATTLESTATUS_FOCUS:1 = 5
		BATTLESTATUS_FOCUS:3 = 2
		BREAK
	ELSEIF RESULT == 2
		BREAK
	ELSEIF RESULT == 99
		CLEARLINE LINECOUNT - LINE
		RESTART
	ENDIF
	CLEARLINE LINECOUNT - LINE
LOOP 1

VARSET MASHO_TOTAL, -1
FOR LCOUNT, 2450, 2500
	IF ITEM:LCOUNT > 0 && DC:(LCOUNT-2450):90 == 0
		SIF STRFIND(EQ_GETNAME_마정장비_ITEM(LCOUNT), "晴明") < 0
				CONTINUE
		MASHO_TOTAL:(LCOUNT:1) = LCOUNT
		LCOUNT:1++
		MASHO_COUNT++
	ENDIF
NEXT

DO
	LINE = LINECOUNT
	DRAWLINE
	PRINTFORML 소지하고 있는 마정 지팡으로 소재로 사용할 수 있습니다
	PRINTFORML 이 경우、소체가 된 마정무기는 소멸하고、스테이터스 보정 증이 계승됩니다
	SETCOLOR COLOR("YELLOW")
	PRINTFORML ※『공격』이나『마법위력』등의 전투능력은 계승되지 않습니다！
	RESETCOLOR
	DRAWLINE
	
	FOR LCOUNT, PAGE*20, PAGE*20+20
		SIF MASHO_TOTAL:LCOUNT < 1
			CONTINUE
		PRINTFORMLC [{MASHO_TOTAL:LCOUNT}] %EQ_GETNAME_마정장비_ITEM(MASHO_TOTAL:LCOUNT),20,LEFT%×{ITEM:(MASHO_TOTAL:LCOUNT),2}  
		RESETCOLOR
	NEXT
	PRINTFORML
	DRAWLINE
	PRINTFORMLC \@(PAGE <= 0) ? %" " * 13% # [7]이전페이지\@
	PRINTLC [0]사용하지 않고 작성한다
	PRINTFORMLC \@(PAGE >= MASHO_COUNT/20) ? %" " * 13% # [9]다음페이지\@
	INPUT
	INPUT_RESULT = RESULT
	IF INPUT_RESULT == 0
		BREAK
	ELSEIF INPUT_RESULT == 7 && PAGE > 0
		PAGE -= 1
	ELSEIF INPUT_RESULT == 9 && PAGE < MASHO_COUNT/20
		PAGE += 1
	ELSEIF MATCH(MASHO_TOTAL, RESULT, 0) > 0
		TRYCCALLFORM 장비설명, INPUT_RESULT
			MATERIAL:1 = INPUT_RESULT - 2450
			IF GET_STATE(DC:(MATERIAL:1):45) != "GOOD"
				PRINTFORML [추가효과] %GET_STATE(DC:(MATERIAL:1):45)% {DC:(MATERIAL:1):47}％ ～ {DC:(MATERIAL:1):48}％
				DRAWLINE
			ENDIF
			PRINTFORML 이 마정 지팡이를 소재로 합니까？
			CALL INPUT_YN()
			IF RESULT == 0
				BREAK
			ELSE
				MATERIAL:1 = -1
			ENDIF
		CATCH
		ENDCATCH
	ELSE
		PRINTW 올바른 값을 입력해주세요
	ENDIF
	CLEARLINE LINECOUNT - LINE
LOOP 1


;合体杖の属性を決める
VARSET ATTRIBUTE, -1
FOR LCOUNT, 1, FLAG:스킬수+1
	LSKILL = 스킬{LCOUNT}
	IF ABL:MATERIAL:LSKILL > 0
		;相性による条件
		TRYCCALLFORM SKILL_TYPE_{ABL:MATERIAL:LSKILL}, MATERIAL
			ATTRIBUTE:RESULT = 1
		CATCH
		ENDCATCH
	ENDIF
NEXT
;魔晶杖を素材にしている場合はそれの属性を追加
SIF MATERIAL:1 > -1
	ATTRIBUTE:(DC:(MATERIAL:1):40) = 1

;打撃以外の物理は無し
ATTRIBUTE:0 = -1
ATTRIBUTE:1 = -1
ATTRIBUTE:2 = 1
ATTRIBUTE:3 = -1


;神経・精神は特殊
;そのスキルで発生する状態異常を取得する。攻撃相性は打撃のまま
ATTRIBUTE:8 = -1
ATTRIBUTE:9 = -1
FOR LCOUNT, 1, FLAG:스킬수+1
	LSKILL = 스킬{LCOUNT}
	IF ABL:MATERIAL:LSKILL > 0
		;相性による条件
		TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ABL:MATERIAL:LSKILL}, MATERIAL
			SELECTCASE GET_STATE(RESULT)
				CASE "POISON"
					ATTRIBUTE:20 = 1
				CASE "SLEEP"
					ATTRIBUTE:21 = 1
				CASE "PANIC"
					ATTRIBUTE:22 = 1
				CASE "HAPPY"
					ATTRIBUTE:23 = 1
				CASE "BIND"
					ATTRIBUTE:24 = 1
				CASE "CHARM"
					ATTRIBUTE:25 = 1
			ENDSELECT
		CATCH
		ENDCATCH
	ENDIF
NEXT
IF MATERIAL:1 > -1 && DC:(MATERIAL:1):45 > 0
	SELECTCASE GET_STATE(DC:(MATERIAL:1):45)
		CASE "POISON"
			ATTRIBUTE:20 = 1
		CASE "SLEEP"
			ATTRIBUTE:21 = 1
		CASE "PANIC"
			ATTRIBUTE:22 = 1
		CASE "HAPPY"
			ATTRIBUTE:23 = 1
		CASE "BIND"
			ATTRIBUTE:24 = 1
		CASE "CHARM"
			ATTRIBUTE:25 = 1
	ENDSELECT
ENDIF

;杖の攻撃相性を選択
DRAWLINE
PRINTL 지팡이의 공격상성을 선택해주십시오
FOR LCOUNT, 0, FLAG:상성수
	SIF ATTRIBUTE:LCOUNT > 0
		PRINTFORM [{LCOUNT,2}] 【%GET_TYPE(LCOUNT)%】
	SIF ATTRIBUTE:LCOUNT > 0 && (LCOUNT == 4 || LCOUNT == 16)
		PRINT & FLAME ２０％
	SIF ATTRIBUTE:LCOUNT > 0 && LCOUNT == 5
		PRINT & FREEZE ２０％
	SIF ATTRIBUTE:LCOUNT > 0 && LCOUNT == 6
		PRINT & SHOCK ２０％
	SIF ATTRIBUTE:LCOUNT > 0
		PRINTL
NEXT
SIF ATTRIBUTE:20 > 0
	PRINTL [20] 【타격】& POISON ２０％
SIF ATTRIBUTE:21 > 0
	PRINTL [21] 【타격】& SLEEP ２５％
SIF ATTRIBUTE:22 > 0
	PRINTL [22] 【타격】& PANIC ２５％
SIF ATTRIBUTE:23 > 0
	PRINTL [23] 【타격】& HAPPY ３５％
SIF ATTRIBUTE:24 > 0
	PRINTL [24] 【타격】& BIND ２５％
SIF ATTRIBUTE:25 > 0
	PRINTL [25] 【타격】& CHARM ２０％

DRAWLINE
PRINTL [100] キャンセル

$INPUT_TYPE
INPUT

IF RESULT == 100
	RETURN 0
ELSEIF ATTRIBUTE:RESULT > 0
	;DCにデータを入力
	;まずは名称と相性、追加効果を決めておく
	;40:相性。45:추가효과。46：追加効果相性。47:추가효과基本付与確率。48:추가효과付与確率上限。49:名称。
	;名称はStr.csvを参照
	SELECTCASE RESULT
		CASE 2
			DC:STAFF:40 = RESULT
			DC:STAFF:49 = 20
		CASE 4, 16
			DC:STAFF:40 = RESULT
			DC:STAFF:49 = 21
			DC:STAFF:45 = GET_STATE_NUM("FLAME")
			DC:STAFF:46 = RESULT
			DC:STAFF:47 = 20
			DC:STAFF:48 = 95
		CASE 5
			DC:STAFF:40 = RESULT
			DC:STAFF:49 = 22
			DC:STAFF:45 = GET_STATE_NUM("FREEZE")
			DC:STAFF:46 = 5
			DC:STAFF:47 = 20
			DC:STAFF:48 = 95
		CASE 6
			DC:STAFF:40 = RESULT
			DC:STAFF:49 = 23
			DC:STAFF:45 = GET_STATE_NUM("SHOCK")
			DC:STAFF:46 = 6
			DC:STAFF:47 = 20
			DC:STAFF:48 = 95
		CASE 7
			DC:STAFF:40 = RESULT
			DC:STAFF:49 = 24
		CASE 10, 11
			DC:STAFF:40 = RESULT
			DC:STAFF:49 = RESULT + 19
		CASE 12 TO 15
			DC:STAFF:40 = RESULT
			DC:STAFF:49 = RESULT + 13
		CASE 20
			DC:STAFF:40 = 2
			DC:STAFF:49 = 31
			DC:STAFF:45 = GET_STATE_NUM("POISON")
			DC:STAFF:46 = 8
			DC:STAFF:47 = 20
			DC:STAFF:48 = 95
		CASE 21
			DC:STAFF:40 = 2
			DC:STAFF:49 = 32
			DC:STAFF:45 = GET_STATE_NUM("SLEEP")
			DC:STAFF:46 = 8
			DC:STAFF:47 = 25
			DC:STAFF:48 = 95
		CASE 22
			DC:STAFF:40 = 2
			DC:STAFF:49 = 33
			DC:STAFF:45 = GET_STATE_NUM("PANIC")
			DC:STAFF:46 = 9
			DC:STAFF:47 = 25
			DC:STAFF:48 = 95
		CASE 23
			DC:STAFF:40 = 2
			DC:STAFF:49 = 34
			DC:STAFF:45 = GET_STATE_NUM("HAPPY")
			DC:STAFF:46 = 9
			DC:STAFF:47 = 35
			DC:STAFF:48 = 95
		CASE 24
			DC:STAFF:40 = 2
			DC:STAFF:49 = 35
			DC:STAFF:45 = GET_STATE_NUM("BIND")
			DC:STAFF:46 = 8
			DC:STAFF:47 = 25
			DC:STAFF:48 = 95
		CASE 25
			DC:STAFF:40 = 2
			DC:STAFF:49 = 36
			DC:STAFF:45 = GET_STATE_NUM("CHARM")
			DC:STAFF:46 = 9
			DC:STAFF:47 = 20
			DC:STAFF:48 = 95
		CASE 17
			DC:STAFF:40 = RESULT
			DC:STAFF:49 = 37
	ENDSELECT
	
	;ステータス再割り振り。魔晶杖用にステ振りしていない素体の為の処理なので、極振り素体は恩恵をほぼ受けられないようにする
	;  66%だと極振り素体がちょっと強くなるので65%まで
	FOR LCOUNT, 1, 7
		BASE_STATUS:LCOUNT = BASE:MATERIAL:(GETNUM(BASE, "LV") + LCOUNT)
		SELECTCASE LCOUNT
			CASE BATTLESTATUS_FOCUS:0, BATTLESTATUS_FOCUS:1, BATTLESTATUS_FOCUS:2, BATTLESTATUS_FOCUS:3
			CASEELSE
				BASE_STATUS:0 += BASE:MATERIAL:(GETNUM(BASE, "LV") + LCOUNT) * 65 / 100
		ENDSELECT
	NEXT
	
	IF SUMARRAY(BATTLESTATUS_FOCUS) > 0
		FOR LCOUNT, 0, 4
			SIF BATTLESTATUS_FOCUS:LCOUNT > 0
				FOCUS_COUNT++
		NEXT
		
		;割り振られる数値には上限を設定する。でないと素体の基本能力が全く違うのに重視した能力がほぼ同等になる事があるので
		IF BASE_STATUS:0 > FOCUS_COUNT && FOCUS_COUNT > 0
			FOR LCOUNT, 1, 7
				SELECTCASE LCOUNT
					CASE BATTLESTATUS_FOCUS:0, BATTLESTATUS_FOCUS:1, BATTLESTATUS_FOCUS:2, BATTLESTATUS_FOCUS:3
						BASE_STATUS:LCOUNT += MIN(BASE_STATUS:0 / FOCUS_COUNT, BASE:MATERIAL:(GETNUM(BASE, "LV") + LCOUNT) * 2 / FOCUS_COUNT)
					CASEELSE
						BASE_STATUS:LCOUNT -= BASE_STATUS:LCOUNT * 65 / 100
				ENDSELECT
			NEXT
		ENDIF
	ENDIF
	
	;デバッグ用
;	FOR LCOUNT, 0, 7
;		printforml %GET_BASESTATUS(LCOUNT),8,LEFT%:{BASE_STATUS:LCOUNT}
;	NEXT
;	FOR LCOUNT, 0, 4
;		PRINTFORML BATTLESTATUS_FOCUS:{LCOUNT}：{BATTLESTATUS_FOCUS:LCOUNT}
;	NEXT
	
	;基本能力値修正
	;0:レベル。1:힘。2:지혜。3:마력。4:인내력。5:속도。6:운。
	DC:STAFF:0 = (ABL:MATERIAL:순종 + ABL:MATERIAL:욕망 + ABL:MATERIAL:기교 + 3) / 6 
	DC:STAFF:1 = MIN(ABL:MATERIAL:욕망 + 1, 10)/2
	DC:STAFF:2 = MIN(ABL:MATERIAL:기교 + 1, 10)/2
	DC:STAFF:3 = MIN(ABL:MATERIAL:욕망 + 1, 10)/2
	DC:STAFF:4 = MIN(ABL:MATERIAL:순종 + 1, 10)/2
	DC:STAFF:5 = MIN(ABL:MATERIAL:기교 + 1, 10)/2
	DC:STAFF:6 = MIN(ABL:MATERIAL:순종 + 1, 10)/2
	
	;魔晶杖を素材にした場合のボーナス
	;レベル補正が戦闘能力に影響するのでここで入力。戦闘能力以外の能力の継承が主な目的なので戦闘能力の継承はなし
	IF MATERIAL:1 > -1
		FOR LCOUNT, 0, FLAG:기본능력수
			BATTLESTATUS_HIGH = MAX(DC:STAFF:LCOUNT, DC:(MATERIAL:1):LCOUNT)
			BATTLESTATUS_LOW = MIN(DC:STAFF:LCOUNT, DC:(MATERIAL:1):LCOUNT)
			IF BATTLESTATUS_LOW > BATTLESTATUS_HIGH/2
				DC:STAFF:LCOUNT = MIN(BATTLESTATUS_HIGH + (BATTLESTATUS_LOW - BATTLESTATUS_HIGH/2), 5)
			ELSE
				DC:STAFF:LCOUNT = BATTLESTATUS_HIGH
			ENDIF
		NEXT
		
		FOR LCOUNT, 0, 2
			SIF DC:(MATERIAL:1):(53+LCOUNT) > 0
				SKILL_COUNT++
		NEXT
		
		DC:STAFF:43 = MAX(ABL:MATERIAL:사거리, DC:(MATERIAL:1):43)
		DC:STAFF:44 = MAX(ABL:MATERIAL:공격범위, DC:(MATERIAL:1):44)
	ELSE
		DC:STAFF:43 = ABL:MATERIAL:사거리
		DC:STAFF:44 = ABL:MATERIAL:공격범위
	ENDIF
	
	;戦闘能力修正
	;10:공격。11:명중。14:마법위력。15:마법효과。
	DC:STAFF:10 = (MIN(BASE:MATERIAL:LV +5, BASE:MASTER:LV + FLAG:경험ED수, 80) + DC:STAFF:0*2)*3/2 + BASE_STATUS:1*3/2 + BASE_STATUS:4/2 + (DC:STAFF:0*2)
	DC:STAFF:11 = (MIN(BASE:MATERIAL:LV +5, BASE:MASTER:LV + FLAG:경험ED수, 80) + DC:STAFF:0*2) + BASE_STATUS:5*3/2 + BASE_STATUS:6/2 + (DC:STAFF:0*2)
	DC:STAFF:14 = (MIN(BASE:MATERIAL:LV +5, BASE:MASTER:LV + FLAG:경험ED수, 80) + DC:STAFF:0*2)*3/2 + BASE_STATUS:3*3/2 + BASE_STATUS:2/2 + (DC:STAFF:0*2)
	DC:STAFF:15 = (MIN(BASE:MATERIAL:LV +5, BASE:MASTER:LV + FLAG:경험ED수, 80) + DC:STAFF:0*2) + BASE_STATUS:2 + BASE_STATUS:6/2 + BASE_STATUS:3/2
	
	;杖仕様に変更
	LEVEL_POINT = (BASE:MATERIAL:LV * 3 / 2)
	
	TIMES DC:STAFF:10, 0.65
	DC:STAFF:11 = DC:STAFF:11 * LIMIT(BASE_STATUS:5 * 100 / (LEVEL_POINT+3) * 3, 65, 130) / 100
	TIMES DC:STAFF:14, 1.20
	DC:STAFF:15 = DC:STAFF:15 * LIMIT(BASE_STATUS:2 * 100 / (LEVEL_POINT+3) * 2, 40, 80) / 100
	
	;41:최저공격횟수。42:최대공격횟수
	;攻撃形態は素体のものをコピー。あと魔法系の武器なので攻撃回数は1固定
	DC:STAFF:41 = 1
	DC:STAFF:42 = 1
	
	;状態異常に関するボーナス。『範囲による修正』で使用
	CALL 상성소질체크(MATERIAL, DC:STAFF:46, 1, 1)
	SIF RESULT == 1
		BAD_STATE_BONUS = 1
	SIF BASE:MATERIAL:충성도 >= 10000
		BAD_STATE_BONUS += 1
	
	;範囲による修正。
	;1:単体 2:列 3:全体
	SELECTCASE DC:STAFF:44
		CASE 1
			SIF DC:STAFF:47 > 0 && BAD_STATE_BONUS > 0
				DC:STAFF:47 += 10 + BAD_STATE_BONUS * 5
		CASE 2
			TIMES DC:STAFF:10, 0.90
			
			SIF DC:STAFF:47 > 0 && BAD_STATE_BONUS > 0
				DC:STAFF:47 += 5 + BAD_STATE_BONUS * 5
		CASE 3
			TIMES DC:STAFF:10, 0.60
			
			SIF DC:STAFF:47 > 0 && BAD_STATE_BONUS > 0
				DC:STAFF:47 += BAD_STATE_BONUS * 5
		CASEELSE
	ENDSELECT
	
	;素材の魔晶杖と範囲、相性、追加効果が同じで、素材の確率の方が高いのなら確率を引き継げる。
	SIF MATERIAL:1 > -1 && (DC:STAFF:44 <= DC:(MATERIAL:1):44 && DC:STAFF:40 == DC:(MATERIAL:1):40 && DC:STAFF:45 == DC:(MATERIAL:1):45 && DC:STAFF:47 < DC:(MATERIAL:1):47)
		DC:STAFF:47 = DC:(MATERIAL:1):47
	
	DC:STAFF:39 = GET_EQUIPNUM("검")
	
	;追加効果付きの杖は修正を加える
	SELECTCASE DC:STAFF:49
		;氷結、電撃
		CASE 22, 23
			TIMES DC:STAFF:10 , 0.95
			TIMES DC:STAFF:11 , 0.95
		;その他の追加効果付き
		CASE 21, 31 TO 36
			TIMES DC:STAFF:10 , 0.90
			TIMES DC:STAFF:11 , 0.90
	ENDSELECT
	
	;防御相性
	FOR LCOUNT, 0, FLAG:상성수
		DC:STAFF:(20+LCOUNT) = 100
	NEXT
	;万能は攻撃が75%になる
	SIF DC:STAFF:40 == 17
		TIMES DC:STAFF:10 , 0.75
	
	SKILL_COUNT_MAX = CHARA_SKILLCOUNT(MATERIAL)
	
	;追加スキル設定。長くなったので範囲による修正と分けた
	IF DC:STAFF:44 == 1
		DRAWLINE
		PRINTFORML 추가스킬1의 부여가 가능합니다
		PRINTFORML 스킬을 부여하면、해당 스킬카드를 소비하고『전투능력』과『상태이상의 확률』이 저하됩니다
		PRINTFORML 또한、소체에 따라 스킬이 변화하는 경우가 있습니다。유의해주십시오
		DRAWLINE
		FOR LCOUNT, 0, SKILL_COUNT_MAX
			LSKILL = 스킬{LCOUNT+1}
			
			IF ABL:MATERIAL:LSKILL
				USE_CARD:LCOUNT = 1
				CALLFORM SKILL_SUCCESSION_TYPE_{ABL:MATERIAL:LSKILL}
				CALLFORM 상성소질체크(MATERIAL, RESULT, 0, 1)
				SIF RESULT != 1
					USE_CARD:LCOUNT += 1
				CALLFORM SKILL_RANK_{ABL:MATERIAL:LSKILL}
				USE_CARD:LCOUNT += RESULT / 5
				SIF BASE:MATERIAL:충성도 < 10000
					USE_CARD:LCOUNT += 1

				SIF USE_CARD:LCOUNT > ITEM:(ABL:MATERIAL:LSKILL+10000)
					NON_CHOICE:LCOUNT = 2
					
				TRYCCALLFORM 계승불능_{ABL:MATERIAL:LSKILL}
					SIF RESULT == 1
						NON_CHOICE:LCOUNT = 1
				CATCH
				ENDCATCH
				
				;ランク0はスキルカード作成ができないのでそれに合わせる
				CALLFORM SKILL_RANK_{ABL:MATERIAL:LSKILL}
				SIF RESULT == 0
					NON_CHOICE:LCOUNT = 1
				
				SIF NON_CHOICE:LCOUNT > 0
					SETCOLOR COLOR("GRAY")
				CALLFORM SKILL_NAME_{ABL:MATERIAL:LSKILL}, -1
				PRINTFORM [{LCOUNT + 1,2}]%RESULTS,20,LEFT%({USE_CARD:LCOUNT, 2, RIGHT}/{ITEM:(ABL:MATERIAL:LSKILL+10000), 2, RIGHT}枚)
				RESETCOLOR
				
				SKILL_COUNT:1++
			ENDIF
			SIF SKILL_COUNT:1 % 3 == 0 || LCOUNT + 1 == SKILL_COUNT_MAX
				PRINTL
		NEXT
		
		IF SKILL_COUNT > 0
			FOR LCOUNT, 0, SKILL_COUNT
				IF DC:(MATERIAL:1):53 > 0
					CALLFORM SKILL_NAME_{DC:(MATERIAL:1):(53 + LCOUNT)}, -1
					SIF NON_CHOICE:(LCOUNT + SKILL_COUNT_MAX) > 0
						SETCOLOR COLOR("GRAY")
					PRINTFORM [{LCOUNT + SKILL_COUNT_MAX + 1,2}]%RESULTS,20,LEFT%({USE_CARD:(LCOUNT + SKILL_COUNT_MAX + 1), 2, RIGHT}/{ITEM:(DC:(MATERIAL:1):53 + 10000), 2, RIGHT}枚)
					RESETCOLOR
				ENDIF
			NEXT
			PRINTL
		ENDIF
		DRAWLINE
		PRINTFORML [100]추가스킬을 설정하지 않는다
		
		$INPUT_SKILL
		INPUT
			
		INPUT_RESULT = RESULT-1
		IF INPUT_RESULT == 99
		ELSEIF INPUT_RESULT < 0 || SKILL_COUNT_MAX + 3 < INPUT_RESULT
			CLEARLINE 1
			GOTO INPUT_SKILL
		ELSEIF NON_CHOICE:INPUT_RESULT == 1
			PRINTW 이 스킬은 설정할 수 없습니다
			CLEARLINE 2
			GOTO INPUT_SKILL
		ELSEIF NON_CHOICE:INPUT_RESULT == 2
			PRINTW 스킬카드가 부족합니다
			CLEARLINE 2
			GOTO INPUT_SKILL
		ELSE
			IF INPUT_RESULT >= SKILL_COUNT_MAX
				IF MATERIAL:1 < 0 || DC:(MATERIAL:1):(53 + INPUT_RESULT - SKILL_COUNT_MAX) < 1
					CLEARLINE 1
					GOTO INPUT_SKILL
				ELSE
					DC:STAFF:53 = DC:(MATERIAL:1):(53 + INPUT_RESULT - SKILL_COUNT_MAX)
				ENDIF
			ELSE
				LSKILL = 스킬{INPUT_RESULT+1}
				IF ABL:MATERIAL:LSKILL < 1
					CLEARLINE 1
					GOTO INPUT_SKILL
				ELSE
					DC:STAFF:53 = ABL:MATERIAL:LSKILL
					USE_CARD:15 = USE_CARD:INPUT_RESULT
				ENDIF
			ENDIF
			SKILL_COUNT:2++
		ENDIF
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_TYPE
ENDIF

;追加スキルによる戦闘能力と状態異常確率の減少
;スキル１個につき戦闘能力６．５％　状態異常確率５％減少
IF SKILL_COUNT:2 > 0
	FOR LCOUNT, 10, 16
		SIF LCOUNT == 12 || LCOUNT == 13
			CONTINUE
		DC:STAFF:LCOUNT = DC:STAFF:LCOUNT * (1000 - SKILL_COUNT:2 * 130 / 2) / 1000
	NEXT
	SIF DC:STAFF:47 > 0
		DC:STAFF:47 -= BAD_STATE_BONUS * 5
ENDIF

;合体結果の杖を表示
CALL 장비설명, (2450+STAFF+450*DC:STAFF:39)

;状態異常とその確率を表示
IF GET_STATE(DC:STAFF:45) != "GOOD"
	PRINTFORML [추가효과] %GET_STATE(DC:STAFF:45)% {DC:STAFF:47}％ ～ {DC:STAFF:48}％
	DRAWLINE
ENDIF

;合体するかどうか → 合体実行（実際には既に終わっている）
IF BASE:MATERIAL:LV > BASE:MASTER:LV + FLAG:경험ED수
	PRINTL 당신의 레벨이 소체보다 낮기 때문에、성능이 약간 저하되어 있습니다
	PRINT 그래도、
ENDIF
PRINT 이 지팡이를 제작합니까？ 

CALL INPUT_YN, "Yes", "No", 2

SELECTCASE RESULT
	CASE 0
		ITEM:(2450+STAFF+450*DC:STAFF:39) = 1
	CASE 1
		FOR LCOUNT, 0, 99
			DC:STAFF:LCOUNT = 0
		NEXT
		RETURN 0
ENDSELECT

PRINTFORMW %STR:(DC:STAFF:49+100)%를 손에 넣었다

CALL FUSION_LETTER, MATERIAL, 1

;合体素材の悪魔を消去
CALL 캐릭터삭제, MATERIAL

ITEM:무명의도 -= 1
SIF SKILL_COUNT:2 > 0
	ITEM:(DC:STAFF:53 + 10000) = ITEM:(DC:STAFF:53 + 10000) - USE_CARD:15
IF MATERIAL:1 > -1
	ITEM:(2450+(MATERIAL:1)+450*DC:STAFF:39) -= 1
	FOR LCOUNT, 0, 99
		DC:(MATERIAL:1):LCOUNT = 0
	NEXT
ENDIF

RETURN 0

GOTO START_MAKE

;素材化等の共通の処理は剣のものを使用する
