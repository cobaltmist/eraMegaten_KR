;================================================================
;마정장비２　別仕様の마정장비。마정による강화が가능
;================================================================
@MAKE_MASHO2
#DIM LCOUNT
FLAG:합체예정악마1 = -1
;LOCAL ループ用
;LOCAL:1 소재の등록번호
LOCAL:1 = -1
;LOCAL:2 悪魔の表示用変数
;LOCAL:3 合体結果の剣枠
;LOCAL:4 作成する魔晶装備の種類
;G 合体剣のフラグを管理する変数 20-25:バッドステータス村正用

$START_MAKE
LOCAL:6 = 0

;キャラリストを표시・選択
CALL INPUT_CHARA_LIST("소체로 쓸 악마를 선택해주십시오","CASTING_MASHO2")
SIF RESULT == 1000
	JUMP SELECT_MASHO2
LOCAL:1 = RESULT


LOCAL:3 = -1
;空いている合体검枠を参照
FOR LOCAL,0,50
	IF DC:LOCAL:49 == 0
		LOCAL:3 = LOCAL
		BREAK
	ENDIF
NEXT
IF LOCAL:3 == -1
	PRINTW 더 이상의 합체검을 가질 수 없습니다
	RETURN 0
ENDIF

;合体검の종류を決める
;습득스킬によってGを설정
VARSET G,-1
G:0 = 1
FOR LOCAL,1,FLAG:스킬수+1
	LOCALS = 스킬{LOCAL}
	IF ABL:(LOCAL:1):LOCALS > 0
		RESULT = 0
		;相性による조건
		TRYCALLFORM SKILL_TYPE_{ABL:(LOCAL:1):LOCALS},LOCAL:1
		G:RESULT = 1
	ENDIF
NEXT
;검격以外の물리は無し
G:1 = -1
G:2 = -1
G:3 = -1


;신경・정신は特殊
G:8 = -1
G:9 = -1
FOR LOCAL,1,FLAG:스킬수+1
	LOCALS = 스킬{LOCAL}
	IF ABL:(LOCAL:1):LOCALS > 0
		RESULT = 0
		;相性による조건
		TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ABL:(LOCAL:1):LOCALS},LOCAL:1
			SELECTCASE GET_STATE(RESULT)
				CASE "POISON"
					G:20 = 1
				CASE "SLEEP"
					G:21 = 1
				CASE "PANIC"
					G:22 = 1
				CASE "HAPPY"
					G:23 = 1
				CASE "BIND"
					G:24 = 1
				CASE "CHARM"
					G:25 = 1
			ENDSELECT
		CATCH
		ENDCATCH
	ENDIF
NEXT

;검の타입を選択
$PRINT_TYPE
DRAWLINE
PRINTL 검의 공격상성을 선택해주세요
FOR LOCAL,0,FLAG:상성수
	SIF G:LOCAL > 0
		PRINTFORM [{LOCAL}] 【%GET_TYPE(LOCAL)%】
	SIF G:LOCAL > 0 && LOCAL == 5
		PRINT & FREEZE ２０％
	SIF G:LOCAL > 0 && LOCAL == 6
		PRINT & SHOCK ２０％
	SIF G:LOCAL > 0
		PRINTL
NEXT
SIF G:20 > 0
	PRINTL [20] 【검격】& POISON ２０％
SIF G:21 > 0
	PRINTL [21] 【검격】& SLEEP ２５％
SIF G:22 > 0
	PRINTL [22] 【검격】& PANIC ２５％
SIF G:23 > 0
	PRINTL [23] 【검격】& HAPPY ３５％
SIF G:24 > 0
	PRINTL [24] 【검격】& BIND ２５％
SIF G:25 > 0
	PRINTL [25] 【검격】& CHARM ２０％
DRAWLINE
PRINTL [100] 취소

$INPUT_TYPE
INPUT

IF RESULT == 100
	RESTART
ELSEIF G:RESULT > 0
	;DCに데이터を입력
	;まずは명칭と相性、추가효과を決めておく
	SELECTCASE RESULT
		CASE 0
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 1
		CASE 4,16
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 2
		CASE 5
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 3
			DC:(LOCAL:3):45 = GET_STATE_NUM("FREEZE")
			DC:(LOCAL:3):46 = 5
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 6
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 4
			DC:(LOCAL:3):45 = GET_STATE_NUM("SHOCK")
			DC:(LOCAL:3):46 = 6
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 7
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 5
		CASE 10,11
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = RESULT
		CASE 12 TO 15
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = RESULT - 6
		CASE 20
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 12
			DC:(LOCAL:3):45 = GET_STATE_NUM("POISON")
			DC:(LOCAL:3):46 = 8
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 21
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 13
			DC:(LOCAL:3):45 = GET_STATE_NUM("SLEEP")
			DC:(LOCAL:3):46 = 8
			DC:(LOCAL:3):47 = 25
			DC:(LOCAL:3):48 = 95
		CASE 22
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 14
			DC:(LOCAL:3):45 = GET_STATE_NUM("PANIC")
			DC:(LOCAL:3):46 = 9
			DC:(LOCAL:3):47 = 25
			DC:(LOCAL:3):48 = 95
		CASE 23
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 15
			DC:(LOCAL:3):45 = GET_STATE_NUM("HAPPY")
			DC:(LOCAL:3):46 = 9
			DC:(LOCAL:3):47 = 35
			DC:(LOCAL:3):48 = 95
		CASE 24
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 16
			DC:(LOCAL:3):45 = GET_STATE_NUM("BIND")
			DC:(LOCAL:3):46 = 8
			DC:(LOCAL:3):47 = 25
			DC:(LOCAL:3):48 = 95
		CASE 25
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 17
			DC:(LOCAL:3):45 = GET_STATE_NUM("CHARM")
			DC:(LOCAL:3):46 = 9
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 17
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 18
	ENDSELECT

	;사양타입を１に。引き継ぎ가능
	DC:(LOCAL:3):90 = 1

	;장비가능레벨を설정、함락資質で緩和
	DC:(LOCAL:3):50 = BASE:(LOCAL:1):LV

	SIF 함락(LOCAL:1) == 1
		DC:(LOCAL:3):50 -= 20
	SIF 함락(LOCAL:1) == 2
		DC:(LOCAL:3):50 -= 30
	SIF 함락(LOCAL:1) == 3
		DC:(LOCAL:3):50 = MIN(DC:(LOCAL:3):50/2, DC:(LOCAL:3):50 - 30)

	;かなりの長期間、連れ添っていると장비가능레벨が完全に免除される
	SIF 함락(LOCAL:1) == 3 && BASE:(LOCAL:1):충성도 >= 50000
		DC:(LOCAL:3):50 = 1

	DC:(LOCAL:3):50 = MAX(DC:(LOCAL:3):50, 1)

	;기본능력値수정、전투능력値수정を決定
	;기본
	DC:(LOCAL:3):0 += (MIN(ABL:(LOCAL:1):순종 , 10) + MIN(ABL:(LOCAL:1):욕망 , 10) + MIN(ABL:(LOCAL:1):기교 , 10) +3)/6
	DC:(LOCAL:3):1 += (MIN(ABL:(LOCAL:1):욕망 , 10)+1)/5
	DC:(LOCAL:3):3 += (MIN(ABL:(LOCAL:1):욕망 , 10)+1)/5
	DC:(LOCAL:3):6 += (MIN(ABL:(LOCAL:1):순종 , 10)+1)/5
	DC:(LOCAL:3):4 += (MIN(ABL:(LOCAL:1):순종 , 10)+1)/5
	DC:(LOCAL:3):2 += (MIN(ABL:(LOCAL:1):기교 , 10)+1)/5
	DC:(LOCAL:3):5 += (MIN(ABL:(LOCAL:1):기교 , 10)+1)/5

	DC:(LOCAL:3):1 += MAX(BASE:(LOCAL:1):힘/20 - 2, 0)
	DC:(LOCAL:3):3 += MAX(BASE:(LOCAL:1):마력/20 - 2, 0)
	DC:(LOCAL:3):6 += MAX(BASE:(LOCAL:1):운/20 - 2, 0)
	DC:(LOCAL:3):4 += MAX(BASE:(LOCAL:1):인내력/20 - 2, 0)
	DC:(LOCAL:3):2 += MAX(BASE:(LOCAL:1):지혜/20 - 2, 0)
	DC:(LOCAL:3):5 += MAX(BASE:(LOCAL:1):속도/20 - 2, 0)

	;전투
	DC:(LOCAL:3):10 = (MIN(BASE:(LOCAL:1):LV,255)+5+DC:(LOCAL:3):0*2)*3/2 + BASE:(LOCAL:1):힘*3/2 + BASE:(LOCAL:1):인내력 / 2
	DC:(LOCAL:3):11 = (MIN(BASE:(LOCAL:1):LV,255)+5+DC:(LOCAL:3):0*2)*6/5 + BASE:(LOCAL:1):속도*3/2 + BASE:(LOCAL:1):운/2
	DC:(LOCAL:3):14 = (MIN(BASE:(LOCAL:1):LV,255)+5+DC:(LOCAL:3):0*2)*3/2 + BASE:(LOCAL:1):마력*3/2 + BASE:(LOCAL:1):지혜 / 2
	DC:(LOCAL:3):15 = (MIN(BASE:(LOCAL:1):LV,255)+5+DC:(LOCAL:3):0*2)*6/5 + BASE:(LOCAL:1):지혜 + BASE:(LOCAL:1):운/2 + BASE:(LOCAL:1):마력/2

	;공격形態は소재のものをコピー
;	DC:(LOCAL:3):41 = ABL:(LOCAL:1):최저공격횟수
	DC:(LOCAL:3):41 = 1
;	DC:(LOCAL:3):42 = ABL:(LOCAL:1):최대공격횟수
	DC:(LOCAL:3):42 = 1
	DC:(LOCAL:3):43 = ABL:(LOCAL:1):사거리
	DC:(LOCAL:3):44 = ABL:(LOCAL:1):공격범위

	; 운特化で최대２回공격
	SIF BASE:(LOCAL:1):운 >= 100
		DC:(LOCAL:3):42 = 2

	; 인내력特化で防除を上昇（운に比べて弱いのは仕方なし）
	SIF BASE:(LOCAL:1):인내력 >= 100
		DC:(LOCAL:3):12 = BASE:(LOCAL:1):인내력*2/3+1

	; 힘一定이상で、데미지ディー라用の무기と見做す
	SIF DC:(LOCAL:3):44 == 1 && BASE:(LOCAL:1):힘 >= 50
		DC:(LOCAL:3):10 += BASE:(LOCAL:1):힘
	SIF DC:(LOCAL:3):44 == 2 && BASE:(LOCAL:1):힘 >= 50
		DC:(LOCAL:3):10 += BASE:(LOCAL:1):힘/2

	; 속도は申し訳程度の강화で
	SIF BASE:(LOCAL:1):속도 >= 100
		DC:(LOCAL:3):11 += 20

	;1명は명중を강화
	SIF DC:(LOCAL:3):44 == 1
		TIMES DC:(LOCAL:3):11 , 1.10

	; 전체は火힘弱体化
	SIF DC:(LOCAL:3):44 == 3
		TIMES DC:(LOCAL:3):10 , 0.80

	; 명중での調整案も置いておく。無조건での割合減少は拙いので、MINで上限掛けるとか
	;SIF DC:(LOCAL:3):44 == 3
	;	DC:(LOCAL:3):11 -= MIN(DC:(LOCAL:3):10/8, 30)

	;검仕様に변경 独自仕様として、性能を반감
	TIMES DC:(LOCAL:3):10 , 0.50
	TIMES DC:(LOCAL:3):11 , 0.50
	DC:(LOCAL:3):11 += 5
	TIMES DC:(LOCAL:3):14 , 0.40
	TIMES DC:(LOCAL:3):15 , 0.20

	;;사거리・공격범위によって명중と공격に수정が入る
	;SELECTCASE DC:(LOCAL:3):43
	;	CASE 2
	;		TIMES DC:(LOCAL:3):11 , 0.8
	;	CASE 3
	;		TIMES DC:(LOCAL:3):11 , 0.6
	;ENDSELECT
	;SELECTCASE DC:(LOCAL:3):44
	;	CASE 2
	;		TIMES DC:(LOCAL:3):10 , 0.7
	;		TIMES DC:(LOCAL:3):11 , 0.8
	;	CASE 3
	;		TIMES DC:(LOCAL:3):10 , 0.4
	;		TIMES DC:(LOCAL:3):11 , 0.5
	;ENDSELECT
	
	DC:(LOCAL:3):39 = GET_EQUIPNUM("검")

	;추가효과付きの검は수정を加える
	SELECTCASE DC:(LOCAL:3):49
		CASE 3,4
			TIMES DC:(LOCAL:3):10 , 0.85
			TIMES DC:(LOCAL:3):11 , 0.85
		CASE 12 TO 18
			TIMES DC:(LOCAL:3):10 , 0.80
			TIMES DC:(LOCAL:3):11 , 0.80
	ENDSELECT
	;방어상성
	FOR LOCAL,0,FLAG:상성수
		DC:(LOCAL:3):(20+LOCAL) = 100
	NEXT

	;おそらくは만능をさらに弱体化させるための記述
	;ミスで매료が下がっていたが、ここでは삭제
	;SIF DC:(LOCAL:3):40 == 18
	;	TIMES DC:(LOCAL:3):10 , 0.75
	
ELSE
	CLEARLINE 1
	GOTO INPUT_TYPE
ENDIF

;명칭설정
CALL NAME_MASHO2,LOCAL:1,LOCAL:3

;合体결과の검を표시
CALL 장비설명,(2450+LOCAL:3+450*DC:(LOCAL:3):39)
CALL 장비조건설명_마정장비,LOCAL:3
PRINTL 

;合体するかどうか　→　合体実行（実際には既に終わっている）
PRINT 이 검을 만듭니까？ 

CALL INPUT_YN,"Yes","No",2

SELECTCASE RESULT
	CASE 0
		ITEM:(2450+LOCAL:3+450*DC:(LOCAL:3):39) = 1
	CASE 1
		FOR LCOUNT, 0, 50
			DC:(LOCAL:3):LCOUNT = 0
		NEXT
		RESTART
ENDSELECT


PRINTFORMW %EQ_GETNAME_마정장비(LOCAL:3)%를 입수했다

CALL FUSION_LETTER,LOCAL:1,1

;合体소재の악마を소거
CALL 캐릭터삭제, LOCAL:1

ITEM:무명의도 -= 1

RETURN 0

GOTO START_MAKE


@SELECT_MASHO2
IF DAY > 50
	PRINTL 인스톨 소프트 『마장의 공방』의 사용기간인 50일이 지났습니다.
	PRINTL 현재 이 기능을 사용할 수 없습니다.
	PRINTL 일반 마정무기를 작성하는 경우에는 소프트를 off하고 작성해주세요. 확장모드를 종료합니다……
	PRINTL 
	RETURN 0
ENDIF

PRINTL 인스톨 소프트 『마장의 공방』을 확인. 사용기간임을 확인.
PRINTL 확장모드를 시작합니다.
PRINTL
PRINTL [ 1] 마정검 작성
;PRINTL [ 2]마정杖作成（未実装）
;PRINTL [ 5]合体검作成（未実装）
PRINTL [10] 마정검 강화
;PRINTL []마정장비の命名
PRINTL [99] 해설
PRINTL [ 0] 돌아간다

$INPUT_LOOP
INPUT
IF RESULT == 1
	IF ITEM:무명의도 >= 1
		CALL MAKE_MASHO2
	ELSE
		PRINTW 작성할 때는 무명의 도가 필요합니다.
		PRINTL 
		RETURN 0
	ENDIF
ELSEIF RESULT == 10
	CALL PLUS_MASHO2
ELSEIF RESULT == 99
	CALL KAISETU_MASHO2
ELSEIF RESULT == 0
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF


@PLUS_MASHO2
;[10]마정검강화
;LOCAL:3 마정무기用の데이터
;LOCAL:4 소비される仲魔
;LOCAL:5 소비마카

;生贄
CALL INPUT_CHARA_LIST("소재로 할 악마를 선택해주십시오","CASTING_MASHO2_2")

; 디버그용の抜け道を作っておく
SIF FLAG:DEBUG == 0 && RESULT == 1000
	JUMP SELECT_MASHO2
SIF FLAG:DEBUG == 1 && RESULT == 1000
	RESULT = 0

LOCAL:4 = RESULT

PRINTFORML 어떤 장비를 강화하겠습니까？　＜page.{P + 1}＞
DRAWLINE
;持っている아이템を표시
VARSET G,-1
FOR LOCAL,2000,VARSIZE("ITEM")
	IF 마정장비(LOCAL)
		IF (ITEM:LOCAL > 0) && (DC:(LOCAL-2450-450*((LOCAL-2000)/500)):90 == 1)
			PRINTFORMLC [{LOCAL}]%EQ_GETNAME_마정장비(LOCAL-2450-450*((LOCAL-2000)/500))%

			G:LOCAL = 1
		ENDIF
	ELSE
	ENDIF
NEXT

$PRINT_LIST
DRAWLINE
PRINTL [1000] 돌아간다
$INPUT_LOOP_1
INPUT
IF RESULT == 1000
		JUMP SELECT_MASHO2
ELSEIF RESULT < 0 || RESULT >= VARSIZE("G")
	GOTO INPUT_LOOP_1
ELSEIF G:RESULT != 1
	GOTO INPUT_LOOP_1
ENDIF
LOCAL:2 = RESULT
LOCAL:3 = (LOCAL:2-2450-450*((LOCAL:2-2000)/500))
CALL 장비설명,LOCAL:2
CALL 장비조건설명_마정장비,LOCAL:3
PRINTL 

;가격決定
LOCAL:5 = 1000 + (DC:(LOCAL:3):52)*(DC:(LOCAL:3):52-2)*200
SIF LOCAL:5 < 1000
	LOCAL:5 = 1000
SIF DC:(LOCAL:3):10 < 100 && DC:(LOCAL:3):11 < 100 && DC:(LOCAL:3):14 < 100 && DC:(LOCAL:3):15 < 100
	LOCAL:5 = LOCAL:5/10


PRINTFORML 이 장비를 강화하겠습니까？　[강화횟수：{DC:(LOCAL:3):52}]　[마카{LOCAL:5}＋동료마소비]
PRINTL [0]Yes [1]No
$INPUT_LOOP2
INPUT
IF RESULT == 1
	RESTART
ELSEIF RESULT != 0
	GOTO INPUT_LOOP2
ENDIF

IF MONEY:1 < LOCAL:5
	PRINTW 마카가 부족합니다.
	PRINTL 

	RESTART
ENDIF

	;기본능력値수정、전투능력値수정を決定
	;기본
	TIMES DC:(LOCAL:3):1 , 1.15
	TIMES DC:(LOCAL:3):3 , 1.15
	TIMES DC:(LOCAL:3):6 , 1.15
	TIMES DC:(LOCAL:3):4 , 1.15
	TIMES DC:(LOCAL:3):2 , 1.15
	TIMES DC:(LOCAL:3):5 , 1.15

	IF DC:(LOCAL:3):52 % 10 == 9
		FOR LOCAL, 1, 7
			SIF DC:(LOCAL:3):LOCAL >= 1
			DC:(LOCAL:3):LOCAL += 1
		NEXT
	ENDIF

	;전투
	IF  DC:(LOCAL:3):52 <= 9
		TIMES DC:(LOCAL:3):10 , 1.15
		TIMES DC:(LOCAL:3):11 , 1.15
		TIMES DC:(LOCAL:3):14 , 1.15
		TIMES DC:(LOCAL:3):15 , 1.15
	ELSE
		TIMES DC:(LOCAL:3):10 , 1.03
		TIMES DC:(LOCAL:3):11 , 1.03
		TIMES DC:(LOCAL:3):14 , 1.03
		TIMES DC:(LOCAL:3):15 , 1.03
	ENDIF

;生贄の종족による변화

	SELECTCASE ABL:(LOCAL:4):80
		;지모신
		CASE 1
			DC:(LOCAL:3):10 -= 5
			DC:(LOCAL:3):11 += 5
		;여신
		CASE 2
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):2 += 1
		;마왕
		CASE 3
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):3 += 1
		;천사
		CASE 4
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):4 += 1

			SIF DC:(LOCAL:3):40 == 10
				DC:(LOCAL:3):10 += 10
		;요마
		CASE 5
			DC:(LOCAL:3):15 += 5
			DC:(LOCAL:3):14 -= 5

			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):6 += 1
		;요정
		CASE 6
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):2 += 1
		;요귀
		CASE 7
			DC:(LOCAL:3):10 += 5
			DC:(LOCAL:3):11 -= 5

			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):1 += 1
		;용왕
		CASE 8
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):1 += 2
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):6 -= 1
		;마수
		CASE 9
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):4 += 1
		;요조
		CASE 10
			DC:(LOCAL:3):15 -= 5
			DC:(LOCAL:3):11 += 5
		;외도
		CASE 11
			SIF DC:(LOCAL:3):40 == 11
				DC:(LOCAL:3):10 += 10
		;머신
		CASE 12
			;特になし
		;정령
		CASE 13
			DC:(LOCAL:3):11 += 1
			DC:(LOCAL:3):15 += 1
		;어령
		CASE 14
			;特になし
		;귀신
		CASE 15
			DC:(LOCAL:3):10 += 2

			IF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):1 += 2
				DC:(LOCAL:3):4 += 2
				DC:(LOCAL:3):2 -= 1
				DC:(LOCAL:3):3 -= 1
			ENDIF
		;마신
		CASE 16
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):3 += 1
		;타천사
		CASE 18
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):6 += 1

		;마인
		CASE 42
			DC:(LOCAL:3):10 += 1
			DC:(LOCAL:3):14 += 1
			IF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):1 += 1
				DC:(LOCAL:3):2 += 1
				DC:(LOCAL:3):3 += 1
				DC:(LOCAL:3):4 += 1
				DC:(LOCAL:3):5 += 1
				DC:(LOCAL:3):6 += 1
			ENDIF

	ENDSELECT

;월령による변화
IF FLAG:월령 == 8
	DC:(LOCAL:3):10 += 5
	DC:(LOCAL:3):11 -= 5
	DC:(LOCAL:3):14 += 5
	DC:(LOCAL:3):15 -= 5
ELSEIF FLAG:월령 == 0
	DC:(LOCAL:3):10 -= 5
	DC:(LOCAL:3):11 += 5
	DC:(LOCAL:3):14 -= 5
	DC:(LOCAL:3):15 += 5
ENDIF

CALL 장비설명,LOCAL:2
PRINTW 지정된 마정무기가 강화되었습니다！
PRINTL 

MONEY:1 -= LOCAL:5
SIF LOCAL:4 != 0
	CALL 캐릭터삭제, LOCAL:4
;강화횟수증가
DC:(LOCAL:3):52 += 1

@KAISETU_MASHO2
PRINTL 마정무기 작성、확장모드 해설
PRINTL 
PRINTL ・무명의 도와 악마를 합체시켜 마정무기를 작성할 수 있습니다.
PRINTL ・마정무기는 마정을 사용하여 더욱 강화 할 수 있습니다.
PRINTL ・마정은 악마를 보석이나 특수한 금속에 봉인함으로써 생성됩니다.
PRINTL ・이 소프트에서 보석이나 금속은 마카로 대체됩니다.
PRINTL 
PRINTL ●작성되는 마정무기의 특징
PRINTL ・작성시 성능이 대폭 감소
PRINTL ・소재의 레벨에 따라 장비 가능 레벨이 설정된다
PRINTL ・인계 가능
PRINTL ・마카와 동료마를 소비헤서 단계적인 강화가 가능
PRINTL ・소비할 수 있는 동료마는 함락하거나 충성도가 일정이상인 자.
PRINTL 
PRINTL ●주의 사항
PRINTL 이 소프트는 사용 기간이 존재해서 50 일 이후에는 사용할 수 없습니다.
PRINTW

;================================================================
;마정무기の명칭설정
;================================================================
@NAME_MASHO2,ARG,ARG:1
PRINTL 마정 무기의 이름을 정해주세요
PRINTL 
PRINTFORML [0] %STR:(DC:(ARG:1):49+100)%
PRINTFORML [1] 마정검：%NAME:(ARG)%
PRINTFORML [2] 스스로 정한다
$INPUT_LOOP
INPUT
IF RESULT == 0

ELSEIF RESULT == 1
	SAVESTR:(550+ARG:1) = 마정검：%NAME:(ARG)%
	DC:(ARG:1):49 = 19
ELSEIF RESULT == 2
	PRINTL 마정무기의 이름을 입력해주세요.
	$INPUT_LOOP2

	INPUTS
	IF RESULTS == ""
		GOTO INPUT_LOOP2
	ELSE
		SAVESTR:(550+ARG:1) = %RESULTS%
		DC:(ARG:1):49 = 19
	ENDIF

ELSE
	GOTO INPUT_LOOP
ENDIF
PRINTL 

;================================================================
;材料にできる악마の조건
;================================================================
; 作成用、확장モードでは계약していても可
@CASTING_MASHO2(ARG)
SIF ABL:ARG:종족 == 0 || ABL:ARG:종족 > 44
	RETURN 0
SIF CFLAG:ARG:노역플래그 == 3
	RETURN 0
SIF CFLAG:ARG:이자리에없는플래그 == 1
	RETURN 0
SIF CFLAG:ARG:합체불가
	RETURN 0
SIF EQUIP:ARG:검 != 0
	RETURN 0
SIF EQUIP:ARG:총 != 0
	RETURN 0
SIF EQUIP:ARG:머리 != 0
	RETURN 0
SIF EQUIP:ARG:몸통 != 0
	RETURN 0
SIF EQUIP:ARG:팔 != 0
	RETURN 0
SIF EQUIP:ARG:발 != 0
	RETURN 0
RETURN 1

;こちらは강화用、함락しているか충성도が필요
@CASTING_MASHO2_2(ARG)
SIF 계약(ARG) > 0
	RETURN 0
SIF 함락(ARG) <= 0 && BASE:ARG:충성도 <= 14999
	RETURN 0
SIF ABL:ARG:종족 == 0 || ABL:ARG:종족 > 44
	RETURN 0
SIF CFLAG:ARG:노역플래그 == 3
	RETURN 0
SIF CFLAG:ARG:이자리에없는플래그 == 1
	RETURN 0
SIF CFLAG:ARG:합체불가
	RETURN 0
SIF EQUIP:ARG:검 != 0
	RETURN 0
SIF EQUIP:ARG:총 != 0
	RETURN 0
SIF EQUIP:ARG:머리 != 0
	RETURN 0
SIF EQUIP:ARG:몸통 != 0
	RETURN 0
SIF EQUIP:ARG:팔 != 0
	RETURN 0
SIF EQUIP:ARG:발 != 0
	RETURN 0
RETURN 1
