;================================================================
;마정장비
;================================================================
@MAKE_MASHO
#DIM LCOUNT
FLAG:合体予定악마1 = -1
;LOCAL ループ用
;LOCAL:1 소재の등록번호
LOCAL:1 = -1
;LOCAL:2 악마の표시用変数
;LOCAL:3 合体결과の검枠
;LOCAL:4 作成する마정장비の종류
;G 合体검のフラグを管理する変数 20-25:배드스테이터스촌정用

$START_MAKE
LOCAL:6 = 0

;キャラリストを표시・選択
CALL INPUT_CHARA_LIST("소체로 쓸 악마를 선택해주십시오","CASTING_MASHO")
SIF RESULT == 1000
	RETURN 0
LOCAL:1 = RESULT


LOCAL:3 = -1
;空いている合体검枠を参照
FOR LOCAL,0,50
	IF DC:LOCAL:49 == 0
		LOCAL:3 = LOCAL
		BREAK
	ENDIF
NEXT
IF LOCAL:3 == -1
	PRINTW 더 이상의 합체검을 가질 수 없습니다
	RETURN 0
ENDIF

;合体검の종류を決める
;습득스킬によってGを설정
VARSET G,-1
G:0 = 1
FOR LOCAL,1,FLAG:스킬수+1
	LOCALS = 스킬{LOCAL}
	IF ABL:(LOCAL:1):LOCALS > 0
		RESULT = 0
		;相性による조건
		TRYCALLFORM SKILL_TYPE_{ABL:(LOCAL:1):LOCALS},LOCAL:1
		G:RESULT = 1
	ENDIF
NEXT
;검격以外の물리は無し
G:1 = -1
G:2 = -1
G:3 = -1


;신경・정신は特殊
G:8 = -1
G:9 = -1
FOR LOCAL,1,FLAG:스킬수+1
	LOCALS = 스킬{LOCAL}
	IF ABL:(LOCAL:1):LOCALS > 0
		RESULT = 0
		;相性による조건
		TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ABL:(LOCAL:1):LOCALS},LOCAL:1
			SELECTCASE GET_STATE(RESULT)
				CASE "POISON"
					G:20 = 1
				CASE "SLEEP"
					G:21 = 1
				CASE "PANIC"
					G:22 = 1
				CASE "HAPPY"
					G:23 = 1
				CASE "BIND"
					G:24 = 1
				CASE "CHARM"
					G:25 = 1
			ENDSELECT
		CATCH
		ENDCATCH
	ENDIF
NEXT

;검の타입を選択
$PRINT_TYPE
DRAWLINE
PRINTL 검의 공격상성을 선택해주세요
FOR LOCAL,0,FLAG:相性数
	SIF G:LOCAL > 0
		PRINTFORM [{LOCAL}] 【%GET_TYPE(LOCAL)%】
	SIF G:LOCAL > 0 && LOCAL == 5
		PRINT & FREEZE ２０％
	SIF G:LOCAL > 0 && LOCAL == 6
		PRINT & SHOCK ２０％
	SIF G:LOCAL > 0
		PRINTL
NEXT
SIF G:20 > 0
	PRINTL [20] 【검격】& POISON ２０％
SIF G:21 > 0
	PRINTL [21] 【검격】& SLEEP ２５％
SIF G:22 > 0
	PRINTL [22] 【검격】& PANIC ２５％
SIF G:23 > 0
	PRINTL [23] 【검격】& HAPPY ３５％
SIF G:24 > 0
	PRINTL [24] 【검격】& BIND ２５％
SIF G:25 > 0
	PRINTL [25] 【검격】& CHARM ２０％

DRAWLINE
PRINTL [100] 취소

$INPUT_TYPE
INPUT

IF RESULT == 100
	RESTART
ELSEIF G:RESULT > 0
	;DCに데이터を입력
	;まずは명칭と相性、추가효과を決めておく
	SELECTCASE RESULT
		CASE 0
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 1
		CASE 4,16
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 2
		CASE 5
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 3
			DC:(LOCAL:3):45 = GET_STATE_NUM("FREEZE")
			DC:(LOCAL:3):46 = 5
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 6
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 4
			DC:(LOCAL:3):45 = GET_STATE_NUM("SHOCK")
			DC:(LOCAL:3):46 = 6
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 7
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 5
		CASE 10,11
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = RESULT
		CASE 12 TO 15
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = RESULT - 6
		CASE 20
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 12
			DC:(LOCAL:3):45 = GET_STATE_NUM("POISON")
			DC:(LOCAL:3):46 = 8
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 21
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 13
			DC:(LOCAL:3):45 = GET_STATE_NUM("SLEEP")
			DC:(LOCAL:3):46 = 8
			DC:(LOCAL:3):47 = 25
			DC:(LOCAL:3):48 = 95
		CASE 22
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 14
			DC:(LOCAL:3):45 = GET_STATE_NUM("PANIC")
			DC:(LOCAL:3):46 = 9
			DC:(LOCAL:3):47 = 25
			DC:(LOCAL:3):48 = 95
		CASE 23
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 15
			DC:(LOCAL:3):45 = GET_STATE_NUM("HAPPY")
			DC:(LOCAL:3):46 = 9
			DC:(LOCAL:3):47 = 35
			DC:(LOCAL:3):48 = 95
		CASE 24
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 16
			DC:(LOCAL:3):45 = GET_STATE_NUM("BIND")
			DC:(LOCAL:3):46 = 8
			DC:(LOCAL:3):47 = 25
			DC:(LOCAL:3):48 = 95
		CASE 25
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 17
			DC:(LOCAL:3):45 = GET_STATE_NUM("CHARM")
			DC:(LOCAL:3):46 = 9
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 17
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 18
	ENDSELECT

	;공격횟수を選択
	;1명공격＆속도30이상で２回공격、50이상で３回공격을選択可
	;범위が1명でない、または속도が30미만の場合は選択肢が出ない

	IF BASE:(LOCAL:1):속도 >= 30 && ABL:(LOCAL:1):공격범위 == 1
		;공격횟수によってGを설정
		VARSET G,-1

		$PRINT_ATKTIMES
		DRAWLINE
		PRINTL 검의 공격횟수를 선택해주세요
		IF BASE:(LOCAL:1):속도 >= 50
			PRINTL [1] １회 공격
			G:1 = 1
			PRINTL [2] ２회 공격（공격８０％　명중７０％）
			G:2 = 1
			PRINTL [3] ３회 공격（공격７０％　명중５０％）
			G:3 = 1
		ELSE
			PRINTL [1] １회 공격
			G:1 = 1
			PRINTL [2] ２회 공격（공격８０％　명중７０％）
			G:2 = 1
		ENDIF
		DRAWLINE
		PRINTL [100] 취소

		$INPUT_ATKTIMES
		INPUT
	
		IF RESULT == 100
			RESTART
		ELSEIF G:RESULT > 0
			;DCに공격횟수を입력。마이ナス보정はあとで行う
			SELECTCASE RESULT
				CASE 1
					DC:(LOCAL:3):41 = 1
					DC:(LOCAL:3):42 = 1
				CASE 2
					DC:(LOCAL:3):41 = 2
					DC:(LOCAL:3):42 = 2
				CASE 3
					DC:(LOCAL:3):41 = 3
					DC:(LOCAL:3):42 = 3
			ENDSELECT
		ELSE
			CLEARLINE 1
			GOTO INPUT_ATKTIMES
		ENDIF
	ELSE
		DC:(LOCAL:3):41 = 1
		DC:(LOCAL:3):42 = 1
	ENDIF


	;기본능력値수정、전투능력値수정を決定
	;기본
	DC:(LOCAL:3):0 += (MIN(ABL:(LOCAL:1):순종 , 10) + MIN(ABL:(LOCAL:1):욕망 , 10) + MIN(ABL:(LOCAL:1):기교 , 10) +3)/6 *(BASE:(LOCAL:1):LV+20)/20
	DC:(LOCAL:3):1 += (MIN(ABL:(LOCAL:1):욕망 , 10)+1)/2 *(BASE:(LOCAL:1):LV+40)/40
	DC:(LOCAL:3):3 += (MIN(ABL:(LOCAL:1):욕망 , 10)+1)/2 *(BASE:(LOCAL:1):LV+40)/40
	DC:(LOCAL:3):6 += (MIN(ABL:(LOCAL:1):순종 , 10)+1)/2 *(BASE:(LOCAL:1):LV+40)/40
	DC:(LOCAL:3):4 += (MIN(ABL:(LOCAL:1):순종 , 10)+1)/2 *(BASE:(LOCAL:1):LV+40)/40
	DC:(LOCAL:3):2 += (MIN(ABL:(LOCAL:1):기교 , 10)+1)/2 *(BASE:(LOCAL:1):LV+40)/40
	DC:(LOCAL:3):5 += (MIN(ABL:(LOCAL:1):기교 , 10)+1)/2 *(BASE:(LOCAL:1):LV+40)/40
	;레벨による조교効果보정
;	DC:(LOCAL:3):0 *= (BASE:(LOCAL:1):LV+20)/20
;	DC:(LOCAL:3):1 *= (BASE:(LOCAL:1):LV+40)/40
;	DC:(LOCAL:3):2 *= (BASE:(LOCAL:1):LV+40)/40
;	DC:(LOCAL:3):3 *= (BASE:(LOCAL:1):LV+40)/40
;	DC:(LOCAL:3):4 *= (BASE:(LOCAL:1):LV+40)/40
;	DC:(LOCAL:3):5 *= (BASE:(LOCAL:1):LV+40)/40
;	DC:(LOCAL:3):6 *= (BASE:(LOCAL:1):LV+40)/40
	;전투
	DC:(LOCAL:3):10 = (MIN(BASE:(LOCAL:1):LV,80)+5+DC:(LOCAL:3):0*2)*3/2 + BASE:(LOCAL:1):힘*3/2 + BASE:(LOCAL:1):인내력 / 2
	DC:(LOCAL:3):11 = (MIN(BASE:(LOCAL:1):LV,80)+5+DC:(LOCAL:3):0*2) + BASE:(LOCAL:1):속도*3/2 + BASE:(LOCAL:1):운/2
	DC:(LOCAL:3):14 = (MIN(BASE:(LOCAL:1):LV,80)+5+DC:(LOCAL:3):0*2)*3/2 + BASE:(LOCAL:1):마력*3/2 + BASE:(LOCAL:1):지혜 / 2
	DC:(LOCAL:3):15 = (MIN(BASE:(LOCAL:1):LV,80)+5+DC:(LOCAL:3):0*2) + BASE:(LOCAL:1):지혜 + BASE:(LOCAL:1):운/2 + BASE:(LOCAL:1):마력/2
	;검仕様に변경
	TIMES DC:(LOCAL:3):10 , 1.00
	DC:(LOCAL:3):11 += 10
	TIMES DC:(LOCAL:3):14 , 0.80
	TIMES DC:(LOCAL:3):15 , 0.40
	;충성도による加算
	DC:(LOCAL:3):10 += MIN(BASE:(LOCAL:1):충성도/500,30)
	DC:(LOCAL:3):11 += MIN(BASE:(LOCAL:1):충성도/500,30)
	DC:(LOCAL:3):14 += MIN(BASE:(LOCAL:1):충성도/500,30)
	DC:(LOCAL:3):15 += MIN(BASE:(LOCAL:1):충성도/500,30)
	
	
	;공격形態は소재のものをコピー
;	DC:(LOCAL:3):41 = ABL:(LOCAL:1):최저공격횟수
;	DC:(LOCAL:3):41 = 1
;	DC:(LOCAL:3):42 = ABL:(LOCAL:1):최대공격횟수
;	DC:(LOCAL:3):42 = 1
	DC:(LOCAL:3):43 = ABL:(LOCAL:1):사거리
	DC:(LOCAL:3):44 = ABL:(LOCAL:1):공격범위

	;공격횟수によって공격と명중に수정が入る
	SELECTCASE DC:(LOCAL:3):41
		CASE 2
			TIMES DC:(LOCAL:3):10 , 0.80
			TIMES DC:(LOCAL:3):11 , 0.70
		CASE 3
			TIMES DC:(LOCAL:3):10 , 0.70
			TIMES DC:(LOCAL:3):11 , 0.50
	ENDSELECT
	
	;;사거리・공격범위によって명중と공격に수정が入る
	;SELECTCASE DC:(LOCAL:3):43
	;	CASE 2
	;		TIMES DC:(LOCAL:3):11 , 0.8
	;	CASE 3
	;		TIMES DC:(LOCAL:3):11 , 0.6
	;ENDSELECT
	;1명は소재악마の함락상태によって최대공격횟수が증가
	;공격が列は90%、전체は60%
	SELECTCASE DC:(LOCAL:3):44
		CASE 1
			IF TALENT:(LOCAL:1):3 || TALENT:(LOCAL:1):4 || TALENT:(LOCAL:1):5
				DC:(LOCAL:3):42 += 1
			ELSEIF TALENT:(LOCAL:1):6 || TALENT:(LOCAL:1):7 || TALENT:(LOCAL:1):8
				DC:(LOCAL:3):42 += 2
			ELSE
			ENDIF
		CASE 2
			TIMES DC:(LOCAL:3):10 , 0.9
	;		TIMES DC:(LOCAL:3):10 , 0.7
	;		TIMES DC:(LOCAL:3):11 , 0.8
		CASE 3
			TIMES DC:(LOCAL:3):10 , 0.6
	;		TIMES DC:(LOCAL:3):10 , 0.4
	;		TIMES DC:(LOCAL:3):11 , 0.5
	ENDSELECT
	
	DC:(LOCAL:3):39 = GET_EQUIPNUM("검")
	
	;추가효과付きの검は수정を加える
	SELECTCASE DC:(LOCAL:3):49
		CASE 3,4
			TIMES DC:(LOCAL:3):10 , 0.95
			TIMES DC:(LOCAL:3):11 , 0.95
		CASE 12 TO 17
			TIMES DC:(LOCAL:3):10 , 0.90
			TIMES DC:(LOCAL:3):11 , 0.90
	ENDSELECT
	;방어상성
	FOR LOCAL,0,FLAG:相性数
		DC:(LOCAL:3):(20+LOCAL) = 100
	NEXT
	SIF DC:(LOCAL:3):40 == 17
		TIMES DC:(LOCAL:3):10 , 0.75
	
ELSE
	CLEARLINE 1
	GOTO INPUT_TYPE
ENDIF

;合体결과の검を표시
CALL 장비설명,(2450+LOCAL:3+450*DC:(LOCAL:3):39)



;合体するかどうか　→　合体実行（実際には既に終わっている）
PRINT 이 검을 만듭니까？ 

CALL INPUT_YN,"Yes","No",2

SELECTCASE RESULT
	CASE 0
		ITEM:(2450+LOCAL:3+450*DC:(LOCAL:3):39) = 1
	CASE 1
		FOR LCOUNT, 0, 50
			DC:(LOCAL:3):LCOUNT = 0
		NEXT
		RESTART
ENDSELECT

PRINTFORMW %STR:(DC:(LOCAL:3):49+100)%를 입수했다

CALL FUSION_LETTER,LOCAL:1,1

;合体소재の악마を소거
CALL キャラ삭제, LOCAL:1

ITEM:무명의도 -= 1

RETURN 0

GOTO START_MAKE


;==========================================================================
;마정장비を소재に戻す
;==========================================================================
@DEL_MASHO
PRINTFORML 어느 장비를 소재로 되돌립니까？　＜page.{P + 1}＞
DRAWLINE
;持っている아이템を표시
VARSET G,-1
FOR LOCAL,2000,VARSIZE("ITEM")
	IF 마정장비(LOCAL)
		IF ITEM:LOCAL > 0
			PRINTFORMLC [{LOCAL}]%EQ_GETNAME_마정장비(LOCAL-2450-450*((LOCAL-2000)/500))%
			G:LOCAL = 1
		ENDIF
	ELSE
	ENDIF
NEXT
PRINTL

$PRINT_LIST
DRAWLINE
PRINTL [1000] 돌아간다
$INPUT_LOOP_1
INPUT
IF RESULT == 1000
		RETURN 0
ELSEIF RESULT < 0 || RESULT >= VARSIZE("G")
	GOTO INPUT_LOOP_1
ELSEIF G:RESULT != 1
	GOTO INPUT_LOOP_1
ENDIF
LOCAL:2 = RESULT
CALL 장비설명,LOCAL:2

PRINTL 이 장비를 소재로 되돌리시겠습니까？
PRINTL [0]Yes [1]No
$INPUT_LOOP2
INPUT
IF RESULT == 1
	RESTART
ELSEIF RESULT != 0
	GOTO INPUT_LOOP2
ENDIF


ITEM:무명의도 += 1

FOR LOCAL,0,99
	DC:(LOCAL:2-2450-450*((LOCAL:2-2000)/500)):LOCAL = 0
NEXT

ITEM:(LOCAL:2) -= 1


;================================================================
;材料にできる악마の조건
;================================================================
@CASTING_MASHO(ARG)
SIF 계약(ARG) > 0
	RETURN 0
SIF ABL:ARG:종족 == 0 || ABL:ARG:종족 > 44
	RETURN 0
SIF CFLAG:ARG:노역플래그 == 3
	RETURN 0
SIF CFLAG:ARG:この場に居ないフラグ == 1
	RETURN 0
SIF CFLAG:ARG:합체불가
	RETURN 0
SIF EQUIP:ARG:검 != 0
	RETURN 0
SIF EQUIP:ARG:총 != 0
	RETURN 0
SIF EQUIP:ARG:머리 != 0
	RETURN 0
SIF EQUIP:ARG:몸통 != 0
	RETURN 0
SIF EQUIP:ARG:팔 != 0
	RETURN 0
SIF EQUIP:ARG:발 != 0
	RETURN 0
RETURN 1
