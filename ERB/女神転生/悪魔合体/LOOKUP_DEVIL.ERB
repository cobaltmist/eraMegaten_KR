;-------------------------
;소질から악마を検索
;대상は소환できる악마
;-------------------------
@TALENT_LOOKUP_DEVIL
#LOCALSIZE 2
PRINTL 검색할 소질의 이름을 입력해주세요
PRINTL (아무것도 입력하지 않으면、종료합니다)
INPUTS
SIF RESULTS == ""
	RETURN 0
IF GETNUM(TALENT, RESULTS) == -1
	PRINTFORMW [%RESULTS%](이)라는 이름의 소질은 존재하지 않습니다
	CLEARLINE 5
	RESTART
ENDIF
CLEARLINE 3
DRAWLINE
PRINTFORML □%RESULTS%
LOCAL:1 = 0
FOR LOCAL, 0, 6500
	SIF FLAG:(20000+LOCAL) < 100
		CONTINUE
	SIF !EXISTCSV(LOCAL, 0)
		CONTINUE
	SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"전서소환불가")),0) == 1
		CONTINUE
	SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"合体조건有り")),0) == 1 && FLAG:(10000+LOCAL) == 0
		CONTINUE
	SIF !CSVTALENT(LOCAL, GETNUM(TALENT, RESULTS), 0)
		CONTINUE
	IF CSVCSTR(LOCAL, GETNUM(CSTR, "종족명"), 0) == ""
		PRINTFORM %@"[{LOCAL:1,2}] "+ CSVNAME(LOCAL, 0) + "(" + STR:CSVABL(LOCAL, GETNUM(ABL, "종족"), 0) + ")",36,LEFT%
	ELSE
		PRINTFORM %@"[{LOCAL:1,2}] "+ CSVNAME(LOCAL, 0) + "(" + CSVCSTR(LOCAL, GETNUM(CSTR, "종족명"), 0) + ")",36,LEFT%
	ENDIF
	;PRINTFORM %@"%CSVNAME(LOCAL, 0)%(\@CSVCSTR(LOCAL, 0) == "" ? %STR:(CSVABL(LOCAL, GETNUM(ABL, "종족", 0), 0)%#%CSVCSTR(LOCAL, 0)%\@)",30,LEFT%
	SIF LOCAL:1 % 3 == 2
		PRINTL 
	LOCAL:1++
NEXT
SIF LINEISEMPTY() == 0
	PRINTL  
SIF LOCAL:1 == 0
	PRINTFORML %조사처리(RESULTS,"를")% 갖고 있는 악마는 등록되어있지 않습니다
RESTART
;-------------------------
;스킬から악마を検索
;대상は소환できる악마
;-------------------------
@SKILL_LOOKUP_DEVIL
#LOCALSIZE 4
#LOCALSSIZE 200
#DIMS SKILLNAME, 1
#DIM SKILL, 1
VARSET D, 0
PRINTL 검색할 스킬 이름을 입력해주세요
PRINTL (아무것도 입력하지 않으면、종료합니다)
INPUTS
SIF RESULTS == ""
	RETURN 0
SKILL = -1
SKILLNAME = %RESULTS%
;입력された스킬名が存在するものかどうかを確かめつつ、스킬번호を취득する
FOR LOCAL, 0, 2500
	TRYCCALLFORM SKILL_NAME_{LOCAL}
		;ややこしいので、「・」は생략した場合も通るようにする
		RESULTS:1 = %REPLACE(RESULTS, "・", "")%
		SIF RESULTS != SKILLNAME && RESULTS:1 != SKILLNAME
			CONTINUE
		SKILL = LOCAL
		SKILLNAME = %RESULTS%
		BREAK
	CATCH
	ENDCATCH
NEXT
IF SKILL == -1
	PRINTFORMW [%SKILLNAME%]%조사만처리(SKILLNAME,"이라")%는 이름의 스킬은 존재하지 않습니다
	CLEARLINE 5
	RESTART
ENDIF
CLEARLINE 3
DRAWLINE
PRINTFORML □%SKILLNAME%
;등록악마で持っているものを検索
;이름は出さず、D配列にヒットした악마を記録
FOR LOCAL, 2000, 7000
	;空번호ひいたら종료
	SIF SAVESTR:LOCAL == ""
		BREAK
	SPLIT SAVESTR:LOCAL, ",", LOCALS
	FOR LOCAL:1, 1, 9
		IF TOINT(LOCALS:GET_CPD_STRFLAG_NUM(@"스킬{LOCAL:1}")) == SKILL
			D:TOINT(LOCALS:GET_CPD_STRFLAG_NUM("NO")) = 1
			BREAK
		ENDIF
	NEXT
NEXT
LOCAL:3 = 0
;통상の全書検索
FOR LOCAL, 0, 1900
	SIF !EXISTCSV(LOCAL , 0)
		CONTINUE
	SIF FLAG:(20000+LOCAL) < 100
		CONTINUE
	SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"전서소환불가")),0) == 1
		CONTINUE
	SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"合体조건有り")),0) == 1 && FLAG:(10000+LOCAL) == 0
		CONTINUE
	SIF !EXISTCSV(LOCAL, 0)
		CONTINUE
	;さきほどの등록악마検索でひっかかった악마は스킬捜査を飛ばす
	IF !D:LOCAL
		LOCAL:2 = 0
		FOR LOCAL:1, 1, 9
			IF CSVABL(LOCAL, GETNUM(ABL, @"스킬{LOCAL:1}"), 0) == SKILL
				LOCAL:2 = 1
				BREAK
			ENDIF
		NEXT
		SIF !LOCAL:2
			CONTINUE
	ENDIF
	IF CSVCSTR(LOCAL, GETNUM(CSTR, "종족명"), 0) == ""
		PRINTFORM %@"[{LOCAL:3,2}] "+ CSVNAME(LOCAL, 0) + "(" + STR:CSVABL(LOCAL, GETNUM(ABL, "종족"), 0) + ")",36,LEFT%
	ELSE
		PRINTFORM %@"[{LOCAL:3,2}] "+ CSVNAME(LOCAL, 0) + "(" + CSVCSTR(LOCAL, GETNUM(CSTR, "종족명"), 0) + ")",36,LEFT%
	ENDIF
	;PRINTFORM %@"%CSVNAME(LOCAL, 0)%(\@CSVCSTR(LOCAL, 0) == "" ? %STR:(CSVABL(LOCAL, GETNUM(ABL, "종족", 0), 0)%#%CSVCSTR(LOCAL, 0)%\@)",30,LEFT%
	SIF LOCAL:3 % 3 == 2
		PRINTL 
	LOCAL:3++
NEXT
SIF LINEISEMPTY() == 0
	PRINTL  
SIF LOCAL:3 == 0
	PRINTFORML %SKILLNAME%)%조사만처리(SKILLNAME,"를")% 갖고 있는 악마는 등록되어있지 않습니다
RESTART

