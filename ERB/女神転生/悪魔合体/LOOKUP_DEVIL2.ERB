
; 상성패턴
; 0:不問(なんでも)
; 1:약점(100%以上かつ999じゃない)
; 2:통상(100%以下あるいは999)
; 3:내성以上(75%以下あるいは999)
; 4:無効(0%)
; 5:흡수(-1以下)
; 6:반사(999)
;옵션
; B.適合率(すべて一致or不一致1つまで)←내성変化で何とかできる레벨の算出用
; C.より上位の조건を含むか(含むor含まない)
@RESIST_LOOKUP_DEVIL
#DIM LINE,2
#DIM RESIST
#DIM 상성패턴,19
#DIM 옵션
#DIM 조건数,2
#DIM COL
$TOP
LINE = LINECOUNT
FOR RESIST,0,18
	SELECTCASE 상성패턴:RESIST
	CASE 0
		COL = 0
		LOCALS = ----
	CASE 1
		COL = FLAG:약점컬러
		LOCALS = 약점
	CASE 2
		COL = 0
		LOCALS = 통상
	CASE 3
		COL = FLAG:내성컬러
		LOCALS = 내성
	CASE 4
		COL = FLAG:무효컬러
		LOCALS = 무효
	CASE 5
		COL = FLAG:흡수컬러
		LOCALS = 흡수
	CASE 6
		COL = FLAG:반사컬러
		LOCALS = 반사
	ENDSELECT
	SIF COL
		SETCOLOR COL / 1000000 , COL /1000 % 1000 , COL % 1000
	PRINTFORM [{RESIST,2}]%GET_TYPE(RESIST)%%LOCALS,8%　
	RESETCOLOR
	SIF RESIST % 6 == 5
		PRINTL
NEXT
PRINTL [25] 검색 시작
PRINTL [26] 검색 조건 초기화
PRINTFORML [27] 옵션１：\@ GETBIT(옵션,0) ? 내성 이상은 1개까지 불일치 허용 # 모두 일치하는 것만 검색 \@
PRINTFORML [28] 옵션２：\@ GETBIT(옵션,1) ? 상위의 조건을 포함하지 않는다 # 상위의 조건을 포함한다 \@
PRINTL [29] 돌아간다
INPUT
LOCAL:1 = RESULT
IF INRANGE(RESULT,0,18)
	CALL RESIST_LOOKUP_DEVIL_SETPETTERN(LOCAL:1,상성패턴:(LOCAL:1))
	SIF RESULT >= 0
		상성패턴:(LOCAL:1) = RESULT
ELSEIF RESULT == 26
	VARSET 상성패턴,0
ELSEIF RESULT == 27
	INVERTBIT 옵션,0
ELSEIF RESULT == 28
	INVERTBIT 옵션,1
ELSEIF RESULT == 29
	RETURN
ELSEIF RESULT == 25
	LINE:1 = LINECOUNT
	$SEARCH
	CUSTOMDRAWLINE =
	LOCAL:1 = 0
	FOR LOCAL, 1, 1900
		SIF FLAG:(20000+LOCAL) < 100
			CONTINUE
		SIF !EXISTCSV(LOCAL, 0)
			CONTINUE
		SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"전서소환불가")),0) == 1
			CONTINUE
		SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"합체조건있음")),0) == 1 && FLAG:(10000+LOCAL) == 0
			CONTINUE

		;ここに조건を入れる
		VARSET 조건数
		FOR RESIST,0,18
			SIF 상성패턴:RESIST == 0
				CONTINUE
			조건数:0++
			CALL CHECK_DEVIL_RESIST(LOCAL,RESIST,상성패턴:RESIST,!GETBIT(옵션,0))
			SIF RESULT == 1
				조건数:1++
		NEXT
		SIF 조건数:1 < 조건数:0
			CONTINUE
		IF CSVCSTR(LOCAL, GETNUM(CSTR, "종족명"), 0) == ""
			PRINTFORM %@"[{LOCAL,4}] "+ CSVNAME(LOCAL, 0) + "(" + STR:CSVABL(LOCAL, GETNUM(ABL, "종족"), 0) + ")",36,LEFT%
		ELSE
			PRINTFORM %@"[{LOCAL,4}] "+ CSVNAME(LOCAL, 0) + "(" + CSVCSTR(LOCAL, GETNUM(CSTR, "종족명"), 0) + ")",36,LEFT%
		ENDIF
		;PRINTFORM %@"%CSVNAME(LOCAL, 0)%(\@CSVCSTR(LOCAL, 0) == "" ? %STR:(CSVABL(LOCAL, GETNUM(ABL, "종족", 0), 0)%#%CSVCSTR(LOCAL, 0)%\@)",30,LEFT%
		SIF LOCAL:1 % 3 == 2
			PRINTL 
		LOCAL:1++
	NEXT
	SIF !LINEISEMPTY()
		PRINTL
	CUSTOMDRAWLINE =
	IF LOCAL:1
		PRINTFORML {LOCAL:1}건이 검색되었습니다
		PRINTL [   0] 돌아간다
		$INP
		INPUT
		IF RESULT == 0
			GOTO TOP
		ELSEIF FLAG:(20000+RESULT) >= 100
			CUSTOMDRAWLINE =
			LOCAL = RESULT
			PRINTFORM \@ CSVCSTR(LOCAL,GETNUM(CSTR,"종족명"),0) != "" ? %CSVCSTR(LOCAL,GETNUM(CSTR,"종족명"),0)% # %STR:CSVABL(LOCAL,GETNUM(ABL,"종족"),0)% \@ 
			PRINTFORML %CSVNAME(LOCAL,0)%
			FOR RESIST,0,18
				LOCAL:1 = CSVBASE(LOCAL,GETNUM(BASE,"검격")+RESIST,0)
				SELECTCASE LOCAL:1
				CASE 999
					COL = FLAG:반사컬러
					LOCALS = 반사
				CASE IS < 0
					COL = FLAG:흡수컬러
					LOCALS = 흡{ABS(LOCAL:1)}\%
				CASE 0
					COL = FLAG:무효컬러
					LOCALS = 무효
				CASE IS < 100
					COL = FLAG:내성컬러
					LOCALS = {LOCAL:1}\%
				CASE 100
					COL = 0
					LOCALS = {LOCAL:1}\%
				CASE IS > 100
					COL = FLAG:약점컬러
					LOCALS = {LOCAL:1}\%
				ENDSELECT
				SIF COL
					SETCOLOR COL / 1000000 , COL /1000 % 1000 , COL % 1000
				PRINTFORM %GET_TYPE(RESIST)%%LOCALS,8%　
				RESETCOLOR
				SIF RESIST % 6 == 5
					PRINTL
			NEXT
			WAIT
		ELSE
			CLEARLINE 1
			REUSELASTLINE [{RESULT}]는 무효인 값입니다
			GOTO INP
		ENDIF
		CLEARLINE LINECOUNT-LINE:1
		GOTO SEARCH
	ELSE
		PRINTFORMW 해당 없음
		GOTO TOP
	ENDIF
ENDIF
CLEARLINE LINECOUNT-LINE
GOTO TOP

@RESIST_LOOKUP_DEVIL_SETPETTERN(ARG,ARG:1)
IF LOCAL:1 == 0
	LOCAL:1 = 1
	LOCALS:0 =   상관없음
	LOCALS:1 =   약점
	LOCALS:2 =   통상
	LOCALS:3 =   내성
	LOCALS:4 =   무효
	LOCALS:5 =   흡수
	LOCALS:6 =   반사
	LOCALS:7 =   취소
ENDIF
FOR LOCAL,0,8
	SIF LOCAL == ARG:1
		SETCOLOR COLOR("aqua")
	PRINTFORML [{LOCAL,2}] %LOCALS:LOCAL%
	RESETCOLOR
NEXT
ONEINPUT
SIF RESULT == 7
	RETURN -1
RETURN RESULT

@CHECK_DEVIL_RESIST(ARG,ARG:1,ARG:2,ARG:3)
#DIM CSVRES
CSVRES = CSVBASE(ARG,GETNUM(BASE,"검격")+ARG:1,0)
;パターン1：약점
SIF ARG:2 == 1 && CSVRES > 100 && CSVRES != 999
	RETURN 1
;パターン2:通常
SIF ARG:2 == 2 && ( CSVRES == 100 || ( ARG:3 && CSVRES < 100 ) || ( ARG:3 && CSVRES == 999 ) )
	RETURN 1
;パターン3:내성
SIF ARG:2 == 3 && ( INRANGE(CSVRES,1,99) || ( ARG:3 && CSVRES <= 0 ) || ( ARG:3 && CSVRES == 999 ) )
	RETURN 1
;パターン4:無効 
SIF ARG:2 == 4 && ( CSVRES == 0 || ( ARG:3 && CSVRES < 0 ) || ( ARG:3 && CSVRES == 999 ) )
	RETURN 1
;パターン5:흡수 
SIF ARG:2 == 5 && CSVRES < 0
	RETURN 1
;パターン6:반사
SIF ARG:2 == 6 && CSVRES == 999
	RETURN 1
RETURN 0

;=========================================================================================
@ALI_LOOKUP_DEVIL
#DIM LINE,2
#DIM ALI_LC
#DIM ALI_LD
#DIM SEX
$TOP
LINE = LINECOUNT
PRINT L-D축 
FOR LOCAL,0,4
	SIF ALI_LD == LOCAL
		SETCOLOR COLOR("aqua")
	IF !LOCAL
		PRINTBUTTON @"[{LOCAL,2}] %"상관없음",8,LEFT%",LOCAL
	ELSE
		PRINTBUTTON @"[{LOCAL,2}] %GET_ALI1(LOCAL),8,LEFT%",LOCAL
	ENDIF
	RESETCOLOR
NEXT
PRINTL
PRINT L-C축
FOR LOCAL,0,4
	SIF ALI_LC == LOCAL
		SETCOLOR COLOR("aqua")
	IF !LOCAL
		PRINTBUTTON @"[{LOCAL+4,2}] %"상관없음",8,LEFT%",LOCAL+4
	ELSE
		PRINTBUTTON @"[{LOCAL+4,2}] %GET_ALI2(LOCAL),8,LEFT%",LOCAL+4
	ENDIF
	RESETCOLOR
NEXT
PRINTL
PRINT 성별  
LOCALS = ♂＋♀
LOCALS:1 = ♀만
LOCALS:2 = ♂만
FOR LOCAL,0,3
	SIF SEX == LOCAL
		SETCOLOR COLOR("aqua")
	PRINTBUTTON @"[{LOCAL+10,2}] %LOCALS:LOCAL,8,LEFT%",LOCAL+10
	RESETCOLOR
NEXT
PRINTL
IF !ALI_LD && !ALI_LC
	SETCOLOR COLOR("gray")
	PRINTPLAIN [18] 검색 시작
	PRINTL
	RESETCOLOR
ELSE
	PRINTL [18] 검색 시작
ENDIF
PRINTL [19] 돌아간다
INPUT
LOCAL:1 = RESULT
IF INRANGE(RESULT,0,3)
	ALI_LD = RESULT
ELSEIF INRANGE(RESULT,4,7)
	ALI_LC = RESULT-4
ELSEIF INRANGE(RESULT,10,12)
	SEX = RESULT-10
ELSEIF RESULT == 19
	RETURN
ELSEIF RESULT == 18 && (ALI_LC || ALI_LD)
	LINE:1 = LINECOUNT
	$SEARCH
	CUSTOMDRAWLINE =
	LOCAL:1 = 0
	FOR LOCAL, 1, 1900
		SIF FLAG:(20000+LOCAL) < 100
			CONTINUE
		SIF !EXISTCSV(LOCAL, 0)
			CONTINUE
		SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"전서소환불가")),0) == 1
			CONTINUE
		SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"합체조건있음")),0) == 1 && FLAG:(10000+LOCAL) == 0
			CONTINUE

		;ここに조건を入れる
		SIF ALI_LD && CSVABL(LOCAL,GETNUM(ABL,"속성LD"),0) != ALI_LD
			CONTINUE
		SIF ALI_LC && CSVABL(LOCAL,GETNUM(ABL,"속성LC"),0) != ALI_LC
			CONTINUE
		SIF SEX == 1 && CSVTALENT(LOCAL,GETNUM(TALENT,"남자"),0)
			CONTINUE
		SIF SEX == 2 && !CSVTALENT(LOCAL,GETNUM(TALENT,"남자"),0)
			CONTINUE
		;조건ここまで

		IF CSVTALENT(LOCAL,GETNUM(TALENT,"남자"),0)
			IF CSVTALENT(LOCAL,GETNUM(TALENT,"오토코노코"),0)
				SETCOLOR 0x80FF80
			ELSE
				SETCOLOR 0x8080FF
			ENDIF
		ELSE
			SETCOLOR 0xFF8080
		ENDIF
		PRINTPLAINFORM %@"[{LOCAL,4}] %CSVNAME(LOCAL, 0)%(%CSV종족명(LOCAL,0)%)",36,LEFT%
		;PRINTFORM %@"%CSVNAME(LOCAL, 0)%(\@CSVCSTR(LOCAL, 0) == "" ? %STR:(CSVABL(LOCAL, GETNUM(ABL, "종족", 0), 0)%#%CSVCSTR(LOCAL, 0)%\@)",30,LEFT%
		SIF LOCAL:1 % 3 == 2
			PRINTL 
		LOCAL:1++
		RESETCOLOR
	NEXT
	SIF !LINEISEMPTY()
		PRINTL
	CUSTOMDRAWLINE =
	IF LOCAL:1
		PRINTFORML {LOCAL:1}건이 검색되었습니다
	ELSE
		PRINTFORMW 해당 없음
	ENDIF
	GOTO TOP
ENDIF
CLEARLINE LINECOUNT-LINE
GOTO TOP

