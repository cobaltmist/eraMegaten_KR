;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:FUSION.ERB
;	Facility	:悪魔合体のベースとなる処理群
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;	002		2011/01/28		P						ジェンダーフリーの処理を追加
;	003		2011/02/03		名無し					手紙システムで汎用だけでなく個人口上にも対応
;	004		2011/02/05		Rsp暇人					重複習得可能スキルは複数スキル継承するように変更
;	005		2011/02/16		Ｎ鳥					ストレージ機能に対応・継承方式変更
;	006		2011/02/22		Rsp暇人					特殊合体時用処理を呼び出すタイミングを追加
;	007		2011/03/08		P						造魔強化合体が実行不能なときでも表示されていた場合があったのを修正。また判定を変更
;	008		2011/05/19		P						コーディング処理を追加。コーディングのためにCHARANUMを全部回す処理が出来たので、シフターイケニエの判定を統合
;	009		2011/06/13		名無し					3身合体の素材スキル表示にミスがあったのを修正
;	010		2012/01/06		TR						悪魔ストレージを邪教の館から呼び出せるように、ボタンの数字表記を右揃えで整列
;	011		2013/01/12		ネトリス				合体用ソフトクイック設定を追加
;	012		2012/01/16		TR						「ラジエルの書」適用時に一部悪魔の素質や変異先を変更する処理を追記
;	013		2013/12/02		ひみつ					リスト表示数設定と合体用ソフトクイック設定(改)を追加。スキル属性表示の適用範囲拡大。
;													合体でデビルシフターや喰奴が作られた場合の処理を追加。チラつき抑止。
;													スキル属性表示にキャラ情報を渡すように
;	014		2015/01/01		セーロGUN				館内変身悪魔変更コマンドから戦闘用素質が複合したデビルシフターを弾く処理を追加
;	015		2017/06/23		kuni					キャラ表示でINPUT_CHARA_LISTを使用するように変更
;	016		2015/10/02		魔晶拡張パッチ			魔晶武器作成に分岐処理を追加
;	017		2017/09/23		名無					ガチャモード(「ガチャ」素質継承)
;	018		2017/10/03		名無					悪魔合体ライト処理追加
;	019		2018/10/13		しぐれみそ				魔晶武器作成に杖作成の分岐処理を追加
;	020		2019/08/28		空気					COMPに仲魔が加入できる余裕があるかどうか判定する関数を導入
;	021		2019/10/04		なんとなくの人			スキルカード確認コマンドを追加
;	022		2019/11/21		JK好き					@CASTING_CHANGE_LINKをIF文に使えるように#FUNCTION対応
;	023		2019/12/02		JK好き					@CASTING_CHANGE_LINK_Fを新たに追加して@CASTING_CHANGE_LINKは差し戻しました
;	024		2020/08/31		JK好き					達人悪魔は素材のスキルを継承できないように変更
;	025		2020/12/20		JK好き					ABL引き継ぎチェックに素材:3を追加
;	026		2021/12/30		楽中					時間城に向かう処理の修正
;	027		2022/06/01		Collared				陥落合体に関する処理を追加
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
@SHOPCOMABLE_108
RESULT:1 = 1
RESULTS = 사교의관
RETURN 1



@SHOP_COM_108
#LOCALSIZE 10
;---- EDIT 008 ADD START -------------------------
VARSET LOCAL
;---- EDIT 008 ADD END   -------------------------
;---- EDIT 010 MOD START -------------------------
CUSTOMDRAWLINE =
PRINTL [ 1] ２신 합체
PRINTL [ 2] ３신 합체
SIF ITEM:도리・카드몬 > 0
	PRINTL [ 3] 조마 작성
SIF NUM_ZOUMA() > 0 && NUM_FUSIONABLE() > 0
	PRINTL [ 4] 조마 강화 합체
;メアリが居る場合は必要な造魔の数に+1
SIF NUM_ZOUMA() > (GETCHARA(4509) > -1) ? 1 # 0
	PRINTL [ 5] 조마 초기화
SIF ITEM:무명의도 || EQUIP:MASTER:마장의공방
	PRINTL [ 6] 마정무구 작성
FOR LOCAL,2000,VARSIZE("ITEM")
	IF 마정장비(LOCAL)
		IF ITEM:LOCAL > 0
			PRINTL [ 7] 마정무구 소재화
			LOCAL:3 = 1
			BREAK
		ENDIF
	ELSE
	ENDIF
NEXT

PRINTL [ 8] 스킬 카드 작성
;---- EDIT 008 MOD START -------------------------
FOR LOCAL,0,CHARANUM
	SIF 계약(LOCAL) > 0 && ABL:LOCAL:종족 > 0 && ABL:LOCAL:종족 < 45
		LOCAL:4 = 1
	SIF TALENT:LOCAL:데빌시프터
		LOCAL:5 = 1
	IF FLAG:(NO:LOCAL + 20000) == 1000
		IF ABL:LOCAL:코딩 > 0 && ITEM:(ABL:LOCAL:코딩) == 0
			LOCAL:6 = 1
		ELSEIF CSVABL(NO:LOCAL, GETNUM(ABL, "코딩") ,0) > 0 && ITEM:(CSVABL(NO:LOCAL, GETNUM(ABL, "코딩"), 0)) == 0
			LOCAL:6 = 1
		ENDIF
		IF ABL:LOCAL:코딩 == 0
			IF (ABL:LOCAL:종족 == 1 && ITEM:8056 == 0) || (ABL:LOCAL:종족 == 2 && ITEM:8067 == 0)
				LOCAL:6 = 1
			ELSEIF (ABL:LOCAL:종족 == 3 && ITEM:8071 == 0) || (ABL:LOCAL:종족 == 4 && ITEM:8058 == 0)
				LOCAL:6 = 1
			ELSEIF (ABL:LOCAL:종족 == 5 && ITEM:8065 == 0) || (ABL:LOCAL:종족 == 6 && TALENT:LOCAL:남자 && ITEM:8064 == 0)
				LOCAL:6 = 1
			ELSEIF (ABL:LOCAL:종족 == 6 && !TALENT:LOCAL:남자 && ITEM:8055 == 0) || (ABL:LOCAL:종족 == 7 && ITEM:8063 == 0)
				LOCAL:6 = 1
			ELSEIF (ABL:LOCAL:종족 == 8 && TALENT:LOCAL:남자 && ITEM:8066 == 0) || (ABL:LOCAL:종족 == 8 && !TALENT:LOCAL:남자 && ITEM:8060 == 0)
				LOCAL:6 = 1
			ELSEIF (ABL:LOCAL:종족 == 9 && ITEM:8069 == 0) || (ABL:LOCAL:종족 == 10 && ITEM:8068 == 0)
				LOCAL:6 = 1
			ELSEIF (ABL:LOCAL:종족 == 11 && TALENT:LOCAL:남자 && ITEM:8059 == 0) || (ABL:LOCAL:종족 == 11 && !TALENT:LOCAL:남자 && ITEM:8062 == 0)
				LOCAL:6 = 1
			ELSEIF (ABL:LOCAL:종족 == 15 && ITEM:8061 == 0) || (ABL:LOCAL:종족 == 16 && ITEM:8070 == 0)
				LOCAL:6 = 1
			ELSEIF ABL:LOCAL:종족 == 18 && ITEM:8057 == 0
				LOCAL:6 = 1
			ENDIF
		ENDIF
	ENDIF
NEXT
SIF LOCAL:4 == 1
	PRINTL [ 9] 제물 합체
;---- EDIT 010 MOD END   -------------------------
PRINTL [10] 악마전서
SIF LOCAL:5 == 1
	PRINTL [11] 변신악마 변경
SIF LOCAL:6 == 1
	PRINTL [12] 코딩
;---- EDIT 008 MOD END   -------------------------
;---- EDIT 010 ADD START -------------------------
PRINTL [13] 악마서버전송
;---- EDIT 010 ADD END ---------------------------
;---- EDIT WITH YOU ADD START -------------------------
SIF TALENT:MASTER:144
	PRINTL [14] 당신 합체
;---- EDIT WITH YOU ADD END ---------------------------
SIF GETBIT(FLAG:발견ED, 30)
	PRINTL [15] 시간성

	;---- EDIT 027 ADD START -------------------------
	PRINTL [17] 함락 2신합체
	PRINTL [18] 함락 3신합체
	PRINTL [99] 여기는 어디？
	;---- EDIT 027 ADD END -------------------------
	
CUSTOMDRAWLINE =
PRINTL [ 0]돌아간다

$INPUT_LOOP
INPUT
IF RESULT == 1
	CALL FUSION_DOUBLE
ELSEIF RESULT == 2
	CALL FUSION_DOUBLE,3
ELSEIF RESULT == 3 && ITEM:도리・카드몬 > 0
	CALL MAKE_ZOUMA
ELSEIF RESULT == 4 && NUM_ZOUMA() > 0 && NUM_FUSIONABLE() > 0
	CALL FUSION_ZOUMA
ELSEIF RESULT == 5 && NUM_ZOUMA() > (GETCHARA(4509) > -1) ? 1 # 0
	CALL DEL_ZOUMA
ELSEIF RESULT == 6 && ((ITEM:무명의도) || (EQUIP:MASTER:마장의공방))
	PRINTL [ 1]마정검작성
	PRINTL [ 2]마정지팡이작성
	DRAWLINE
	PRINTL [0]돌아간다
	
	ONEINPUT
	
	IF RESULT == 1
		SIF EQUIP:MASTER:마장의공방
			CALL SELECT_MASHO2
		SIF !EQUIP:MASTER:마장의공방
		CALL MAKE_MASHO
	ELSEIF RESULT == 2
		CALL MAKE_MASHO_STAFF
	ELSEIF RESULT == 0
		RETURN 0
	ENDIF
ELSEIF RESULT == 7 && LOCAL:3
	CALL DEL_MASHO
ELSEIF RESULT == 8
	CALL MAKE_SKILLCARD
ELSEIF RESULT == 9 && LOCAL:4
	CALL FUSION_SACRIFICE
ELSEIF RESULT == 10
	CALL DEVIL_COMPENDIUM
ELSEIF RESULT == 0
	RETURN 0
ELSEIF RESULT == 11 && LOCAL:5
	CALL CHANGE_LINK
;---- EDIT 008 ADD START -------------------------
ELSEIF RESULT == 12 && LOCAL:6
;---- EDIT 008 ADD END   -------------------------
	CALL CODING
;---- EDIT 010 ADD START -------------------------
ELSEIF RESULT == 13
	CALL DEVIL_STORAGE
;---- EDIT 010 ADD END ---------------------------
;---- EDIT WITH YOU ADD START -------------------------
ELSEIF RESULT == 14
	CALL FUSION_WITH_YOU
;---- EDIT WITH YOU ADD END ---------------------------
ELSEIF RESULT == 15
	IF GETBIT(FLAG:발견ED, 30)
		CALL CASTLE_OF_WATCH
	ELSE
		PRINTL 시간성 이사 예정지라고 쓰여져 있다……
		CALL SHOP_COM_108
	ENDIF
ELSEIF RESULT == 16
	CALL SKILLCARD_CHECK

;---- EDIT 027 ADD START -------------------------
ELSEIF RESULT == 17
	FLAG:함락합체 = 1
	CALL FUSION_DOUBLE
ELSEIF RESULT == 18
	FLAG:함락합체 = 1
	CALL FUSION_DOUBLE,3
ELSEIF RESULT == 99
	CALL MIRKO
;---- EDIT 027 ADD END -------------------------

ELSE
	GOTO INPUT_LOOP
ENDIF
;末尾キャラがフィルタリングフラグを持っていない場合（新キャラを作った場合）ソート実行
;IF !CFLAG:(CHARANUM-1):필터링플래그
;	FOR LOCAL:10, 0, CHARANUM - 1
;		IF CFLAG:(LOCAL:10):필터링플래그
;			FOR LOCAL:11, LOCAL:10, CHARANUM
;				SIF CFLAG:(LOCAL:11):필터링플래그
;					CONTINUE
;				SIF TARGET == LOCAL:11
;					TARGET = LOCAL:10
;				SWAPCHARA LOCAL:10, LOCAL:11
;				BREAK
;			NEXT
;		ENDIF
;	NEXT
;	;一応ポジション復帰
;	CALL REFRESH_POS
;ENDIF
RESTART
;================================================================
;２身合体
;================================================================
@FUSION_DOUBLE, ARG = 2, ARG:1 = 0, ARG:2 = -1
;ARG:1 探索中フラグ、なおここが2なら悪魔合体ライトとする
;ARG:2 合体者。悪魔合体ライトで万が一自身を合体소재にするのを防ぐため。
#DIM LLINE
#DIM LREDRAW
#DIM LLOCK
#DIM PCOUNT, 2
#DIM SKILLCOLOR,1
#DIM 소재 , 4
#DIM 完成品 , 1
#DIM レベル不足合体 , 1
#DIM LCOUNT

LREDRAW = LLOCK ? LREDRAW # CURRENTREDRAW()
SIF !LLOCK
	REDRAW 2
LLOCK = 1

;素材:1、完成品　一体目と２体目の登録番号
소재:1 = -1
소재:2 = -1
소재:3 = -1
FLAG:합체인수 = ARG
LOCAL:11 = 0
FOR LCOUNT, 500, 600
	IF ITEM:LCOUNT
		IF SOFT_IS_FUSIONSOFT(LCOUNT, ARG:1)
			LOCAL:11 = 1
			BREAK
		ENDIF
	ENDIF
NEXT
$START_FUSION
FLAG:합체예정악마1 = 소재:1
FLAG:합체예정악마2 = 소재:2
LOCAL:6 = 0
LOCAL:9 = 0

$PRINT_LIST
Z = ARG:1
CALL INPUT_CHARA_LIST("합체 시키고 싶은 악마를 선택하세요", "CASTING_FUSION_DOUBLE", "UP_PRINT_FUSION_DOUBLE", "DOWN_PRINT_FUSION_DOUBLE", "MODECHANGE_FUSION_DOUBLE")

IF RESULT == 1000
	IF 소재:2 > -1
		소재:2 = -1
		GOTO START_FUSION
	ELSEIF 소재:1 > -1
		소재:1 = -1
		GOTO START_FUSION
	ELSE
		;---- EDIT 027 ADD START -------------------------
		;一応陥落合体をオフにする。
		FLAG:함락합체 = 0
		;---- EDIT 027 ADD END -------------------------
		REDRAW LREDRAW
		LLOCK = 0
		RETURN 0
	ENDIF
ELSEIF RESULT == 1010
	IF LOCAL:11
		;CALL SETUP_SOFT, 500, ARG:1, ARG:1
		CALL SETUP_SOFT, 500, 1, ARG:1
	ENDIF
	GOTO PRINT_LIST
ELSEIF RESULT == 1011 || RESULT == 1012 || RESULT == 1013 || RESULT == 1014
	FLAG:이신합체시남녀표시 = RESULT - 1011
	GOTO START_FUSION

;ELSEIF GETBIT(FLAG:합체용소프트퀵설정, 6) == 1 && (RESULT == 1020 || RESULT == 1021 || RESULT == 1022 || RESULT == 1023 || RESULT == 1024 || RESULT == 1025)
;	SELECTCASE RESULT
;		CASE 1020
;			IF EQUIP:MASTER:알버트 == 1
;				EQUIP:MASTER:알버트 = 0
;			ELSEIF EQUIP:MASTER:알버트 == 0 && FLAG:ＣＯＭＰ용량-NUM_NAKAMA()-소프트용량() >= SOFT_SIZE(500)
;				EQUIP:MASTER:알버트 = 1
;			ENDIF
;		CASE 1021
;			IF EQUIP:MASTER:파라켈수스 == 1
;				EQUIP:MASTER:파라켈수스 = 0
;			ELSEIF EQUIP:MASTER:파라켈수스 == 0 && FLAG:ＣＯＭＰ용량-NUM_NAKAMA()-소프트용량() >= SOFT_SIZE(534)
;				EQUIP:MASTER:파라켈수스 = 1
;			ENDIF
;		CASE 1022
;			IF EQUIP:MASTER:라플라스 == 1
;				EQUIP:MASTER:라플라스 = 0
;			ELSEIF EQUIP:MASTER:라플라스 == 0 && FLAG:ＣＯＭＰ용량-NUM_NAKAMA()-소프트용량() >= SOFT_SIZE(536)
;				EQUIP:MASTER:라플라스 = 1
;			ENDIF
;		CASE 1023
;			IF EQUIP:MASTER:코페르니쿠스 == 1
;				EQUIP:MASTER:코페르니쿠스 = 0
;			ELSEIF EQUIP:MASTER:코페르니쿠스 == 0 && FLAG:ＣＯＭＰ용량-NUM_NAKAMA()-소프트용량() >= SOFT_SIZE(537)
;				EQUIP:MASTER:코페르니쿠스 = 1
;			ENDIF
;		CASE 1024
;			IF EQUIP:MASTER:고에티아 == 1
;				EQUIP:MASTER:고에티아 = 0
;			ELSEIF EQUIP:MASTER:고에티아 == 0
;				EQUIP:MASTER:고에티아 = 1
;			ENDIF
;		CASE 1025
;			IF EQUIP:MASTER:라지엘의서 == 1
;				EQUIP:MASTER:라지엘의서 = 0
;			ELSEIF EQUIP:MASTER:라지엘의서 == 0
;				EQUIP:MASTER:라지엘의서 = 1
;			ENDIF
;	ENDSELECT
;	GOTO PRINT_LIST

;==합체용소프트퀵설정 その２ =========================Start
ELSEIF INRANGE(RESULT, 1500, 1599) && SOFT_IS_FUSIONSOFT(RESULT - 1000, ARG:1) && FSQS_BIT(RESULT - 1000)
	IF EQUIP:MASTER:(ITEMNAME:(RESULT - 1000))
		EQUIP:MASTER:(ITEMNAME:(RESULT - 1000)) = 0
	ELSEIF FLAG:ＣＯＭＰ용량 - NUM_NAKAMA() - 소프트용량() >= SOFT_SIZE(RESULT - 1000)
		EQUIP:MASTER:(ITEMNAME:(RESULT - 1000)) = 1
	ENDIF
	GOTO PRINT_LIST
;===========================================================End
ENDIF
;---- EDIT 018 MOD START -------------------------
;悪魔召喚ライトなら戦闘中、召喚されようとしている悪魔や敵、自分自身を소재に出来ない。
IF Z == 2 && RESULT > -1
	SIF FLAG:DEBUG
		PRINTFORMW 디버그용　소환대상번호RESULT={RESULT}
	IF CFLAG:RESULT:피소환플래그
		PRINTFORMW 다른 캐릭터에 소환하려 하고 있습니다.
		GOTO PRINT_LIST
	ELSEIF CFLAG:RESULT:PT플래그 <= 0
		PRINTFORMW 적은 합체 소재로 쓸 수 없습니다.
		GOTO PRINT_LIST
	ELSEIF RESULT == ARG:2
		PRINTFORMW 악마 합체 라이트로 자신을 합체 소재로 하는 것은 불가능합니다.
		GOTO PRINT_LIST
	ENDIF
;---- EDIT 027 ADD START -------------------------
ELSEIF FLAG:함락합체 == 1 && 소재:1 < 0 && 함락(RESULT) == 0
	PRINTFORMW 함락합체에서는 1체째가 함락되어 있어야 합니다。
	GOTO PRINT_LIST
;---- EDIT 027 ADD END -------------------------
ENDIF
;---- EDIT 018 MOD END -------------------------
IF 소재:2 > 0
	소재:3 = RESULT
ELSEIF 소재:1 > 0
	소재:2 = RESULT
ELSE
	소재:1 = RESULT
ENDIF

IF 소재:(FLAG:합체인수) > 0
	;合体人数が2なら소재:3 = -1なので投げてＯＫ
	CALL RESULT_FUSION,소재:1,소재:2,소재:3
	IF RESULT == 0
		소재:(FLAG:합체인수) = -1
		GOTO START_FUSION
	ENDIF
	完成品 = CHARANUM
	LOCAL:4 = RESULT
	SIF CSVCFLAG(LOCAL:4 , GETNUM(CFLAG,"１체만"),0) && FINDCHARA(NO,LOCAL:4) > -1
		LOCAL:9 = 1
	;合体結果の悪魔が既に仲魔(未契約かつＣＯＭＰに居る)である場合
	SIF FINDCHARA_NO_C(LOCAL:4) > -1
		LOCAL:6 = 1
	
	;一旦合体結果の悪魔をキャラ登録
	IF FLAG:합체인수 == 2
		CALL ADD_NEW_COMPANION,LOCAL:4,MAX(100 , (BASE:(소재:1):충성도 + BASE:(소재:2):충성도)/2), -1
	ELSE
		CALL ADD_NEW_COMPANION,LOCAL:4,MAX(100 , (BASE:(소재:1):충성도 + BASE:(소재:2):충성도 + BASE:(소재:3):충성도)/3), -1
	ENDIF
	;なんらかの理由でキャラ登録できない場合
	IF RESULT == 0
		PRINTW 캐릭터 등록이 불가능하므로 합체 처리를 중단합니다
		REDRAW LREDRAW
		LLOCK = 0
		RETURN 0
	ENDIF
	;---- EDIT 006 ADD START -------------------------
	;特殊合体時専用処理があれば呼び出し
	TRYCALLFORM FUSION_SPECIAL_PROCESS_{LOCAL:4}, 完成品, 소재:1, 소재:2 , 소재:3
	;---- EDIT 006 ADD END -------------------------
	;スキル継承
	;達人悪魔はスキルを継承できないように修正
	IF !TALENT:完成品:달인
		CALL SUCCESS_SKILL,完成品,소재:1,소재:2,소재:3,0
		;邪教の館なら更に素質のないスキルも継承
		SIF FLAG:SHOP커맨드 == [[SHOP:사교의관]]
			CALL SUCCESS_SKILL,完成品,소재:1,소재:2,소재:3,1
	ENDIF
	;邪教の館なら更に素質も継承
	SIF FLAG:SHOP커맨드 == [[SHOP:사교의관]]
		CALL SUCCESS_TALENT,完成品,소재:1,소재:2,소재:3
	;----------------------------------------
	;ガチャモード時、強引に素質継承処理
	;----------------------------------------
	IF FLAG:악마가챠모드 > 0
		TALENT:完成品:ガチャ = 1
		SIF 소재:1 > -1
			TALENT:完成品:ガチャ *= TALENT:(소재:1):ガチャ
		SIF 소재:2 > -1
			TALENT:完成品:ガチャ *= TALENT:(소재:1):ガチャ
		SIF 소재:3 > -1
			TALENT:完成品:ガチャ *= TALENT:(소재:1):ガチャ
	ENDIF
	;---- EDIT 011 ADD START ----------------------
	;라지엘의서장비中なら一部素質や랭크アップ先を変化
	SIF EQUIP:MASTER:라지엘의서 && EXTRACHECK_DEVIL_CHILDREN(NO:完成品)
		CALL EXTRA_DEVIL_CHILDREN , 完成品
	;---- EDIT 011 ADD END ------------------------
	;스테이터스강화
	CALL FUSION_ENHANCE_STATUS,完成品,소재:1,소재:2,소재:3
	;조교効果による能力アップ
	;BASE:(完成品):힘 +=   ((ABL:(소재:1):욕망+1) + (ABL:(소재:2):욕망+1))/4
	;BASE:(完成品):지혜 += ((ABL:(소재:1):기교+1) + (ABL:(소재:2):기교+1))/4
	;BASE:(完成品):마력 += ((ABL:(소재:1):욕망+1) + (ABL:(소재:2):욕망+1))/4
	;BASE:(完成品):인내력 += ((ABL:(소재:1):순종+1) + (ABL:(소재:2):순종+1))/4
	;BASE:(完成品):속도 += ((ABL:(소재:1):기교+1) + (ABL:(소재:2):기교+1))/4
	;BASE:(完成品):운 +=   ((ABL:(소재:1):순종+1) + (ABL:(소재:2):순종+1))/4
	;----- 변신する악마向けというか主に東方ＭＯＤ用 はじめ -----
	IF TALENT:完成品:데빌시프터 || TALENT:完成品:식노
		FOR PCOUNT, 1, FLAG:스킬수 + 1
			ABL:完成品:@"인간시스킬{PCOUNT}" = ABL:完成品:@"스킬{PCOUNT}"
		NEXT
	ENDIF
	;----- 변신する악마向けというか主に東方ＭＯＤ用 결론 -----
	CALL SYNC_STATUS,完成品
	
	
	;표시用に충성도を변경しておく
	;BASE:(完成品):충성도 = MAX(100,(BASE:(소재:1):충성도+BASE:(소재:2):충성도)/2)
	レベル不足合体 = 0
	IF (BASE:(完成品):LV > BASE:MASTER:LV) && (EQUIP:MASTER:스티븐 == 0)
		IF BASE:完成品:충성도 >= POWER(BASE:(完成品):LV - (BASE:MASTER:LV + FLAG:경험ED수) , 2)*500
			レベル不足合体 = 1
			BASE:完成品:충성도 -= POWER(BASE:(完成品):LV - (BASE:MASTER:LV + FLAG:경험ED수) , 2)*500
		ELSE
		ENDIF
	ENDIF
	
	CUSTOMDRAWLINE =
	;合体結果の스테이터스を표시
	U = 完成品
	;스테이터스표시
	LOCAL:10 = 2
	LLINE = LINECOUNT
	$PRINT_STATUS
	CLEARLINE LINECOUNT - LLINE
	PRINTFORM %종족명(U)%　　
	PRINTFORML %CALLNAME:U%　　
	RESETCOLOR
	FLAG:이신합체표시 = 1
	CALLFORM SHOW_INFO_PAGE_{LOCAL:10}
	IF LOCAL:10 == 2
		IF GETBIT(FLAG:스테이터스화면타입,1)
			PRINTFORM 소지스킬(합체 후:%CALLNAME:(完成品)%)
				FOR PCOUNT,1,21
					IF ABL:(完成品):@"습득스킬{PCOUNT}" > 0
						IF PCOUNT:1 == 0
							PCOUNT:1 = PCOUNT
						ELSE
							SIF ABL:(完成品):@"습득LV{PCOUNT:1}" > ABL:(完成品):@"습득LV{PCOUNT}"
								PCOUNT:1 = PCOUNT
						ENDIF
					ENDIF
				NEXT
				IF PCOUNT:1 > 0
					IF ABL:(完成品):@"습득LV{PCOUNT:1}" > (BASE:(完成品):LV)+5
						SETCOLOR 0x666666
						PRINTFORML    NEXT[????????]  습득LV {ABL:(完成品):@"습득LV{PCOUNT:1}"}
						RESETCOLOR
					ELSE
						CALLFORM SKILL_NAME_{ABL:(完成品):@"습득스킬{PCOUNT:1}"},完成品
						SETCOLOR 0x666666
						PRINTFORML    NEXT[%RESULTS%]  습득LV {ABL:(完成品):@"습득LV{PCOUNT:1}"}
						RESETCOLOR
					ENDIF
				ELSE
					PRINTL
				ENDIF
			FOR PCOUNT,1,FLAG:스킬수+1
				IF ABL:(完成品):@"스킬{PCOUNT}"
					PRINTFORM 　[{PCOUNT,2}]
					SKILLCOLOR = 0
					SIF GROUPMATCH(ABL:(完成品):@"스킬{PCOUNT}", ABL:(소재:1):스킬1, ABL:(소재:1):스킬2, ABL:(소재:1):스킬3, ABL:(소재:1):스킬4, ABL:(소재:1):스킬5, ABL:(소재:1):스킬6, ABL:(소재:1):스킬7, ABL:(소재:1):스킬8)
						SKILLCOLOR |= 1
					SIF GROUPMATCH(ABL:(完成品):@"스킬{PCOUNT}", ABL:(소재:2):스킬1, ABL:(소재:2):스킬2, ABL:(소재:2):스킬3, ABL:(소재:2):스킬4, ABL:(소재:2):스킬5, ABL:(소재:2):스킬6, ABL:(소재:2):스킬7, ABL:(소재:2):스킬8)
						SKILLCOLOR |= 2
					SIF 소재:3 > 0 && GROUPMATCH(ABL:(完成品):@"스킬{PCOUNT}", ABL:(소재:3):스킬1, ABL:(소재:3):스킬2, ABL:(소재:3):스킬3, ABL:(소재:3):스킬4, ABL:(소재:3):스킬5, ABL:(소재:3):스킬6, ABL:(소재:3):스킬7, ABL:(소재:3):스킬8)
						SKILLCOLOR |= 2
					IF SKILLCOLOR == 3
						SETCOLOR 0xff69b4
					ELSEIF SKILLCOLOR == 2
						SETCOLOR 0x00ff00
					ELSEIF SKILLCOLOR == 1
						SETCOLOR COLOR("aqua")
					ENDIF
					SIF GROUPMATCH(ABL:(完成品):@"스킬{PCOUNT}", CSVABL(NO:(完成品), GETNUM(ABL, "스킬1"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬2"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬3"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬4"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬5"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬6"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬7"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬8"), 0))
						RESETCOLOR
					CALLFORM SKILL_NAME_{ABL:(完成品):@"스킬{PCOUNT}"},完成品
					IF GETBIT(FLAG:커스텀게임화면,1)
						PRINTFORM %RESULTS,38,LEFT%
					ELSE
						PRINTFORM %RESULTS,20,LEFT%
					ENDIF
				ENDIF
				RESETCOLOR
				IF GETBIT(FLAG:커스텀게임화면,1)
					SIF PCOUNT  == 3 || PCOUNT  == 6 || PCOUNT  == 8
						PRINTL
				ELSE
					SIF PCOUNT % 4 == 0
						PRINTL
			ENDIF
			NEXT
			PCOUNT:1 = 0
			IF GETBIT(FLAG:커스텀게임화면,1) == 0
				FOR PCOUNT,1,21
					IF ABL:(完成品):@"습득스킬{PCOUNT}" > 0
						IF PCOUNT:1 == 0
							PCOUNT:1 = PCOUNT
						ELSE
							SIF ABL:(完成品):@"습득LV{PCOUNT:1}" > ABL:(完成品):@"습득LV{PCOUNT}"
								PCOUNT:1 = PCOUNT
						ENDIF
					ENDIF
				NEXT
				IF PCOUNT:1 > 0
					IF ABL:(完成品):@"습득LV{PCOUNT:1}" > (BASE:(完成品):LV)+5
						SETCOLOR 0x666666
						PRINTFORML    NEXT[????????]  습득LV {ABL:(完成品):@"습득LV{PCOUNT:1}"}
						RESETCOLOR
					ELSE
						CALLFORM SKILL_NAME_{ABL:(完成品):@"습득스킬{PCOUNT:1}"},完成品
						SETCOLOR 0x666666
						PRINTFORML    NEXT[%RESULTS%]  습득LV {ABL:(完成品):@"습득LV{PCOUNT:1}"}
						RESETCOLOR
					ENDIF
				ENDIF
			ENDIF
			DRAWLINE
		ENDIF
		PRINTFORML 소지 스킬(첫번째:%CALLNAME:(소재:1)%)
		FOR PCOUNT,1,FLAG:스킬수+1
			IF ABL:(소재:1):@"스킬{PCOUNT}"
				PRINTFORM 　[{PCOUNT+10,2}]
				SKILLCOLOR = 0
				SIF GROUPMATCH(ABL:(소재:1):@"스킬{PCOUNT}", ABL:(完成品):스킬1, ABL:(完成品):스킬2, ABL:(完成品):스킬3, ABL:(完成品):스킬4, ABL:(完成品):스킬5, ABL:(完成品):스킬6, ABL:(完成品):스킬7, ABL:(完成品):스킬8)
					SKILLCOLOR |= 1
				SIF GROUPMATCH(ABL:(소재:1):@"스킬{PCOUNT}", ABL:(소재:2):스킬1, ABL:(소재:2):스킬2, ABL:(소재:2):스킬3, ABL:(소재:2):스킬4, ABL:(소재:2):스킬5, ABL:(소재:2):스킬6, ABL:(소재:2):스킬7, ABL:(소재:2):스킬8)
					SKILLCOLOR |= 2
				SIF 소재:3 > 0 && GROUPMATCH(ABL:(소재:1):@"스킬{PCOUNT}", ABL:(소재:3):스킬1, ABL:(소재:3):스킬2, ABL:(소재:3):스킬3, ABL:(소재:3):스킬4, ABL:(소재:3):스킬5, ABL:(소재:3):스킬6, ABL:(소재:3):스킬7, ABL:(소재:3):스킬8)
					SKILLCOLOR |= 2
				IF SKILLCOLOR == 3
					SETCOLOR 0xff69b4
				ELSEIF SKILLCOLOR == 1
					SETCOLOR COLOR("aqua")
				ELSE
					SETCOLOR COLOR("gray")
				ENDIF
				SIF GROUPMATCH(ABL:(소재:1):@"스킬{PCOUNT}", CSVABL(NO:(完成品), GETNUM(ABL, "스킬1"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬2"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬3"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬4"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬5"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬6"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬7"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬8"), 0))
					RESETCOLOR
				CALLFORM SKILL_NAME_{ABL:(소재:1):@"스킬{PCOUNT}"},소재:1
				IF GETBIT(FLAG:커스텀게임화면,1)
					PRINTFORM %RESULTS,38,LEFT%
				ELSE
					PRINTFORM %RESULTS,20,LEFT%
				ENDIF
			ENDIF
			RESETCOLOR
			IF GETBIT(FLAG:커스텀게임화면,1)
				SIF PCOUNT  == 3 || PCOUNT  == 6 || PCOUNT  == 8
					PRINTL
			ELSE
				SIF PCOUNT % 4 == 0
					PRINTL
			ENDIF
		NEXT
		SIF GETBIT(FLAG:커스텀게임화면,1) == 0
			DRAWLINE
		PRINTFORML 소지 스킬(두번째:%CALLNAME:(소재:2)%)
		FOR PCOUNT,1,FLAG:스킬수+1
			IF ABL:(소재:2):@"스킬{PCOUNT}"
				PRINTFORM 　[{PCOUNT+20,2}]
				SKILLCOLOR = 0
				SIF GROUPMATCH(ABL:(소재:2):@"스킬{PCOUNT}", ABL:(完成品):스킬1, ABL:(完成品):스킬2, ABL:(完成品):스킬3, ABL:(完成品):스킬4, ABL:(完成品):스킬5, ABL:(完成品):스킬6, ABL:(完成品):스킬7, ABL:(完成品):스킬8)
					SKILLCOLOR |= 1
				SIF GROUPMATCH(ABL:(소재:2):@"스킬{PCOUNT}", ABL:(소재:1):스킬1, ABL:(소재:1):스킬2, ABL:(소재:1):스킬3, ABL:(소재:1):스킬4, ABL:(소재:1):스킬5, ABL:(소재:1):스킬6, ABL:(소재:1):스킬7, ABL:(소재:1):스킬8)
					SKILLCOLOR |= 2
				SIF 소재:3 > 0 && GROUPMATCH(ABL:(소재:2):@"스킬{PCOUNT}", ABL:(소재:3):스킬1, ABL:(소재:3):스킬2, ABL:(소재:3):스킬3, ABL:(소재:3):스킬4, ABL:(소재:3):스킬5, ABL:(소재:3):스킬6, ABL:(소재:3):스킬7, ABL:(소재:3):스킬8)
					SKILLCOLOR |= 2
				IF SKILLCOLOR == 3
					SETCOLOR 0xff69b4
				ELSEIF SKILLCOLOR == 1
					SETCOLOR 0x00ff00
				ELSE
					SETCOLOR COLOR("gray")
				ENDIF
				SIF GROUPMATCH(ABL:(소재:2):@"스킬{PCOUNT}", CSVABL(NO:(完成品), GETNUM(ABL, "스킬1"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬2"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬3"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬4"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬5"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬6"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬7"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬8"), 0))
					RESETCOLOR
				CALLFORM SKILL_NAME_{ABL:(소재:2):@"스킬{PCOUNT}"},소재:2
				IF GETBIT(FLAG:커스텀게임화면,1)
					PRINTFORM %RESULTS,38,LEFT%
				ELSE
					PRINTFORM %RESULTS,20,LEFT%
				ENDIF
			ENDIF
			RESETCOLOR
			IF GETBIT(FLAG:커스텀게임화면,1)
				SIF PCOUNT  == 3 || PCOUNT  == 6 || PCOUNT  == 8
					PRINTL
			ELSE
				SIF PCOUNT % 4 == 0
					PRINTL
			ENDIF
		NEXT
		IF 소재:3 > 0
			SIF GETBIT(FLAG:커스텀게임화면,1) == 0
				DRAWLINE
			PRINTFORML 소지 스킬(세번째:%CALLNAME:(소재:3)%)
			FOR PCOUNT,1,FLAG:스킬수+1
				IF ABL:(소재:3):@"스킬{PCOUNT}"
					PRINTFORM 　[{PCOUNT+30,2}]
					SKILLCOLOR = 0
					SIF GROUPMATCH(ABL:(소재:3):@"스킬{PCOUNT}", ABL:(完成品):스킬1, ABL:(完成品):스킬2, ABL:(完成品):스킬3, ABL:(完成品):스킬4, ABL:(完成品):스킬5, ABL:(完成品):스킬6, ABL:(完成品):스킬7, ABL:(完成品):스킬8)
						SKILLCOLOR |= 1
					SIF GROUPMATCH(ABL:(소재:3):@"스킬{PCOUNT}", ABL:(소재:1):스킬1, ABL:(소재:1):스킬2, ABL:(소재:1):스킬3, ABL:(소재:1):스킬4, ABL:(소재:1):스킬5, ABL:(소재:1):스킬6, ABL:(소재:1):스킬7, ABL:(소재:1):스킬8)
						SKILLCOLOR |= 2
					SIF 소재:3 > 0 && GROUPMATCH(ABL:(소재:3):@"스킬{PCOUNT}", ABL:(소재:2):스킬1, ABL:(소재:2):스킬2, ABL:(소재:2):스킬3, ABL:(소재:2):스킬4, ABL:(소재:2):스킬5, ABL:(소재:2):스킬6, ABL:(소재:2):스킬7, ABL:(소재:2):스킬8)
						SKILLCOLOR |= 2
					IF SKILLCOLOR == 3
						SETCOLOR 0xff69b4
					ELSEIF SKILLCOLOR == 1
						SETCOLOR 0x00ff00
					ELSE
						SETCOLOR COLOR("gray")
					ENDIF
					SIF GROUPMATCH(ABL:(소재:3):@"스킬{PCOUNT}", CSVABL(NO:(完成品), GETNUM(ABL, "스킬1"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬2"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬3"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬4"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬5"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬6"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬7"), 0), CSVABL(NO:(完成品), GETNUM(ABL, "스킬8"), 0))
						RESETCOLOR
					CALLFORM SKILL_NAME_{ABL:(소재:3):@"스킬{PCOUNT}"},소재:2
					IF GETBIT(FLAG:커스텀게임화면,1)
						PRINTFORM %RESULTS,38,LEFT%
					ELSE
						PRINTFORM %RESULTS,20,LEFT%
					ENDIF
				ENDIF
				RESETCOLOR
				IF GETBIT(FLAG:커스텀게임화면,1)
					SIF PCOUNT  == 3 || PCOUNT  == 6 || PCOUNT  == 8
						PRINTL
				ELSE
					SIF PCOUNT % 4 == 0
						PRINTL
				ENDIF
			NEXT
		ENDIF
	ENDIF
	FLAG:이신합체표시 = 0
	PRINTFORM %"\n" * MAX(0, (ARG == 3 ? 41 # 37) - LINECOUNT + LLINE)%
	IF GETBIT(FLAG:스테이터스화면타입,1)
		CLEARLINE 1
	ELSE
		CLEARLINE 4
	ENDIF
	CUSTOMDRAWLINE =
	IF LOCAL:9 == 1
		IF FLAG:SHOP커맨드 != [[SHOP:사교의관]]
			PRINTW 유감스럽지만 이 악마는 2체 이상 동료마로 삼을 수 없다
		ELSE
			PRINTW ２체 이상 동료마로 삼을 수 없는 악마입니다
		ENDIF
		DELCHARA 完成品
		
		RESTART
		
	ENDIF
	
	IF LOCAL:6 == 1 && FLAG:SHOP커맨드 != [[SHOP:사교의관]]
		PRINTW ＣＯＭＰ 안에 동일한 악마가 있는 것 같다
		DELCHARA 完成品
		RESTART
		
	ENDIF
	;合体結果の悪魔のレベル>主人のレベルだった場合駄目
	;スティーブン
	IF (BASE:(完成品):LV > BASE:MASTER:LV + FLAG:경험ED수) && (EQUIP:MASTER:스티븐 == 0) && レベル不足合体 == 0
		IF FLAG:SHOP커맨드 != [[SHOP:탐색]]
			PRINTW 유감스럽지만…… 네 레벨로는 다룰 수 없다
		ELSE
			PRINTW 레벨이 부족합니다
		ENDIF
		DELCHARA 完成品
		RESTART
	ENDIF
	
	;ステータスの表示切替用
	PRINTFORMLC \@ LOCAL:10 == 2 ? [1007]이전페이지 # %" " * 16% \@
	PRINTFORMLC \@ U != 完成品 && FLAG:합체인수 == 3 && PRINTCPERLINE() < 4 ? [1012]합체 후 악마 # %" " * 18% \@
	PRINTFORMLC \@ LOCAL:10 == 1 ? [1009]다음페이지 # %" " * 16% \@
	PRINTL 
	PRINTFORMLC \@ U != 소재:1 ? [1010]첫번째 악마 # %" " * 18% \@
	PRINTFORMLC \@ U != 소재:2 ? [1011]두번째 악마 # %" " * 18% \@
	SIF FLAG:합체인수 == 3
		PRINTFORMLC \@ U != 소재:3 ? [1013]세번째 악마 # %" " * 18% \@
	SIF FLAG:합체인수 != 3 || PRINTCPERLINE() > 3
		PRINTFORMLC \@ U != 完成品 ? [1012]합체 후 악마 # %" " * 18% \@
	PRINTL
	
	;合体するかどうか　→　合体実行（実際には既に終わっている）
	IF レベル不足合体
		PRINTL 레벨이 부족하지만 충성도를 소비해서 작성하겠습니까? [0]Yes [9]No
	ELSE
		PRINTL 이 악마면 되겠습니까? [0]Yes [9]No
	ENDIF
	$INPUT_LOOP
	INPUT
	IF RESULT == 1007 && LOCAL:10 == 2
		LOCAL:10 = 1
		GOTO PRINT_STATUS
	ELSEIF RESULT == 1009 && LOCAL:10 == 1
		LOCAL:10 = 2
		GOTO PRINT_STATUS
	ELSEIF RESULT == 1010 && U != 소재:1
		U = 소재:1
		GOTO PRINT_STATUS
	ELSEIF RESULT == 1011 && U != 소재:2
		U = 소재:2
		GOTO PRINT_STATUS
	ELSEIF RESULT == 1013 && U != 소재:3 && 소재:3 > 0
		U = 소재:3
		GOTO PRINT_STATUS
	ELSEIF RESULT == 1012 && U != 完成品
		U = 完成品
		GOTO PRINT_STATUS
	ELSEIF RESULT <= 8 && RESULT > 0
		IF ABL:(完成品):@"스킬{RESULT}" > 0
			SIF FLAG:스킬속성표시설정 == 1
				CALL SKILL_EXPLAIN_PERFORMANCE, ABL:完成品:@"스킬{RESULT}", 完成品
			TRYCALLFORM SKILL_EXPLAIN_{ABL:完成品:@"스킬{RESULT}"}
			WAIT
			GOTO PRINT_STATUS
		ELSE
			GOTO INPUT_LOOP
		ENDIF
	ELSEIF RESULT <= 18 && RESULT > 10
		IF ABL:(소재:1):@"스킬{(RESULT-10)}" > 0
			SIF FLAG:스킬속성표시설정 == 1
				CALL SKILL_EXPLAIN_PERFORMANCE, ABL:(소재:1):@"스킬{RESULT - 10}", 소재:1
			TRYCALLFORM SKILL_EXPLAIN_{ABL:(소재:1):@"스킬{(RESULT - 10)}"}
			WAIT
			GOTO PRINT_STATUS
		ELSE
			GOTO INPUT_LOOP
		ENDIF
	ELSEIF RESULT <= 28 && RESULT > 20
		IF ABL:(소재:2):@"스킬{RESULT-20}" > 0
			SIF FLAG:스킬속성표시설정 == 1
				CALL SKILL_EXPLAIN_PERFORMANCE, ABL:(소재:2):@"스킬{RESULT - 20}", 소재:2
			TRYCALLFORM SKILL_EXPLAIN_{ABL:(소재:2):@"스킬{(RESULT - 20)}"}
			WAIT
			GOTO PRINT_STATUS
		ELSE
			GOTO INPUT_LOOP
		ENDIF
	ELSEIF RESULT <= 38 && RESULT > 30 && 소재:3 > 0
		IF ABL:(소재:3):@"스킬{RESULT-30}" > 0
			SIF FLAG:스킬속성표시설정 == 1
				CALL SKILL_EXPLAIN_PERFORMANCE, ABL:(소재:3):@"스킬{RESULT - 30}", 소재:3
			TRYCALLFORM SKILL_EXPLAIN_{ABL:(소재:3):@"스킬{(RESULT - 30)}"}
			WAIT
			GOTO PRINT_STATUS
		ELSE
			GOTO INPUT_LOOP
		ENDIF
	ELSEIF RESULT == 9
		DELCHARA 完成品
		RESTART
	ELSEIF RESULT == 0
	ELSE
		GOTO INPUT_LOOP
	ENDIF
	
	;作成時のセリフを表示
	IF FLAG:SHOP커맨드 == [[SHOP:탐색]]
		; 合体事故発生チェック
		IF FUSION_ACCIDENT_CHECK(1)
			CALL FUSION_ACCIDENT,完成品,소재:1,소재:2,소재:3
			PRINTFORMW 합체 사고가 발생
			PRINTFORMW 예정과는 다른 악마 \@CSTR:(完成品):종족명 != "" ? %CSTR:(完成品):종족명% # %STR:(ABL:(完成品):종족)% \@ %조사처리(CALLNAME:完成品,"가")% 작성되고 말았습니다
		ELSE
			PRINTFORML 새로운 소환 프로그램을 작성
			PRINTFORMW \@CSTR:(完成品):종족명 != "" ? %CSTR:(完成品):종족명% # %STR:(ABL:(完成品):종족)% \@ %조사처리(CALLNAME:完成品,"가")% 소환 가능해졌습니다
		ENDIF
	ELSE
		; 合体事故発生チェック
		IF FUSION_ACCIDENT_CHECK(0)
			CALL FUSION_ACCIDENT,完成品,소재:1,소재:2,소재:3
			PRINTFORMW 합체 사고가 발생
			PRINTFORMW 예정과는 다른 악마 \@CSTR:(完成品):종족명 != "" ? %CSTR:(完成品):종족명% # %STR:(ABL:(完成品):종족)% \@ %조사처리(CALLNAME:完成品,"가")% 작성되고 말았습니다
		ELSE
		ENDIF
		IF FLAG:함락합체 == 0
			TRYCCALLFORM FUSION_MESSAGE_K{NO:(完成品)},完成品
			CATCH
				TRYCALLFORM FUSION_MESSAGE_PUB{ABL:(完成品):회화타입},完成品
			ENDCATCH
		ENDIF
	ENDIF
	IF FLAG:SHOP커맨드 == [[SHOP:사교의관]] && FLAG:함락합체 == 0
		LOCAL:8 = 소재:1
		;忠誠度の高い方の手紙を選択
		SIF BASE:(LOCAL:8):충성도 < BASE:(소재:2):충성도
			LOCAL:8 = 소재:2
		SIF 소재:3 > -1 && BASE:(LOCAL:8):충성도 < BASE:(소재:3):충성도
			LOCAL:8 = 소재:3
		CALL FUSION_LETTER,LOCAL:8,0
	ENDIF
	;ここに忠誠度による素質変化とか予定
	;フライデー
	;CALL MAKE_TALENT,完成品,(BASE:(소재:1):충성도+BASE:(소재:2):충성도)/10
	;BASE:(完成品):충성도 = MIN((BASE:(소재:1):충성도+BASE:(소재:2):충성도) / 2 , (RESULT*10 + (BASE:(소재:1):충성도+BASE:(소재:2):충성도)%10)/2)
	SIF EQUIP:MASTER:프라이데이 == 1 && BASE:(完成品):충성도 < 1000
		BASE:(完成品):충성도 = 1000
	
	;LOCAL:7 ABL引き継ぎレベル
	LOCAL:7 = ABL인계체크(소재:1,소재:2,소재:3)
	;---- EDIT 027 ADD START -------------------------
	IF BASE:(完成品):충성도 >= 200 && LOCAL:7 >= 1 && FLAG:함락합체 == 0
	;---- EDIT 027 ADD END -------------------------
		PRINTL 충성도를 소비해 조교에 관련된 능력을 계승할 수 있습니다
		
		PRINTL [0] 계승하지 않는다
		LOCAL:7 = ABL인계체크(소재:1,소재:2,소재:3)
		SIF BASE:(完成品):충성도 >= 200 && LOCAL:7 >= 1
			PRINTL [1] LV1까지 계승  -  소비충성도 200
		SIF BASE:(完成品):충성도 >= 500 && LOCAL:7 >= 2
			PRINTL [2] LV2까지 계승  -  소비충성도 500
		SIF BASE:(完成品):충성도 >= 1000 && LOCAL:7 >= 3
			PRINTL [3] LV3까지 계승  -  소비충성도 1000
		SIF BASE:(完成品):충성도 >= 3000 && LOCAL:7 >= 4
			PRINTL [4] LV4까지 계승  -  소비충성도 3000
		SIF BASE:(完成品):충성도 >= 5000 && LOCAL:7 >= 5
			PRINTL [5] LV5까지 계승  -  소비충성도 5000
		
		$INPUT_LOOP_ABL
		INPUT
		IF RESULT == 0
			LOCAL:7 = 0
		ELSEIF RESULT == 1 && BASE:(完成品):충성도 >= 200
			LOCAL:7 = 1
			BASE:(完成品):충성도 -= 200
		ELSEIF RESULT == 2 && BASE:(完成品):충성도 >= 500
			LOCAL:7 = 2
			BASE:(完成品):충성도 -= 500
		ELSEIF RESULT == 3 && BASE:(完成品):충성도 >= 1000
			LOCAL:7 = 3
			BASE:(完成品):충성도 -= 1000
		ELSEIF RESULT == 4 && BASE:(完成品):충성도 >= 3000
			LOCAL:7 = 4
			BASE:(完成品):충성도 -= 3000
		ELSEIF RESULT == 5 && BASE:(完成品):충성도 >= 5000
			LOCAL:7 = 5
			BASE:(完成品):충성도 -= 5000
		ELSE
			GOTO INPUT_LOOP_ABL
		ENDIF
		;調教効果の引き継ぎ
		IF 소재:3 > 0
			FOR LOCAL,0,26
				ABL:(完成品):LOCAL = MAX(MIN(LOCAL:7 , MAX(ABL:(소재:1):LOCAL , ABL:(소재:2):LOCAL , ABL:(소재:3):LOCAL)) , ABL:(完成品):LOCAL)
			NEXT
		ELSE
			FOR LOCAL,0,26
				ABL:(完成品):LOCAL = MAX(MIN(LOCAL:7 , MAX(ABL:(소재:1):LOCAL , ABL:(소재:2):LOCAL)) , ABL:(完成品):LOCAL)
			NEXT
		ENDIF
	ENDIF
	MAXBASE:(完成品):충성도 = BASE:(完成品):충성도

	PRINTFORMW %종족명(完成品)% %CALLNAME:(完成品)%이(가) 동료가 되었다。
	
	;悪魔解析度が100未満なら100に。
	SIF FLAG:(20000+NO:(完成品)) < 100
		FLAG:(20000+NO:(完成品)) = 100
	;ハッピーバースデーの能力低下はここでやる
	IF EQUIP:MASTER:해피버스데이 && NUM_SUMMONER()
		LOCAL:7 = BASE:(完成品):LV
		BASE:(完成品):LV = 1
		BASE:(完成品):ＥＸＰ = 0
		BASE:(完成品):ＭＡＧ = 0
		FOR LCOUNT, 0, 6
			BASE:(完成品):GET_BASESTATUS(LCOUNT + 1) = 3
		NEXT
	ENDIF
	CALL SYNC_STATUS,完成品
	CALL HEALTH_CHARA,完成品

	;---- EDIT 027 ADD START -------------------------
	;陥落合体の場合、後でもう一回レベルアップ機会があるのでオフになる。
	SIF FLAG:함락합체 == 0
		CALL CHECK_LEVEL_UP,完成品
	;---- EDIT 027 ADD END -------------------------
	;ＭＡＧ継承
;	CALL CONTROL_MAG,完成品,((BASE:(소재:1):ＭＡＧ + BASE:(소재:2):ＭＡＧ) + CFLAG:(소재:1):강화사용ＭＡＧ + CFLAG:(소재:2):강화사용ＭＡＧ)
	CALL CONTROL_MAG,完成品,((BASE:(소재:1):ＭＡＧ) + CFLAG:(소재:1):강화사용ＭＡＧ) + ((BASE:(소재:2):ＭＡＧ) + CFLAG:(소재:2):강화사용ＭＡＧ)
	SIF 소재:3 > 0
		CALL CONTROL_MAG,完成品, ((BASE:(소재:3):ＭＡＧ) + CFLAG:(소재:3):강화사용ＭＡＧ)
	SIF BASE:(完成品):ＭＡＧ > MAXBASE:(完成品):ＭＡＧ
		BASE:(完成品):ＭＡＧ = MAXBASE:(完成品):ＭＡＧ
	;合体소재の悪魔を消去
	;---- EDIT 018 MOD START -------------------------
	;悪魔召喚ライトなら戦闘中、ポジション占領してる悪魔を外す。ついでに完成品の入力行動をリセット。
	IF Z == 2
		IF CFLAG:(소재:1):포지션 > 0
			CALL REMOVE_POSITION, CPOS(소재:1)
		ELSEIF CFLAG:(소재:2):포지션 > 0
			CALL REMOVE_POSITION, CPOS(소재:2)
		ENDIF
	ENDIF
	CFLAG:(完成品):입력행동 = -1
	;---- EDIT 018 MOD END -------------------------
	;素材にした悪魔が瀕死だと主人がDARKに傾く
	SIF GET_STATE(CFLAG:(소재:1):상태이상) == "DYING"
		CALL INCREASE_LD,-7
	SIF GET_STATE(CFLAG:(소재:2):상태이상) == "DYING"
		CALL INCREASE_LD,-7
	SIF 소재:3 > 0 && GET_STATE(CFLAG:(소재:3):상태이상) == "DYING"
		CALL INCREASE_LD,-7
	;素材にした悪魔の反発刻印によってDARKに傾く
	CALL INCREASE_LD,-3*(MARK:(소재:1):반발각인 + MARK:(소재:2):반발각인)
	SIF 소재:3 > 0
		CALL INCREASE_LD,-3*(MARK:(소재:3):반발각인)
	
	IF 소재:3 > 0
		;---- EDIT 027 ADD START -------------------------
		;陥落合体中なら陥落合体の処理に飛ぶ。
		IF FLAG:함락합체 == 1
			CALL FUSION_KANRAKU,完成品,소재:1,소재:2,소재:3
			DELCHARA 完成品
			CALL 캐릭터삭제, MAX(소재:2,소재:3)
			CALL 캐릭터삭제, MIN(소재:2,소재:3)
		ELSE
		;---- EDIT 027 ADD END -------------------------
			FOR LOCAL, CHARANUM-1 , 0 , -1
			SIF MATCH(소재 , LOCAL)
				CALL 캐릭터삭제, LOCAL
			NEXT
		ENDIF
	ELSE
	;---- EDIT 027 ADD START -------------------------
	;陥落合体中なら陥落合体の処理に飛ぶ。
		IF FLAG:함락합체 == 1
			CALL FUSION_KANRAKU,完成品,소재:1,소재:2,소재:3
			DELCHARA 完成品
			CALL 캐릭터삭제, 소재:2
		ELSE
			;---- EDIT 027 ADD START -------------------------
			CALL 캐릭터삭제, MAX(소재:1,소재:2)
			CALL 캐릭터삭제, MIN(소재:1,소재:2)
		ENDIF
	ENDIF
;---- EDIT 020 MOD START -------------------------
	SIF ＣＯＭＰ빈용량() < 0 || LOCAL:6 || ABL:(完成品 - FLAG:합체인수):종족 < 1 || ABL:(完成品 - FLAG:합체인수):종족 > 45 || CFLAG:(完成品 - FLAG:합체인수):용량미사용
		CFLAG:(完成品 - FLAG:합체인수):소속ＣＯＭＰ = -1
;---- EDIT 020 MOD END ---------------------------
	FLAG:합체예정악마1 = -1
	FLAG:합체예정악마2 = -1
	LLOCK = 0
	REDRAW LREDRAW
	;---- EDIT 018 MOD START -------------------------
	;悪魔召喚ライトなら1を返してDUO回数消費させにいく
	IF Z >= 2
		RETURN 1
	ENDIF
	;---- EDIT 018 MOD END -------------------------
	RETURN 0
;ELSE
;	소재:1 = RESULT
ENDIF

GOTO START_FUSION

@CASTING_FUSION_DOUBLE(ARG)
;---- EDIT 027 ADD START -------------------------
;テスト実装、契約キャラでも陥落合体ではベースに採用可能に
IF 계약(ARG) > 0
	IF FLAG:함락합체 == 0 || FLAG:합체예정악마1  > -1
		RETURN 0
	ENDIF
ENDIF
;---- EDIT 027 ADD END -------------------------
;SIF ABL:ARG:종족 == 0 || ABL:ARG:종족 > 44
;	RETURN 0
SIF (ABL:ARG:종족 == 0 && NO:ARG != 4552)|| NO:ARG == 4509
	RETURN 0
SIF ARG == FLAG:합체예정악마1 || ARG == FLAG:합체예정악마2
	RETURN 0
SIF CFLAG:ARG:노역플래그 == 3
	RETURN 0
SIF CFLAG:ARG:합체불가
	RETURN 0
SIF CFLAG:ARG:이자리에없는플래그 == 1
	RETURN 0
SIF FLAG:이신합체시남녀표시 == 3 && !FLAG:악마가챠모드
	FLAG:이신합체시남녀표시 = 0
SIF FLAG:이신합체시남녀표시 == 1 && TALENT:ARG:남자
	RETURN 0
SIF FLAG:이신합체시남녀표시 == 2 && !TALENT:ARG:남자
	RETURN 0
SIF FLAG:이신합체시남녀표시 == 3 && !TALENT:ARG:ガチャ
	RETURN 0
IF FLAG:합체예정악마1 > -1 && FLAG:합체인수 == 2
	;特殊合体が設定されている組み合わせは性別が違ってもおｋ
	CALL FUSION_SPECIAL, FLAG:합체예정악마1 , ARG
	SIF RESULT > 0
		RETURN 1
ELSEIF FLAG:합체예정악마2 > -1 && FLAG:합체인수 == 3
	;特殊合体が設定されている組み合わせは性別が違ってもおｋ
	CALL FUSION_SPECIAL, FLAG:합체예정악마1 , FLAG:합체예정악마2 , ARG
	SIF RESULT > 0
		RETURN 1
ENDIF
IF FLAG:합체예정악마1 > -1
	;ジェンダーフリーの条件を追加
	;それ以外は性別が同じ悪魔でないとダメ
	SIF TALENT:(FLAG:합체예정악마1):남자 == TALENT:(ARG):남자 || EQUIP:MASTER:젠더프리 == 1
		RETURN 1
	;---- EDIT 006 ADD END   -------------------------
	RETURN 0
ENDIF
RETURN 1

@UP_PRINT_FUSION_DOUBLE
PRINTFORML 합체시키고 싶은 악마를 선택하십시오　\@ A > 리스트표시수() ? ＜page.{P + 1}/{A / 리스트표시수() + 1}＞ # \@　%NAME:MASTER% LV:{BASE:MASTER:LV,3} \@ FLAG:경험ED수 > 0 ? + 경험ED수:{FLAG:경험ED수,3} # \@

@DOWN_PRINT_FUSION_DOUBLE
IF FLAG:합체예정악마1 > -1
	PRINTL 
	PRINTFORML １번째:%CALLNAME:(FLAG:합체예정악마1)%
ENDIF
SIF FLAG:합체예정악마2 > -1
	PRINTFORML ２번째:%CALLNAME:(FLAG:합체예정악마2)%

@MODECHANGE_FUSION_DOUBLE , ARG
#DIM LCOUNT
SELECTCASE ARG
	CASE -1
		LOCAL:11 = 0
		FOR LCOUNT, 500, 600
			IF ITEM:LCOUNT
				IF SOFT_IS_FUSIONSOFT(LCOUNT, Z)
					LOCAL:11 = 1
					BREAK
				ENDIF
			ENDIF
		NEXT
		IF Z == 0
			SIF LOCAL:11
				;PRINTLC [1010]인스톨 소프트 변경
				PRINTLC [1010]합체 소프트 변경
		ELSE
			SIF LOCAL:11
				PRINTLC [1010]컨버터 변경
		ENDIF
		PRINTL 
		SIF !FLAG:이신합체시남녀표시
			SETCOLOR COLOR("aqua")
		PRINTLC [1011] 남녀전부
		RESETCOLOR
		SIF FLAG:이신합체시남녀표시 == 1
			SETCOLOR COLOR("aqua")
		PRINTLC [1012] 여자만
		RESETCOLOR
		SIF FLAG:이신합체시남녀표시 == 2
			SETCOLOR COLOR("aqua")
		PRINTLC [1013] 남자만
		RESETCOLOR
		;가챠모드時
		IF FLAG:악마가챠모드 > 0
			SIF FLAG:이신합체시남녀표시 == 3
				SETCOLOR COLOR("aqua")
			PRINTLC [1014] 가챠산만
			RESETCOLOR
		ENDIF
		
		;SIF GETBIT(FLAG:합체용소프트퀵설정, 6) == 1
		;	CALL 합체용소프트퀵설정_FUSION
		;==합체용소프트퀵설정 その１ =========================Start
		PRINTL 
		FOR LCOUNT, 500, 600
			IF SOFT_IS_FUSIONSOFT(LCOUNT, Z) && FSQS_BIT(LCOUNT)
				SETCOLOR EQUIP:MASTER:(ITEMNAME:LCOUNT) ? GETCOLOR() # COLOR("gray")
				PRINTFORMLC [{1000 + LCOUNT}] %ITEMNAME:LCOUNT%
				RESETCOLOR
			ENDIF
		NEXT
		;===========================================================End
	CASE 1010 TO 1013
		RETURN ARG, 0
	CASE 1014
		SIF FLAG:악마가챠모드 > 0
			RETURN ARG, 0
ENDSELECT
RETURN ARG , 1

;=====================================================
;스킬継承
;=====================================================
@SUCCESS_SKILL,ARG,ARG:1,ARG:2,ARG:3 = -1,ARG:4 = 0
#DIM CHARA , 1
#DIM 카운트 , 1
#DIM SKILL
;ARG = 合体後悪魔
;ARG:1 = 合体前悪魔Ａ
;ARG:2 = 同Ｂ
;ARG:3 = 同Ｃ
;ARG:4 = 真の時、邪教の館での合体扱いとなる
;LOCAL = 継承スキル(仮)
;LOCAL:1 = 優先度
;LOCAL:3 = LOCALのスキルの優先度（保存用）
;LOCALS:1 = 継承するスキル枠

;ARGの空いているスキル枠を参照する
;空いていなければRETURN 0
FOR LOCAL:2,1,FLAG:스킬수+1
	LOCALS = 스킬{LOCAL:2}
	IF ABL:ARG:LOCALS == 0
		LOCALS:1 = %LOCALS%
		BREAK
	ENDIF
	SIF LOCAL:2 == FLAG:스킬수
		RETURN 0
NEXT

;ARG:1とARG:2のスキルの中から継承可能かつ持っていないスキルをピックアップ。
;見つけた場合、LOCALに放り込む。
;LOCALが埋まっている場合は、優先度がより高ければLOCALに上書き。

LOCAL = 0

FOR 카운트 , 1 , (ARG:3 > -1 ? 4 # 3)
	CHARA = ARG:카운트
	FOR LOCAL:2,1,FLAG:스킬수+1
		SKILL = ABL:(CHARA):@"스킬{LOCAL:2}"
		;未修得なら次へ
		SIF SKILL == 0
			CONTINUE
		TRYCCALLFORM 계승불능_{SKILL}
			SIF RESULT
				CONTINUE
		CATCH
		ENDCATCH
		;条件無視スキルは判定しない
		TRYCCALLFORM 계승조건무시_{SKILL}
		CATCH
			;継承タイプ読み込み、ARGが継承できるタイプでなければ次へ
			CALLFORM SKILL_SUCCESSION_TYPE_{SKILL}
			;SIF TALENT:ARG:(320+RESULT) == 0 || RESULT == 22
			;一般の範囲外のスキルを弾く・邪教の館でないときは自動効果と継承タイプを持っているスキルのみを引き継ぐ
			SIF !RANGE(RESULT, 0, 22) || (TALENT:ARG:GET_SUCCESSION(RESULT) == 0 && RESULT != 22 && ARG:4 == 0)
				CONTINUE
		ENDCATCH
		;継承必要部位読み込み、邪教の館でなくARGが持ってなければ次へ
		CALLFORM 계승부위_{SKILL},ARG
		SIF RESULT == 0 && ARG:4 == 0
			CONTINUE
		;既に習得しているスキルかつ、重複習得数（通常１）以上になるならば次へ
		IF CHECK_SKILL(ARG,SKILL)
			TRYCCALLFORM 중복습득수_{SKILL}
				SIF HAVE_SKILL_OVERLAP(ARG,SKILL) + 1 > RESULT
					CONTINUE
			CATCH
				CONTINUE
			ENDCATCH
		ENDIF
		
		IF LOCAL == 0
			LOCAL = SKILL
			TRYCCALLFORM SKILL_RANK_{SKILL}
				LOCAL:3 = RESULT
			CATCH
				LOCAL:3 = 0
			ENDCATCH
		ENDIF
		
		TRYCCALLFORM SKILL_RANK_{SKILL}
			LOCAL:1 = RESULT
		CATCH
			LOCAL:1 = 0
		ENDCATCH
		
		IF LOCAL:1 > LOCAL:3
			LOCAL = SKILL
			LOCAL:3 = LOCAL:1
		ENDIF
	NEXT
NEXT


;この時点でLOCALが0＝継承できるスキルが無いのでRETURN
SIF LOCAL == 0
	RETURN 0

;全部捜査したらLOCAL:1のスキル枠に習得させる。
ABL:ARG:(LOCALS:1) = LOCAL

RESTART

;========================================
;合体時の能力強化処理
;========================================
@FUSION_ENHANCE_STATUS,ARG,ARG:1,ARG:2,ARG:3 = -1
#DIM CHARA , 1
#DIM 경험치 , 3
#DIM 카운트 , 1
VARSET 경험치


FOR 카운트 , 0 , (ARG:3 > -1 ? 3 # 2)
	CHARA = ARG:(카운트+1)
	LOCAL = CFLAG:CHARA:초기LV
	LOCAL:1 = BASE:CHARA:ＥＸＰ - LOCAL * (LOCAL-1) * (LOCAL +1) * 5 / 3
	SIF BASE:ARG:LV > LOCAL
		LOCAL:1 /= ((BASE:ARG:LV - LOCAL)/4+1)
	SIF BASE:ARG:LV < LOCAL
		LOCAL:1 *= POWER(2,(BASE:ARG:LV - LOCAL)/4)
	경험치:카운트 = LOCAL:1
NEXT

;;LOCAL = CSVBASE(NO:(ARG:1),(GETNUM(BASE,"LV")),0)
;LOCAL = CFLAG:(ARG:1):초기LV
;LOCAL:1 = BASE:(ARG:1):ＥＸＰ - LOCAL * (LOCAL-1) * (LOCAL +1) * 5 / 3
;SIF BASE:ARG:LV > LOCAL
;	LOCAL:1 /= ((BASE:ARG:LV - LOCAL)/4+1)
;SIF BASE:ARG:LV < LOCAL
;	LOCAL:1 *= POWER(2,(BASE:ARG:LV - LOCAL)/4)
;LOCAL:2 = LOCAL:1
;
;LOCAL = CFLAG:(ARG:2):초기LV
;LOCAL:1 = BASE:(ARG:2):ＥＸＰ - LOCAL * (LOCAL-1) * (LOCAL +1) * 5 / 3
;SIF BASE:ARG:LV > LOCAL
;	LOCAL:1 /= ((BASE:ARG:LV - LOCAL)/4+1)
;SIF BASE:ARG:LV < LOCAL
;	LOCAL:1 *= POWER(2,(BASE:ARG:LV - LOCAL)/4)

IF ARG:3 > -1
	TIMES 경험치:0,0.5
	TIMES 경험치:1,0.5
	TIMES 경험치:2,0.5
ELSE
	TIMES 경험치:0,0.7
	TIMES 경험치:1,0.7
ENDIF
IF FLAG:SHOP커맨드 != [[SHOP:사교의관]]
	BASE:ARG:ＥＸＰ += SUMARRAY(경험치)/2
ELSE
	BASE:ARG:ＥＸＰ += SUMARRAY(경험치)
ENDIF

;CFLAG:ARG:능력강화횟수 = CFLAG:(ARG:1):능력강화횟수
;FOR LOCAL,1,7
;	LOCALS = %GET_BASESTATUS(LOCAL)%강화횟수
;	LOCAL:1 = BASE:(ARG:1):LOCALS - CSVBASE(NO:(ARG:1),(GETNUM(BASE,LOCALS)),0)
;	LOCAL:2 = BASE:(ARG:2):LOCALS - CSVBASE(NO:(ARG:2),(GETNUM(BASE,LOCALS)),0)
;	
;	CFLAG:ARG:LOCALS += CFLAG:(ARG:1):LOCALS
;NEXT

;========================================
;デビルシフター用、邪教の館内変身悪魔変更
;========================================
@CHANGE_LINK
CALL INPUT_CHARA_LIST("누구를 변경합니까？", "CASTING_CHANGE_LINK")
IF RESULT == 1000
	RETURN 0
ENDIF
CALL ACTION_2311,RESULT

@CASTING_CHANGE_LINK(ARG)
;人間・造間・半魔は無理
SIF TALENT:ARG:데빌시프터 == 0
	RETURN 0
SIF CFLAG:ARG:이자리에없는플래그 == 1
	RETURN 0
SIF CFLAG:ARG:PT플래그 == 0
	RETURN 0
;デビルシフター以外の戦闘系素質持ってたらアウト
SIF TALENT:ARG:건슬링거 || TALENT:ARG:석세서 || TALENT:ARG:이능자 || TALENT:ARG:페르소나구사자 || TALENT:ARG:달인
	RETURN 0
RETURN 1

;IF文にそのまま入れたかったので新規に作成
@CASTING_CHANGE_LINK_F(ARG)
#FUNCTION
;人間・造間・半魔は無理
SIF TALENT:ARG:데빌시프터 == 0
	RETURNF 0
SIF CFLAG:ARG:이자리에없는플래그 == 1
	RETURNF 0
SIF CFLAG:ARG:PT플래그 == 0
	RETURNF 0
;デビルシフター以外の戦闘系素質持ってたらアウト
SIF TALENT:ARG:건슬링거 || TALENT:ARG:석세서 || TALENT:ARG:이능자 || TALENT:ARG:페르소나구사자 || TALENT:ARG:달인
	RETURNF 0
RETURNF 1

;========================================
;ABL引き継ぎ時に、そもそもそのレベルのABLが存在するかを調べる
;========================================
@ABL인계체크(ARG,ARG:1,ARG:2 = -1)
#FUNCTION
LOCAL:1 = 0
IF ARG:2 < 1
	FOR LOCAL,0,26
		LOCAL:1 = MAX(ABL:ARG:LOCAL , ABL:(ARG:1):LOCAL , LOCAL:1)
	NEXT
ELSE
	FOR LOCAL,0,26
		LOCAL:1 = MAX(ABL:ARG:LOCAL , ABL:(ARG:1):LOCAL, ABL:(ARG:2):LOCAL , LOCAL:1)
	NEXT
ENDIF

RETURNF LOCAL:1

;========================================
;手紙システム
;	ARG:1（0:通常合体　1:魔晶武具　2:スキルカード　3:조마作成　4:조마強化　5:조마初期化 6:イケニエ）
;========================================
@FUSION_LETTER,ARG,ARG:1

PRINTL 편지가 있습니다. 읽겠습니까?

CALL INPUT_YN,"Yes","NO"
IF RESULT == 0
	TRYCCALLFORM FUSION_LETTER_MESSAGE_K{NO:ARG},ARG,ARG:1
	CATCH
		TRYCCALLFORM FUSION_LETTER_MESSAGE_PUB{ABL:(ARG):회화타입},ARG,ARG:1
		CATCH
			PRINTFORMW %CALLNAME:(ARG)%으로부터의 편지다。
			CALL FUSION_LETTER_MESSAGE_PUB,ARG,ARG:1
		ENDCATCH
	ENDCATCH
ENDIF

;=====================================================
;素質継承
;=====================================================
@SUCCESS_TALENT,ARG,ARG:1,ARG:2,ARG:3 = -1
#DIM CHARA , 1
#DIM 카운트 , 1
CALL SUCCESS_TALENT_CHECK, "처녀", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "겁쟁이", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "꿋꿋함", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "반항적", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "솔직함", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "얌전함", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "프라이드높음", "프라이드낮음" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "건방짐", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "츤데레", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "자제심", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "무관심", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "감정결핍", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "호기심", "보수적" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "낙관적", "비관적" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "일선을넘지않음", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "눈에띄고싶어함", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "정조관념", "정조관결여" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "억압", "해방" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "저항", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "부끄럼쟁이", "부끄럼없음" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "헌신적", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "소악마", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "광기", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "유치", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "아픔에강함", "아픔에약함" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "젖기어려움", "젖기쉬움" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "습득빠름", "습득느림" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "바늘다루기", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "약독내성", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "오줌싸개버릇", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "자위하기쉬움", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "악취민감", "악취둔감" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "불결무시", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "쾌감에솔직", "쾌감을부정" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "중독되기쉬움", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "도착적", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "바이", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "남성혐오", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "여성혐오", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "Ｃ민감", "Ｃ둔감" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "Ｖ민감", "Ｖ둔감" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "Ａ민감", "Ａ둔감" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "Ｂ민감", "Ｂ둔감" ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "매력", ,ARG,ARG:1,ARG:2,ARG:3
CALL SUCCESS_TALENT_CHECK, "매혹", ,ARG,ARG:1,ARG:2,ARG:3
;가슴・체격・性別・후타나리はお薬で弄くれるのでとりあえず無し




;=====================================================
;소질継承체크
;=====================================================
@SUCCESS_TALENT_CHECK,ARGS,ARGS:1 = "",ARG,ARG:1,ARG:2,ARG:3 = -1
#LOCALSIZE 3
;ARGS 継承チェックを行う素質
;ARGS:1 その相反する素質
LOCAL:1 = 0
;LOCAL:2 素質ポイントを2倍にするキャラ
;LOCAL:2 = 1
;FOR LOCAL , 2 , (ARG:3 > 0 ? 4 # 3)
;	IF ABS(ABL:ARG:종족 - ABL:(ARG:(LOCAL:2)):종족) == ABS(ABL:ARG:종족 - ABL:(ARG:(LOCAL)):종족)
;		;同じ場合はCSV番号の若い方
;		SIF NO:(ARG:(LOCAL:2)) > NO:(ARG:(LOCAL))
;			LOCAL:2 = LOCAL
;	ELSEIF ABS(ABL:ARG:종족 - ABL:(ARG:(LOCAL:2)):종족) > ABS(ABL:ARG:종족 - ABL:(ARG:(LOCAL)):종족)
;		;種族番号が近いキャラ優先
;		LOCAL:2 = LOCAL
;	ENDIF
;NEXT


IF TALENT:ARG:ARGS
	;ARGSを所持している場合
	;相反する素質が指定されていれば、持っている素材悪魔の数を数える
	IF ARGS:1 != ""
		FOR LOCAL , 1 , (ARG:3 > 0 ? 4 # 3)
			SIF TALENT:(ARG:LOCAL):(ARGS:1)
				LOCAL:1 ++
			SIF TALENT:(ARG:LOCAL):(ARGS)
				LOCAL:1 --
		NEXT
		;１Ｐ以上で打ち消し
		SIF LOCAL:1 >= 1
			TALENT:ARG:ARGS = 0
		;２Ｐ以上で相反する素質を継承
		SIF LOCAL:1 >= 2
			TALENT:ARG:(ARGS:1) = 1
	ELSE
		RETURN 0
	ENDIF
ELSEIF ARGS:1 != "" && TALENT:ARG:(ARGS:1)
	;ARGS:1を所持している場合
	;相反する素質が指定されていれば、持っている素材悪魔の数を数える
	FOR LOCAL , 1 , (ARG:3 > 0 ? 4 # 3)
		SIF TALENT:(ARG:LOCAL):(ARGS)
			LOCAL:1 ++
		SIF TALENT:(ARG:LOCAL):(ARGS:1)
			LOCAL:1 --
	NEXT
		;１Ｐ以上で打ち消し
	SIF LOCAL:1 >= 1
		TALENT:ARG:(ARGS:1) = 0
		;２Ｐ以上で相反する素質を継承
	SIF LOCAL:1 >= 2
		TALENT:ARG:(ARGS) = 1
ELSE
	;どちらも持っていない場合
	;素質:ARGSを所持している素材悪魔を数える
	;相反する素質がある場合はその素質を持つ悪魔の数だけ減算
	FOR LOCAL , 1 , (ARG:3 > 0 ? 4 # 3)
		SIF TALENT:(ARG:LOCAL):(ARGS)
			LOCAL:1 ++
		SIF ARGS:1 != "" && TALENT:(ARG:LOCAL):(ARGS:1)
			LOCAL:1 --
	NEXT
	;２Ｐ(相反する素質がある素質なら１Ｐ)以上で継承
	IF LOCAL:1 >= 2 || (ARGS:1 != "" && LOCAL:1 >= 1)
		TALENT:ARG:(ARGS) = 1
	;－１Ｐ以下なら相反する素質を継承
	ELSEIF LOCAL:1 <= -1
		TALENT:ARG:(ARGS:1) = 1
	ENDIF
ENDIF

;=====================================================
;合体事故発生チェック
;ARG = ダンジョンかどうか
;=====================================================
@FUSION_ACCIDENT_CHECK(ARG = 0)
#FUNCTION

; 合体事故確率初期値設定
LOCAL = 0

; アルバートを装備している場合、必ず0。
; ただし、アルバートとコペルニクスを同時にONしていると相殺される
IF EQUIP:MASTER:알버트 == 0 || EQUIP:MASTER:코페르니쿠스 == 1
	
	; 月齢をチェックして、基本確率を算出
	SELECTCASE FLAG:월령
		; 新月は1%
		CASE 0
			LOCAL = 1
		; 満月は4%
		CASE 8
			LOCAL = 4
		; その他は2%
		CASEELSE
			LOCAL = 2
	ENDSELECT
	
	; 探索中なら3倍
	SIF ARG == 1
		LOCAL = LOCAL * 3
	
	; コペルニクス装備してて、アルバート装備してないならさらに5倍
	SIF EQUIP:MASTER:코페르니쿠스 == 1 && EQUIP:MASTER:알버트 == 0
		LOCAL = LOCAL * 5
ENDIF

IF LOCAL == 0
	RETURNF 0
ELSEIF RAND:100 < LOCAL
	RETURNF 1
ENDIF

RETURNF 0

;@합체용소프트퀵설정_FUSION
;#LOCALSIZE 6
;#LOCALSSIZE 6
;#DIM LCOUNT
;PRINTL 
;LOCAL = 150
;LOCAL:1 = 184
;LOCAL:2 = 186
;LOCAL:3 = 187
;LOCAL:4 = 190
;LOCAL:5 = 192
;LOCALS = [1020] 알버트
;LOCALS:1 = [1021] 파라켈수스
;LOCALS:2 = [1022] 라플라스
;LOCALS:3 = [1023] 코페르니쿠스
;LOCALS:4 = [1024] 고에티아
;LOCALS:5 = [1025] 라지엘의서
;
;FOR LCOUNT, 0, 6
;	RESETCOLOR
;	SIF GETBIT(FLAG:합체용소프트퀵설정, LCOUNT) == 0
;		CONTINUE
;	SIF EQUIP:MASTER:(LOCAL:LCOUNT) == 0
;		SETCOLOR COLOR("灰色")
;	PRINTFORMLC %LOCALS:LCOUNT%
;NEXT
;RESETCOLOR
;

@MIRKO
;陥落合体の追加に伴い説明役が必要……ということで仮実装しました。
;世界観的なフォローも含めて大雑把にですが説明してくれます。

PRINTL ――邪教の館をうろついていると、異様な見た目の女性に出会った。
PRINTW なんというかその……牛柄のケープと水着、ロンググローブとサイハイブーツに身を包んだ異様なファッションの女性だ。
PRINTL 

CALL PRINT_ICON, 4991, 1

PRINTW 
PRINTL 「やあやあ、こんな所で人に会うということは、君はデビル・サマナーかな？
PRINTW 　そういうことであれば、この邪教の館でできる悪魔合体、それに関する話を僕にさせてくれよ」

PRINTL 
PRINTL 「もちろん。 君が良ければの話だし、何なら君の質問にだけ答えようじゃないか。
PRINTL 　何、気に食わなければ妄言と聞き流しても構わないさ。 話したいだけだしね。
PRINTW 　ほらはーやーく、はーやーく、質問してくーれーよー。 僕が話していいかがわからないじゃないか」

PRINTL 
PRINTW ――面倒な相手に絡まれてしまった。
$INPUT_LOOP
PRINTL 
CUSTOMDRAWLINE =
PRINTL 
PRINTL [ 1]そもそも悪魔合体って？
PRINTL [ 2]陥落合体って何なの？
PRINTL [ 3]合体事故って何？
PRINTL [ 4]精霊合体って何？
PRINTL [ 5]特殊合体って何？
PRINTL [ 6]造魔って何？
PRINTL [ 7]英雄合体？
PRINTL [ 8]イケニエ合体？
PRINTL [ 9]もっと詳しい話
CUSTOMDRAWLINE =
PRINTL [ 0]逃げる
INPUT
IF RESULT == 1
	PRINTL 
	PRINTL 「その名の通り、異なる悪魔を合体させて新たな悪魔とするものさ！
	PRINTL 　大まかな法則性については割愛するけれども、
	PRINTW 　大体合体した悪魔の平均よりちょっと強いぐらいの悪魔となるかな」
	GOTO INPUT_LOOP
ELSEIF RESULT == 2
	PRINTL 
	PRINTL 「この合体は、悪魔の見た目や喋ることはそのままに新たな悪魔とするのさ！
	PRINTL 　とはいえ、最初の１体目のものしか引き継ぐことはできないし、
	PRINTW 　その最初の１体が君に対して心を許している……いわゆる陥落した状態である必要はあるね」
	GOTO INPUT_LOOP
ELSEIF RESULT == 3
	PRINTL 
	PRINTL 「悪魔合体も決して確実なものではなくて、１００回に２回ぐらい失敗することもある。
	PRINTL 　でもまあそれでも合体自体は行われて、予定とは違う悪魔になるのさ。
	PRINTW 　基本的には歓迎できない事態で……特に満月の時や邪教の館以外での合体だと事故が起きやすいみたいだね」
	GOTO INPUT_LOOP
ELSEIF RESULT == 4
	PRINTL 
	PRINTL 「悪魔の中には、合体した際に特殊な性質を発揮する悪魔もいる。
	PRINTL 　中でも精霊という悪魔……種別の同じ悪魔同士の合体でできる悪魔は特殊でね。
	PRINTW 　この悪魔が合体した場合、合体後は同じ種別の一つ格上か一つ格下の悪魔になるのさ」
	GOTO INPUT_LOOP
ELSEIF RESULT == 5
	PRINTL 
	PRINTL 「悪魔合体の中には特殊な法則性を持つ合体も存在していてね。
	PRINTL 　特別な逸話がある悪魔同士での合体とかが行われると、逸話に沿った結果になることがある。
	PRINTW 　それこそが特殊合体と呼ばれるもので、これで出来上がる悪魔は大抵が他では完成しないユニークな悪魔なのさ」
	GOTO INPUT_LOOP
ELSEIF RESULT == 6
	PRINTL 
	PRINTL 「ドリーカドモンと呼ばれる触媒を使って作られた人造の悪魔、それが造魔さ。
	PRINTL 　この悪魔は他の悪魔と違ってＭＡＧを消費しないが、戦闘で強くなることもない。
	PRINTW 　代わりにＭＡＧを使って強くなる悪魔なのさ。 ちなみに弱点はなく何でも覚えられる特徴もある」
	GOTO INPUT_LOOP
ELSEIF RESULT == 7
	PRINTL 
	PRINTL 「普通は合体できない造魔を利用しての特殊合体、それが英雄合体さ。
	PRINTL 　これでは英雄という特別なカテゴリーの悪魔が完成してね。
	PRINTW 　一定以上の強さの造魔と逸話が絡む悪魔との合体で出来上がるらしいが……あまり見たことはなくてね」
	GOTO INPUT_LOOP
ELSEIF RESULT == 8
	PRINTL 
	PRINTL 「悪魔の種類はそのままに、その悪魔を強くしたりできるのがイケニエ合体さ。
	PRINTL 　戦闘経験を積むのと同じ感じに強くなれるし、君と共闘した忠誠心ある悪魔なら副次効果もある。
	PRINTW 　まあ基本的には、その副次効果である素質の変更……体や心の状態を調えたりするのにすることになるかな？」
	GOTO INPUT_LOOP
ELSEIF RESULT == 9
	PRINTL 
	PRINTL 「……ふむ。 僕の話は長くなるけれども、それでも構わないかい？」
	PRINTL 
	PRINTL [1000]はい
	PRINTL [ 9]いいえ
	INPUT
	IF RESULT == 1000
		PRINTW 「それなら本気で話すとしようか」
		PRINTL
		PRINTL 「そもそも悪魔合体とは、ダンテ・アリギエーリの『神曲』いわく、地獄における悪魔の生態の一つであり、地獄に堕ちた人間を悪魔が取り込み新たなる悪魔となったという話から着想を得て作られたシステムとも言われていてね」
		PRINTL 「悪魔が持つ他者と混ざる生態、その性質を利用したものが悪魔同士を混ぜ合わせて新たなる悪魔を作り出す悪魔合体さ。 全てが混ざり一つの形を成すのが悪魔合体。その悪魔合体の中でも精霊合体や御霊合体というのは、特殊な性質を持った悪魔を混ぜ合わせる事で普通とは違う合体を行う事を指すが広義においてはニ身合体や三身合体と同じさ」
		PRINTL 
		PRINTW 「まあ要は、悪魔同士を混ぜ合わせて別の悪魔にするのが普通の悪魔合体」
		PRINTL 
		PRINTL 「じゃあ次は陥落合体について話そうか。 この新しい悪魔合体は、主へと心を許した仲魔の魂の形や自我を保持しての合体を可能とする。 平たくいえば、陥落していれば悪魔の見た目や喋る内容をそのままに、悪魔を強くする事ができるんだ。 普通の悪魔合体は格が変われば別の存在となり性格や見た目も変わってしまい例外はない」
		PRINTL 「でもそんな古のルールを打ち破る物が存在してね、それこそが人間の持つ強力な自我即ちエゴさ。 人間から見た『この悪魔はこうでなくてはならない』というエゴは、時に悪魔の在り方すら大きく縛り付けてしまう。 悪魔自身がそう望むのであれば尚更強く縛り付ける。 悪魔合体を行う際にこれを利用し、混じり合う在り方を歪めて人間のエゴで悪魔を定義しなおす。 それこそがこの陥落合体さ」
		PRINTL 
		PRINTW 「話長くなったけども要は、両想いの悪魔を、君の好きな悪魔でいさせる為に、悪魔合体に手を加えて、強さを変えて好きな所を残すができるよ」
		PRINTL 
		PRINTL 「では次に造魔と英雄合体について話そうか、ざっと言ってしまえば造魔というのはドリーカドモンというものを使用して作る人造の悪魔、中でも生体マグネタイトに依らない肉体を持った悪魔の事を指す。 身体が生体マグネタイトで構成されているのではなく物質として存在しているから、身体の維持にＭＡＧが要らない。 ただ人間や悪魔などと違って戦闘などを通じて生体マグネタイトを回収するのが苦手だから、造魔合体や身体にＭＡＧを注ぎ込まれることでしか強くなれない欠点はあるね」
		PRINTL 「で、この肉体を持った悪魔という性質と人造で生まれたばかりなので逸話を持たない性質を利用するのが英雄合体。 神話や歴史に名を残すような強大な肉体を持った存在である英雄や猛将、それらに関わりの深い悪魔を触媒として逸話を持たない造魔の肉体に混ぜ合わせることで、かつての英雄や猛将を悪魔という形で再現するのが英雄合体さ。 まあ、その英雄や猛将に並び立てる程度の強化をしていないと再現できなくて合体で作れないけれどもね」
		PRINTL 
		PRINTW 「大事な部分は、造魔はＭＡＧを消費しないが普通のレベルアップもせず、専用の合体かＭＡＧ強化でのみ強くなること。 強くなった造魔を利用して、英雄や猛将と縁の強い悪魔を合体させることで擬似的に英雄や猛将を再現するのが英雄合体さ」
		PRINTL 
		PRINTL 「で、イケニエ合体の場合だけども……これについては神話における同一性について話そうかな？ まず、多神教における主神とされるようなメジャー所の悪魔は、誰々と婚姻を結び子を成したとか何処其処に訪れて何かしていったとか、そういう箔付けのダシに使われている事が少なくないが……実はこれ、見方を変えると面白いことが見えてきてね」
		PRINTL 「逆に考えて婚姻を結んだ悪魔やどこかに訪れた悪魔が、後に主神格になる悪魔と同一の存在に……それこそ悪魔合体で混ざりあったのだとしたら？ それは事実上、かつての強力な悪魔がやったことになると思わないかい？ この性質を利用して特定の悪魔へ逸話を追加していくのがイケニエ合体という訳だ」
		PRINTL 
		PRINTW 「しかもこの性質を利用した場合、悪魔は強くなっていくものの姿形や悪魔としての格が変わったりはしない。 あくまでも逸話の追加による強化だからねこれ。 長くなったけども、悪魔を同一の悪魔のまま強くするのがイケニエ合体」
		PRINTL 
		PRINTW …
		PRINTW ……
		PRINTW ………
		PRINTL 
		PRINTW 「まあざっとこんなところかな？ ……百聞は一見にしかずとも言うし、実際に試してみるか、あと100回聞いてみるなりするのをオススメするよ」
		GOTO INPUT_LOOP
	ELSE
		PRINTL 
		PRINTL 「では、やめておくとしよう」
		GOTO INPUT_LOOP
	ENDIF
ELSEIF RESULT == 0
	PRINTL 付き合っていられない……。
	PRINTFORMW %CALLNAME:MASTER%は、その場から離れた。
ELSE
	GOTO INPUT_LOOP
ENDIF