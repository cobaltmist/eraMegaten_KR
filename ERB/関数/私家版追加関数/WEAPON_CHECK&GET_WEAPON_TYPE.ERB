;AGR キャラ
@MATCHING_WEAPON_CHECK(ARG)
#DIM 검타입
SIF (ABL:ARG:종족 != 0 && ABL:ARG:종족 != 36) || CFLAG:ARG:악마변신 > 0
	RETURN 0
검타입 = 0
RESULT = 0
TRYCALLFORM 검타입_{EQUIP:ARG:검}
IF RESULT > 0 && RESULT < 12
	SIF TALENT:ARG:(249 + RESULT) > 0
		검타입 = RESULT
ENDIF
RETURN 검타입

;AGR キャラ
@WEAPON_STYLE_CHECK(ARG,검타입 = 0)
#DIM 검타입
SIF (ABL:ARG:종족 != 0 && ABL:ARG:종족 != 36) || CFLAG:ARG:악마변신 > 0
	RETURN 0
SIF 검타입 < 1 || 검타입 > 11
	RETURN 0
RESULT = 0
TRYCALLFORM 검타입_{EQUIP:ARG:검}
SIF RESULT > 0 && RESULT < 12 && 검타입 == RESULT
	RETURN TALENT:ARG:(249 + RESULT)
RETURN 0

;AGR キャラ
@WEAPON_CHECK_MIX(ARG)
#LOCALSIZE 1
;1.2ケタに得意武器（素質と合っていない装備をしていると0）
CALL MATCHING_WEAPON_CHECK(ARG)
LOCAL = RESULT
CALL WEAPON_STYLE_CHECK(ARG,RESULT)
;3ケタ以降は型
LOCAL += RESULT * 100
RETURN LOCAL
;刀術の二ノ型の場合201を返す

@GET_WEAPON_TYPE(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 1
		RETURNF "刀"
	CASE 2
		RETURNF "레이피어"
	CASE 3
		RETURNF "弓"
	CASE 4
		RETURNF "검"
	CASE 5
		RETURNF "槍"
	CASE 6
		RETURNF "斧、鈍器"
	CASE 7
		RETURNF "채찍"
	CASE 8
		RETURNF "投具"
	CASE 9
		RETURNF "杖、祭具"
	CASE 10
		RETURNF "棍"
	CASE 11
		RETURNF "拳闘具"
	CASEELSE
		RETURNF "그외"
ENDSELECT

@GET_WEAPON_TYPE_NUM(ARGS)
#FUNCTION
SELECTCASE ARGS
;KR판 임의수정, 충돌을 대비한 일본어, 한국어 병행 CASE화
	CASE "刀","日本刀","도","도검","일본도"
		RETURNF 1
	CASE "レイピア","突剣","레이피어","돌검"
		RETURNF 2
	CASE "弓","활"
		RETURNF 3
	CASE "剣","검"
		RETURNF 4
	CASE "槍","薙刀","長刀","창","나가나타","장도"
		RETURNF 5
	CASE "斧、鈍器","斧","鈍器","ハンマー","도끼、둔기","도끼","둔기","해머"
		RETURNF 6
	CASE "鞭","ムチ","채찍"
		RETURNF 7
	CASE "投具","ブーメラン","飛刀","투척","투구","부메랑","비도"
		RETURNF 8
	CASE "杖、祭具","杖","祭具","지팡이、제구","지팡이","제구"
		RETURNF 9
	CASE "棍","棒","곤봉","봉"
		RETURNF 10
	CASE "拳闘具","拳","グローブ","권투구","권","글로브"
		RETURNF 11
	CASEELSE
		RETURNF 0
ENDSELECT

