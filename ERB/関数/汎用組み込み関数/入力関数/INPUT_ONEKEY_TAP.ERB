;--------------------------------------------------------------------------------------------------
;키 입력 윈도우 함수　@INPUT_ONEKEY_TAP
;게임패드같은 배치의 버튼 및 입력 대기 상황 생성
;키 입력과 마우스 버튼 모두 입력 가능
;
;ARG 　　 "십자키"의 취급。
;  　값이0이면　　이외의 값이라면
;
; 　　　8　　　　　　W
;　　　4 6          A D (대소문자 구별 없음)
; 　　　2　　　　    S
;
;ARGS　 상하 윈도우 장식에 사용하는 문자
;ARGS:1 이후의 문자열 구분에 사용하는 문자。디폴트로는 "_"
;ARG_T  입력 대응 버튼。"키_버튼표시문자_첨부텍스트_색"의 형식으로 기술
;--------------------------------------------------------------------------------------------------
@INPUT_ONEKEY_TAP(ARG:0 = 0,ARGS:0 = "-",ARGS:1 = "_",ARG_T:0 = "",ARG_T:1 = "",ARG_T:2 = "",ARG_T:3 = "",ARG_T:4 = "",ARG_T:5 = "",ARG_T:6 = "",ARG_T:7 = "",ARG_T:8 = "",ARG_T:9 = "",ARG_T:10 = "",ARG_T:11 = "",ARG_T:12 = "",ARG_T:13 = "",ARG_T:14 = "",ARG_T:15 = "",ARG_T:16 = "",ARG_T:17 = "",ARG_T:18 = "",ARG_T:19 = "")
#DIMS ARG_T,20
#DIMS 버튼,20
#DIMS 키,20
#DIMS 버튼태그,20
#DIM 버튼컬러,20
#DIM 버튼문자수,1 = 0
#DIM 일렬폭,1 = 0
#DIM 가로,1
#DIM 세로,1
#DIM 리턴키유효,1
#LOCALSSIZE 4
#LOCALSIZE 1
ARG:1 = LIMIT(ARG:1,1,20)
FOR LOCAL,0,20
	ARRAYREMOVE LOCALS,0,4
	SPLIT ARG_T:LOCAL,ARGS:1,LOCALS
	키:LOCAL         = %LOCALS:0%
	버튼:LOCAL       = %LOCALS:1 == "" ? "" # "["+LOCALS:1+"]" %
	버튼태그:LOCAL   = %LOCALS:2%
	버튼컬러:LOCAL = ISNUMERIC(LOCALS:3) == 1 && LOCALS:3 != "" ? TOINT(LOCALS:3) # GETCOLOR()
	일렬폭 = MAX(일렬폭,STRLENS(LOCALS:0)+STRLENS(LOCALS:1)+STRLENS(LOCALS:2)+2)
NEXT

DRAWLINEFORM %ARGS:0%
FOR 세로,0,4
	SELECTCASE 세로
		CASE 1
			PRINTPLAIN 　 　
			PRINTBUTTON \@ ARG == 0 ? [８] # [Ｗ] \@,\@ ARG == 0 ? 8 # w \@
			PRINTPLAIN 　 　
		CASE 2
			PRINTPLAIN 　
			PRINTBUTTON \@ ARG == 0 ? [４] # [Ａ] \@,\@ ARG == 0 ? 4 # a \@
			PRINTPLAIN ┼
			PRINTBUTTON \@ ARG == 0 ? [６] # [Ｄ] \@,\@ ARG == 0 ? 6 # d \@
			PRINTPLAIN 　
		CASE 3
			PRINTPLAIN 　 　
			PRINTBUTTON \@ ARG == 0 ? [２] # [Ｓ] \@,\@ ARG == 0 ? 2 # s \@
			PRINTPLAIN 　 　
		CASEELSE
			PRINTPLAIN 　　 　 　　
	ENDSELECT
	FOR 가로,세로*5,세로*5+5,1
		SETCOLOR 버튼컬러:가로
		PRINTBUTTON 버튼:가로,키:가로
		PRINTPLAINFORM %버튼태그:가로,일렬폭-STRLENS(버튼태그:가로),LEFT%
		RESETCOLOR
	NEXT
	PRINTL
NEXT
DRAWLINEFORM %ARGS:0%

리턴키유효 = 0
LOCALS = \@ ARG == 0 ? 8462 # WASDwasd \@
FOR LOCAL,0,20
	LOCALS = %LOCALS%%키:LOCAL%
	SIF 키:LOCAL == "" && 버튼:LOCAL != ""
		리턴키유효 = 1
NEXT
CALL INPUT_CHAR(\@ FLAG:던전내조작설정 == 0 ? 8462 # wasdWASD \@,리턴키유효)
IF RESULTS != ""
	REUSELASTLINE 
	CLEARLINE 1
ENDIF

;입력용 Syntax Sugar
;ARG_T배열에 RESULTS:0～19를 대입한다
@INPUT_ONEKEY_TAP_RESULTS(ARG = 0,ARGS = "-",ARGS:1 = "_")
CALL INPUT_ONEKEY_TAP(ARG,ARGS,ARGS:1,RESULTS:0,RESULTS:1,RESULTS:2,RESULTS:3,RESULTS:4,RESULTS:5,RESULTS:6,RESULTS:7,RESULTS:8,RESULTS:9,RESULTS:10,RESULTS:11,RESULTS:12,RESULTS:13,RESULTS:14,RESULTS:15,RESULTS:16,RESULTS:17,RESULTS:18,RESULTS:19)
