;-------------------------------------------------
;수량 입력용 함수　@INPUT_MANY
;ARG에 최소값、ARG:1에 최대값을 지정
;계산기풍 콘솔에서 수치를 입력시킨다
;
;ARG・ARG:1　최저치 혹은 최대치　순서는 반대여도 좋다
;ARGS　		 옵션。일단 지금은 "로그를 남긴다(ログを残す)" 뿐 
;ARGS:1　	 예외수치 설정。범위 외에서도 통과할 수치를 설정한다
;		 	 기술방법은 0/20/30/-1 처럼、문자열로 "/" 를 구분 문자로 한다
;-------------------------------------------------
@INPUT_MANY(ARG, ARG:1, ARGS = "로그를 남긴다", ARGS:1)
#LOCALSIZE 3
#LOCALSSIZE 20
#DIM LINE, 1
;最初は0
LOCAL = 0
LOCAL:1 = 1
VARSET LOCALS
SPLIT ARGS:1, "/", LOCALS
LINE = LINECOUNT
$START
REDRAW 0
CLEARLINE LINECOUNT - LINE
PRINTFORML 【{LOCAL}】　《【{ARG}】 - 【{ARG:1}】》　\@FLAG:SHOP커맨드 == [[SHOP:마카교환]] ?【￥{LOCAL*50}】#\@
PRINTBUTTON "[7]", "７"
PRINT 　
PRINTBUTTON "[8]", "８"
PRINT 　
PRINTBUTTON "[9]", "９"
PRINT 　
PRINTBUTTON "[ AC]", "AC"
PRINTL 

PRINTBUTTON "[4]", "４"
PRINT 　
PRINTBUTTON "[5]", "５"
PRINT 　
PRINTBUTTON "[6]", "６"
PRINT 　
PRINTBUTTON "[Max]", "MAX"
PRINTL 

PRINTBUTTON "[1]", "１"
PRINT 　
PRINTBUTTON "[2]", "２"
PRINT 　
PRINTBUTTON "[3]", "３"
PRINT 　
PRINTBUTTON "[Min]", "MIN"
PRINTL 

PRINTBUTTON "[0]", "０"
PRINT 　
IF LOCAL:1 == -1
	PRINTBUTTON "[+]", "+"
ELSE
	PRINTBUTTON "[-]", "-"
ENDIF
PRINT 　
PRINTBUTTON "[ENTER]", "ENTER"
PRINTL 
IF FLAG:SHOP커맨드 == [[SHOP:마카교환]] && ARG:1 > 20000
PRINT 　
PRINTBUTTON "[￥1,000,000]", "20000"
PRINTL 
ENDIF
PRINTFORML ※키보드로、직접 입력할 수도 있습니다
INPUTS
SELECTCASE RESULTS
	CASE "AC"
		RESTART
	CASE "+"
		LOCAL *= -1
		LOCAL:1 = 1
	CASE "-"
		LOCAL *= -1
		LOCAL:1 = -1
	CASE "MIN"
		LOCAL = ARG
	CASE "MAX"
		LOCAL = ARG:1
	CASE "０", "１", "２", "３", "４", "５", "６", "７", "８", "９"
		IF LOCAL == 0
			LOCAL = ABS(LOCAL) + TOINT(TOHALF(RESULTS)) * LOCAL:1
		ELSE
			;桁数を取る
			LOCAL:2 = 1
			WHILE LOCAL >= POWER(10, LOCAL:2)
				LOCAL:2++
				IF LOCAL:2 > 18
					PRINTFORMW 자릿수가 너무 많습니다！
					GOTO START
				ENDIF
			WEND
			LOCAL = LOCAL * 10 + TOINT(TOHALF(RESULTS))
		ENDIF
	CASEELSE
		IF RESULTS != "ENTER"
 			FOR LOCAL:2, 0, STRLENS(RESULTS)
 				SIF GROUPMATCH(SUBSTRING(RESULTS, LOCAL:2, 1), "0", "1", "2", "3", "4", "5", "6", "7", "8", "9")
 					CONTINUE
				PRINTFORMW 입력이 이상합니다。입력을 다시 해주세요
				RESTART
 			NEXT
			LOCAL = TOINT(RESULTS)
		ENDIF
		IF RANGE(LOCAL, ARG, ARG:1) || MATCH(LOCALS, TOSTR(LOCAL))
			SIF !STRCOUNT(ARGS, "로그를 남긴다")
				CLEARLINE LINE - LINECOUNT
			REDRAW 1
			RETURN LOCAL
		ELSE
			PRINTFORMW 수치가 범위 밖입니다。입력을 다시 해주세요
			RESTART
		ENDIF
ENDSELECT
GOTO START