;********************************************************************************************
;2020/06/19 作成 なんとなくパッチを作ってる人
;
;Licence  :ライセンスフリー(CC0)。
;*画像リソースに関してはリソース内にあるreadme&利用規約に従って下さい
;
;画像エディットの画像合成処理部分
;********************************************************************************************

;*******************************************************************************************************
;画像の合成処理に使うグラフィック領域を初期化
; 既に存在する場合はスキップ
;*******************************************************************************************************
@合成画像領域初期化
CALL 画像領域初期化(グラフィックID_小, 画像サイズ_小, 画像サイズ_小)
CALL 画像領域初期化(グラフィックID_中, 画像サイズ_中, 画像サイズ_中)
SIF !SPRITECREATED("スプライト_小")
	SPRITECREATE "スプライト_小", グラフィックID_小
SIF !SPRITECREATED("スプライト_中")
	SPRITECREATE "スプライト_中", グラフィックID_中

;*******************************************************************************************************
;指定されたパーツ文字列を使用して合成画像を作成
; 生成された画像のスプライト文字列をRESULTに収納
;
; *使用例
;	CALL 合成画像生成("[画像リソース:default][画像種別:顔絵][前髪:前髪_お嬢様][後髪:後髪_ウェーブセミロング][頭部:頭部_普通な顔1]")
;*******************************************************************************************************
@合成画像生成(パーツ文字列)
#DIMS パーツ文字列
#DIMS パーツセット名
#DIM 顔絵フラグ
パーツセット名 '= DIC_GET(パーツ文字列, "画像リソース")
顔絵フラグ = (DIC_GET(パーツ文字列, "画像種別") == "顔絵")
IF パーツ画像サイズ(パーツセット名, 顔絵フラグ) == 画像サイズ_中
	CALL モブ画像合成(グラフィックID_中, パーツ文字列)
	RESULTS = スプライト_中
ELSE
	CALL モブ画像合成(グラフィックID_小, パーツ文字列)
	RESULTS = スプライト_小
ENDIF

;*******************************************************************************************************
@ 画像領域初期化(グラフィックID, グラフィック横幅, グラフィック縦幅)
#DIM グラフィックID
#DIM グラフィック横幅
#DIM グラフィック縦幅
SIF !GCREATED(グラフィックID)
	GCREATE グラフィックID, グラフィック横幅, グラフィック縦幅

;*******************************************************************************************************
@モブ画像合成(グラフィックID, パーツ文字列)
#DIM グラフィックID
#DIMS パーツ文字列
#DIMS _CN_CHARAIMG_FILELIST
#DIMS _SPRITENAME_TOKEN,10
#DIM _CNT
#DIMS _COLORNAME
#DIM COLORINT
#DIMS _SPRITE
#DIM TOKEN_COLOR_NO = 2
#DIMS パーツセット名
#DIM 顔絵フラグ

パーツセット名 '= DIC_GET(パーツ文字列, "画像リソース")
顔絵フラグ = (DIC_GET(パーツ文字列, "画像種別") == "顔絵")

CALL カラーマトリクス初期化
IF (!GCREATED(グラフィックID))
	THROW 未作成のGrahpicsが指定された:グラフィックID == {グラフィックID}
ENDIF
_CN_DRAW_CHARASPRITE_COUNT = 0
CALL DIC_FOREACH(パーツ文字列, "CN_FE_CHARACTER_SPRITE_LIST", パーツセット名)

_CN_DRAW_CHARASPRITE_NAME:_CN_DRAW_CHARASPRITE_COUNT '= ""
_CN_DRAW_CHARASPRITE_DEPTH:_CN_DRAW_CHARASPRITE_COUNT = 0
ARRAYMSORT _CN_DRAW_CHARASPRITE_DEPTH, _CN_DRAW_CHARASPRITE_NAME

GCLEAR グラフィックID, 0x00000000
FOR _CNT,0,_CN_DRAW_CHARASPRITE_COUNT
	_SPRITE '= _CN_DRAW_CHARASPRITE_NAME:_CNT
	SIF 顔絵フラグ
		_SPRITE '= _SPRITE + "_顔絵"

	IF !SPRITECREATED(_SPRITE)
		CONTINUE
	ENDIF

	SPLIT _SPRITE, "_", _SPRITENAME_TOKEN
	IF (RESULT > 1 && _SPRITENAME_TOKEN:TOKEN_COLOR_NO != "無")
		_COLORNAME '= DIC_GET(パーツ文字列, _SPRITENAME_TOKEN:TOKEN_COLOR_NO +"色")
		IF (_COLORNAME != "")
			COLORINT = TOINT(DIC_GET(CN_COLORMATRIX_DIC, _COLORNAME))
			GDRAWSPRITE グラフィックID, _SPRITE, 0, 0, SPRITEWIDTH(_SPRITE), SPRITEHEIGHT(_SPRITE), カラーマトリクス:COLORINT:0:0
			CONTINUE 
		ENDIF
	ENDIF
	GDRAWSPRITE グラフィックID, _SPRITE
NEXT

;*******************************************************************************************************
@CN_FE_CHARACTER_SPRITE_LIST(_KEY, _VALUE, パーツセット名)
#DIMS _KEY
#DIMS _VALUE
#DIMS パーツセット名
#DIM _SUBKEY = 0
#DIMS _FILELIST
#DIM _CNT
#DIMS _TOKENS,100
IF (STRLENS(_KEY) < 2)
	THROW データ異常
ELSEIF (SUBSTRINGU(_KEY, 1, 1) == "色")
	RETURN
ENDIF
_SUBKEY = 0
IF (STRLENS(_KEY) > 2)
	_SUBKEY = TOINT(SUBSTRINGU(_KEY, 2, -1))
ENDIF

_FILELIST '= DIC_GET(パーツデータ(パーツセット名), _VALUE)
SPLIT _FILELIST, ",", _TOKENS
FOR _CNT,0,RESULT
	IF (_TOKENS:_CNT == "")
		CONTINUE
	ENDIF
	_CN_DRAW_CHARASPRITE_NAME:_CN_DRAW_CHARASPRITE_COUNT '= _TOKENS:_CNT
	_CN_DRAW_CHARASPRITE_DEPTH:_CN_DRAW_CHARASPRITE_COUNT = -(100 + TOINT(SUBSTRINGU(_TOKENS:_CNT, 2, 2))*100 + (_SUBKEY + 1))
	_CN_DRAW_CHARASPRITE_COUNT++
NEXT

;*******************************************************************************************************
@TEXTR(ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6)
#FUNCTIONS
#DIM  L_RESULT               ;RESULT退避用
#DIMS DYNAMIC CHOICES        ;結果
#DIMS DYNAMIC STR_ARRAY, 500 ;選択肢格納用の一時変数
L_RESULT = RESULT
;まず一つにまとめる
ARGS = %ARGS%\/%ARGS:1%\/%ARGS:2%\/%ARGS:3%\/%ARGS:4%\/%ARGS:5%\/%ARGS:6%
;不要な"/"を削る作業
;空のARGSがあると//がたくさん出来て判定の邪魔になるので
;1.//は置換で削る
;2.最末尾が/なら削る（SPLITとRANDをそのまま使う都合上）
ARGS '= REPLACE(ARGS, "\/+", "/")
SIF CHARATU(ARGS, STRLENSU(ARGS) - 1) == "/"
	ARGS '= SUBSTRINGU(ARGS, 0, STRLENSU(ARGS) - 1)
SPLIT ARGS, "/", STR_ARRAY
CHOICES '= STR_ARRAY:(RAND:RESULT)
SIF CHOICES == "＿"
	CHOICES = 
RESULT = L_RESULT
RETURNF CHOICES

;********************************************************************************************
;画像生成に使用する色設定を初期化する
;********************************************************************************************
@カラーマトリクス初期化
#DIM _CNT

; 既に初期化済みの場合、処理を抜ける
SIF カラーマトリクス初期化フラグ
	RETURN

_CNT = 0;"[青:0][紫:1][赤:2][黄:3][緑:4][水:5][白:6][黒:7][灰:8]"
	カラーマトリクス:_CNT:0:0 = 256,  0,  0,  0,  0;R
	カラーマトリクス:_CNT:1:0 =   0,256,  0,  0,  0;G
	カラーマトリクス:_CNT:2:0 =   0,  0,256,  0,  0;B
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 1;"紫"
	カラーマトリクス:_CNT:0:0 = 196,196,  0,  0,  0;R
	カラーマトリクス:_CNT:1:0 =   0,196,196,  0,  0;G
	カラーマトリクス:_CNT:2:0 = 196,  0,196,  0,  0;B
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 2;"赤"
	カラーマトリクス:_CNT:0:0 =   0,256,  0,  0,  0
	カラーマトリクス:_CNT:1:0 =   0,  0,256,  0,  0
	カラーマトリクス:_CNT:2:0 = 256,  0,  0,  0,  0
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256
_CNT = 3;"黄"
	カラーマトリクス:_CNT:0:0 =   0,196,196,  0,  0;
	カラーマトリクス:_CNT:1:0 = 196,  0,196,  0,  0;
	カラーマトリクス:_CNT:2:0 = 196,196,  0,  0,  0;
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256;
_CNT = 4;"緑"
	カラーマトリクス:_CNT:0:0 =   0,  0,256,  0,  0
	カラーマトリクス:_CNT:1:0 = 256,  0,  0,  0,  0
	カラーマトリクス:_CNT:2:0 =   0,256,  0,  0,  0
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256
_CNT = 5;"水"
	カラーマトリクス:_CNT:0:0 = 196,  0,196,  0,  0;
	カラーマトリクス:_CNT:1:0 = 196,196,  0,  0,  0;
	カラーマトリクス:_CNT:2:0 =   0,196,196,  0,  0;
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256;
_CNT = 6;"白"//元絵が青一色だから
	カラーマトリクス:_CNT:0:0 = 128,128,128,  0,  0;
	カラーマトリクス:_CNT:1:0 = 128,128,128,  0,  0;
	カラーマトリクス:_CNT:2:0 = 128,128,128,  0,  0;
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256;
_CNT = 7;"灰"
	カラーマトリクス:_CNT:0:0 = 128,128,128,  0,  0;
	カラーマトリクス:_CNT:1:0 = 128,128,128,  0,  0;
	カラーマトリクス:_CNT:2:0 = 128,128,128,  0,  0;
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクス:_CNT:4:0 = -64,-64,-64,  0,256;
_CNT = 8;"黒"
	カラーマトリクス:_CNT:0:0 = 128,128,128,  0,  0;
	カラーマトリクス:_CNT:1:0 = 128,128,128,  0,  0;
	カラーマトリクス:_CNT:2:0 = 128,128,128,  0,  0;
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクス:_CNT:4:0 = -128,-128,-128,  0,256;
;追加色
_CNT = 11;"[栗:11][桃:12][橙:13]"
	カラーマトリクス:_CNT:0:0 =  56, 30,  6,  0,  0;
	カラーマトリクス:_CNT:1:0 =  56, 30,  6,  0,  0;
	カラーマトリクス:_CNT:2:0 =  56, 30,  6,  0,  0;
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256;
_CNT = 12;"桃"
	カラーマトリクス:_CNT:0:0 = 126, 58, 76,  0,  0;
	カラーマトリクス:_CNT:1:0 = 126, 58, 76,  0,  0;
	カラーマトリクス:_CNT:2:0 = 126, 58, 76,  0,  0;
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0
	カラーマトリクス:_CNT:4:0 =   3,  3,  3,  0,256
_CNT = 13;"橙"
	カラーマトリクス:_CNT:0:0 = 128, 71,  0,  0,  0;
	カラーマトリクス:_CNT:1:0 = 128, 71,  0,  0,  0;
	カラーマトリクス:_CNT:2:0 = 128, 71,  0,  0,  0;
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256

_CNT = 31;"[普通:31][褐色:32][黄色:33][日焼:34][不健康:35][悪魔:36]"
	カラーマトリクス:_CNT:0:0 = 256,  0,  0,  0,  0;R
	カラーマトリクス:_CNT:1:0 =   0,256,  0,  0,  0;G
	カラーマトリクス:_CNT:2:0 =   0,  0,256,  0,  0;B
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 32;"褐色" (0.9, 0.7, 0.7)
	カラーマトリクス:_CNT:0:0 = 231,  0,  0,  0,  0;R
	カラーマトリクス:_CNT:1:0 =   0,180,  0,  0,  0;G
	カラーマトリクス:_CNT:2:0 =   0,  0,180,  0,  0;B
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 33;"黄色" (1.05, 1.05, 1)
	カラーマトリクス:_CNT:0:0 = 269,  0,  0,  0,  0;R
	カラーマトリクス:_CNT:1:0 =   0,269,  0,  0,  0;G
	カラーマトリクス:_CNT:2:0 =   0,  0,256,  0,  0;B
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 34;"日焼" (1.2, 0.95, 0.95)
	カラーマトリクス:_CNT:0:0 = 308,  0,  0,  0,  0;R
	カラーマトリクス:_CNT:1:0 =   0,244,  0,  0,  0;G
	カラーマトリクス:_CNT:2:0 =   0,  0,244,  0,  0;B
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 35;"不健康" (1.2, 1.05, 1.2)
	カラーマトリクス:_CNT:0:0 = 308,  0,  0,  0,  0;R
	カラーマトリクス:_CNT:1:0 =   0,269,  0,  0,  0;G
	カラーマトリクス:_CNT:2:0 =   0,  0,308,  0,  0;B
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256;平行
_CNT = 36;"악마" (0.75, 0.75, 1.5)
	カラーマトリクス:_CNT:0:0 = 192,  0,  0,  0,  0;R
	カラーマトリクス:_CNT:1:0 =   0,192,  0,  0,  0;G
	カラーマトリクス:_CNT:2:0 =   0,  0,384,  0,  0;B
	カラーマトリクス:_CNT:3:0 =   0,  0,  0,256,  0;A
	カラーマトリクス:_CNT:4:0 =   0,  0,  0,  0,256;平行

カラーマトリクス初期化フラグ = 1
