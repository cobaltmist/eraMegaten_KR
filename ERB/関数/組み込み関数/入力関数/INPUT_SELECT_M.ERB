;-------------------------------------------------
;테두리포함 선택지 제시+입력함수 @INPUT_SELECT_M
;ARGS 		표시할 문자열。구분문자를 이용해 줄바꿈
;구분문자	이것을 적음으로써 개행된다
;옵션		로그를 남긴다/버튼을 이용한다
;열수 		열의 수를 정의한다。
;행수		행의 수를 정의한다。내용이 행수에 미치지 못한 경우도 공행이 기술된다。
;			정의된 행수보다 내용이 많았던 경우는 자동적으로 행수는 조정된다
;위치		LEFT/CENTER/RIGHT
;자폭		1행의 사이즈를 결정한다。또한、자폭을 초과한 행이 있는 경우는 그에 맞춘다
;-------------------------------------------------
@INPUT_SELECT_M, ARGS, 구분문자 = "/", 옵션 = "로그를 남기지 않는다/버튼을 이용한다" , 열수 = 1, 행수 = 1, 위치 = "LEFT", 자폭 = 72
#DIM DYNAMIC LINE
#DIMS DYNAMIC NUM, 50
#DIMS DYNAMIC CHOICE, 50
#DIMS DYNAMIC 구분문자
#DIM DYNAMIC 열수
#DIM DYNAMIC 행수
#DIM DYNAMIC 자폭
#DIM DYNAMIC 총자폭
#DIM DYNAMIC 선택지수
#DIMS DYNAMIC 옵션
#DIM DYNAMIC 로그취득완료
#DIM DYNAMIC ONEINPUTS체크
#DIMS DYNAMIC 로그
#DIMS DYNAMIC 위치
#LOCALSSIZE 2
VARSET CHOICE
VARSET NUM, "EMPTY"
LINE = LINECOUNT
SPLIT ARGS, 구분문자, CHOICE
선택지수 = RESULT
로그취득완료 = 0
자폭 = (자폭 - (2 * (열수 - 1))) / 열수
ONEINPUTS체크 = 1
;偶数にしつつ、文字列の長さを취득
FOR LOCAL, 0, 선택지수
	IF STRCOUNT(CHOICE:LOCAL , "]")
		;[]の中身を취득
		SPLIT CHOICE:LOCAL, "]", LOCALS
		SPLIT LOCALS, "[", RESULTS
		SIF STRLENS(RESULTS:1) > 1
			ONEINPUTS체크 = 0
		NUM:LOCAL = %RESULTS:1%
	ENDIF
	;偶数化しつつ、최대値操作
	자폭 = (MAX(STRLENS(CHOICE:LOCAL), 자폭) + 1) / 2 * 2
	;選択肢は空白を부여して자폭を揃える
	CHOICE:LOCAL = %CHOICE:LOCAL, 자폭, LEFT%
NEXT
총자폭 = 자폭 * 열수 + (열수 - 1) * 2
행수 = MAX(행수, 선택지수 / 열수)
$START
로그 = 
CALL SET_ALIGNMENT(위치)
CLEARLINE LINECOUNT - LINE
PRINTFORML ┌%"─" * (총자폭/2)%┐
FOR LOCAL, 0, 행수
	PRINT │
	FOR LOCAL:1, 0, 열수
		IF LOCAL:1 > 0
			PRINT 　
			로그 += "　"
		ENDIF
		IF CHOICE:(LOCAL * 열수 + LOCAL:1) == ""
			PRINTFORM %" " * 자폭%
			로그 += " " * 자폭
		ELSEIF NUM:(LOCAL * 열수 + LOCAL:1) == "EMPTY"
			PRINTFORM %CHOICE:(LOCAL * 열수 + LOCAL:1)%
			로그 += CHOICE:(LOCAL * 열수 + LOCAL:1)
		ELSE
			PRINTBUTTON CHOICE:(LOCAL * 열수 + LOCAL:1), NUM:(LOCAL * 열수 + LOCAL:1)
			로그 += CHOICE:(LOCAL * 열수 + LOCAL:1)
		ENDIF
	NEXT
	로그 += 구분문자
	PRINTL │
NEXT
;下표시
PRINTFORM └%"─"*(총자폭/2-23)%
IF STRCOUNT(옵션, "버튼을 이용한다")
	PRINTBUTTON "[+] LOG", "+"
	PRINTBUTTON @" [-] AUTO<\@FLAG:자동넘기기 ?%" ON"%#OFF\@>", "-"
	PRINTBUTTON @" [*] SKIP<\@FLAG:윈도우메시지스킵 ?%" ON"%#OFF\@>", "*"
	PRINTBUTTON " [/] CONFIG", "/"
ELSE
	PRINTFORM %"─"*23%
ENDIF
PRINTL ┘
CALL SET_ALIGNMENT("PREV")
IF 로그취득완료++ == 0
	;誤クリック防止に少し웨이트をかける
	TWAIT 250, 1
	CALL MESSAGE_WINDOW_LOG("", 로그, 구분문자, 행수, 총자폭)
ENDIF
REDRAW 0
IF ONEINPUTS체크
	ONEINPUTS
ELSE
	INPUTS
ENDIF
SIF RESULTS != ""
	CLEARLINE 1
REDRAW 0
;ボタン選択
IF (GROUPMATCH(RESULTS, "+", "-", "/")) && STRCOUNT(옵션, "버튼을 이용한다")
	;로그閲覧
	IF RESULTS == "+"
		CALL MESSAGE_WINDOW_LOG(, , , , , 1)
	;자동넘기기モード切替
	ELSEIF RESULTS == "-"
		INVERTBIT FLAG:자동넘기기, 0
	;スキップモード切替
	ELSEIF RESULTS == "*"
		INVERTBIT FLAG:윈도우메시지스킵, 0
	ELSEIF RESULTS == "/"
		CALL MESSAGE_WINDOW_CONFIG
	ENDIF
	GOTO START
ENDIF
SIF !MATCH(NUM, RESULTS) || RESULTS == "EMPTY"
	GOTO START
LOCAL = TOINT(RESULTS)
CALL MESSAGE_WINDOW_LOG("" , CHOICE:FINDELEMENT(NUM, RESULTS), 구분문자, 행수 , 총자폭)
SIF !STRCOUNT(옵션, "로그를 남긴다")
	CLEARLINE LINECOUNT - LINE
RETURN LOCAL
