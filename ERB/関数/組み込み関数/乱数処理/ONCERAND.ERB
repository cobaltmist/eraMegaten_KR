;----------------------------------------------------------
;@ONCERAND
;랜덤하게 1～ARG:0을 반환한다。다만 한번 반환한 숫자는 반환하지 않는다。숫자를 다 쓰면 0을 반환한다。
;ARG:0　乱数の最大値（最大63?）
;ARG:1　パターン番号（）　呼び出す時は被らないような番号で
;@LOSE_COLOSSEUM以外での使用は想定してない
;なんかもっとわかりやすい・スマートな方法があったら直してくれ
;LOCAL:0 一時変数
;LOCAL:（パターン番号＋１）　数字の使用状況　
;　　　（使用した数字－１）番目のビットが１：使用済み０：未使用
;----------------------------------------------------------
@ONCERAND(ARG:0, ARG:1, ARG:2 = 0)
#FUNCTION
#LOCALSIZE 300
#DIM LCOUNT
;ARG:2 が1以上なら、LOCALをリセット
SIF ARG:2 > 0
	VARSET LOCAL
;ARG:2 が2で有る場合、乱数を使用せずに帰る。CALLFで呼ぶことしか想定してない
SIF ARG:2 == 2
	RETURNF 0
	;後いくつ数字が残っているか
	FOR LCOUNT, 0, ARG:0
		LOCAL += GETBIT(LOCAL:(ARG:1 +1), LCOUNT)
	NEXT
	;数字が残ってないなら0を返す
	SIF ARG:0 - LOCAL <= 0
		RETURNF 0
	;０～（残っている数字の数－１）　の乱数発生
	LOCAL = RAND:(ARG:0 - LOCAL)
	;（乱数）番目の残っている数字を返す
	FOR LCOUNT, 0, ARG:0
		IF GETBIT(LOCAL:(ARG:1 + 1), LCOUNT) == 0
			IF LOCAL == 0
				SETBIT LOCAL:(ARG:1 +1), LCOUNT
				RETURNF LCOUNT + 1
			ELSE
				LOCAL -= 1
			ENDIF
		ENDIF
	NEXT
;エラー
RETURNF -1