;-------------------------------------------------
;明智吾郎口上（今明智を使っているプレイヤーが一体何人いるんですかね）
;Ｐ５Ｒの三学期をイメージしているので黒い方のクロウです　白い時のセリフも使う情緒不安定さは原作通りです
;戦闘口上しか作ってないがCFLAGを使ってるので一応ALLにした
;ネタバレとか嫌な人は今の内に見るのを止めよう！
;-------------------------------------------------
;-------------------------------------------------
;eraMegaten専用口上テンプレート
;4769とX2をキャラ番号に置換し、Y1を助手のキャラ番号に置換してください
;ただし、キャラ番号が一桁のキャラの場合、4769,Y1は1、X2は01のように変えてください
;そしてコマンド内の適当な条件の下のPRINTFORMWの後ろに半角スペースを置いてセリフを追加してください
;口上を追加しないコマンドは無記入でも構いませんが、CFLAG:XXXの頭に『;』をつけてコメントアウトすると尚良いでしょう
;エキストラ構文など、コメントアウトされている構文を使う際には、『;』を外してコメントアウトを解除する必要があります
;最後にread meにも目を通しておくことをお勧めします
;-------------------------------------------------
;-------------------------------------------------
;汎用口上の作成に使用する場合は、4769→キャラ番号ではなく、
;K4769→PUB会話タイプ番号　へと変換してご使用ください
;-------------------------------------------------
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:EVENT_K4769_ASSI.ERB
;	Facility	:회화타입：助手時口上
;
;	Modification Data:
;	Edit	Date			Author					Reason
;	001		2020/12/11		木綿豆腐				新規作成　ただし戦闘口上のみ
;													明智君を調教したいあなたによる加筆を求む
;	002		2021/03/08		木綿豆腐				Rev134で更新された戦闘口上に対応
;	003		2021/08/28		木綿豆腐				最新版戦闘口上に対応したり구상변수초기화を仕込んだり
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;ライセンスについて
;	・基本項目(許可する項目を"○"に、許可しない項目を"×"に変更してください。)
;	+----+----+-------------------------------+
;	|番号|許可|ライセンス内容                 |
;	+----+----+-------------------------------+
;	|   1| ○ | 処理上のバグ修正              |
;	+----+----+-------------------------------+
;	|   2| ○ | 口上の誤字・脱字の修正        |
;	+----+----+-------------------------------+
;	|   3| ○ | 口上まとめへの収録            |
;	+----+----+-------------------------------+
;	|   4| ○ | 口上の新規追加                |
;	+----+----+-------------------------------+
;	|   5| ○ | 既存口上の改変                |
;	+----+----+-------------------------------+
;	|   6| ○ | 改変した口上の自由な再配布    |
;	+----+----+-------------------------------+
;	|   7| ○ | 口上内容の転載                |
;	+----+----+-------------------------------+
;-------------------------------------------------
;【使用CFLAG】
;　801：デビルＣＯ－ＯＰフラグ。総攻撃フィニッシュ用
;--------------------------------------------------
@BATTLE_EVENT_K4769, ARG, ARG:1, ARG:2
;■台詞の行数を増やしたいときは
;			LOCALS:10 = 一行目
;			LOCALS:11 = 二行目
;			LOCALS:12 = 三行目
;			LOCALS:13 = 四行目
;			LOCALS:14 = 五行目
;と増やしていく
SIF ARG < 0 && NO:ARG != 4769
	RETURN 0
VARSET LOCALS
CALL 구상변수초기화

CFLAG:ARG:801 += 1
;大きくなり過ぎないよう定期的に戻す
IF CFLAG:ARG:801 == 6
ELSEIF CFLAG:ARG:801 > 3
	CFLAG:ARG:801 = 0
ENDIF

SELECTCASE TFLAG:전투이벤트
	;COOP発動 ARGは発動者
	CASE 1
		IF RAND:4 == 0
			LOCALS:10 = 『TAKE YOuR hEaRT』！
			LOCALS:11 = （お前の心は頂いた！）
		ELSEIF RAND:3 == 0
			LOCALS:10 = お楽しみの時間だ！
		ELSEIF RAND:2 == 0
			LOCALS:10 = チェックメイトだな！
		ELSE
			LOCALS:10 = そのまま死ねッ！
		ENDIF
		CFLAG:ARG:801 = 5
	;COOP参加
	CASE 2
		;主人がCOOP発動
		IF FLAG:행동순1 != -1 && FLAG:행동순1 == MASTER
			IF RAND:3 == 0
				LOCALS:10 = アッハッハ！　殺しの時間だッ！
			ELSEIF RAND:2 == 0
				LOCALS:10 = いいね、盛り上がってきた……！
			ELSE
				LOCALS:10 = ハッ、狩ってやるぜ！
			ENDIF
		;ジョーカーがCOOP発動
		ELSEIF FLAG:행동순1 != -1 && NO:(FLAG:행동순1) == [[キャラ:P5주인공]]
			IF RAND:3 == 0
				LOCALS:10 = アッハッハ！　殺しの時間だッ！
			ELSEIF RAND:2 == 0
				LOCALS:10 = それぐらいはやって貰わないと困る
			ELSE
				LOCALS:10 = お前ならこの程度、出来て当然だよな？
			ENDIF
		;それ以外
		ELSE
			IF RAND:2 == 0
				LOCALS:10 = さっさと終わらせるぞ！
			ELSE
				LOCALS:10 = 俺が追撃してやる！
			ENDIF
		ENDIF
	;被召喚時
	CASE 3
			;LOCALS:10 = 
	;공격　ARGが攻撃をする仲魔、ARG:1が敵、ARG:2がダメージ量
	CASE 11
		CALLFORM SKILL_TYPE_{CFLAG:ARG:입력행동}
		LOCAL = MAXBASE:(ARG:1):GET_TYPE(RESULT)
		;味方に攻撃した
		;IF CFLAG:ARG:PT플래그 == CFLAG:(ARG:1):PT플래그
		;相手が死んだ
		IF BASE:(ARG:1):ＨＰ <= 0
			IF RAND:3 == 0
				LOCALS:10 = 二度と邪魔をするな
			ELSEIF RAND:2 == 0
				LOCALS:10 = どうだ、敗北の味は？
			ELSE
				LOCALS:10 = 思い知ったか？　邪魔するからだ
			ENDIF
		;弱点をついた。つまりCOOP
		ELSEIF LOCAL > 100 && LOCAL < 999
			IF CFLAG:(ARG:1):방어플래그
				LOCALS:10 = チッ、小癪なマネを……！
			ELSE
				;こっちはCOOP発動に回すためにカット
				RETURN 0
			ENDIF
		;大ダメージ
		ELSEIF 데미지비율(ARG:1, ARG:2) > 50
			LOCALS:10 = 往生際の悪い……！
		ELSEIF 데미지비율(ARG:1, ARG:2) > 25
			LOCALS:10 = 思ったよりしぶといな
		ELSEIF 데미지비율(ARG:1, ARG:2) > 0
			LOCALS:10 = 効果が薄い……切り替えて次だ
		;無効された
		ELSEIF 데미지비율(ARG:1, ARG:2) <= 0
			LOCALS:10 = 命拾いしたな？
		ENDIF
	;被ダメージ
	;ARGが攻撃を受ける仲魔、ARG:1が敵、ARG:2がダメージ量
	CASE 12
		;死んだ
		IF BASE:ARG:ＨＰ <= 0
			LOCALS:10 = あり得ない……この僕が……！
		;味方に攻撃された
		ELSEIF CFLAG:ARG:PT플래그 == CFLAG:(ARG:1):PT플래그
			LOCALS:10 = 貴様、どこ狙ってやがる！？
		;大ダメージ
		ELSEIF 데미지비율(ARG, ARG:2) > 50 || 현HP비율(ARG) < 25
			LOCALS:10 = クソッ……後悔させてやるよ！
		;무효
		ELSEIF ARG:2 == 0
			LOCALS:10 = 無駄なんだよ、間抜け
		;흡수
		ELSEIF ARG:2 < 0
			LOCALS:10 = なあ、今どんな気分だ？
		ELSE
			IF RAND:3 == 0
				LOCALS:10 = うざいなぁ……！
			ELSEIF RAND:2 == 0
				LOCALS:10 = よくもやってくれた
			ELSE
				LOCALS:10 = クソッ、避け切れない……！
			ENDIF
		ENDIF
	;範囲攻撃
	;ARGが攻撃をする仲魔、ARG:1が攻撃された敵数、ARG:2が無効数(回避も含む)
	CASE 13
		;全弾命中した
		IF ARG:2 == 0
			LOCALS:10 = こんなのは、当たり前の結果だよ
		;普通
		ELSEIF ARG:2 < ARG:1
			LOCALS:10 = まあ、こんなものかな
		;全弾回避された
		ELSEIF ARG:2 == ARG:1
			IF RAND:3 == 0
				LOCALS:10 = クッソうぜえ！
			ELSEIF RAND:2 == 0
				LOCALS:10 = 貴様、避けるな！
			ELSE
				LOCALS:10 = ちょこまかと！
			ENDIF
		ENDIF
	;範囲攻撃で敵を撃破した
	;ARGが攻撃をする仲魔、ARG:1が攻撃された敵数、ARG:2が撃破数
	CASE 14
		;全員撃破
		IF ARG:2 == ARG:1
			IF RAND:3 == 0
				LOCALS:10 = 二度と邪魔をするな
			ELSEIF RAND:2 == 0
				LOCALS:10 = どうだ、敗北の味は？
			ELSE
				LOCALS:10 = 思い知ったか？　邪魔するからだ
			ENDIF
		ELSE
			LOCALS:10 = フン……次はお前だ
		ENDIF
	;反射された
	CASE 15
		LOCALS:10 = ゴミの癖に……！
	;反射した
	CASE 16
		LOCALS:10 = ハッ、無駄だって
	;回避された
	CASE 17
		IF RAND:3 == 0
			LOCALS:10 = クッソうぜえ！
		ELSEIF RAND:2 == 0
			LOCALS:10 = 貴様、避けるな！
		ELSE
			LOCALS:10 = ちょこまかと！
		ENDIF
	;回避した
	CASE 18
		IF RAND:3 == 0
			LOCALS:10 = 無様だね
		ELSEIF RAND:2 == 0
			LOCALS:10 = どこを狙ってる？
		ELSE
			LOCALS:10 = 当たるか、ザコがッ！
		ENDIF
	;敵味方全体攻撃(詳細処理無し)
	;CASE 19
	;		LOCALS:10 = 
	;反撃スキル発動した
	CASE 20
		SELECTCASE TFLAG:발동연동스킬
		;닷지카운터
		CASE 2484
			LOCALS:10 = やれやれ……鈍間過ぎる
		;그외
		CASEELSE
			LOCALS:10 = やったな？　お返しだ……！
		ENDSELECT
	;トリガースキルを発動した　現状トリガースキル持ちの悪魔って何がおるんやろか
	;CASE 21
		;SELECTCASE TFLAG:발동연동스킬
		;○○
		;CASE 
		;	LOCALS:10 = 
		;그외
		;CASEELSE
		;	LOCALS:10 = 
		;ENDSELECT
	;バッドステータスを受けた
	CASE 22
		SELECTCASE GET_STATE(CFLAG:ARG:상태이상)
			CASE "HAPPY"
				LOCALS:10 = ハ、ハハハハハ！　最高の気分だ！
			CASE "SHOCK"
				LOCALS:10 = まずい、感電して……
			CASE "FREEZE"
				LOCALS:10 = 身体が……凍って……
			CASE "BIND","PALYZE"
				LOCALS:10 = 身体が、動かない……！
			CASE "PANIC"
				LOCALS:10 = 頭が……混乱している……？
			CASE "SLEEP"
				LOCALS:10 = 睡眠……状態か……
			CASE "CLOSE"
				LOCALS:10 = 魔法を封じられた！？
			CASE "POISON"
				LOCALS:10 = 気分が悪い……
			;CASE "BOMB"
			CASE "CHARM"
				LOCALS:10 = だ、誰が貴様の言うことなど……！
			;CASE "CURSE"
			;CASE "FLY"
			;CASE "STONE"
			CASE "FLAME"
				LOCALS:10 = くそっ、火が……！
			CASE "SLIP"
				LOCALS:10 = この代償、高くつくぞ
			CASE "BRAND"
				LOCALS:10 = 焼き印だと？　ふざけやがって……！
			;その他の状態異常
			;CASEELSE
		ENDSELECT
	;食いしばり等で耐えた
	CASE 23
		;이악물기
		IF BASE:ARG:ＨＰ == 1
			LOCALS:10 = 貴様ァ……調子に乗ってんじゃねえよ……！
		;그외
		ELSE
			LOCALS:10 = 俺を本気にさせたいようだな……？
		ENDIF
	;回復してもらった
	CASE 24
		;ARG:2　回復量
		LOCAL = ARG:2 * 100 / BASE:ARG:ＨＰ
		;LOCAL　回復量割合
		IF 0
		;キャラ分岐
		ELSEIF NO:(ARG:1) == [[キャラ:P5주인공]]
			LOCALS:10 = 余計な真似を……
		;50%以上 & HPほぼ満タン
		ELSEIF LOCAL > 50 && 현HP비율(ARG) > 90
			LOCALS:10 = いいタイミングだな
		;50%以上
		ELSEIF LOCAL > 50
			LOCALS:10 = 礼は後だ
		;그외
		ELSE
			LOCALS:10 = フン……
		ENDIF
	;状態異常を直してもらった
	CASE 25
		;ARG:2　直ったバステ
		IF 0
		;キャラ分岐
		;ELSEIF NO:(ARG:1) == XXXX
		ELSE
			SELECTCASE GET_STATE(ARG:2)
				CASE "SLEEP"
					LOCALS:10 = この分は取り戻す
				;その他の状態異常
				CASEELSE
					LOCALS:10 = 礼は後だ
			ENDSELECT
		ENDIF
	;ターン終了時状態異常が自然回復した
	CASE 50
		SELECTCASE GET_STATE(CFLAG:ARG:상태이상)
			CASE "SLEEP"
				LOCALS:10 = チッ……まんまと嵌まっちまった
			;その他の状態異常
			CASEELSE
				LOCALS:10 = やり返してやるよ
		ENDSELECT
	;バトルに勝利した
	CASE 90
		;COOP発動時にCFLAG:ARG:801を固定しているのでCOOP発動で勝利すればここに入る
		IF CFLAG:ARG:801 == 6 && FLAG:행동순1 == ARG
			LOCALS:10 = 『I DECIDE THE TRUTH』
			LOCALS:11 = （真実は俺が決める）
		ELSEIF RAND:3 == 0
			LOCALS:10 = いいね。ハイな気分だ
		ELSEIF RAND:2 == 0
			LOCALS:10 = 手応えが無さ過ぎる
		ELSE
			LOCALS:10 = 簡単に壊れちゃったな
		ENDIF
	;レベルアップした
	CASE 91
		IF RAND:2 == 0
			LOCALS:10 = これでより一層の貢献を誓うよ
		ELSE
			LOCALS:10 = どうだい？　凄い力だろう？
		ENDIF
	;逃走に成功した
	CASE 92
		LOCALS:10 = 戦略的撤退だ
	;逃走に失敗した
	CASE 93
		LOCALS:10 = しつこい奴らだ……！
	;戦闘開始時
	CASE 94
		IF FLAG:선제공격플래그
			IF RAND:2 == 0
				LOCALS:10 = 無様だな、一気に潰そう
			ELSE
				LOCALS:10 = 先制だ！　可愛がってやるか！
			ENDIF
		ELSEIF 현HP비율(ARG) < 25
			;瀕死
			LOCALS:10 = 反撃の時間を与えるな！　一気に叩く！
		ELSE
			LOCALS:10 = 挑んできたことを後悔させてやる
		ENDIF
	;バックアタックを受けた
	CASE 95
		IF 현HP비율(ARG) < 25
			;瀕死
			LOCALS:10 = まずいな……さっさと片付けるぞ！
		ELSE
			LOCALS:10 = チッ、無駄な時間を……
		ENDIF
	;敵の先制攻撃を受けた
	CASE 96
		IF 현HP비율(ARG) < 25
			;瀕死
			LOCALS:10 = ザコの癖に……！
		ELSE
			LOCALS:10 = 調子に乗りやがって……！
		ENDIF
	;リンケージに参加した
	CASE 100
		IF 0
		;スキル分岐
		;ELSEIF CFLAG:(FLAG:행동순1):입력행동 == [[스킬:○○]]
		;キャラ分岐
		ELSEIF FLAG:행동순1 == MASTER
			LOCAL = RAND:3
			SELECTCASE LOCAL
			CASE 0
				LOCALS:10 = 手ぐらいは貸すさ
			CASE 1
				LOCALS:10 = これでチェックメイトだ
			CASE 2
				LOCALS:10 = 行くぞ、用意はいいな？
			ENDSELECT
		ELSEIF NO:(FLAG:행동순1) == [[キャラ:P5주인공]]
			LOCAL = RAND:3
			SELECTCASE LOCAL
			CASE 0
				LOCALS:10 = お前に合わせてやる
			CASE 1
				LOCALS:10 = 俺の力、使いこなしてみせろ！
			CASE 2
				LOCALS:10 = やれないとは言わせないぞ
			ENDSELECT
		ELSE
			LOCAL = RAND:3
			SELECTCASE LOCAL
			CASE 0
				LOCALS:10 = フン……俺を失望させるなよ？
			CASE 1
				LOCALS:10 = カバーはこちらでやってやる
			CASE 2
				LOCALS:10 = 行くぞ、用意はいいな？
			ENDSELECT
		ENDIF
ENDSELECT
SIF !(LOCALS:10 == "")
	CALL MESSAGE_B2(CALLNAME:ARG,0, LOCALS:10, LOCALS:11, LOCALS:12, LOCALS:13, LOCALS:14)

@BATTLE_MESSAGE_K4769,ARG,ARG:1 = 0
;■台詞の行数を増やしたいときは
;			LOCALS:10 = 一行目
;			LOCALS:11 = 二行目
;			LOCALS:12 = 三行目
;			LOCALS:13 = 四行目
;			LOCALS:14 = 五行目
;と増やしていく
VARSET LOCALS
CALL 구상변수초기화
;使用したスキルの名前をLOCALSに代入
CALLFORM SKILL_NAME_{CFLAG:ARG:입력행동},ARG
LOCALS = %RESULTS%
;FLAG:ARG:타겟

;崩壊してる場合は口上表示しないようにもできる
;SIF TALENT:ARG:9
;	RETURN 0

CALLFORM SKILL_EFECT_{CFLAG:ARG:입력행동}
;コメントアウトしやすいように
IF 1 == 0
;-------------------------------------------------
;状態異常時
;-------------------------------------------------
;魅了時
ELSEIF CFLAG:ARG:상태이상 == GET_STATE_NUM("CHARM")
	;행동불능（ムラムラしている）
	IF CFLAG:ARG:입력행동 == 2314
		LOCALS:10 = 一体、何が起きてる……？
	ELSE
		LOCALS:10 = くそ……頭が……ッ！
	ENDIF
;混乱時
ELSEIF CFLAG:ARG:상태이상 == GET_STATE_NUM("PANIC")
	;금전뿌리기
	IF CFLAG:ARG:입력행동 == 2315
		LOCALS:10 = 金ェ？　それが何だってんだよ……！
	;행동불능（ぼんやりしている）
	ELSEIF CFLAG:ARG:입력행동 == 2314
		LOCALS:10 = 頭が痛い……考えが、纏まらない……？
	ELSE
		LOCALS:10 = クソ、一体どうなってやがる……！？
	ENDIF
;-------------------------------------------------
;特定スキル
;-------------------------------------------------
;これは通常攻撃
ELSEIF CFLAG:ARG:입력행동 == 0
	IF RAND:3 == 0
		LOCALS:10 = 死んでもらおう
	ELSEIF RAND:2 == 0
		LOCALS:10 = 切り刻む
	ELSE
		LOCALS:10 = あの世行きだ！
	ENDIF
;섹시댄스
;ELSEIF CFLAG:ARG:입력행동 == 1212
;	LOCALS:10 = 
;캔디보이스
;ELSEIF CFLAG:ARG:입력행동 == 1213
;	LOCALS:10 = 
;파이널누드
;ELSEIF CFLAG:ARG:입력행동 == 1214
;	LOCALS:10 = 
;-------------------------------------------------
;총공격（特殊弾丸含む）
;-------------------------------------------------
ELSEIF RANGE(CFLAG:ARG:입력행동,2101, 2200)
	;GUN
	IF CFLAG:ARG:입력행동 == 2101
		IF RAND:3 == 0
			LOCALS:10 = 弾丸のプレゼントだ
		ELSEIF RAND:2 == 0
			LOCALS:10 = ハハッ！　効くだろ？
		ELSE
			LOCALS:10 = さっさと死ねッ！
		ENDIF
	;그외（特殊弾丸）
	ELSE
		LOCALS:10 = とっておきだ、外すかよ
	ENDIF
;-------------------------------------------------
;基本行動
;-------------------------------------------------
ELSEIF RANGE(CFLAG:ARG:입력행동,2301, 2400)
	;ＣＨＡＮＧＥ
	IF CFLAG:ARG:입력행동 == 2301
		LOCALS:10 = しくじるなよ？
	;Ｐ・ＣＨＡＮＧＥ
	ELSEIF CFLAG:ARG:입력행동 == 2304
		IF RAND:2 == 0
			LOCALS:10 = 俺の恐ろしさを味わえ！
		ELSE
			LOCALS:10 = ぶっ壊してやる……！
		ENDIF
	ELSE
		;LOCALS:10 = 
	ENDIF
;-------------------------------------------------
;アイテム
;-------------------------------------------------
ELSEIF RANGE(CFLAG:ARG:입력행동,3001, 3500)
	;LOCALS:10 = 
;-------------------------------------------------
;リンケージ
;-------------------------------------------------
ELSEIF RANGE(CFLAG:ARG:입력행동,4000, 5000)
	;메시아라이저
	IF CFLAG:ARG:입력행동 == 4160
		LOCALS:10 = 柄にもないことをさせる
	;Lヒートライザ
	ELSEIF CFLAG:ARG:입력행동 == 4200
		LOCALS:10 = ククッ……ヒャハハハハハッ！！
	;사망유희
	ELSEIF CFLAG:ARG:입력행동 == 4800
		LOCALS:10 = やってやるよ……そのための命、なんだろ
	ELSE
		IF RAND:2 == 0
			LOCALS:10 = これで決めるぞ、いいな？
		ELSE
			LOCALS:10 = 俺に合わせろ、行くぞッ！
		ENDIF
	ENDIF
ELSEIF CFLAG:ARG:입력행동 == [[스킬:레바테인]]
	LOCALS:10 = コイツを防げると思うなよ……！
ELSEIF CFLAG:ARG:입력행동 == [[스킬:반역의칼날]]
	;習得するのはヘリワードだけなので考慮する必要はないんだけど、一応
	IF EQUIP:ARG:장비페르소나 == 0 && ABL:ARG:초기페르소나 == 5526
		LOCALS:10 = 何もかもぶっ壊せ……ロキッ！
	ELSEIF EQUIP:ARG:장비페르소나 == 0 && ABL:ARG:초기페르소나 == 5527
		LOCALS:10 = 何もかもぶっ壊せ……ヘリワードッ！
	ELSE
		LOCALS:10 = 何もかもぶっ壊せ……%GET_PERSONA_NAME(ARG)%ッ！
	ENDIF
;-------------------------------------------------
;汎用スキル
;スキル系は1/2で専用口上、1/2で汎用口上
;-------------------------------------------------
;검격
ELSEIF RANGE(CFLAG:ARG:입력행동,1, 99) && RAND:2 == 0
	LOCALS:10 = ズタズタに切り刻んでやるッ
;비구
ELSEIF RANGE(CFLAG:ARG:입력행동,101, 199) && RAND:2 == 0
	IF CFLAG:ARG:입력행동 == [[스킬:암파쌍비조]]
		LOCALS:10 = 逃がすかよ、お前ら全員ぶっ殺すに決まってんだろ！
	ELSEIF CFLAG:ARG:입력행동 == [[스킬:오절절우]]
		LOCALS:10 = 一発も無駄にする気はない。全て当ててやるよ
	ELSE
		LOCALS:10 = 丸見えなんだよ、間抜けがァ！
	ENDIF
;타격
ELSEIF RANGE(CFLAG:ARG:입력행동,201, 299) && RAND:2 == 0
	LOCALS:10 = 殴り合うなんて泥臭い。僕のイメージに合わないな
;전술
ELSEIF RANGE(CFLAG:ARG:입력행동,301, 400) && RAND:2 == 0
	;噛み付き系
	IF RANGE(CFLAG:ARG:입력행동,321, 326)
		LOCALS:10 = 野蛮な技だな。気が乗らないよ
	;그외
	ELSE
		LOCALS:10 = 迂闊に俺を近づけた愚かさを悔いろ！
	ENDIF
;회복
ELSEIF RANGE(CFLAG:ARG:입력행동,401, 499) && RAND:2 == 0
	;全体回復
	IF RANGE(CFLAG:ARG:타겟,17, 19)
		LOCALS:10 = これでいいだろう？
	ELSE
		LOCALS:10 = ここは回復に徹しよう
	ENDIF
;지원
ELSEIF RANGE(CFLAG:ARG:입력행동,501, 599) && RAND:2 == 0
	;強化系
	IF RANGE(CFLAG:ARG:입력행동,501, 505) || CFLAG:ARG:입력행동 == 558
		LOCALS:10 = 了解だ、支援に回る
	;弱体系
	ELSEIF RANGE(CFLAG:ARG:입력행동,506, 509) || CFLAG:ARG:입력행동 == 541 || CFLAG:ARG:입력행동 == 556
		LOCALS:10 = 了解だ、支援に回る
	;도발
	ELSEIF CFLAG:ARG:입력행동 == 515
		IF RAND:2 == 0
			LOCALS:10 = 死にたい奴はどいつだ？
		ELSE
			LOCALS:10 = ほら、当ててみろよ
		ENDIF
	;그외
	ELSE
		LOCALS:10 = 了解だ、支援に回る
	ENDIF
;방어
ELSEIF RANGE(CFLAG:ARG:입력행동,601, 699) && RAND:2 == 0
	LOCALS:10 = 手の内はとっくに読めてんだよ
;화염
ELSEIF RANGE(CFLAG:ARG:입력행동,701, 799) && RAND:2 == 0
	LOCALS:10 = 焼け死ぬのは辛いらしいな
;빙결
ELSEIF RANGE(CFLAG:ARG:입력행동,801, 899) && RAND:2 == 0
	LOCALS:10 = 凍っている相手を砕くのも悪くない
;전격
ELSEIF RANGE(CFLAG:ARG:입력행동,901, 999) && RAND:2 == 0
	LOCALS:10 = 当たれば足止めくらいにはなるだろう
;충격
ELSEIF RANGE(CFLAG:ARG:입력행동,1001, 1099) && RAND:2 == 0
	;LOCALS:10 = 
;신경
ELSEIF RANGE(CFLAG:ARG:입력행동,1101, 1199) && RAND:2 == 0
	LOCALS:10 = まさか卑怯だなんて言わないよな？
;정신
ELSEIF RANGE(CFLAG:ARG:입력행동,1201, 1299) && RAND:2 == 0
	LOCALS:10 = 相手の戦力を削ぐのも手か
;파마
ELSEIF RANGE(CFLAG:ARG:입력행동,1301, 1399) && RAND:2 == 0
	LOCALS:10 = 僕はこっちも得意なんでね
;주살
ELSEIF RANGE(CFLAG:ARG:입력행동,1401, 1499) && RAND:2 == 0
	LOCALS:10 = いいだろう、俺の本領発揮だ
;지변
ELSEIF RANGE(CFLAG:ARG:입력행동,1501, 1599) && RAND:2 == 0
	;LOCALS:10 = 
;수격
ELSEIF RANGE(CFLAG:ARG:입력행동,1601, 1699) && RAND:2 == 0
	;LOCALS:10 = 
;질풍
ELSEIF RANGE(CFLAG:ARG:입력행동,1701, 1799) && RAND:2 == 0
	LOCALS:10 = 塵は吹き飛ばすに限る
;중력
ELSEIF RANGE(CFLAG:ARG:입력행동,1801, 1899) && RAND:2 == 0
	LOCALS:10 = 無様に這い蹲っていろ
;핵열
ELSEIF RANGE(CFLAG:ARG:입력행동,1901, 1999) && RAND:2 == 0
	LOCALS:10 = 俺にはこういう手もある
;만능
ELSEIF RANGE(CFLAG:ARG:입력행동,2001, 2099) && RAND:2 == 0
	;自爆系
	IF RANGE(CFLAG:ARG:입력행동,2005, 2007)
		LOCALS:10 = ここまでさせて全滅なんてしたら許さないからな
	ELSEIF CFLAG:ARG:입력행동 == [[스킬:메기도라온]]
		LOCALS:10 = 了解だ、全力で潰す！
	ELSE
		LOCALS:10 = ああ、派手にやってやろう
	ENDIF
;魔法少女系
ELSEIF RANGE(CFLAG:ARG:입력행동,2989, 2999) && RAND:2 == 0
	LOCALS:10 = %CSTR:일인칭%にこんなモノを使わせるなんて……気は確かか？
;-------------------------------------------------
;그외
;-------------------------------------------------
ELSE
	IF RAND:5 == 0
		IF EQUIP:ARG:장비페르소나 == 0
			IF ABL:ARG:초기페르소나 == 3985
				LOCALS:10 = 射殺せ、ロビンフッド！
			ELSEIF ABL:ARG:초기페르소나 == 5526
				LOCALS:10 = 降臨せよ、ロキ！
			ELSEIF ABL:ARG:초기페르소나 == 5527
				LOCALS:10 = 殺せ、ヘリワード！
			ELSE
				LOCALS:10 = 顕現せよ、%GET_PERSONA_NAME(ARG)%！
			ENDIF
		ELSE
			LOCALS:10 = 顕現せよ、%GET_PERSONA_NAME(ARG)%！
		ENDIF
	ELSEIF RAND:4 == 0
		LOCALS:10 = 顕現しやがれェッ！
	ELSEIF RAND:3 == 0
		LOCALS:10 = ペルソナァー！
	ELSEIF RAND:2 == 0
		LOCALS:10 = ははははは！　来やがれ、ペルソナッ！
	ELSE
		IF EQUIP:ARG:장비페르소나 == 0 && ABL:ARG:초기페르소나 == 5526
			LOCALS:10 = 来い、ロキ！
		ELSEIF EQUIP:ARG:장비페르소나 == 0 && ABL:ARG:초기페르소나 == 5527
			LOCALS:10 = 来い、ヘリワード！
		ELSE
			LOCALS:10 = 来い、%GET_PERSONA_NAME(ARG)%！
		ENDIF
	ENDIF
ENDIF
;-------------------------------------------------
;真・그외
;-------------------------------------------------
IF LOCALS:10 == ""
	;必ず何か喋る
	LOCALS:10 = 了解だ、%LOCALS%で行く！
ENDIF

SIF !(LOCALS:10 == "")
	CALL MESSAGE_B2(CALLNAME:ARG,1, LOCALS:10, LOCALS:11, LOCALS:12, LOCALS:13, LOCALS:14)
