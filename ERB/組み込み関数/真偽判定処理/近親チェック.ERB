;@近親チェック
;ARG, ARG:1의 캐릭터가 근친관계에 있는지 판단하고, 그 관계의 문자열을 LOCALS, LOCALS:1에 저장 후 RESULTS, RESULTS:1로 반환하는 함수
@근친체크(ARG , ARG:1)
#FUNCTION
#DIM CHARA , 2
#DIM 親ＩＤ , 4

FOR LOCAL , 0 , 2
	CHARA = ARG:LOCAL
	CHARA:1 = ARG:(!LOCAL)
	FOR LOCAL:1 , 0 , 7
		;친족관계를 결정
		LOCALS:LOCAL = %AUTO_SPLIT("부_모_형제_자매_딸_아들" , "_" , LOCAL:1)%
		;상대가 친족관계에 맞는 존재인지 판정
		IF AUTO_SPLIT(CSTR:(CHARA):@"근친판정_%LOCALS:LOCAL%" , "_" , 0 , CSVNAME(NO:(CHARA:1) , 0) ) != ""
			;성별과 친족관계가 맞이 않는 경우 여기서 변경
			;부・모는 종자였는지・낳았는지만으로 판정한다
			IF GROUPMATCH(LOCALS:LOCAL , "형제" , "아들") && !IS_MALE(CHARA:1)
				;형제or아들인데 성별이 여자라면 자매or딸
				LOCALS:LOCAL = \@ LOCALS:LOCAL == "형제" ? 자매 # 딸 \@
			ELSEIF GROUPMATCH(LOCALS:LOCAL , "자매" , "딸") && IS_MALE(CHARA:1)
				;그 반대
				LOCALS:LOCAL = \@ LOCALS:LOCAL == "자매" ? 형제 # 아들 \@
			ENDIF
			BREAK
		ENDIF
		;지금까지 발견되지 않았다면、CSV에서는 무관한 캐릭터이므로 임신관계의 변수로 판단한다
		IF LOCAL:1 == 5
			IF CFLAG:CHARA:딸의부친고유번호딸 == CFLAG:(CHARA:1):캐릭터고유번호
				LOCALS:LOCAL = 부
				LOCALS:(!LOCAL) = \@ IS_MALE(CHARA) ? 아들 # 딸 \@
			ELSEIF CFLAG:CHARA:딸의친부모고유번호딸 == CFLAG:(CHARA:1):캐릭터고유번호
				LOCALS:LOCAL = 모
				LOCALS:(!LOCAL) = \@ IS_MALE(CHARA) ? 아들 # 딸 \@
			ELSEIF CFLAG:CHARA:캐릭터고유번호 == CFLAG:(CHARA:1):딸의부친고유번호딸
				LOCALS:LOCAL = \@ IS_MALE(CHARA:1) ? 아들 # 딸 \@
				LOCALS:(!LOCAL) = 부
			ELSEIF CFLAG:CHARA:캐릭터고유번호 == CFLAG:(CHARA:1):딸의친부모고유번호딸
				LOCALS:LOCAL = \@ IS_MALE(CHARA:1) ? 아들 # 딸 \@
				LOCALS:(!LOCAL) = 모
			ELSE
				親ＩＤ:0 = (CFLAG:CHARA:딸의부친고유번호딸 == -1 ? 9999 # CFLAG:CHARA:딸의부친고유번호딸 )
				親ＩＤ:1 = (CFLAG:(CHARA:1):딸의부친고유번호딸 == -1 ? 9998 # CFLAG:(CHARA:1):딸의부친고유번호딸 )
				親ＩＤ:2 = (CFLAG:CHARA:딸의친부모고유번호딸 == -1 ? 9997 # CFLAG:CHARA:딸의친부모고유번호딸 )
				親ＩＤ:3 = (CFLAG:(CHARA:1):딸의친부모고유번호딸 == -1 ? 9996 # CFLAG:(CHARA:1):딸의친부모고유번호딸 )
				IF GROUPMATCH(親ＩＤ:0 , 親ＩＤ:1 , 親ＩＤ:3) || GROUPMATCH(親ＩＤ:2 , 親ＩＤ:1 , 親ＩＤ:3)
					;두명의 부・모 중 하나가 일치한다면 형제자매 취급
					LOCALS:LOCAL = \@ IS_MALE(CHARA:1) ? 형제 # 자매 \@
					LOCALS:(!LOCAL) = \@ IS_MALE(CHARA) ? 형제 # 자매 \@
				ELSE
					RETURNF 0
				ENDIF
			ENDIF
			GOTO HERE
		ENDIF
	NEXT
NEXT
$HERE
RESULTS = %LOCALS%
RESULTS:1 = %LOCALS:1%
RETURNF 1
