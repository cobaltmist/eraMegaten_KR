;-------------------------------------------------
;枠付き選択肢提示＋입력関数@INPUT_SELECT_M
;ARGS 		표시する文字列。区切り文字を이용して改行する
;区切り文字	これを書くことで改行される
;옵션	로그를 남긴다/버튼을 이용한다
;列数 		列の数を定義する。
;行数		行の数を定義する。内容が行数に及ばなかった場合も空行が記述される。
;			定義された行数より内容が多かった場合は自動的に行数は調整される
;位置		LEFT/CENTER/RIGHT
;字幅		一行のサイズを決定する。なお、字幅を越えてる行がある場合はそれにあわせる
;-------------------------------------------------
@INPUT_SELECT_M, ARGS, 区切り文字 = "/", 옵션 = "로그를 남기지 않는다/버튼을 이용한다" , 列数 = 1, 行数 = 1, 位置 = "LEFT", 字幅 = 72
#DIM DYNAMIC LINE
#DIMS DYNAMIC NUM, 50
#DIMS DYNAMIC CHOICE, 50
#DIMS DYNAMIC 区切り文字
#DIM DYNAMIC 列数
#DIM DYNAMIC 行数
#DIM DYNAMIC 字幅
#DIM DYNAMIC 総字幅
#DIM DYNAMIC 選択肢数
#DIMS DYNAMIC 옵션
#DIM DYNAMIC ログ취득済み
#DIM DYNAMIC ONEINPUTS체크
#DIMS DYNAMIC ログ
#DIMS DYNAMIC 位置
#LOCALSSIZE 2
VARSET CHOICE
VARSET NUM, "EMPTY"
LINE = LINECOUNT
SPLIT ARGS, 区切り文字, CHOICE
選択肢数 = RESULT
ログ취득済み = 0
字幅 = (字幅 - (2 * (列数 - 1))) / 列数
ONEINPUTS체크 = 1
;偶数にしつつ、文字列の長さを취득
FOR LOCAL, 0, 選択肢数
	IF STRCOUNT(CHOICE:LOCAL , "]")
		;[]の中身を취득
		SPLIT CHOICE:LOCAL, "]", LOCALS
		SPLIT LOCALS, "[", RESULTS
		SIF STRLENS(RESULTS:1) > 1
			ONEINPUTS체크 = 0
		NUM:LOCAL = %RESULTS:1%
	ENDIF
	;偶数化しつつ、최대値操作
	字幅 = (MAX(STRLENS(CHOICE:LOCAL), 字幅) + 1) / 2 * 2
	;選択肢は空白を부여して字幅を揃える
	CHOICE:LOCAL = %CHOICE:LOCAL, 字幅, LEFT%
NEXT
総字幅 = 字幅 * 列数 + (列数 - 1) * 2
行数 = MAX(行数, 選択肢数 / 列数)
$START
ログ = 
CALL SET_ALIGNMENT(位置)
CLEARLINE LINECOUNT - LINE
PRINTFORML ┌%"─" * (総字幅/2)%┐
FOR LOCAL, 0, 行数
	PRINT │
	FOR LOCAL:1, 0, 列数
		IF LOCAL:1 > 0
			PRINT 　
			ログ += "　"
		ENDIF
		IF CHOICE:(LOCAL * 列数 + LOCAL:1) == ""
			PRINTFORM %" " * 字幅%
			ログ += " " * 字幅
		ELSEIF NUM:(LOCAL * 列数 + LOCAL:1) == "EMPTY"
			PRINTFORM %CHOICE:(LOCAL * 列数 + LOCAL:1)%
			ログ += CHOICE:(LOCAL * 列数 + LOCAL:1)
		ELSE
			PRINTBUTTON CHOICE:(LOCAL * 列数 + LOCAL:1), NUM:(LOCAL * 列数 + LOCAL:1)
			ログ += CHOICE:(LOCAL * 列数 + LOCAL:1)
		ENDIF
	NEXT
	ログ += 区切り文字
	PRINTL │
NEXT
;下표시
PRINTFORM └%"─"*(総字幅/2-23)%
IF STRCOUNT(옵션, "버튼을 이용한다")
	PRINTBUTTON "[+] LOG", "+"
	PRINTBUTTON @" [-] AUTO<\@FLAG:자동넘기기 ?%" ON"%#OFF\@>", "-"
	PRINTBUTTON @" [*] SKIP<\@FLAG:윈도우메시지스킵 ?%" ON"%#OFF\@>", "*"
	PRINTBUTTON " [/] CONFIG", "/"
ELSE
	PRINTFORM %"─"*23%
ENDIF
PRINTL ┘
CALL SET_ALIGNMENT("PREV")
IF ログ취득済み++ == 0
	;誤クリック防止に少し웨이트をかける
	TWAIT 250, 1
	CALL MESSAGE_WINDOW_LOG("", ログ, 区切り文字, 行数, 総字幅)
ENDIF
REDRAW 0
IF ONEINPUTS체크
	ONEINPUTS
ELSE
	INPUTS
ENDIF
SIF RESULTS != ""
	CLEARLINE 1
REDRAW 0
;ボタン選択
IF (GROUPMATCH(RESULTS, "+", "-", "/")) && STRCOUNT(옵션, "버튼을 이용한다")
	;ログ閲覧
	IF RESULTS == "+"
		CALL MESSAGE_WINDOW_LOG(, , , , , 1)
	;자동넘기기モード切替
	ELSEIF RESULTS == "-"
		INVERTBIT FLAG:자동넘기기, 0
	;スキップモード切替
	ELSEIF RESULTS == "*"
		INVERTBIT FLAG:윈도우메시지스킵, 0
	ELSEIF RESULTS == "/"
		CALL MESSAGE_WINDOW_CONFIG
	ENDIF
	GOTO START
ENDIF
SIF !MATCH(NUM, RESULTS) || RESULTS == "EMPTY"
	GOTO START
LOCAL = TOINT(RESULTS)
CALL MESSAGE_WINDOW_LOG("" , CHOICE:FINDELEMENT(NUM, RESULTS), 区切り文字, 行数 , 総字幅)
SIF !STRCOUNT(옵션, "로그를 남긴다")
	CLEARLINE LINECOUNT - LINE
RETURN LOCAL
