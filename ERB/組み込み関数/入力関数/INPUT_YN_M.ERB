;-------------------------------------------------
;이지선다 함수　@INPUT_YN_M
;테두리 판도 만든다。일단 행수도 가변입니다
;-------------------------------------------------
@INPUT_YN_M,ARGS = "네",ARGS:1 = "아니오", 구분문자 = "/", 옵션 = "로그를 남기지 않는다/버튼을 이용한다", 위치 = "LEFT", 행수 = 2, 자폭 = 72
#DIM 로그남김
#DIMS 위치
#DIM 자폭
#DIM 행수
#DIMS 구분문자
#DIMS 옵션
#DIM LINE
#DIMS 로그
#DIM 로그취득완료
#LOCALSIZE 1
LINE = LINECOUNT
REDRAW 0
;幅取得
자폭 = MAX(STRLENS(ARGS)+4, STRLENS(ARGS:1)+4, 자폭)
자폭 = (자폭+1)/2*2
로그취득완료 = 0
$START
로그 = 
CLEARLINE LINECOUNT - LINE
CALL SET_ALIGNMENT(위치)
PRINTFORML ┌%"─" * (자폭/2)%┐
PRINT │
PRINTBUTTON @"%"[0] "+ARGS,자폭,LEFT%", "0"
PRINTL │
PRINT │
PRINTBUTTON @"%"[1] "+ARGS:1,자폭,LEFT%", "1"
로그 += "[0] "+ARGS+구분문자+"[1] "+ARGS:1
PRINTL │
FOR LOCAL, 2, 행수
	로그 += 구분문자+" "*자폭
	PRINTFORML │%" ",자폭%│
NEXT
;下表示
PRINTFORM └%"─"*(자폭/2-23)%
IF STRCOUNT(옵션, "버튼을 이용한다")
	PRINTBUTTON "[+] LOG", "+"
	PRINTBUTTON @" [-] AUTO<\@FLAG:자동넘기기 ?%" ON"%#OFF\@>", "-"
	PRINTBUTTON @" [*] SKIP<\@FLAG:윈도우메시지스킵 ?%" ON"%#OFF\@>", "*"
	PRINTBUTTON " [/] CONFIG", "/"
ELSE
	PRINTFORM %"─"*23%
ENDIF
PRINTL ┘
CALL SET_ALIGNMENT("PREV")
IF 로그취득완료++ == 0
	CALL MESSAGE_WINDOW_LOG("" , 로그, 구분문자, 행수, 자폭)
	;誤クリック防止に少しウェイトをかける
	TWAIT 250, 1
ENDIF
$INPUT_LOOP
ONEINPUTS
IF (GROUPMATCH(RESULTS, "+", "-", "/" , "*")) && STRCOUNT(옵션, "버튼을 이용한다")
	;ログ閲覧
	IF RESULTS == "+"
		CALL MESSAGE_WINDOW_LOG(, , , , , 1)
	;オート送りモード切替
	ELSEIF RESULTS == "-"
		INVERTBIT FLAG:자동넘기기, 0
	;スキップモード切替
	ELSEIF RESULTS == "*"
		INVERTBIT FLAG:윈도우메시지스킵, 0
	ELSEIF RESULTS == "/"
		CALL MESSAGE_WINDOW_CONFIG
	ENDIF
	GOTO START
ENDIF
IF RESULTS == "0" || RESULTS == " " || (FLAG:이지선다입력설정 == 1 && (ARGS == "はい" || ARGS == "예" || ARGS == "네" ||ARGS == "Yes") && (RESULTS == "y" || RESULTS == "Y")) || (FLAG:이지선다입력설정 == 2 && (RESULTS == "y" || RESULTS == "Y"))
	LOCAL = 0
ELSEIF RESULTS == "1" || (FLAG:이지선다입력설정 == 1 && (ARGS:1 == "いいえ" || ARGS:1 == "아니오" || ARGS:1 == "No") && (RESULTS == "n" || RESULTS == "N")) || (FLAG:이지선다입력설정 == 2 && (RESULTS == "n" || RESULTS == "N"))
	LOCAL = 1
ELSE
	SIF RESULTS != ""
		CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
CALL MESSAGE_WINDOW_LOG("", @"[{LOCAL}] "+ ARGS:LOCAL, 구분문자, 행수 ,자폭)
SIF !STRCOUNT(옵션, "로그를 남긴다")
	CLEARLINE LINECOUNT - LINE
REDRAW 1
RETURN LOCAL
