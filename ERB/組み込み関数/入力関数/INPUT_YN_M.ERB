;-------------------------------------------------
;2択提示関数　@INPUT_YN_M
;枠囲み版も作る。とりあえず行数も可変です
;-------------------------------------------------
@INPUT_YN_M,ARGS = "네",ARGS:1 = "아니오", 区切り文字 = "/", 옵션 = "로그를 남기지 않는다/버튼을 이용한다", 位置 = "LEFT", 行数 = 2, 字幅 = 72
#DIM ログ残し
#DIMS 位置
#DIM 字幅
#DIM 行数
#DIMS 区切り文字
#DIMS 옵션
#DIM LINE
#DIMS ログ
#DIM ログ취득済み
#LOCALSIZE 1
LINE = LINECOUNT
REDRAW 0
;幅취득
字幅 = MAX(STRLENS(ARGS)+4, STRLENS(ARGS:1)+4, 字幅)
字幅 = (字幅+1)/2*2
ログ취득済み = 0
$START
ログ = 
CLEARLINE LINECOUNT - LINE
CALL SET_ALIGNMENT(位置)
PRINTFORML ┌%"─" * (字幅/2)%┐
PRINT │
PRINTBUTTON @"%"[0] "+ARGS,字幅,LEFT%", "0"
PRINTL │
PRINT │
PRINTBUTTON @"%"[1] "+ARGS:1,字幅,LEFT%", "1"
ログ += "[0] "+ARGS+区切り文字+"[1] "+ARGS:1
PRINTL │
FOR LOCAL, 2, 行数
	ログ += 区切り文字+" "*字幅
	PRINTFORML │%" ",字幅%│
NEXT
;下표시
PRINTFORM └%"─"*(字幅/2-23)%
IF STRCOUNT(옵션, "버튼을 이용한다")
	PRINTBUTTON "[+] LOG", "+"
	PRINTBUTTON @" [-] AUTO<\@FLAG:자동넘기기 ?%" ON"%#OFF\@>", "-"
	PRINTBUTTON @" [*] SKIP<\@FLAG:윈도우메시지스킵 ?%" ON"%#OFF\@>", "*"
	PRINTBUTTON " [/] CONFIG", "/"
ELSE
	PRINTFORM %"─"*23%
ENDIF
PRINTL ┘
CALL SET_ALIGNMENT("PREV")
IF ログ취득済み++ == 0
	CALL MESSAGE_WINDOW_LOG("" , ログ, 区切り文字, 行数, 字幅)
	;誤クリック防止に少し웨이트をかける
	TWAIT 250, 1
ENDIF
$INPUT_LOOP
ONEINPUTS
IF (GROUPMATCH(RESULTS, "+", "-", "/" , "*")) && STRCOUNT(옵션, "버튼을 이용한다")
	;ログ閲覧
	IF RESULTS == "+"
		CALL MESSAGE_WINDOW_LOG(, , , , , 1)
	;자동넘기기モード切替
	ELSEIF RESULTS == "-"
		INVERTBIT FLAG:자동넘기기, 0
	;スキップモード切替
	ELSEIF RESULTS == "*"
		INVERTBIT FLAG:윈도우메시지스킵, 0
	ELSEIF RESULTS == "/"
		CALL MESSAGE_WINDOW_CONFIG
	ENDIF
	GOTO START
ENDIF
IF RESULTS == "0" || RESULTS == " " || (FLAG:이지선다입력설정 == 1 && (ARGS == "예" || ARGS == "네" ||ARGS == "Yes") && (RESULTS == "y" || RESULTS == "Y")) || (FLAG:이지선다입력설정 == 2 && (RESULTS == "y" || RESULTS == "Y"))
	LOCAL = 0
ELSEIF RESULTS == "1" || (FLAG:이지선다입력설정 == 1 && (ARGS:1 == "아니오" || ARGS:1 == "No") && (RESULTS == "n" || RESULTS == "N")) || (FLAG:이지선다입력설정 == 2 && (RESULTS == "n" || RESULTS == "N"))
	LOCAL = 1
ELSE
	SIF RESULTS != ""
		CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
CALL MESSAGE_WINDOW_LOG("", @"[{LOCAL}] "+ ARGS:LOCAL, 区切り文字, 行数 ,字幅)
SIF !STRCOUNT(옵션, "로그를 남긴다")
	CLEARLINE LINECOUNT - LINE
REDRAW 1
RETURN LOCAL
