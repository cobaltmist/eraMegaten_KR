;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;	002		2019/08/28		空気					デビルチェンバーの動作修正のため、関数を追加
;	003		2021/11/26		JK好き					@NUM_NAKAMA()@NUM_NAKAMA_HEADCOUNT()のCOMP容量計算からMASTERを除外するように追加
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;===============================
;仲魔（悪魔）が消費しているCOMP容量
;==============================
@NUM_NAKAMA()
#FUNCTION
LOCAL:1 = 0
FOR LOCAL,0,CHARANUM
	SIF LOCAL == MASTER
		CONTINUE
	SIF ABL:LOCAL:종족 < 1 || ABL:LOCAL:종족 > 45
		CONTINUE
	SIF CFLAG:LOCAL:용량미사용
		CONTINUE
	SIF CFLAG:LOCAL:소속ＣＯＭＰ == -1
		CONTINUE
	SIF CFLAG:LOCAL:PT플래그 == 0
		CONTINUE
	LOCAL:1 += 1
NEXT

;6以下のとき、デビルチェンバー装備中なら0に
SIF LOCAL:1 <= 6 && EQUIP:MASTER:데빌챔버
	LOCAL:1 = 0


RETURNF LOCAL:1


;---- EDIT 002 ADD START -------------------------
;===============================
;実際の仲魔（悪魔）の人数
;==============================
@NUM_NAKAMA_HEADCOUNT()
#FUNCTION
LOCAL:1 = 0
FOR LOCAL,0,CHARANUM
	SIF LOCAL == MASTER
		CONTINUE
	SIF ABL:LOCAL:종족 < 1 || ABL:LOCAL:종족 > 45
		CONTINUE
	SIF CFLAG:LOCAL:용량미사용
		CONTINUE
	SIF CFLAG:LOCAL:소속ＣＯＭＰ == -1
		CONTINUE
	SIF CFLAG:LOCAL:PT플래그 == 0
		CONTINUE
	LOCAL:1 += 1
NEXT

RETURNF LOCAL:1

;===============================
;COMPに仲魔が加入できる余裕があるかどうか判定
;==============================
@ＣＯＭＰ빈용량()
;戻り値 仲魔が加入できる余裕があるなら1、仲間を加入させる余裕がないなら0、既に容量オーバーを起こしているなら-1
;		3種に分けてるのは、加入させてから容量オーバーしてるか判定してストレージ送りにする処理などでも使えるようにするため
#FUNCTION
LOCAL:1 = 0

IF EQUIP:MASTER:데빌챔버
	IF NUM_NAKAMA_HEADCOUNT() < 6 || FLAG:ＣＯＭＰ용량 > NUM_NAKAMA_HEADCOUNT() + 소프트용량()
		RETURNF 1
	ELSEIF (NUM_NAKAMA_HEADCOUNT() == 6 && FLAG:ＣＯＭＰ용량 - 소프트용량() < 7) || (NUM_NAKAMA_HEADCOUNT() > 6 && FLAG:ＣＯＭＰ용량 == NUM_NAKAMA_HEADCOUNT() + 소프트용량())
		RETURNF 0
	ELSE
		RETURNF -1
	ENDIF
ELSEIF FLAG:ＣＯＭＰ용량 > NUM_NAKAMA() + 소프트용량()
	RETURNF 1
ELSEIF FLAG:ＣＯＭＰ용량 == NUM_NAKAMA() + 소프트용량()
	RETURNF 0
ELSE
	RETURNF -1
ENDIF
;---- EDIT 002 ADD END ---------------------------
