;========================================
;相性判定関数
;ARG:1（PLAYER）→ARG（TARGET）に対する相性を返す
;========================================
@GET_RELATION(ARG = -1, ARG:1 = -1 , ARG:2 = -1)
#FUNCTION
#LOCALSIZE 2
#DIM L_RELATION
#DIMS L_GROUP, 10
SIF ARG == -1
	ARG = TARGET
SIF ARG:1 == -1
	ARG:1 =PLAYER
;基礎値
L_RELATION = 100
;個別のNOによる定義
IF STRFIND("_" + CSTR:ARG:상성_최고, "_" + CSVCALLNAME(NO:(ARG:1), 0)) >= 0
	L_RELATION = 200
ELSEIF STRFIND("_" + CSTR:ARG:상성_발군, "_" + CSVCALLNAME(NO:(ARG:1), 0)) >= 0
	L_RELATION = 150
ELSEIF STRFIND("_" + CSTR:ARG:상성_양호, "_" + CSVCALLNAME(NO:(ARG:1), 0)) >= 0
	L_RELATION = 125
ELSEIF STRFIND("_" + CSTR:ARG:상성_불량, "_" + CSVCALLNAME(NO:(ARG:1), 0)) >= 0
	L_RELATION = 75
ELSEIF STRFIND("_" + CSTR:ARG:상성_최악, "_" + CSVCALLNAME(NO:(ARG:1), 0)) >= 0
	L_RELATION = 50
;配偶者チェック
ELSEIF CSV배우자(ARG, ARG:1)
	L_RELATION = 200
;家族関係による相性
ELSEIF 근친체크(ARG , ARG:1)
	L_RELATION = 150
ELSE
	;個別 × 相性グループによる相性チェック
	IF CSTR:(ARG:1):상성그룹 != ""
		SPLIT CSTR:(ARG:1):상성그룹, "_", L_GROUP
		FOR LOCAL, 0, RESULT
			IF STRFIND("_" + CSTR:ARG:상성_최고, "_" + L_GROUP:LOCAL) >= 0
				L_RELATION = 200
			ELSEIF STRFIND("_" + CSTR:ARG:상성_발군, "_" + L_GROUP:LOCAL) >= 0
				L_RELATION = 150
			ELSEIF STRFIND("_" + CSTR:ARG:상성_양호, "_" + L_GROUP:LOCAL) >= 0
				L_RELATION = 125
			ELSEIF STRFIND("_" + CSTR:ARG:상성_불량, "_" + L_GROUP:LOCAL) >= 0
				L_RELATION = 75
			ELSEIF STRFIND("_" + CSTR:ARG:상성_최악, "_" + L_GROUP:LOCAL) >= 0
				L_RELATION = 50
			ENDIF
			SIF L_RELATION != 100
				BREAK
		NEXT
		;상성그룹 × 상성그룹
		IF L_RELATION == 100 && CSTR:ARG:상성그룹 != ""
			SPLIT CSTR:(ARG:1):상성그룹, "_", L_GROUP
			FOR LOCAL, 0, RESULT
				L_RELATION = GET_RELATION_GROUP(ARG, ARG:1, L_GROUP:LOCAL)
				;100以外のをひいた時点でBREAK
				SIF L_RELATION != 100
					BREAK
			NEXT
		ENDIF
	ENDIF
ENDIF
;主にあとから追加することになるキャラIDに対する好感度
FOR LOCAL, 1, 21
	SIF CFLAG:ARG:@"캐릭터상성{LOCAL}" < 0
		CONTINUE
	IF CFLAG:ARG:@"캐릭터상성{LOCAL}" == CFLAG:(ARG:1):캐릭터고유번호
		L_RELATION = CFLAG:ARG:@"캐릭터상성치{LOCAL}"
		BREAK
	ENDIF
NEXT
;第三引数を渡されている場合、属性を省略する
SIF ARG:2 > -1
	RETURNF L_RELATION
;属性による相性変更
;NはLDにたいして、変更なしだが
;N同士でも補正が弱い
IF ABL:ARG:속성LD == 2 && ABL:(ARG:1):속성LD == 2
	L_RELATION += 10
;LDは一致してると+25
ELSEIF ABL:ARG:속성LD == ABL:(ARG:1):속성LD
	L_RELATION += 25
;LDはお互いに-25
ELSEIF (ABL:ARG:속성LD == 1 && ABL:(ARG:1):속성LD == 3) || (ABL:ARG:속성LD == 3 && ABL:(ARG:1):속성LD == 1)
	L_RELATION -= 25
ENDIF

IF ABL:ARG:속성LC == 2 && ABL:(ARG:1):속성LC == 2
	L_RELATION += 10
ELSEIF ABL:ARG:속성LC == ABL:(ARG:1):속성LC
	L_RELATION += 25
ELSEIF (ABL:ARG:속성LC == 1 && ABL:(ARG:1):속성LC == 3) || (ABL:ARG:속성LC == 3 && ABL:(ARG:1):속성LC == 1)
	L_RELATION -= 25
ENDIF
RETURNF L_RELATION
