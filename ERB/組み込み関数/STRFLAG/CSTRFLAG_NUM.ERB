;-------------------------------------------------
;@CSTRFLAG_NUM(ARGS, ARGS:1, ARG, ARG:1, ARG:2)　CSTRでFLAGを数値管理する関数
;ARGSはフラグ名、ARGS:1は記号、ARGは판정用数値、ARG:1は代入用数値、ARG:2は사용するCSTR번호（생략時は1を사용）
;符号は = += -= == > >= < <= != が入る
;=はARGSの数値をARG:1にする、+=、-=は、ARGSの数値を±ARG:1する
;==などは、フラグ内の数字が　== ARG, > ARG といった조건を満たしたとき、ARG:1を代入する
;また、조건を満たしたときは1、満たさなかったときは0を返す
;符号を생략した場合は中に入ってる数字を返す
;
;ややこしいので実例をしめすと
;CSTRFLAG_NUM("フラグ1","==", 0,12,50)
;これで、フラグ1が0のとき、1が帰ってきて、中には12が격납される
;CSTRFLAG_NUM("フラグ1", "=", , 7)
;この場合はフラグ1に7が入る
;フラグを조건や式に使いたい場合はCSTRFLAG_NUM("フラグ1")とだけ記述すると、0や12や7が帰ってくるので その数値に== などを使える
;CSTR内には /|フラグ1|12|/|フラグ3|7|/ のように격납される
;
;SPLITの仕様とLOCALSが100しかないので、최대で30フラグ程度しか定義できないので注意
;-------------------------------------------------
@CSTRFLAG_NUM(ARGS, ARGS:1, ARG, ARG:1, ARG:2 = 1)
#FUNCTION
#LOCALSIZE 2
SIF ARG:2 < 0 || ARG:2 > 99
	THROW "CSTR 번호가 이상합니다"
SIF ARG:2 == 11 || ARG:2 == 12 || ARG:2 == 16 || ARG:2 == 17 || (ARG:2 > 19 && ARG:2 < 28) || (ARG:2 > 29 && ARG:2 < 41)
	THROW "CSV에서 예약된 CSTR 번호입니다"
LOCAL = 0
SPLIT CSTR:(ARG:2), "|", RESULTS
FOR LOCAL:1, 0, RESULT + 1
	IF RESULTS:(LOCAL:1) == ARGS
		LOCAL = TOINT(RESULTS:(LOCAL:1 + 1))
		BREAK
	ENDIF
NEXT
IF ARGS:1 == "="
	CSTR:(ARG:2) = %CHANGE_STRFLAG_NUM(CSTR:(ARG:2), ARGS, ARG:1)%
	RETURNF 1
ELSEIF ARGS:1 == "+="
	CSTR:(ARG:2) = %CHANGE_STRFLAG_NUM(CSTR:(ARG:2), ARGS, LOCAL + ARG:1)%
	RETURNF 1
ELSEIF ARGS:1 == "-="
	CSTR:(ARG:2) = %CHANGE_STRFLAG_NUM(CSTR:(ARG:2), ARGS, LOCAL - ARG:1)%
	RETURNF 1
ELSEIF ARGS:1 == "=="
	IF LOCAL == ARG
		CSTR:(ARG:2) = %CHANGE_STRFLAG_NUM(CSTR:(ARG:2), ARGS, ARG:1)%
		RETURNF 1
	ENDIF
	RETURNF 0
ELSEIF ARGS:1 == ">"
	IF LOCAL > ARG
		CSTR:(ARG:2) = %CHANGE_STRFLAG_NUM(CSTR:(ARG:2), ARGS, ARG:1)%
		RETURNF 1
	ENDIF
	RETURNF 0
ELSEIF ARGS:1 == ">="
	IF LOCAL >= ARG
		CSTR:(ARG:2) = %CHANGE_STRFLAG_NUM(CSTR:(ARG:2), ARGS, ARG:1)%
		RETURNF 1
	ENDIF
	RETURNF 0
ELSEIF ARGS:1 == "<"
	IF LOCAL < ARG
		CSTR:(ARG:2) = %CHANGE_STRFLAG_NUM(CSTR:(ARG:2), ARGS, ARG:1)%
		RETURNF 1
	ENDIF
	RETURNF 0
ELSEIF ARGS:1 == "<="
	IF LOCAL <= ARG
		CSTR:(ARG:2) = %CHANGE_STRFLAG_NUM(CSTR:(ARG:2), ARGS, ARG:1)%
		RETURNF 1
	ENDIF
	RETURNF 0
ELSEIF ARGS:1 == "!="
	IF LOCAL != ARG
		CSTR:(ARG:2) = %CHANGE_STRFLAG_NUM(CSTR:(ARG:2), ARGS, ARG:1)%
		RETURNF 1
	ENDIF
	RETURNF 0
ELSE
	RETURNF LOCAL
ENDIF
