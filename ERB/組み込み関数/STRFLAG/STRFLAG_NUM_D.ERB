;-------------------------------------------------
;@STRFLAG_NUM_D(ARGS, ARGS:1, ARG, ARG:1, ARG:2)　STRFLAG数値管理関数
;ARGSはフラグ名、ARGS:1は記号、ARGは판정用数値、ARG:1は代入用数値、ARG:2は던전번호
;符号は = += -= == > >= < <= != が入る
;=はARGSの数値をARG:1にする、+=、-=は、ARGSの数値を±ARG:1する
;==などは、フラグ内の数字が　== ARG, > ARG といった조건を満たしたとき、ARG:1を代入する
;また、조건を満たしたときは1、満たさなかったときは0を返す
;符号を생략した場合は中に入ってる数字を返す
;
;ややこしいので実例をしめすと
;STRFLAG_NUM_D("던전進行度", "==", 0, 1)
;これで、던전進行度が0のとき、1が帰ってきて、中には1が격납される
;STRFLAG_NUM_D("던전進行度", "=", , 7)
;この場合は던전進行度に7が入る
;フラグを조건や式に使いたい場合はSTRFLAG_NUM_D("던전進行度")とだけ、記述すると0や1や7が帰ってくるので その数値に== などを使える
;
;SPLITの仕様とLOCALSが100しかないので、최대で30フラグ程度しか定義できないので注意
;그리고、ここまでするんなら、おとなしく変数1個使えばいいと思った
;まぁ、던전以外は変数1個しかないんで有効なんですが
;-------------------------------------------------
@STRFLAG_NUM_D(ARGS, ARGS:1, ARG, ARG:1, ARG:2 = -1)
#FUNCTION
#LOCALSSIZE 2
ARG:2 = ARG:2 != -1 ? ARG:2 # FLAG:現던전
SIF ARG:2 < 0 || ARG:2 > 99
	THROW "던전 넘버가 이상합니다"
;사용するSAVESTRの配列にあわせる
ARG:2 += 100
LOCAL = 0
VARSET RESULTS
SPLIT SAVESTR:(ARG:2), "|", RESULTS
FOR LOCAL:1, 0, RESULT + 1
	IF RESULTS:(LOCAL:1) == ARGS
		LOCAL = TOINT(RESULTS:(LOCAL:1 + 1))
		BREAK
	ENDIF
NEXT
IF ARGS:1 == "="
	SAVESTR:(ARG:2) = %CHANGE_STRFLAG_NUM(SAVESTR:(ARG:2), ARGS, ARG:1)%
	RETURNF 1
ELSEIF ARGS:1 == "+="
	SAVESTR:(ARG:2) = %CHANGE_STRFLAG_NUM(SAVESTR:(ARG:2), ARGS, LOCAL + ARG:1)%
	RETURNF 1
ELSEIF ARGS:1 == "-="
	SAVESTR:(ARG:2) = %CHANGE_STRFLAG_NUM(SAVESTR:(ARG:2), ARGS, LOCAL - ARG:1)%
	RETURNF 1
ELSEIF ARGS:1 == "=="
	IF LOCAL == ARG
		SAVESTR:(ARG:2) = %CHANGE_STRFLAG_NUM(SAVESTR:(ARG:2), ARGS, ARG:1)%
		RETURNF 1
	ENDIF
	RETURNF 0
ELSEIF ARGS:1 == ">"
	IF LOCAL > ARG
		SAVESTR:(ARG:2) = %CHANGE_STRFLAG_NUM(SAVESTR:(ARG:2), ARGS, ARG:1)%
		RETURNF 1
	ENDIF
	RETURNF 0
ELSEIF ARGS:1 == ">="
	IF LOCAL >= ARG
		SAVESTR:(ARG:2) = %CHANGE_STRFLAG_NUM(SAVESTR:(ARG:2), ARGS, ARG:1)%
		RETURNF 1
	ENDIF
	RETURNF 0
ELSEIF ARGS:1 == "<"
	IF LOCAL < ARG
		SAVESTR:(ARG:2) = %CHANGE_STRFLAG_NUM(SAVESTR:(ARG:2), ARGS, ARG:1)%
		RETURNF 1
	ENDIF
	RETURNF 0
ELSEIF ARGS:1 == "<="
	IF LOCAL <= ARG
		SAVESTR:(ARG:2) = %CHANGE_STRFLAG_NUM(SAVESTR:(ARG:2), ARGS, ARG:1)%
		RETURNF 1
	ENDIF
	RETURNF 0
ELSEIF ARGS:1 == "!="
	IF LOCAL != ARG
		SAVESTR:(ARG:2) = %CHANGE_STRFLAG_NUM(SAVESTR:(ARG:2), ARGS, ARG:1)%
		RETURNF 1
	ENDIF
	RETURNF 0
ELSE
	RETURNF LOCAL
ENDIF
