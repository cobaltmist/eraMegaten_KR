;-------------------------------------------------
;@캐릭터삭제　ARGのキャラクターを削除し、変数の移動まで済ませる関数
;-------------------------------------------------
@캐릭터삭제, ARG
#LOCALSIZE 2
;ペルソナ使いの場合はペルソナをひっぺがし、つくっていた初期ペルソナを消す
IF TALENT:ARG:페르소나구사자
	FOR LOCAL, 1, 4
		LOCALS = 소지페르소나{LOCAL}
		IF EQUIP:ARG:LOCALS
			DITEMTYPE:(EQUIP:ARG:LOCALS):페르소나("장비상태") = 1
			DITEMTYPE:(EQUIP:ARG:LOCALS):페르소나("페르소나소지자") = -1
		ENDIF
	NEXT
	IF CFLAG:ARG:초기페르소나넘버
		FOR LOCAL, 0, 50
			DITEMTYPE:(CFLAG:ARG:초기페르소나넘버):LOCAL = 0
		NEXT
	ENDIF
ENDIF

;デビルシフター関連
IF CFLAG:ARG:피링크플래그 > -1 && FINDCHARA_ID(CFLAG:ARG:피링크플래그) > -1
	CFLAG:FINDCHARA_ID(CFLAG:ARG:피링크플래그):링크악마 = -1
	;CFLAG:FINDCHARA_ID(CFLAG:ARG:被リンクフラグ):悪魔変身 = 0	
	;スキルを復元
	;FOR LOCAL,1, FLAG:スキル数 + 1
	;	ABL:ARG:("スキル" + TOSTR(LOCAL)) = ABL:ARG:("人間時スキル" + TOSTR(LOCAL))
	;NEXT
	;CALL SYNC_STATUS,FINDCHARA_ID(CFLAG:ARG:被リンクフラグ)
	SIF CFLAG:FINDCHARA_ID(CFLAG:ARG:피링크플래그):악마변신 == 1
		CALL ACTION_2310 , FINDCHARA_ID(CFLAG:ARG:피링크플래그) , 0
ENDIF
IF CFLAG:ARG:링크악마 > -1 && TALENT:ARG:데빌시프터
	CFLAG:FINDCHARA_ID(CFLAG:ARG:링크악마):피링크플래그 = -1
	CFLAG:FINDCHARA_ID(CFLAG:ARG:링크악마):피변신플래그 = 0
ENDIF

;親の数値の破棄
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:딸의친부모고유번호딸 == CFLAG:ARG:캐릭터고유번호
		CFLAG:LOCAL:딸의친부모고유번호딸 = -1
	SIF CFLAG:LOCAL:딸의부친고유번호딸 == CFLAG:ARG:캐릭터고유번호
		CFLAG:LOCAL:딸의부친고유번호딸 = -1
	IF CFLAG:LOCAL:부친의고유번호부모 == CFLAG:ARG:캐릭터고유번호
		CFLAG:LOCAL:부친의고유번호부모 = -1
		CFLAG:LOCAL:부친실종 = 1
	ENDIF
NEXT
;好感度を削除
FOR LOCAL, 0, CHARANUM
	CDFLAG:(LOCAL):캐릭터간호감도:(CFLAG:ARG:캐릭터고유번호+100) = 0
;-------　特殊友情削除
	SIF (CFLAG:ARG:캐릭터고유번호) >= 0
		CDFLAG:(LOCAL):캐릭터간특수우정:(CFLAG:ARG:캐릭터고유번호) = 0
NEXT

SIF CFLAG:ARG:포지션 > 0
	CALL REMOVE_POSITION, CFLAG:ARG:포지션
;予備ターゲットから外す
FOR LOCAL, 0, 10
	IF LOCAL == 0
		SIF TARGET == ARG
			TARGET = -1
		SIF ASSI == ARG
			ASSI = -1
	ENDIF
	SIF TARGET:LOCAL == CFLAG:ARG:캐릭터고유번호
		TARGET:LOCAL = -1
NEXT
;装備解除処理
FOR LOCAL, 0, 8
	IF EQUIP:ARG:GET_EQUIP(LOCAL) > 0
		ITEM:(EQUIP:ARG:GET_EQUIP(LOCAL)) += 1
		EQUIP:ARG:GET_EQUIP(LOCAL) = 0
	ENDIF
NEXT

DELCHARA ARG
IF ARG != -1
	IF TARGET > ARG
		TARGET -= 1
	ELSEIF TARGET == ARG
		TARGET = -1
	ENDIF
ENDIF
IF ARG != -1
	IF ASSI > ARG
		ASSI -= 1
	ELSEIF ASSI == ARG
		ASSI = -1
	ENDIF
ENDIF
;フォーメーション再読み込み
CALL REFRESH_POS
