;-------------------------------------------------
;@キャラ삭제　ARGのキャラクターを削除し、変数の移動まで済ませる関数
;-------------------------------------------------
@キャラ삭제, ARG
#LOCALSIZE 2
;페르소나구사자の場合は페르소나をひっぺがし、つくっていた초기페르소나を消す
IF TALENT:ARG:페르소나구사자
	FOR LOCAL, 1, 4
		LOCALS = 소지페르소나{LOCAL}
		IF EQUIP:ARG:LOCALS
			DITEMTYPE:(EQUIP:ARG:LOCALS):페르소나("장비상태") = 1
			DITEMTYPE:(EQUIP:ARG:LOCALS):페르소나("페르소나소지자") = -1
		ENDIF
	NEXT
	IF CFLAG:ARG:초기페르소나ナンバー
		FOR LOCAL, 0, 50
			DITEMTYPE:(CFLAG:ARG:초기페르소나ナンバー):LOCAL = 0
		NEXT
	ENDIF
ENDIF

;데빌시프터関連
IF CFLAG:ARG:被リンクフラグ > -1 && FINDCHARA_ID(CFLAG:ARG:被リンクフラグ) > -1
	CFLAG:FINDCHARA_ID(CFLAG:ARG:被リンクフラグ):링크악마 = -1
	;CFLAG:FINDCHARA_ID(CFLAG:ARG:被リンクフラグ):악마변신 = 0	
	;스킬を復元
	;FOR LOCAL,1, FLAG:스킬数 + 1
	;	ABL:ARG:("스킬" + TOSTR(LOCAL)) = ABL:ARG:("인간시스킬" + TOSTR(LOCAL))
	;NEXT
	;CALL SYNC_STATUS,FINDCHARA_ID(CFLAG:ARG:被リンクフラグ)
	SIF CFLAG:FINDCHARA_ID(CFLAG:ARG:被リンクフラグ):악마변신 == 1
		CALL ACTION_2310 , FINDCHARA_ID(CFLAG:ARG:被リンクフラグ) , 0
ENDIF
IF CFLAG:ARG:링크악마 > -1 && TALENT:ARG:데빌시프터
	CFLAG:FINDCHARA_ID(CFLAG:ARG:링크악마):被リンクフラグ = -1
	CFLAG:FINDCHARA_ID(CFLAG:ARG:링크악마):被변신フラグ = 0
ENDIF

;親の数値の破棄
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:娘の産みの親の固有번호娘 == CFLAG:ARG:キャラ固有の번호
		CFLAG:LOCAL:娘の産みの親の固有번호娘 = -1
	SIF CFLAG:LOCAL:娘の父親の固有번호娘 == CFLAG:ARG:キャラ固有の번호
		CFLAG:LOCAL:娘の父親の固有번호娘 = -1
	IF CFLAG:LOCAL:父親の固有번호親 == CFLAG:ARG:キャラ固有の번호
		CFLAG:LOCAL:父親の固有번호親 = -1
		CFLAG:LOCAL:父親失踪 = 1
	ENDIF
NEXT
;호감도を削除
FOR LOCAL, 0, CHARANUM
	CDFLAG:(LOCAL):キャラ間好感度:(CFLAG:ARG:キャラ固有の번호+100) = 0
;-------　특별우정삭제
	SIF (CFLAG:ARG:キャラ固有の번호) >= 0
		CDFLAG:(LOCAL):キャラ間特殊友情:(CFLAG:ARG:キャラ固有の번호) = 0
NEXT

SIF CFLAG:ARG:포지션 > 0
	CALL REMOVE_POSITION, CFLAG:ARG:포지션
;予備ターゲットから外す
FOR LOCAL, 0, 10
	IF LOCAL == 0
		SIF TARGET == ARG
			TARGET = -1
		SIF ASSI == ARG
			ASSI = -1
	ENDIF
	SIF TARGET:LOCAL == CFLAG:ARG:キャラ固有の번호
		TARGET:LOCAL = -1
NEXT
;장비해제処理
FOR LOCAL, 0, 8
	IF EQUIP:ARG:GET_EQUIP(LOCAL) > 0
		ITEM:(EQUIP:ARG:GET_EQUIP(LOCAL)) += 1
		EQUIP:ARG:GET_EQUIP(LOCAL) = 0
	ENDIF
NEXT

DELCHARA ARG
IF ARG != -1
	IF TARGET > ARG
		TARGET -= 1
	ELSEIF TARGET == ARG
		TARGET = -1
	ENDIF
ENDIF
IF ARG != -1
	IF ASSI > ARG
		ASSI -= 1
	ELSEIF ASSI == ARG
		ASSI = -1
	ENDIF
ENDIF
;フォーメーション再読み込み
CALL REFRESH_POS
