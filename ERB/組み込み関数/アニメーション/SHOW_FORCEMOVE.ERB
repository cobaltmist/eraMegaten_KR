;강제이동 애니메이션 표시함수
;引数は文字列で
;UDLRで上下左右とします
;<X>で直前の方向移動をX回繰り返します
;例
;UULR<8>Dなら上に2回、左に一回右に8回、下に一回移動
@SHOW_FORCEMOVE, ARGS, ARGS:1, ARGS:2, 発言者, 内容 = "EMPTY", 区切り文字 = "/", 옵션 = "로그를 남긴다/버튼을 이용한다/재이용하지 않는다/NOWAIT/NOANIME", 位置 = "CENTER", 字幅 = 72, 行数 = 4
#LOCALSSIZE 50
#DIM 표시횟수
#DIMS 移動方向
#DIM 이동횟수
#DIM LINE
#DIMS 発言者
#DIMS 内容
#DIMS 区切り文字
#DIMS 옵션
#DIMS 位置
#DIM 字幅
#DIM 行数
LINE = LINECOUNT
표시횟수 = 0
REDRAW 0
WHILE ARGS != ""
	;初回は今の位置を示すために移動なしで表示させる
	;ついでにランランダンジョンのためのRUN中断BITを立てておく
	IF !표시횟수
		이동횟수 = 1
		移動方向 = NONE
		SETBIT FLAG:233,0
	ELSEIF SUBSTRING(ARGS, 0, 1) == "<"
		SPLIT ARGS, "<", LOCALS
		이동횟수 = TOINT(AUTO_SPLIT(LOCALS:1, ">", 0)) - 1
	ELSEIF GROUPMATCH(SUBSTRING(ARGS, 0, 1), "U", "D", "L", "R")
		이동횟수 = 1
		移動方向 = %SUBSTRING(ARGS, 0, 1)%
	ELSE
		移動方向 = 
		이동횟수 = 0
	ENDIF
	FOR LOCAL, 0, 이동횟수
		CLEARLINE LINECOUNT - LINE
		SELECTCASE 移動方向
			CASE "U"
				FLAG:현Y--
			CASE "D"
				FLAG:현Y++
			CASE "L"
				FLAG:현X--
			CASE "R"
				FLAG:현X++
		ENDSELECT
		SELECTCASE ARGS:1
			CASE "D", "ダンジョン", "DUNGEON", "던전"
				CALL AUTOMAP
				CALL SHOW_PICTURE, "D"
				IF 内容 != "EMPTY"
					CALL MESSAGE_WINDOW, 発言者, 内容, 区切り文字, 옵션, 位置, 字幅, 行数
					CALL MESSAGE_WINDOW_LOG(, , , , , -1)
				ELSE
					CALL SHOW_DUNGEON_COMMAND
				ENDIF
				REDRAW 2
				TWAIT 200, 1
				표시횟수++
		ENDSELECT
	NEXT
	SIF 표시횟수 > 1
		ARGS = %SUBSTRING(ARGS, 1)%
WEND
SIF 内容 != "EMPTY"
	CALL MESSAGE_WINDOW_LOG(発言者, 内容, 区切り文字, 行数, 字幅)
REDRAW 1
