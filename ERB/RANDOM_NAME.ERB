;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:RANDOM_NAME.ERB
;	Facility	:名称設定関連の処理です
;
;	Licence		:ライセンスフリー
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;	002		2022/05/29		だま					元の名前を再使用できないので追加
;;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;========================================================================
;名称を設定する
;========================================================================
@CREATE_RANDOM_NAME_IF(生成法リスト, 親:0=-1, 親:1=-1, 親:2=-1, 元の名前="")
#DIMS DYNAMIC NAMES,17
#DIMS DYNAMIC 最有力候補,1
#DIMS DYNAMIC 生成法,1
#DIMS DYNAMIC 生成法リスト,1
#DIMS DYNAMIC 元の名前,1
#DIM DYNAMIC 親, 3
#DIMS DYNAMIC 親の苗字, 3
#DIM DYNAMIC 苗字選択, 1
#DIM DYNAMIC CNT, 1
#DIM DYNAMIC 育て, 1
#DIM DYNAMIC 母, 1
#DIM DYNAMIC 父, 1
#DIM LCOUNT
SIF 生成法リスト == ""
	生成法リスト = 영어권・여성_영어권・남성_독일인・여성_독일인・남성_프랑스인・여성_프랑스인・남성_이탈리아인・여성_이탈리아인・남성_스페인인・여성_스페인인・남성_스웨덴인・여성_스웨덴인・남성_핀란드인・여성_핀란드인・남성_러시아인・여성_러시아인・남성_아랍인・여성_아랍인・남성_인도인・여성_인도인・남성_그리스인・여성_그리스인・남성_중국인・여성_중국인・남성_일본인・남성_일본인・여성_아가씨풍／중세・일본_아가씨풍／근대・일본_아가씨풍／중국・전기_랜덤여성명
育て = 0
母 = 1
父 = 2
FOR CNT, 0, 3
	IF 親:CNT >= 0
		;育ての親や父親が母と同一なら표시しない
		IF CNT == 母 || 親:CNT != 親:母
			IF STRCOUNT( NAME:(親:CNT), " " ) == 1
				親の苗字:CNT = %AUTO_SPLIT( NAME:(親:CNT), " ", 0 ) + " "%
			ELSEIF STRCOUNT( NAME:(親:CNT), "・" ) == 1
				親の苗字:CNT = ・%AUTO_SPLIT( NAME:(親:CNT), "・", 1 )%
			ELSE
			;苗字を持たない親は見なかったことにする
				親:CNT = -1 
			ENDIF
		ELSE
			親:CNT = -1
		ENDIF
	ENDIF
NEXT
苗字選択 = -1
最有力候補 = 
$INPUT_LOOP
FOR LCOUNT,0,100
	生成法 = %AUTO_SPLIT(生成法リスト,"_",LCOUNT)%
	SIF STRLENS(生成法) == 0
		BREAK
	TRYCCALLFORM NAMEDIC_%生成法%,0
		PRINTFORMLC [{LCOUNT,2}]%生成法%
	CATCH
	ENDCATCH
NEXT
PRINTL 
PRINTL 
PRINTL [100]스스로 이름을 입력한다
IF 親:育て >= 0
	IF 苗字選択 == 育て
		SETCOLOR COLOR( "P-YELLOW" )
	ENDIF
	PRINTL [101]키워준 부모의 성을 붙인다
	RESETCOLOR
ENDIF
IF 親:母 >= 0
	IF 苗字選択 == 母
		SETCOLOR COLOR( "P-YELLOW" )
	ENDIF
	PRINTL [102]모친의 성을 붙인다
	RESETCOLOR
ENDIF
IF 親:父 >= 0
	IF 苗字選択 == 父
		SETCOLOR COLOR( "P-YELLOW" )
	ENDIF
	PRINTL [103]부친의 성을 붙인다
	RESETCOLOR
ENDIF
IF 元の名前 != ""
	PRINTFORML [110]원래 이름(%元の名前%)
ENDIF
DO
	INPUT
	SELECTCASE RESULT
		CASE 100
			PRINTL 이름을 입력해주세요
			INPUTS
			最有力候補 = %RESULTS%
			GOTO 確認
		CASE 101 TO 103
			IF 親:( RESULT - 101 ) >= 0
				IF 苗字選択 == RESULT - 101
					苗字選択 = -1
				ELSE
					苗字選択 = RESULT - 101
				ENDIF
			ENDIF
			GOTO INPUT_LOOP
		CASE 110
			最有力候補 = %元の名前%
			GOTO 確認
		CASE 0 TO (STRCOUNT(生成法リスト,"_") + 1)
			SWAP RESULT,LCOUNT
			VARSET RESULTS
			生成法 = %AUTO_SPLIT(生成法リスト,"_",LCOUNT)%
			IF GROUPMATCH( 生成法, "일본인・여성", "일본인・남성" ) && 苗字選択 >= 0
				TRYCCALLFORM NAMEDIC_%生成法%, 16, 親の苗字:苗字選択
				CATCH
					CONTINUE
				ENDCATCH
			ELSE
				TRYCCALLFORM NAMEDIC_%生成法%,16
				CATCH
					CONTINUE
				ENDCATCH
			ENDIF
			GOTO 選択
	ENDSELECT
LOOP 1

$確認
PRINTFORML 「%最有力候補%」%조사만처리(最有力候補,"로")% 좋습니까?
CALL INPUT_YN,"Yes","No"
SIF RESULT == 1
	GOTO INPUT_LOOP
RESULTS = %最有力候補%
RETURN

$選択
FOR LCOUNT,1,17
	IF 苗字選択 >= 0
		IF STRCOUNT( 親の苗字:苗字選択, " " ) == 1
			NAMES:LCOUNT = %親の苗字:苗字選択%%RESULTS:(LCOUNT-1)%
		ELSEIF STRCOUNT( 親の苗字:苗字選択, "・" ) == 1
			NAMES:LCOUNT = %RESULTS:(LCOUNT-1)%%親の苗字:苗字選択%
		ELSE
		;選択が出来ているなら苗字を持っているはずだが、万一のときは苗字抜きで
			NAMES:LCOUNT = %RESULTS:(LCOUNT-1)%
		ENDIF
	ELSE
		NAMES:LCOUNT = %RESULTS:(LCOUNT-1)%
	ENDIF
	PRINTFORMLC [{LCOUNT,2}]%NAMES:LCOUNT%
NEXT
PRINTL 
PRINTL 
PRINTLC	[100]다른 후보
PRINTLC	[200]다시 생각한다
DO
	INPUT
	SELECTCASE RESULT
		CASE 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16
			最有力候補 = %NAMES:RESULT%
			GOTO 確認
		CASE 100
			VARSET RESULTS
			CALLFORM NAMEDIC_%生成法%,16
			GOTO 選択
		CASE 200
			GOTO INPUT_LOOP
		CASE 300
			GOTO INPUT_LOOP
	ENDSELECT
LOOP 1
