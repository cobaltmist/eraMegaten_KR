;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:CONTLOR_SELF.ERB
;	Facility	:キャラクタの요바이허가/불허가바꾸기
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2020/05/04		Jガン					新規作成
;	002		2021/07/28		木綿豆腐				個別イベントにも戦闘中イベントを出せるように
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;この설정がONだと조교コマンド時に相손によって一時的に함락が変わる

@SHOPCOMABLE_662
#LOCALSIZE 3
RESULT:1 = 1
LOCAL:2 = 0

FOR LOCAL,0, CHARANUM
	CALL CASTING_INDIVIDUAL(LOCAL)
	IF RESULT
		FOR LOCAL:1,0, 64
			IF !GETBIT(CFLAG:LOCAL:개별이벤트개방,LOCAL:1)
				RESULT = 0
				TRYCALLFORM INDIVIDUAL_EVENT_K{NO:LOCAL},LOCAL,1,LOCAL:1
				IF RESULT
					SETBIT CFLAG:LOCAL:개별이벤트개방,LOCAL:1
					CALLFORM INDIVIDUAL_EVENT_K{NO:LOCAL},LOCAL,2,LOCAL:1
					LOCALS = %CALLNAME:LOCAL%의 개별이벤트【%RESULTS%】이(가) 출현！
					CALL MESSAGE_B(1, LOCALS)
				ENDIF
			ENDIF
		NEXT
		LOCAL:2 = 1
	ENDIF
NEXT
RESULTS = 개별이벤트
RETURN LOCAL:2

@CASTING_INDIVIDUAL(ARG)
;この場にいない불가
SIF CFLAG:ARG:この場に居ないフラグ
	RETURN 0
;이벤트はキャラ番号ごと（구상は関係なし）
TRYCCALLFORM INDIVIDUAL_EVENT_K{NO:ARG},ARG
	RETURN 1
CATCH
	RETURN 0
ENDCATCH


@SHOP_COM_662
#LOCALSIZE 2
#LOCALSSIZE 1
#DIM CAST
#DIM STOCK
#DIM M_COUNT
;LOCAL:0 
;M_COUNT = 1 + ( TALENT:MASTER:수수께끼의매력 || TALENT:MASTER:매력 || TALENT:MASTER:매혹)
REDRAW 0
$RET
LOCALS = 누구의 개별 이벤트를 봅니까？
CALL INPUT_CHARA_LIST( LOCALS,"CASTING_INDIVIDUAL")
IF RESULT == 1000
	REDRAW 1
	RETURN 0
ENDIF
CAST = RESULT
STOCK = TARGET
TARGET = CAST
DRAWLINE
PRINTFORML %CALLNAME:CAST%의 어느 이벤트를 봅니까？
DRAWLINE
FOR LOCAL,0, 64
	SIF GETBIT(CFLAG:CAST:개별이벤트읽음,LOCAL)
		CONTINUE
	RESULT = 0
	TRYCALLFORM INDIVIDUAL_EVENT_K{NO:CAST},CAST,1,LOCAL
	IF RESULT == 0
		CLEARBIT CFLAG:CAST:개별이벤트개방,LOCAL
		SETCOLOR COLOR("灰色")
	ENDIF
	;SIF !GETBIT(CFLAG:CAST:개별이벤트개방,LOCAL)
	;	SETCOLOR COLOR("灰色")
	RESULTS = 
	TRYCALLFORM INDIVIDUAL_EVENT_K{NO:CAST},CAST,2,LOCAL
	IF RESULTS != ""
		PRINTFORM [{LOCAL,2}]
		PRINTFORML %RESULTS%
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTFORML [100]돌아간다
INPUT
IF RESULT >= 0 && RESULT < 64 && GETBIT(CFLAG:CAST:개별이벤트개방,RESULT) && !GETBIT(CFLAG:CAST:개별이벤트읽음,RESULT)
	LOCAL = RESULT
;---- EDIT 002 MOD START -------------------------
	FLAG:個別イベントキャラ = NO:CAST
	FLAG:進行中個別イベント = LOCAL
;---- EDIT 002 MOD END   -------------------------
	CALLFORM INDIVIDUAL_EVENT_K{NO:CAST},CAST,3,LOCAL
	SIF RESULT == 1
		SETBIT CFLAG:CAST:개별이벤트읽음,LOCAL
;---- EDIT 002 MOD START -------------------------
	FLAG:個別イベントキャラ = -1
	FLAG:進行中個別イベント = -1
;---- EDIT 002 MOD END   -------------------------
	DRAWLINE
	PRINTFORML %CALLNAME:CAST%
	DRAWLINE
	CALL SHOW_EXTRA, CAST
	PRINTFORMW
ENDIF
TARGET = STOCK
GOTO RET

;====================
;出現チェック関数
;====================
@EVENT_IND_APP_CHECK,ARG,ARG:1
#FUNCTION
RETURNF GETBIT(CFLAG:ARG:개별이벤트개방,ARG:1)

;====================
;既読チェック関数
;====================
@EVENT_IND_READ_CHECK,ARG,ARG:1
#FUNCTION
RETURNF GETBIT(CFLAG:ARG:개별이벤트읽음,ARG:1)
