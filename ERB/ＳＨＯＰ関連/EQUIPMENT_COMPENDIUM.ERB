@EQUIPMENT_COMPENDIUM
CUSTOMDRAWLINE =
PRINTL □ 도구전서
PRINTL 열람하고 싶은 아이템 종류를 선택하십시오
DRAWLINE
FOR LOCAL,0,8
	PRINTFORML [{LOCAL}] %GET_EQUIP(LOCAL)%
NEXT
PRINTL [8] 아이템
;PRINTL [9] 전서등록
PRINTL [100] 돌아간다
CALL INPUTINT(0,1,2,3,4,5,6,7,8,100)
;LOCAL:1 종별
LOCAL:1 = RESULT
SELECTCASE LOCAL:1
	CASE 100
		RETURN 0
;	CASE 9
;		CALL ENTRY_EQUIPMENT_COMPENDIUM
	CASEELSE
		CALL MAKE_EQUIPMENT_COMPENDIUM_LIST,LOCAL:1
		IF RESULT == 0
			PRINTL 등록된 아이템이 없습니다
			RESTART
		ENDIF
		LOCAL:2 = RESULT

		P = 0
		$PRINT_LIST
		PRINTFORML 열람하고 싶은 아이템을 선택하십시오　＜page.{P + 1}＞
		DRAWLINE
		FOR LOCAL,P*20,MIN(P*20+20,LOCAL:2)
			PRINTFORMLC [{아이템리스트:LOCAL}] %ITEMNAME:(아이템리스트:LOCAL)%
		NEXT
		PRINTL
		DRAWLINE
		PRINTFORMLC \@(P <= 0) ? %" " * 13% # [7]이전페이지\@
		PRINTLC [0]돌아간다
		PRINTFORMLC \@(P >= (LOCAL:2 - 1) / 20) ? %" " * 13% # [9]다음페이지\@
		PRINTL

		$INPUT_LOOP2
		INPUT
		LOCAL:3 = RESULT
		IF RESULT == 0
			RESTART
		ELSEIF RESULT == 7 && P > 0
			P -= 1
			GOTO PRINT_LIST
		ELSEIF RESULT == 9 && P < LOCAL:2/20
			P += 1
			GOTO PRINT_LIST
		ELSEIF MATCH(아이템리스트,RESULT,0) > 0
			DRAWLINE
			IF LOCAL:1 < 7
				CALL 장비설명,LOCAL:3
				WAIT
			ELSEIF LOCAL:1 == 7
				CALLFORM 장비해설_{LOCAL:3}
				WAIT
				PRINTL
				DRAWLINE
			ELSE
				PRINTFORML %ITEMNAME:(LOCAL:3),20,LEFT%
				SIF FLAG:스킬속성표시설정 == 1
					CALL SKILL_EXPLAIN_PERFORMANCE, LOCAL:3 + 2000, -1
				TRYCALLFORM SKILL_EXPLAIN_{LOCAL:3+2000}
				DRAWLINE
				WAIT
			ENDIF
		ELSE
			GOTO INPUT_LOOP2
		ENDIF
		GOTO PRINT_LIST
ENDSELECT

RESTART

@MAKE_EQUIPMENT_COMPENDIUM_LIST,ARG
VARSET 아이템리스트,-1
LOCAL:1 = 0
IF ARG < 8
	FOR LOCAL,2000,VARSIZE("ITEM")
		SIF ITEMPRICE:LOCAL == 0
			CONTINUE
		RESULT = 12
		IF 마정장비(LOCAL)
		ELSE
			TRYCALLFORM 장비부위_{LOCAL}
			IF RESULT == ARG && FLAG:(LOCAL + 40000) > 0
				아이템리스트:(LOCAL:1) = LOCAL
				LOCAL:1 += 1
			ENDIF
		ENDIF
	NEXT
ELSE
	FOR LOCAL,1001,2000
		SIF ITEMPRICE:LOCAL == 0
			CONTINUE
		IF FLAG:(LOCAL + 40000) > 0
			아이템리스트:(LOCAL:1) = LOCAL
			LOCAL:1 += 1
		ENDIF
	NEXT
ENDIF

RETURN LOCAL:1

@ENTRY_EQUIPMENT_COMPENDIUM
FOR LOCAL,1000,9999
	SIF ITEM:LOCAL > 0
		FLAG:(LOCAL + 40000) = 1
NEXT
