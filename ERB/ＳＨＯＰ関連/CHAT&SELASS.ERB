
@SHOPCOMABLE_108
RESULT:1 = 1
RESULTS = 파트너변경
LOCAL:1 = 0
FOR LOCAL,1,CHARANUM
	SIF LOCAL == MASTER || LOCAL == TARGET
		CONTINUE
	SIF TALENT:LOCAL:맹우
		CONTINUE
	SIF CFLAG:LOCAL:이자리에없는플래그 >= 1
		CONTINUE
	SIF CFLAG:LOCAL:노역플래그 == 3
			CONTINUE
	SIF ((TALENT:LOCAL:임신 && CFLAG:LOCAL:출산예정일 - 2 <= DAY) || TALENT:LOCAL:육아중) && FLAG:출산기능ONOFF == 1
		CONTINUE
	LOCAL:1 += 1
NEXT
IF LOCAL:1 >= 1
	RETURN 1
ENDIF
RETURN 0

@SHOPCOMABLE_109
RESULT:1 = 1
RESULTS = 조수변경
;조수가능キャラがいるなら
SIF CHARANUM < 2
	RETURN 0
LOCAL:1 = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:이자리에없는플래그 >= 1
		CONTINUE
	SIF CFLAG:LOCAL:노역플래그 == 3
		CONTINUE
	SIF ((TALENT:LOCAL:임신 && CFLAG:LOCAL:출산예정일 - 2 <= DAY) || TALENT:LOCAL:육아중) && FLAG:출산기능ONOFF == 1
		CONTINUE
	SIF CFLAG:LOCAL:매각가능 < 2
		CONTINUE
	LOCAL:1 += 1
NEXT
IF LOCAL:1 >= 1
	RETURN 1
ENDIF
RETURN 0

@PARTNER_ABLE, ARG
#FUNCTION
;主人は除外
SIF ARG == MASTER
	RETURNF 0
;노역中キャラは除外
SIF CFLAG:ARG:노역플래그 == 3
	RETURNF 0
SIF ((TALENT:ARG:임신 && CFLAG:ARG:출산예정일 - 2 <= DAY) || TALENT:ARG:육아중) && FLAG:출산기능ONOFF == 1
	RETURNF 0
;失踪中などこの場にいないなら除外
SIF CFLAG:ARG:이자리에없는플래그
	RETURNF 0
RETURNF 1


@SHOP_COM_108
PRINTL
CUSTOMDRAWLINE =
;キャラリストを표시
CALL INPUT_CHARA_LIST("누구를 파트너로 합니까？", "CASTING_SHOPCOM_108")
SIF RESULT == 1000
	RETURN 0
TARGET = RESULT
SIF TARGET == ASSI
	ASSI = -1

@CASTING_SHOPCOM_108(ARG)
;主人は除外
SIF !PARTNER_ABLE(ARG)
	RETURN 0
SIF TALENT:ARG:맹우
	RETURN 0
SIF CFLAG:ARG:필터링플래그
	RETURN 0
RETURN 1

@ASSI_ABLE, ARG
#FUNCTION
SIF MARK:ARG:반발각인 > 0
	RETURNF 0
;主人は除外
SIF ARG == MASTER
	RETURNF 0
;노역中キャラは除外
SIF CFLAG:ARG:노역플래그 == 3
	RETURNF 0
SIF ((TALENT:ARG:임신 && CFLAG:ARG:출산예정일 - 2 <= DAY) || TALENT:ARG:육아중) && FLAG:출산기능ONOFF == 1
	RETURNF 0
;失踪中などこの場にいないなら除外
SIF CFLAG:ARG:이자리에없는플래그
	RETURNF 0
;조수가능でないものは除外
SIF CFLAG:ARG:매각가능 < 2
	RETURNF 0
RETURNF 1

@SHOP_COM_109
#DIM LCOUNT
P = 0
;표시させるキャラを抽出（Q:*に표시するキャラの등록번호、LOCAL:2に人数）
VARSET Q, 0
A = 0
FOR LCOUNT, 0, CHARANUM
	SIF !ASSI_ABLE(LCOUNT)
		CONTINUE
	SIF CFLAG:LCOUNT:필터링플래그
		CONTINUE
	Q:A = LCOUNT
	LOCAL:2 = A
	A += 1
	MARK:LCOUNT:98 = 1
NEXT

$PRINT_LIST
CALL ASSI_LIST
DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 16% # [1001]이전페이지\@
PRINTLC [1000]돌아간다
PRINTFORMLC \@(P >= (LOCAL:2 - 1) / 리스트표시수()) ? %" " * 16% # [1009]다음페이지\@

$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001
	IF P > 0
		P -= 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSEIF RESULT == 1009
	LOCAL = (LOCAL:2 - 1) / 리스트표시수()
	IF P < LOCAL
		P += 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSEIF RESULT == 0
	ASSI = -1
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MATCH(Q,RESULT,0) == 0
	GOTO INPUT_LOOP
ELSEIF MARK:RESULT:98 == 0
	GOTO INPUT_LOOP
ENDIF

ASSI = RESULT
CALL ASSI_CHANGE
;ISASSI:ASSI = 1
IF ASSI == TARGET
	TARGET = -1
	FLAG:996 = -1
ENDIF



;-------------------------------------------------
;조수가능キャラ一覧の표시処理
;-------------------------------------------------
@ASSI_LIST
#DIM LCOUNT
DRAWLINE
PRINT 현재 
PRINTFORML \@(ASSI >= 0) ? %조사처리(CALLNAME:ASSI,"가")% 조수 # 조수가 없습니다\@
PRINTFORML 누구를 조수로 하겠습니까? (현재의 조수는 ☆, 현재의 조교 대상은 ★) ＜page.{P}＞
DRAWLINE
PRINTL   [ 0]조수 없음
DRAWLINE
FOR LCOUNT, 0, 리스트표시수()
	LOCAL = Q:(LCOUNT + P * 리스트표시수())
	SIF LOCAL == 0
		CONTINUE

	IF LOCAL == ASSI
		PRINT ☆
	ELSEIF LOCAL == TARGET
		PRINT ★
	ELSE
		PRINT 　
	ENDIF
	CALL ARRANGE_CHARALIST, LOCAL
	IF !ISASSI:LOCAL
		PRINT [미경험] 
	ELSEIF LOCAL == ASSI
		PRINT 　　　　 
	ELSE
		PRINT [원조수] 
	ENDIF
	PRINTFORM 기교Lv{ABL:LOCAL:기교} 
	IF TALENT:LOCAL:남자
		PRINTFORM ＢＬ끼Lv{ABL:LOCAL:ＢＬ끼} ＢＬ중독Lv{ABL:LOCAL:ＢＬ중독} 
	ELSE
		PRINTFORM 레즈끼Lv{ABL:LOCAL:레즈끼} 레즈중독Lv{ABL:LOCAL:레즈중독} 
	ENDIF
	PRINTFORM 촬영기능Lv{ABL:LOCAL:촬영기능} 
	IF TALENT:LOCAL:바이
		PRINT 바이
	ELSE
		PRINT 　　
	ENDIF
	IF TALENT:LOCAL:치료
		PRINT 치료
	ELSE
		PRINT 　　
	ENDIF
	IF TALENT:LOCAL:고무
		PRINT 고무
	ELSE
		PRINT 　　
	ENDIF
	IF TALENT:LOCAL:수수께끼의매력
		PRINT 매력
	ELSE
		PRINT 　　
	ENDIF
	IF TALENT:LOCAL:매혹
		PRINT 매혹
	ELSE
		PRINT 　　
	ENDIF
	IF TALENT:LOCAL:조합지식
		PRINT 조합
	ELSE
		PRINT 　　
	ENDIF
	PRINTL 

NEXT

