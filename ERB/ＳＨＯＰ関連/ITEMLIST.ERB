
;	001		2022/06/07		Collared				UI整理プロジェクトにより配置変更

@SHOPCOMABLE_112
RESULT:1 = 1
RESULTS = 소지품확인
RETURN 1

@SHOP_COM_112
#LOCALSIZE 7
CUSTOMDRAWLINE =
PRINTL 확인하고 싶은 아이템의 종류를 선택하십시오
PRINTL 또는 확인하고 싶은 아이템의 이름을 직접 입력하십시오
DRAWLINE
FOR LOCAL,0,8
	PRINTFORML [{LOCAL}] %GET_EQUIP(LOCAL)%
NEXT

PRINTL [8] 아이템
PRINTL [9] 스킬카드
PRINTL [10] 이벤트아이템
PRINTL [11] 그외
PRINTL [12] 도구전서
PRINTL [100] 돌아간다
INPUTS
IF GROUPMATCH(RESULTS, "0", "1","2","3","4","5","6","7","8","9","10","11","12","100")
	LOCAL:6 = 0
	TOINT RESULTS
	;CALL INPUTINT(0,1,2,3,4,5,6,7,8,9,10,11,12,100)
	;LOCAL:1 種別
	LOCAL:1 = RESULT
	SELECTCASE LOCAL:1
		CASE 100
			RETURN 0
		CASE 9
			CALL SHOW_SKILL_CARD
			RESTART
		CASE 10,11
			VARSET 아이템리스트,-1
			LOCAL:3 = 0
			FOR LOCAL, RESULT == 10 ? 300 # 200 , RESULT == 10 ? 500 # 300
				SIF ITEMNAME:LOCAL == ""
					CONTINUE
				IF ITEM:LOCAL > 0
					아이템리스트:(LOCAL:3) = LOCAL
					LOCAL:3 += 1
				ENDIF
			NEXT
			LOCAL:2 = LOCAL:3
		CASE 12
			CALL EQUIPMENT_COMPENDIUM
			RESTART
		CASEELSE
			CALL MAKE_SALERPGITEMLIST,LOCAL:1
			LOCAL:2 = RESULT
	ENDSELECT
ELSE
	CLEARLINE 1
	LOCAL:6 = 1
	GETNUM ITEM, RESULTS
	SIF RESULT == -1
		RESTART
	IF ITEM:RESULT < 1
		PRINTFORML %조사처리(RESULTS,"를")% 소지하고 있지 않습니다
		RESTART
	ENDIF
	LOCAL:3 = RESULT
	SELECTCASE LOCAL:3
		CASE 200 TO 299
			PRINT □그외
			LOCAL:1 = 11
		CASE 300 TO 499
			LOCAL:1 = 10
			PRINT □이벤트아이템
		CASE 1001 TO 1999
			PRINT □아이템
			LOCAL:1 = 9
		CASE 2000 TO 5999
			LOCAL:1 = (LOCAL:3-2000)/500
			PRINTFORM □%GET_EQUIP(LOCAL:1)%
		CASEELSE
			RESTART
	ENDSELECT
	PRINTFORMW 　%RESULTS%
	GOTO EXPLANATION
ENDIF
P = 0
LOCAL:5 = LINECOUNT
$PRINT_LIST
CLEARLINE LINECOUNT - LOCAL:5
DRAWLINE
CALL ITEMLIST,LOCAL:2
$INPUT_LOOP2
INPUT
LOCAL:3 = RESULT
IF RESULT == 0
	RESTART
ELSEIF RESULT == 7 && P > 0
	P -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 9 && P < LOCAL:2/20
	P += 1
	GOTO PRINT_LIST
ELSEIF MATCH(아이템리스트,RESULT,0) > 0
	$EXPLANATION
	DRAWLINE
	IF LOCAL:1 == 10 || LOCAL:1 == 11
		TRYCCALLFORM 아이템해설_{RESULT}
			WAIT
		CATCH
		ENDCATCH
;		CLEARLINE 1
;		GOTO INPUT_LOOP2
;	ENDIF
	ELSEIF LOCAL:1 < 7
		CALL 장비설명,LOCAL:3
		WAIT
	ELSEIF LOCAL:1 == 7
		CALLFORM 장비해설_{LOCAL:3}
		WAIT
	ELSE
		CALLFORM 최대소지수_{COUNT_ITEMMAX(LOCAL:3)}
		LOCAL:4 = RESULT
		PRINTFORML %ITEMNAME:(LOCAL:3),20,LEFT%   소지수：{ITEM:(LOCAL:3)}/{LOCAL:4}
		SIF FLAG:스킬속성표시설정 == 1
				CALL SKILL_EXPLAIN_PERFORMANCE, LOCAL:3 + 2000, -1
		TRYCALLFORM SKILL_EXPLAIN_{LOCAL:3+2000}
		DRAWLINE
		WAIT
	ENDIF
ELSE
	GOTO INPUT_LOOP2
ENDIF
SIF LOCAL:6
	RESTART
GOTO PRINT_LIST






;======================================================
;소지している아이템・장비品全てをリスト化して표시
;======================================================

@ITEMLIST,ARG
PRINTFORML 확인하고 싶은 아이템을 선택하십시오　＜page.{P + 1}＞
DRAWLINE
LOCAL:1 = 0
FOR LOCAL,P*20,MIN(P*20+20,ARG)
	TRYCALLFORM 최대소지수_{COUNT_ITEMMAX(아이템리스트:LOCAL)}
	RESULT = 0
	SIF RESULT > 0 && ITEM:(아이템리스트:LOCAL) > RESULT
		ITEM:(아이템리스트:LOCAL) = RESULT
	IF 마정장비(아이템리스트:LOCAL)
		PRINTFORMLC [{아이템리스트:LOCAL}] %EQ_GETNAME_마정장비_ITEM(아이템리스트:LOCAL),20,LEFT%×{ITEM:(아이템리스트:LOCAL),2}　
	ELSE
		PRINTFORMLC [{아이템리스트:LOCAL}] %ITEMNAME:(아이템리스트:LOCAL),20,LEFT%×{ITEM:(아이템리스트:LOCAL),2}　
	ENDIF
NEXT
PRINTL
DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 13% # [7]이전페이지\@
PRINTLC [0]돌아간다
PRINTFORMLC \@(P >= ARG / 20) ? %" " * 13% # [9]다음페이지\@



