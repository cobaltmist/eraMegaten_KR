;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:デビオク.ERB
;	Facility	:데빌 옥션에 관한 함수군입니다.
;
;	Licence		:ライセンスフリー
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2014/09/17		ネトリス					
;	002		2021/01/14		しぐれみそ				リスト更新及び中古品購入の際の処理が、所持している中古品と同じタイプのランダムキャラに行われてしまう不具合の修正。MAXBASEとアライメントを保存する処理を追加。VAR.ERHにMAXBASEとアライメント用の変数を追加
;													
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;デビオク
;매각した固有キャラ（主に인간）の買戻しと、他作品キャラの落札が出来る(NOは5100台:メガテンキャラ、5200台:그외キャラ)
;書品リストは１買い戻し、２他作品キャラで4枠づつ표시される
;買戻しは매각額+매각額/2
;상품の入れ替えは１週間
;入れ替えられた상품は他へ変われた扱いになり、買い取られた先にちなんだ경험を受ける
;
;
;会員情報변경では마카を支払う事で랭크を上げる事が出来る
;랭크が上がると상품の枠が増える
;予約する事で１週間過ぎてもその枠だけ상품が入れ替えられなくなる
;ただし、一日に付き구입額の１％（최저額５００마카）を支払わなければいけない
;
;保存用の変数を追加する場合は@デビオク_リストの中古品の処理、@CHARASELL_AFTER_デビオク、@デビオク_中古経験処理にもそれ用の処理を追加する
;
;==========================================================
;==========================================================
;SHOPCOMABLE_124
; SHOP화면での표시설정
;<引数>
;	ARG = 점포번호
;<戻り値>
;	なし
;========================================================
@SHOPCOMABLE_124
;이벤트『デビオク이용등록のお知らせ』종료後から표시
	RESULT:1 = 5
IF 이벤트플래그:40:0
	RESULTS = 데빌옥션
	RETURN 1
;조건を満たしていない場合は표시しない
ELSE
	RESULTS = ？？？？？
	RETURN 0
ENDIF

;==========================================================
;SHOP_COM_124
; デビオク処理
;<引数>
;	ARG = 점포번호
;<戻り値>
;	なし
;========================================================
@SHOP_COM_124
CLEARLINE LINECOUNT
PRINTFORMW 데빌 옥션에 어서 오십시오
$LOOP_START
CLEARLINE LINECOUNT
PRINTFORML [1] 상품을 본다
PRINTFORML [2] 회원 정보 변경
PRINTFORML [3] 헬프
PRINTFORML [100] 돌아간다

CALL INPUTINT(1, 2, 3, 100)

SELECTCASE RESULT
	;상품を見る
	CASE 1
		CALL デビオク_リスト
		GOTO LOOP_START
	;会員情報변경
	CASE 2
		LOCALS = -일반-
		LOCALS:1 = -골드-
		LOCALS:2 = -플래티나-
		LOCALS:3 = -앙그라-
		;次랭크に필요한마카
		LOCAL = 80000
		LOCAL:1 = 300000
		LOCAL:2 = 1500000
		
		$LOOP_会員情報변경_1
		CLEARLINE LINECOUNT
		
		PRINTFORML 
		PRINTFORML 현재 랭크　%LOCALS:(FLAG:데빌옥션랭크)%
		PRINTFORML 
		
		SELECTCASE FLAG:데빌옥션랭크
			CASE IS < 3
				PRINTFORML [1] 랭크를 올린다　{LOCAL:(FLAG:데빌옥션랭크)}마카
				PRINTFORML [100] 돌아간다
				CALL INPUTINT(1, 100)
				SELECTCASE RESULT
					;랭크を上げる
					CASE 1
						;마카が발りない場合
						IF MONEY:1 < LOCAL:(FLAG:데빌옥션랭크)
							PRINTFORMW 마카가 부족합니다
							GOTO LOOP_会員情報변경_1
						ENDIF
						
						PRINTFORMW 회원 랭크가 %LOCALS:(FLAG:데빌옥션랭크+1)%로 되었습니다
						
						MONEY:1 -= LOCAL:(FLAG:데빌옥션랭크)
						FLAG:데빌옥션랭크 += 1
					;もどる
					CASE 100
						GOTO LOOP_START
				ENDSELECT
			CASE 3
				PRINTFORML [100] 돌아간다
				;ただ押すだけ
				CALL INPUTINT(100)
				GOTO LOOP_START
		ENDSELECT
		GOTO LOOP_START
	;헬プ
	CASE 3
		$LOOP_헬프
		CLEARLINE LINECOUNT
		PRINTFORML [1] 낙찰 방법
		PRINTFORML [2] 중고와 신품
		PRINTFORML [3] 예약
		PRINTFORML [4] 회원 정보 변경
		PRINTFORML [100] 돌아온다
		
		CALL INPUTINT(1, 2, 3, 4, 100)
		SELECTCASE RESULT
			CASE 1
				PRINTFORML -낙찰방법-
				PRINTFORML 
				PRINTFORML 낙찰하고 싶은 캐릭터를 선택해서 『낙찰』 을 눌러 주세요
				PRINTFORMW 그 다음 낙찰 확인을 하고 나서 이상이 없으면 『낙찰한다』 를 선택해 주세요
				PRINTFORML 
				PRINTFORMW 이로써 낙찰은 완료됩니다
				PRINTFORML 
			CASE 2
				
				PRINTFORML -중고와 신품-
				PRINTFORML 
				PRINTFORML 중고품은 아래와 같습니다
				PRINTFORML 
				PRINTFORML 『매각된 고유 인간 캐릭터（CSV:NO.4500～4999）와 데빌 옥션 전용（CSV:NO.5100～5299）』
				PRINTFORML 
				PRINTFORMW 『매각된 캐릭터』가 없을 경우에는 ＜재고 품절＞이 됩니다
				PRINTFORML 
				PRINTFORML 
				PRINTFORML 신품은 『캐릭터 CSV：5100～5299』의 캐릭터입니다
				PRINTFORML (메가텐 캐릭터:5100～5199, 비 메가텐 캐릭터:5200～5299)
				PRINTFORMW 이쪽도 없을 경우에는 ＜재고 품절＞이 됩니다
				PRINTFORML 
				PRINTFORML 내보내고 싶은 캐릭터가 있지만 이벤트 등에 끼워넣기 어렵다
				PRINTFORML 또는 타 작품의 캐릭터를 사용하고 싶다
				PRINTFORMW ...와 같은 경우 사용해 주세요
				PRINTFORML 
				PRINTFORML ＜주의＞
				PRINTFORML 
				PRINTFORML 중고・신품 모두 월요일에 상품의 교체가 있습니다
				PRINTFORMW 낙찰하지 않은 경우 NPC에게 낙찰된 걸로 처리되어, 낙찰처에서 사용되므로 주의해주세요
				PRINTFORML 
				PRINTFORMW 더불어 예약으로 이런 일을 막을 수 있습니다
				PRINTFORML 
				PRINTFORML 
				PRINTFORML 데빌 옥션 내의 정보는 다음 주차로 넘어가지 않습니다
				PRINTFORMW 낙찰하지 않은 캐릭터는 초기화되니 이점에 주의해 주세요
				PRINTFORML 
			CASE 3
				
				PRINTFORML -예약-
				PRINTFORML 
				PRINTFORML 예약을 통해 상품 교체 시기에 외부 낙찰로 사라지는 것을 막을 수 있습니다
				PRINTFORML 다만 예약 중에는 낙찰액의 １％를 지불해야 합니다
				PRINTFORMW 주）최저 금액은 ５００마카
				PRINTFORML 
				PRINTFORML 
			CASE 4
				
				PRINTFORML -회원 정보 변경-
				PRINTFORML 
				PRINTFORML 이쪽은 일정 액수를 지불해 상품의 범위를 늘릴 수 있습니다
				PRINTFORMW 최종적으로는 ４칸까지 확장 가능합니다
				PRINTFORML 
			CASE 100
				GOTO LOOP_START
		ENDSELECT
		GOTO LOOP_헬프
		
	;もどる
	CASE 100
		RETURN
ENDSELECT

;==========================================================
;デビオク_リスト
; デビオク상품処理
;<引数>
;	ARG = 점포번호
;<戻り値>
;	なし
;========================================================
@デビオク_リスト, ARG
#DIM 상품, 4
#DIM 固有번호退避
CLEARLINE LINECOUNT
PRINTFORML [1] 중고품
PRINTFORML [2] 신품
PRINTFORML [100] 돌아간다

CALL INPUTINT(1, 2, 100)

SELECTCASE RESULT
	;中古品
	CASE 1
		$L00P_予約_1
		;見やすいように入れ替え
		FOR LOCAL, 0, 4
			상품:LOCAL = 데빌옥션상품리스트:0:LOCAL
		NEXT
		CLEARLINE LINECOUNT
		PRINTFORML -중고품 리스트-
		FOR LOCAL, 0, FLAG:데빌옥션랭크+1
			IF 상품:LOCAL > 0
				PRINTFORM [{LOCAL+1}]%CSVNAME(상품:LOCAL,0), 18, LEFT%　　　{중고캐릭터매각가격:(상품:LOCAL-4500):0*2}마카
				;予約しているかどうか
				IF 데빌옥션예약플래그:0:LOCAL
					SETCOLOR COLOR("PINK")
					PRINTFORML 　　예약완료
					RESETCOLOR
				ELSE
					PRINTFORML 
				ENDIF
			ELSE
				PRINTFORML ＜재고 품절＞
			ENDIF
		NEXT
		
		PRINTFORML [100] 돌아간다
		$LOOP_1
		;普通にINPUT使えばいいのだろうけれど、INPUTINTが気に入ってるので回りくどく
		SELECTCASE FLAG:데빌옥션랭크
			CASE 0
				CALL INPUTINT(1, 100)
			CASE 1
				CALL INPUTINT(1, 2, 100)
			CASE 2
				CALL INPUTINT(1, 2, 3, 100)
			CASE 3
				CALL INPUTINT(1, 2, 3, 4, 100)
		ENDSELECT
		
		;もどる
		SIF RESULT == 100
			RESTART
		
		;在庫がない場合はループ
		SIF 상품:(RESULT-1) < 1
			GOTO LOOP_1
		
		;選択したキャラ번호
		ARG = 상품:(RESULT-1)
		
		;매각가격
		ARG:1 = 중고캐릭터매각가격:(ARG-4500):0*2
		
		;選んだ데빌옥션상품리스트:0:LOCALの번호を保存
		ARG:2 = RESULT-1
		PRINTFORML 
		PRINTFORML %CSVNAME(ARG,0)%　　　낙찰 가격：{ARG:1}마카
		PRINTFORML 
		PRINTFORML 
		PRINTFORML [1] 낙찰
		PRINTFORML [2] 예약
		PRINTFORML [100] 돌아간다
		CALL INPUTINT( 1, 2, 100)
		
		SELECTCASE RESULT
			;落札
			CASE 1
				PRINTFORML %조사처리(CSVNAME(ARG,0),"를")% 낙찰하겠습니까?　　　낙찰 가격：{ARG:1}마카
				PRINTFORML 
				CALL INPUT_YN, "낙찰한다", "아니오"
				
				;やめる
				SIF RESULT == 1
					RESTART
				
				;발りない場合はもどる
				IF MONEY:1 < ARG:1
					PRINTFORMW 마카가 부족합니다
					RETURN
				ENDIF
				
				PRINTFORMW %조사처리(CSVNAME(ARG,0),"를")% 낙찰했습니다
				
				MONEY:1 -= ARG:1
				
				CALL ADD_NEW_COMPANION, ARG
				IF INRANGE(ARG, 4901, 4912)
					;ランダムキャラは固有番号が同じな場合があるので処理を分ける
					;どっちもCHARANUM - 1で良い気もするけど念の為
					ARG = CHARANUM - 1
				ELSE
					ARG = GETCHARA(ARG)
				ENDIF
				
				固有번호退避 = CFLAG:ARG:캐릭터고유번호
				
				;ABL入れ替え
				FOR LOCAL, 0, 291
					ABL:ARG:LOCAL = 중고캐릭터ABL:(NO:ARG-4500):LOCAL
				NEXT

				;BASE入れ替え
				FOR LOCAL, 0, 100
					BASE:ARG:LOCAL = 중고캐릭터BASE:(NO:ARG-4500):LOCAL
					MAXBASE:ARG:LOCAL = 중고캐릭터MAXBASE:(NO:ARG-4500):LOCAL
				NEXT
				
				;CFLAG保存入れ替え
				FOR LOCAL, 200, 600
					CFLAG:ARG:LOCAL = 중고캐릭터CFLAG:(NO:ARG-4500):(LOCAL-200)
				NEXT
				
				;CFLAG保存2入れ替え
				FOR LOCAL, 600, 1000
					CFLAG:ARG:LOCAL = 중고캐릭터CFLAG2:(NO:ARG-4500):(LOCAL-600)
				NEXT
				
				;CFLAG保存3入れ替え
				FOR LOCAL, 1551, 1572
					CFLAG:ARG:LOCAL = 중고캐릭터CFLAG3:(NO:ARG-4500):(LOCAL-1551)
				NEXT

				;CFLAG이상경험기록保存
				CFLAG:ARG:이상경험기록 = 중고캐릭터CFLAG이상경험기록:(NO:ARG-4500):0
				
								;アライメント入れ替え
				CFLAG:ARG:선악치 = 중고캐릭터얼라인먼트:(NO:ARG-4500):0
				CFLAG:ARG:질혼치 = 중고캐릭터얼라인먼트:(NO:ARG-4500):1
				
				CFLAG:ARG:캐릭터고유번호 = 固有번호退避
	
				;EXP入れ替え
				FOR LOCAL, 0, 84
					EXP:ARG:LOCAL = 중고캐릭터EXP:(NO:ARG-4500):LOCAL
				NEXT

				;TALENT入れ替え
				FOR LOCAL, 0, 160
					TALENT:ARG:LOCAL = 중고캐릭터TALENT:(NO:ARG-4500):LOCAL
				NEXT
				
				;MARK保存
				FOR LOCAL, 0, 4
					MARK:ARG:LOCAL = 중고캐릭터MARK:(NO:ARG-4500):LOCAL
				NEXT
				
				;연모・친애の場合は반발각인をつける
				SIF 연모(ARG) || 친애(ARG)
					MARK:ARG:반발각인 = 3

				;とりあえず부정의を５万にしておく（きつ過ぎる場合は要변경）
				JUEL:ARG:부정 = 50000
				
				
				;中古フラグ해제
				중고캐릭터매각완료:(NO:ARG-4500):0 = 0

				;매각가격리셋
				중고캐릭터매각가격:(NO:ARG-4500):0 = 0
				
				;売り切れ処理
				데빌옥션상품리스트:0:(ARG:2) = -1
				
				;予約해제
				데빌옥션예약플래그:0:(ARG:2) = 0
				GOTO L00P_予約_1

				;予約料計算（落札가격の１％を毎日支払う、최저額は500）
				LOCAL = ARG:1/100
				SIF LOCAL < 500
					LOCAL = 500
				FLAG:데빌옥션예약요금 -= LOCAL
			;予約
			CASE 2
				;予約料計算（落札가격の１％を毎日支払う、최저額は500）
				LOCAL = ARG:1/100
				SIF LOCAL < 500
					LOCAL = 500
				
				SELECTCASE 데빌옥션예약플래그:0:(ARG:2)
					CASE 0
						;予約
						PRINTFORML %조사처리(CSVNAME(ARG,0),"를")% 예약했습니다
						PRINTFORMW 1일 {LOCAL}마카를 지불합니다
						데빌옥션예약플래그:0:(ARG:2) = ARG
						FLAG:데빌옥션예약요금 += LOCAL
					CASEELSE
					;予約해제（取り消しは無料）
						PRINTFORMW 예약을 취소했습니다
						데빌옥션예약플래그:0:(ARG:2) = 0
						FLAG:데빌옥션예약요금 -= LOCAL
				ENDSELECT
				GOTO L00P_予約_1
			CASE 100
				RESTART
		ENDSELECT
	;新品
	CASE 2
		$L00P_予約_新品
		;見やすいように入れ替え
		FOR LOCAL, 0, 4
			상품:LOCAL = 데빌옥션상품리스트:1:LOCAL
		NEXT
		CLEARLINE LINECOUNT
		PRINTFORML -신품 리스트-
		FOR LOCAL, 0, FLAG:데빌옥션랭크+1
			IF 상품:LOCAL > 0
				PRINTFORM [{LOCAL+1}]%CSVNAME(상품:LOCAL,0), 18, LEFT%　　　{중고캐릭터매각가격:(상품:LOCAL-4500):0}마카
				;予約しているかどうか
				IF 데빌옥션예약플래그:1:LOCAL
					SETCOLOR COLOR("PINK")
					PRINTFORML 　　예약 완료
					RESETCOLOR
				ELSE
					PRINTFORML 
				ENDIF
			ELSE
				PRINTFORML ＜재고 품절＞
			ENDIF
		NEXT
		
		PRINTFORML [100] 돌아간다
		$LOOP_2
		;普通にINPUT使えばいいのだろうけれど、INPUTINTが気に入ってるので回りくどく
		SELECTCASE FLAG:데빌옥션랭크
			CASE 0
				CALL INPUTINT(1, 100)
			CASE 1
				CALL INPUTINT(1, 2, 100)
			CASE 2
				CALL INPUTINT(1, 2, 3, 100)
			CASE 3
				CALL INPUTINT(1, 2, 3, 4, 100)
		ENDSELECT
		
		;もどる
		SIF RESULT == 100
			RESTART
		
		;在庫がない場合はループ
		SIF 상품:(RESULT-1) < 1
			GOTO LOOP_2
		
		;選択したキャラ번호
		ARG = 상품:(RESULT-1)
		
		;매각가격
		ARG:1 = 중고캐릭터매각가격:(ARG-4500):0
		
		;選んだ데빌옥션상품리스트:1:LOCALの번호を保存
		ARG:2 = RESULT-1
		PRINTFORML 
		PRINTFORML %CSVNAME(ARG,0)%　　　낙찰 가격：{ARG:1}마카
		PRINTFORML 
		PRINTFORML 
		PRINTFORML [1] 낙찰
		PRINTFORML [2] 예약
		PRINTFORML [100] 돌아간다
		CALL INPUTINT( 1, 2, 100)
		
		SELECTCASE RESULT
			;落札
			CASE 1
				PRINTFORML %조사처리(CSVNAME(ARG,0),"를")% 낙찰하겠습니까?　　　낙찰 가격：{ARG:1}마카
				PRINTFORML 
				CALL INPUT_YN, "낙찰한다", "그만둔다"
				
				;やめる
				SIF RESULT == 1
					RESTART
				
				;발りない場合はもどる
				IF MONEY:1 < ARG:1
					PRINTFORMW 마카가 부족합니다
					RETURN
				ENDIF
				
				PRINTFORMW %조사처리(CSVNAME(ARG,0),"를")% 낙찰했습니다
				
				MONEY:1 -= ARG:1
				
				CALL ADD_NEW_COMPANION, ARG
				ARG = GETCHARA(ARG)

				;売り切れ処理
				데빌옥션상품리스트:1:(ARG:2) = -1
				
				;予約해제
				데빌옥션예약플래그:1:(ARG:2) = 0

				;予約料計算（落札가격の１％を毎日支払う、최저額は500）
				LOCAL = ARG:1/100
				SIF LOCAL < 500
					LOCAL = 500
				FLAG:데빌옥션예약요금 -= LOCAL
			;予約
			CASE 2
				;予約料計算（落札가격の１％を毎日支払う、최저額は500）
				LOCAL = ARG:1/100
				SIF LOCAL < 500
					LOCAL = 500

				SELECTCASE 데빌옥션예약플래그:1:(ARG:2)
					;予約
					CASE 0
						PRINTFORML %조사처리(CSVNAME(ARG,0),"를")% 예약했습니다
						PRINTFORMW 1일 {LOCAL}마카의 지불이 발생합니다
						데빌옥션예약플래그:1:(ARG:2) = ARG
						FLAG:데빌옥션예약요금 += LOCAL
					
					;予約해제（取り消しは無料）
					CASEELSE
						PRINTFORMW 예약을 취소했습니다
						데빌옥션예약플래그:1:(ARG:2) = 0
						FLAG:데빌옥션예약요금 -= LOCAL
				ENDSELECT
				GOTO L00P_予約_新品
			CASE 100
				RESTART
		ENDSELECT
	;もどる
	CASE 100
		RETURN
ENDSELECT

;==========================================================
;デビオク_リスト更新_新品
; デビオクの상품リスト更新の処理
; リストは日曜日に更新される
;<引数>
;	ARG   = キャラ번호
;	LOCAL:1 = リストに入っている번호の数
;	LOCAL:2～3 = 自由枠
;<戻り値>
;	なし
;========================================================
@デビオク_リスト更新_新品
#DIM リスト, 1000
#DIM 상품, 5
#DIM 予約済み상품, 5
;新品リスト更新
VARSET リスト , -1
VARSET 상품 , -1
VARSET 予約済み상품 , 0

LOCAL:1 = -1

;メガテン系이벤트以外불허가の場合
IF GETBIT(FLAG:EXTRA이벤트설정, 0) == 0
	FOR LOCAL, 5100, 5200
		;定義されていないは弾く
		SIF EXISTCSV(LOCAL) == 0
			CONTINUE

		;すでに所有している場合は弾く
		SIF GETCHARA(LOCAL) > 0
			CONTINUE
		
		;매각されている場合は弾く（新品で陳列され、そのままリスト更新された場合も中古になる）
		SIF 중고캐릭터매각완료:(LOCAL-4500):0 == 1
			CONTINUE
		
		;リストの번호を１進める
		LOCAL:1++
		
		;フラグの立っているキャラNOを保存
		リスト:(LOCAL:1) = LOCAL
	NEXT

;メガテン系以外이벤트허가の場合
ELSE
	FOR LOCAL, 5100, 5300
		;定義されていないは弾く
		SIF EXISTCSV(LOCAL) == 0
			CONTINUE

		;すでに所有している場合は弾く
		SIF GETCHARA(LOCAL) > 0
			CONTINUE
		
		;매각されている場合は弾く（新品で陳列され、そのままリスト更新された場合も中古になる）
		SIF 중고캐릭터매각완료:(LOCAL-4500):0 == 1
			CONTINUE
		
		;リストの번호を１進める
		LOCAL:1++
		
		;フラグの立っているキャラNOを保存
		リスト:(LOCAL:1) = LOCAL
	NEXT
ENDIF


;一人もいない場合は戻る
SIF リスト == -1
	RETURN

FOR LOCAL:2 , 0, 4
	IF 데빌옥션예약플래그:1:(LOCAL:2) == 0
		;予約されていなかった場合は他へ落札される、次からは中古品として棚に上がる
		IF 데빌옥션상품리스트:1:(LOCAL:2) > 0
			;대상キャラを一時的に加入
			CALL ADD_NEW_COMPANION, 데빌옥션상품리스트:1:(LOCAL:2)
			;매각額취득
			CALL CHARA_SELL, GETCHARA(데빌옥션상품리스트:1:(LOCAL:2)), 1
			;デビオク用の경험保管処理
			CALL CHARASELL_AFTER_デビオク, GETCHARA(데빌옥션상품리스트:1:(LOCAL:2)), 3000+RESULT*2
			;캐릭터삭제
			CALL 캐릭터삭제, GETCHARA(데빌옥션상품리스트:1:(LOCAL:2))
		ENDIF
	;予約されているキャラを保管
	ELSE
		予約済み상품:(LOCAL:2) = 데빌옥션예약플래그:1:(LOCAL:2)
	ENDIF
	;リスト:Xの中にある予約キャラの번호を-1にする
	FOR LOCAL , 0, LOCAL:1+1
		SIF リスト:LOCAL == 데빌옥션상품리스트:1:(LOCAL:2)
			リスト:LOCAL = -1
	NEXT
NEXT
;데빌옥션상품리스트:1:Xに-1を入れる
FOR LOCAL, 0, 4
	데빌옥션상품리스트:1:LOCAL = -1
NEXT

;등록された人数が現在の枠より少ない場合は順に並べる
;IF FLAG:데빌옥션랭크 >= LOCAL:1
;	FOR LOCAL, 0, 4
;		;랭크が발りない場合は終わる
;		SIF FLAG:데빌옥션랭크 < LOCAL
;			BREAK
;		
;		상품:LOCAL = リスト:LOCAL
;	NEXT
;
;등록された人数が데빌옥션랭크より多い場合
;ELSE
	;번호を상품0～3に入れる
	FOR LOCAL, 0, 4
		PRINTFORML {LOCAL}J
		;랭크が발りない場合は終わる
		SIF FLAG:데빌옥션랭크 < LOCAL
			BREAK
		
		;予約がある場合は入れて次へ
		IF 予約済み상품:LOCAL > 0
			상품:LOCAL = 予約済み상품:LOCAL
			CONTINUE
			
		;予約以外の상품が１つの場合
		ELSEIF LOCAL:1 == 0
			상품:LOCAL = リスト:0
			LOCAL:1 = -10
			CONTINUE

		;リストが空の場合は飛ばす、予約があるかもしれないので最後まで回すため
		ELSEIF LOCAL:1 == -10
			CONTINUE
		ENDIF
		
		LOCAL:2 = RAND:(LOCAL:1)
		
		LOCAL:3 = 0
		$LOOP_2
		
		;無限ループ防止処理、등록されている数より多くループした場合は終わる
		SIF LOCAL:3 > LOCAL:1
			CONTINUE

		;LOCAL:2に-1が入っている場合（他の　상품:Ｘ　に使われていた場合）は有効な数字になるまで一つずつずらす
		IF リスト:(LOCAL:2) == -1
			;LOCAL:2　と　LOCAL:1　が同じ場合は0に
			IF LOCAL:2 == LOCAL:1
				LOCAL:2 = 0
			;LOCAL:2　が　LOCAL:1　より小さい場合は1발す
			ELSEIF LOCAL:2 < LOCAL:1
				LOCAL:2++
			ENDIF
			
			;無限ループ防止処理用カウント
			LOCAL:3++
			
			GOTO LOOP_2
		ENDIF

		상품:LOCAL = リスト:(LOCAL:2)
		
		;사용した번호を-1にする
		リスト:(LOCAL:2) = -1
	NEXT
;ENDIF
FOR LOCAL, 0, 4

;	;予約されている場合
;	IF 予約済み상품:LOCAL > 0
;		데빌옥션상품리스트:1:LOCAL = 予約済み상품:LOCAL
;	
;	ELSE
;
	;-1が入っている場合は弾く
	SIF 상품:LOCAL < 0
		CONTINUE
	데빌옥션상품리스트:1:LOCAL = 상품:LOCAL
	
	;入荷のお知らせ이벤트플래그
	이벤트플래그:40:0 = 2
	;入荷お知らせを표시するためにFLAG:이벤트출현1を리셋する
	CLEARBIT FLAG:이벤트출현1, 40

;	ENDIF
	IF 데빌옥션상품리스트:1:LOCAL > 0
		;落札가격설정
		;대상キャラを一時的に加入
		CALL ADD_NEW_COMPANION, 데빌옥션상품리스트:1:LOCAL
		;매각額취득
		CALL CHARA_SELL, GETCHARA(데빌옥션상품리스트:1:LOCAL), 1
		;가격は3000+매각額*2
		중고캐릭터매각가격:(데빌옥션상품리스트:1:LOCAL-4500):0 = 3000+RESULT*2
		;캐릭터삭제
		CALL 캐릭터삭제, GETCHARA(데빌옥션상품리스트:1:LOCAL)
	ENDIF
NEXT

;==========================================================
;デビオク_リスト更新_中古
; デビオクの상품リスト更新の処理
; リストは日曜日に更新される
;<引数>
;	ARG   = キャラ번호
;	LOCAL:1 = リストに入っている번호の数
;	LOCAL:2～3 = 自由枠
;<戻り値>
;	なし
;========================================================
@デビオク_リスト更新_中古
#DIM リスト, 1000
#DIM 상품, 5
#DIM 予約済み상품, 5
VARSET リスト , -1
VARSET 상품 , -1
;中古リスト更新
LOCAL:1 = -1
FOR LOCAL, 1, 2000
	;중고캐릭터매각완료フラグがない場合は弾く
	SIF 중고캐릭터매각완료:LOCAL:0 == 0
		CONTINUE
	
	;リストの번호を１進める
	LOCAL:1++
	
	;フラグの立っているキャラ번호を保存、4500を引いて保存しているため발しなおす
	リスト:(LOCAL:1) = LOCAL+4500
NEXT

;一人もいない場合は戻る
SIF LOCAL:1 == -1
	RETURN

;予約されているキャラを保管
FOR LOCAL:2 , 0, 4
	SIF 데빌옥션예약플래그:0:(LOCAL:2) == 0
		CONTINUE
		
	予約済み상품:(LOCAL:2) = 데빌옥션예약플래그:0:(LOCAL:2)
	;リスト:Xの中にある予約キャラの번호を-1にする
	FOR LOCAL , 0, LOCAL:1+1
		IF リスト:LOCAL == 予約済み상품:(LOCAL:2)
			リスト:LOCAL = -1
	ENDIF
	NEXT
NEXT

;棚に上がっているキャラを弾く、二週連続載せないための処理
FOR LOCAL:2 , 0, 4
	SIF 데빌옥션상품리스트:0:(LOCAL:2) == -1
		CONTINUE
		
	;リスト:Xの中にある予約キャラの번호を-1にする
	FOR LOCAL , 0, LOCAL:1+1
		IF リスト:LOCAL == 데빌옥션상품리스트:0:(LOCAL:2)
			リスト:LOCAL = -1
	ENDIF
	NEXT
NEXT

;번호を상품0～3に入れる
FOR LOCAL, 0, 4
	;랭크が발りない場合は終わる
	SIF FLAG:데빌옥션랭크 < LOCAL
		BREAK
	
	;予約がある場合は入れて次へ
	IF 予約済み상품:LOCAL > 0
		상품:LOCAL = 予約済み상품:LOCAL
		CONTINUE
		
	;予約以外の상품が１つの場合
	ELSEIF LOCAL:1 == 0
		상품:LOCAL = リスト:0
		LOCAL:1 = -10
		CONTINUE

	;リストが空の場合は飛ばす、予約があるかもしれないので最後まで回すため
	ELSEIF LOCAL:1 == -10
		CONTINUE
	ENDIF
	
	LOCAL:2 = RAND:(LOCAL:1)
	
	LOCAL:3 = 0
	$LOOP_1
	
	;無限ループ防止処理、등록されている数より多くループした場合は終わる
	SIF LOCAL:3 > LOCAL:1
		CONTINUE

	;LOCAL:2に-1が入っている場合（他の　상품:Ｘ　に使われていた場合）は有効な数字になるまで一つずつずらす
	IF リスト:(LOCAL:2) == -1
		IF LOCAL:2 == LOCAL:1
			LOCAL:2 = 0
		ELSEIF LOCAL:2 < LOCAL:1
			LOCAL:2++
		ENDIF
		
		;無限ループ防止処理用カウント
		LOCAL:3++
		
		GOTO LOOP_1
	ENDIF

	상품:LOCAL = リスト:(LOCAL:2)
	
	;사용した번호を-1にする
	リスト:(LOCAL:2) = -1
NEXT

FOR LOCAL, 0, 4
	데빌옥션상품리스트:0:LOCAL = -1
NEXT

;등록の際に4500引いているので발して記録する
FOR LOCAL, 0, 4
;	;予約されている場合
;	IF 予約済み상품:LOCAL > 0
;		데빌옥션상품리스트:0:LOCAL = 予約済み상품:LOCAL
;		CONTINUE
;	ENDIF

	;-1が入っている場合は弾く
	SIF 상품:LOCAL < 0
		CONTINUE

	데빌옥션상품리스트:0:LOCAL = 상품:LOCAL

	;入荷のお知らせ이벤트플래그
	이벤트플래그:40:0 = 2
	;入荷お知らせを표시するためにFLAG:이벤트출현1を리셋する
	CLEARBIT FLAG:이벤트출현1, 40

	;売られていた間の경험加算と次の相손を決める、予約されている場合は飛ばす
	SIF 予約済み상품:LOCAL == 0
		CALL デビオク_中古경험処理, 데빌옥션상품리스트:0:LOCAL
NEXT

;==========================================================
;CHARASELL_AFTER_デビオク
; 매각されたキャラの조교に関する능력、소질、경험、가격を保存
; 中古キャラ～にそれぞれ保存
; NO:X-4500で등록する、戻す時には4500を발すのを忘れないように
;<引数>
;	ARG   = キャラ번호
;	ARG:1 = 매각가격
;	ARG:2 = 중고캐릭터매각처の설정
;<戻り値>
;	なし
;========================================================
@CHARASELL_AFTER_デビオク, ARG, ARG:1, ARG:2 = -1, ARGS = "매각処理"

;인간ではない場合（CSV:NO.4500～4999以外）とデビオク専用でない場合（CSV:NO.5100～5299）は記録しない
SIF NO:ARG < 4500 || NO:ARG > 4999 && NO:ARG < 5100 || NO:ARG > 5299
	RETURN

;中古済み판정
중고캐릭터매각완료:(NO:ARG-4500):0 = 1

;매각가격保存
중고캐릭터매각가격:(NO:ARG-4500):0 = ARG:1

;매각先、상품として上がった時には次の매각先を決定している
;설정がない場合は랜덤
SIF ARG:2 == -1
	ARG:2 = RAND:6
중고캐릭터매각처:(NO:ARG-4500):0 = ARG:2

;『@デビオク_中古경험処理』で使う場合は、この処理を省く（필요한かったので休止）
;IF ARGS == "매각処理"

	;매각日保存
	중고캐릭터매각일:(NO:ARG-4500):0 = DAY
;ENDIF

;ABL保存
FOR LOCAL, 0, 291
	중고캐릭터ABL:(NO:ARG-4500):LOCAL = ABL:ARG:LOCAL
NEXT

FOR LOCAL, 0, 100
	중고캐릭터BASE:(NO:ARG-4500):LOCAL = BASE:ARG:LOCAL
	중고캐릭터MAXBASE:(NO:ARG-4500):LOCAL = MAXBASE:ARG:LOCAL
NEXT

FOR LOCAL, 200, 600
	중고캐릭터CFLAG:(NO:ARG-4500):(LOCAL-200) = CFLAG:ARG:LOCAL
NEXT

FOR LOCAL, 600, 1000
	중고캐릭터CFLAG2:(NO:ARG-4500):(LOCAL-600) = CFLAG:ARG:LOCAL
NEXT

FOR LOCAL, 1551, 1572
	중고캐릭터CFLAG3:(NO:ARG-4500):(LOCAL-1551) = CFLAG:ARG:LOCAL
NEXT

중고캐릭터CFLAG이상경험기록:(NO:ARG-4500):0 = CFLAG:ARG:이상경험기록

중고캐릭터얼라인먼트:(NO:ARG-4500):0 = CFLAG:ARG:선악치
중고캐릭터얼라인먼트:(NO:ARG-4500):1 = CFLAG:ARG:질혼치

FOR LOCAL, 0, 84
	중고캐릭터EXP:(NO:ARG-4500):LOCAL = EXP:ARG:LOCAL
NEXT

FOR LOCAL, 0, 160
	중고캐릭터TALENT:(NO:ARG-4500):LOCAL = TALENT:ARG:LOCAL
NEXT

FOR LOCAL, 0, 4
	중고캐릭터MARK:(NO:ARG-4500):LOCAL = MARK:ARG:LOCAL
NEXT

;임신相손保存
;중고캐릭터임신상대:(NO:ARG-4500):LOCAL = ABL:ARG:LOCAL

;==========================================================
;デビオク_中古경험処理
; 売られていた間の경험を加算する
; 경험は매각先によって変わる
; 落札されずに流れた際の次の買い손も決める
; 
;<引数>
;	ARG   = キャラ번호
;<戻り値>
;	なし
;========================================================
@デビオク_中古경험処理, ARG
#DIM DYNAMIC 매각日
#DIM DYNAMIC 固有번호退避

;대상キャラを一時的に加入
CALL ADD_NEW_COMPANION, ARG
IF INRANGE(ARG, 4901, 4912)
	ARG = CHARANUM - 1
ELSE
	ARG = GETCHARA(ARG)
ENDIF

FOR LOCAL, 0, 291
	ABL:ARG:LOCAL = 중고캐릭터ABL:(NO:ARG-4500):LOCAL
NEXT

FOR LOCAL, 0, 100
	BASE:ARG:LOCAL = 중고캐릭터BASE:(NO:ARG-4500):LOCAL
	MAXBASE:ARG:LOCAL = 중고캐릭터MAXBASE:(NO:ARG-4500):LOCAL
NEXT

固有번호退避 = CFLAG:ARG:캐릭터고유번호

FOR LOCAL, 200, 600
	CFLAG:ARG:LOCAL = 중고캐릭터CFLAG:(NO:ARG-4500):(LOCAL-200)
NEXT

FOR LOCAL, 600, 1000
	CFLAG:ARG:LOCAL = 중고캐릭터CFLAG2:(NO:ARG-4500):(LOCAL-600)
NEXT

FOR LOCAL, 1551, 1572
	CFLAG:ARG:LOCAL = 중고캐릭터CFLAG3:(NO:ARG-4500):(LOCAL-1551)
NEXT

CFLAG:ARG:선악치 = 중고캐릭터얼라인먼트:(NO:ARG-4500):0
CFLAG:ARG:질혼치 = 중고캐릭터얼라인먼트:(NO:ARG-4500):1

CFLAG:ARG:캐릭터고유번호 = 固有번호退避

CFLAG:ARG:이상경험기록 = 중고캐릭터CFLAG이상경험기록:(NO:ARG-4500):0

FOR LOCAL, 0, 84
	EXP:ARG:LOCAL = 중고캐릭터EXP:(NO:ARG-4500):LOCAL
NEXT

FOR LOCAL, 0, 160
	TALENT:ARG:LOCAL = 중고캐릭터TALENT:(NO:ARG-4500):LOCAL
NEXT

FOR LOCAL, 0, 4
	MARK:ARG:LOCAL = 중고캐릭터MARK:(NO:ARG-4500):LOCAL
NEXT

;『중고캐릭터매각처:ARG:0』で경험の종류を、『中古キャラ매각期間:ARG:0』で횟수を決める
;もしなんらかの不具合で『中古キャラ매각期間:ARG:0』が『0』になってしまっている場合、또는『DAY』이상의 場合は『DAY-7』を入れる
SIF 중고캐릭터매각일:(NO:ARG-4500):0 == 0 || 중고캐릭터매각일:(NO:ARG-4500):0 >= DAY
	중고캐릭터매각일:(NO:ARG-4500):0 = DAY-7

;変数が長いので入れ替え
매각日 = 중고캐릭터매각일:(NO:ARG-4500):0

$LOOP_1
LOCAL:1 = 0
SELECTCASE 중고캐릭터매각처:(NO:ARG-4500):0
	;ノーマル（一日０～３回の計算）
	CASE 0
		;総プレイ횟수計算
		FOR LOCAL, 0, DAY-매각日
			LOCAL:1 += RAND:4
		NEXT
		
		;プレイ内容計算
		FOR LOCAL, 0, LOCAL:1
			;オンナの場合
			IF IS_MALE(ARG) == 0
				SELECTCASE RAND:100
					;フェラ
					CASE 0 TO 29
						TCVAR:ARG:획득펠라경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득봉사쾌락경험 += 1
					;本番
					CASE 30 TO 79
						;처녀상실
						IF TALENT:ARG:처녀
							CALL ADDS_ABNORMAL_EXP, "처녀상실（강간）", ARG
							TALENT:ARG:처녀 = 0
						ENDIF
						TCVAR:ARG:획득Ｖ경험 += 1
						TCVAR:ARG:획득성교경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						
						;約５０％の確立で中出し
						SIF RAND:100 < 50
							TCVAR:ARG:획득질사경험 += 1
							
						;約３０％の確立で절정
						SIF RAND:100 < 30
							TCVAR:ARG:획득절정경험 += 1
					;애널
					CASE 80 TO 99
						TCVAR:ARG:획득Ａ경험 += 1
						TCVAR:ARG:획득성교경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						
						;約３０％の確立で절정
						SIF RAND:100 < 30
							TCVAR:ARG:획득절정경험 += 1
				ENDSELECT
			;남자の場合
			ELSE
				TCVAR:ARG:획득성교경험 += 1
				;約７０％の確立で절정
				IF RAND:100 < 30
					TCVAR:ARG:획득사정경험 += 1
					TCVAR:ARG:획득절정경험 += 1
				ENDIF
			ENDIF
		NEXT
	;동성（一日０～３回の計算）
	CASE 1
		;総プレイ횟수計算
		FOR LOCAL, 0, DAY-매각日
			LOCAL:1 += RAND:4
		NEXT
		
		;プレイ内容計算
		FOR LOCAL, 0, LOCAL:1
			;オンナの場合
			IF IS_MALE(ARG) == 0
				SELECTCASE RAND:100
					;애무
					CASE 0 TO 39
						TCVAR:ARG:획득레즈경험 += 1

						;約３０％の確立で절정
						SIF RAND:100 < 30
							TCVAR:ARG:획득절정경험 += 1

						;約５０％の確立で봉사쾌락경험
						SIF RAND:100 < 50
							TCVAR:ARG:획득봉사쾌락경험 += 1
					;바이브
					CASE 40 TO 59
						SELECTCASE RAND:100
							;바이브
							CASE 0 TO 49
								;처녀상실
								IF TALENT:ARG:처녀
									CALL ADDS_ABNORMAL_EXP, "처녀상실（바이브）", ARG
									TALENT:ARG:처녀 = 0
								ENDIF
								
								TCVAR:ARG:획득Ｖ경험 += 1
								TCVAR:ARG:획득레즈경험 += 1
								
								;約３０％の確立で절정
								SIF RAND:100 < 30
									TCVAR:ARG:획득절정경험 += 1
									
							;애널바이브
							CASE 50 TO 99
								TCVAR:ARG:획득Ａ경험 += 1
								TCVAR:ARG:획득레즈경험 += 1
								
								;約３０％の確立で절정
								SIF RAND:100 < 30
									TCVAR:ARG:획득절정경험 += 1
						ENDSELECT
					;ペニバン
					CASE 60 TO 99
						SELECTCASE RAND:100
							;本番
							CASE 0 TO 49
								;처녀상실
								IF TALENT:ARG:처녀
									CALL ADDS_ABNORMAL_EXP, "처녀상실（동성）", ARG
									TALENT:ARG:처녀 = 0
								ENDIF
								
								TCVAR:ARG:획득Ｖ경험 += 1
								TCVAR:ARG:획득성교경험 += 1
								TCVAR:ARG:획득레즈경험 += 1
								
								;約３０％の確立で절정
								SIF RAND:100 < 30
									TCVAR:ARG:획득절정경험 += 1
									
							;애널
							CASE 50 TO 99
								TCVAR:ARG:획득Ａ경험 += 1
								TCVAR:ARG:획득성교경험 += 1
								TCVAR:ARG:획득레즈경험 += 1
								
								;約３０％の確立で절정
								SIF RAND:100 < 30
									TCVAR:ARG:획득절정경험 += 1
						ENDSELECT
				ENDSELECT
			;남자の場合
			ELSE
				SELECTCASE RAND:100
					;フェラ
					CASE 0 TO 29
						TCVAR:ARG:획득펠라경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득봉사쾌락경험 += 1
						TCVAR:ARG:획득ＢＬ경험 += 1
					;本番
					CASE 30 TO 79
						TCVAR:ARG:획득Ａ경험 += 1
						TCVAR:ARG:획득성교경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득ＢＬ경험 += 1
						
						;約３０％の確立で절정
						SIF RAND:100 < 30
							TCVAR:ARG:획득절정경험 += 1
				ENDSELECT
			ENDIF
		NEXT
	;수간（一日１～５回の計算）
	CASE 2
		;総プレイ횟수計算
		FOR LOCAL, 0, DAY-매각日
			LOCAL:1 += 1+RAND:5
		NEXT
		
		;プレイ内容計算
		FOR LOCAL, 0, LOCAL:1
			;オンナの場合
			IF IS_MALE(ARG) == 0
				SELECTCASE RAND:100
					;フェラ
					CASE 0 TO 19
						TCVAR:ARG:획득펠라경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득봉사쾌락경험 += 1
						TCVAR:ARG:획득수간경험 += 1
					;本番
					CASE 20 TO 79
						;처녀상실
						IF TALENT:ARG:처녀
							CALL ADDS_ABNORMAL_EXP, "처녀상실（강간）", ARG
							TALENT:ARG:처녀 = 0
						ENDIF
						TCVAR:ARG:획득Ｖ경험 += 1
						TCVAR:ARG:획득성교경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득수간경험 += 1
						
						;約５０％の確立で中出し
						SIF RAND:100 < 50
							TCVAR:ARG:획득질사경험 += 1
							
						;約３０％の確立で절정
						SIF RAND:100 < 30
							TCVAR:ARG:획득절정경험 += 1
					;애널
					CASE 80 TO 99
						TCVAR:ARG:획득Ａ경험 += 1
						TCVAR:ARG:획득성교경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득수간경험 += 1
						
						;約３０％の確立で절정
						SIF RAND:100 < 30
							TCVAR:ARG:획득절정경험 += 1
				ENDSELECT
			;남자の場合
			ELSE
				TCVAR:ARG:획득성교경험 += 1
				TCVAR:ARG:획득수간경험 += 1
				;約７０％の確立で절정
				IF RAND:100 < 30
					TCVAR:ARG:획득사정경험 += 1
					TCVAR:ARG:획득절정경험 += 1
				ENDIF
			ENDIF
		NEXT
	;ノーマル윤간（一日３～１０回の計算）
	CASE 3
		;総プレイ횟수計算
		FOR LOCAL, 0, DAY-매각日
			LOCAL:1 += 3+RAND:8
		NEXT
		
		;プレイ内容計算
		FOR LOCAL, 0, LOCAL:1
			;オンナの場合
			IF IS_MALE(ARG) == 0
				SELECTCASE RAND:100
					;フェラ
					CASE 0 TO 29
						TCVAR:ARG:획득펠라경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득봉사쾌락경험 += 1
						TCVAR:ARG:획득윤간경험 += 1
					;本番
					CASE 30 TO 79
						;처녀상실
						IF TALENT:ARG:처녀
							CALL ADDS_ABNORMAL_EXP, "처녀상실（윤간）", ARG
							TALENT:ARG:처녀 = 0
						ENDIF
						TCVAR:ARG:획득Ｖ경험 += 1
						TCVAR:ARG:획득성교경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득윤간경험 += 1
						
						;約５０％の確立で中出し
						SIF RAND:100 < 50
							TCVAR:ARG:획득질사경험 += 1
							
						;約３０％の確立で절정
						SIF RAND:100 < 30
							TCVAR:ARG:획득절정경험 += 1
					;애널
					CASE 80 TO 99
						TCVAR:ARG:획득Ａ경험 += 1
						TCVAR:ARG:획득성교경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득윤간경험 += 1
						
						;約３０％の確立で절정
						SIF RAND:100 < 30
							TCVAR:ARG:획득절정경험 += 1
				ENDSELECT
			;남자の場合
			ELSE
				TCVAR:ARG:획득성교경험 += 1
				;約７０％の確立で절정
				IF RAND:100 < 30
					TCVAR:ARG:획득사정경험 += 1
					TCVAR:ARG:획득절정경험 += 1
					TCVAR:ARG:획득윤간경험 += 1
				ENDIF
				
			ENDIF
		NEXT
	;동성윤간（一日３～１０回の計算）
	CASE 4
		;総プレイ횟수計算
		FOR LOCAL, 0, DAY-매각日
			LOCAL:1 += 3+RAND:8
		NEXT
		
		;プレイ内容計算
		FOR LOCAL, 0, LOCAL:1
			;オンナの場合
			IF IS_MALE(ARG) == 0
				SELECTCASE RAND:100
					;애무
					CASE 0 TO 39
						TCVAR:ARG:획득레즈경험 += 1
						TCVAR:ARG:획득윤간경험 += 1

						;約３０％の確立で절정
						SIF RAND:100 < 30
							TCVAR:ARG:획득절정경험 += 1

						;約５０％の確立で봉사쾌락경험
						SIF RAND:100 < 50
							TCVAR:ARG:획득봉사쾌락경험 += 1
					;바이브
					CASE 40 TO 59
						SELECTCASE RAND:100
							;바이브
							CASE 0 TO 49
								;처녀상실
								IF TALENT:ARG:처녀
									CALL ADDS_ABNORMAL_EXP, "처녀상실（바이브）", ARG
									TALENT:ARG:처녀 = 0
								ENDIF
								
								TCVAR:ARG:획득Ｖ경험 += 1
								TCVAR:ARG:획득레즈경험 += 1
								TCVAR:ARG:획득윤간경험 += 1
									
								;約３０％の確立で절정
								SIF RAND:100 < 30
									TCVAR:ARG:획득절정경험 += 1
									
							;애널바이브
							CASE 50 TO 99
								TCVAR:ARG:획득Ａ경험 += 1
								TCVAR:ARG:획득레즈경험 += 1
								TCVAR:ARG:획득윤간경험 += 1
								
								;約３０％の確立で절정
								SIF RAND:100 < 30
									TCVAR:ARG:획득절정경험 += 1
						ENDSELECT
					;ペニバン
					CASE 60 TO 99
						SELECTCASE RAND:100
							;本番
							CASE 0 TO 49
								;처녀상실
								IF TALENT:ARG:처녀
									CALL ADDS_ABNORMAL_EXP, "처녀상실（동성）", ARG
									CALL ADDS_ABNORMAL_EXP, "처녀상실（윤간）", ARG
									TALENT:ARG:처녀 = 0
								ENDIF
								
								TCVAR:ARG:획득Ｖ경험 += 1
								TCVAR:ARG:획득성교경험 += 1
								TCVAR:ARG:획득레즈경험 += 1
								TCVAR:ARG:획득윤간경험 += 1
								
								;約３０％の確立で절정
								SIF RAND:100 < 30
									TCVAR:ARG:획득절정경험 += 1
									
							;애널
							CASE 50 TO 99
								TCVAR:ARG:획득Ａ경험 += 1
								TCVAR:ARG:획득성교경험 += 1
								TCVAR:ARG:획득레즈경험 += 1
								TCVAR:ARG:획득윤간경험 += 1
								
								;約３０％の確立で절정
								SIF RAND:100 < 30
									TCVAR:ARG:획득절정경험 += 1
						ENDSELECT
				ENDSELECT
			;남자の場合
			ELSE
				SELECTCASE RAND:100
					;フェラ
					CASE 0 TO 29
						TCVAR:ARG:획득펠라경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득봉사쾌락경험 += 1
						TCVAR:ARG:획득ＢＬ경험 += 1
						TCVAR:ARG:획득윤간경험 += 1
					;本番
					CASE 30 TO 79
						TCVAR:ARG:획득Ａ경험 += 1
						TCVAR:ARG:획득성교경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득ＢＬ경험 += 1
						TCVAR:ARG:획득윤간경험 += 1
						
						;約３０％の確立で절정
						SIF RAND:100 < 30
							TCVAR:ARG:획득절정경험 += 1
				ENDSELECT
			ENDIF
		NEXT
	;수간윤간（一日５～２０回の計算）
	CASE 5
		;総プレイ횟수計算
		FOR LOCAL, 0, DAY-매각日
			LOCAL:1 += 5+RAND:16
		NEXT
		
		;プレイ内容計算
		FOR LOCAL, 0, LOCAL:1
			;オンナの場合
			IF IS_MALE(ARG) == 0
				SELECTCASE RAND:100
					;フェラ
					CASE 0 TO 19
						TCVAR:ARG:획득펠라경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득봉사쾌락경험 += 1
						TCVAR:ARG:획득수간경험 += 1
						TCVAR:ARG:획득윤간경험 += 1
					;本番
					CASE 20 TO 79
						;처녀상실
						IF TALENT:ARG:처녀
							CALL ADDS_ABNORMAL_EXP, "처녀상실（윤간）", ARG
							TALENT:ARG:처녀 = 0
						ENDIF
						TCVAR:ARG:획득Ｖ경험 += 1
						TCVAR:ARG:획득성교경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득수간경험 += 1
						TCVAR:ARG:획득윤간경험 += 1
						
						;約５０％の確立で中出し
						SIF RAND:100 < 50
							TCVAR:ARG:획득질사경험 += 1
							
						;約３０％の確立で절정
						SIF RAND:100 < 30
							TCVAR:ARG:획득절정경험 += 1
					;애널
					CASE 80 TO 99
						TCVAR:ARG:획득Ａ경험 += 1
						TCVAR:ARG:획득성교경험 += 1
						TCVAR:ARG:획득정액경험 += 1
						TCVAR:ARG:획득수간경험 += 1
						TCVAR:ARG:획득윤간경험 += 1
						
						;約３０％の確立で절정
						SIF RAND:100 < 30
							TCVAR:ARG:획득절정경험 += 1
				ENDSELECT
			;남자の場合
			ELSE
				TCVAR:ARG:획득성교경험 += 1
				TCVAR:ARG:획득수간경험 += 1
				TCVAR:ARG:획득윤간경험 += 1
				;約７０％の確立で절정
				IF RAND:100 < 30
					TCVAR:ARG:획득사정경험 += 1
					TCVAR:ARG:획득절정경험 += 1
				ENDIF
			ENDIF
		NEXT
	;どれにも当てはまらない場合は중고캐릭터매각처:ARG:0に0を入れてループ
	CASEELSE
		중고캐릭터매각처:ARG:0 = 0
		GOTO LOOP_1
ENDSELECT

;매각경험부여
TCVAR:ARG:획득매각경험 = 1
;경험반영
CALL SHOW_ADD_EXP, ARG, "비표시"
;이상경험処理
IF TCVAR:ARG:획득이상경험
	FOR LOCAL, 0, 64
		SIF !GETBIT(TCVAR:ARG:50, LOCAL)
			CONTINUE
		LOCAL:1 = RANK_ABNORMAL_EXP( , LOCAL)
		EXP:ARG:이상경험 += LOCAL:1
		SETBIT CFLAG:ARG:이상경험기록 , LOCAL
	NEXT
	TCVAR:ARG:획득이상경험 = 0
ENDIF
;경험の再격납
CALL CHARASELL_AFTER_デビオク, ARG, 중고캐릭터매각가격:(NO:ARG-4500):0, , 

;캐릭터삭제
CALL 캐릭터삭제, ARG

;==========================================================
;デビオク_予約料金処理
; 予約料金を徴収する
; 
;<引数>
;	
;<戻り値>
;	なし
;========================================================
@デビオク_予約料金処理
SETCOLOR COLOR("pink")
PRINTFORMW 데빌 옥션에서의 예약료로 {FLAG:데빌옥션예약요금}마카가 납입되었습니다
RESETCOLOR

MONEY:1 -= FLAG:데빌옥션예약요금

DRAWLINE

