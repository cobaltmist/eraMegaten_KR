;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:가챠모드.ERB
;	Facility	:SHOP커맨드[]ガチャの処理。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	000		2017/09/23		名無					新規作成
;	001		2022/06/07		Collared				UI整理プロジェクトにより配置変更
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;=================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name
;
; *- SHOP関連(RPG)
;
;	# 메뉴関連
;	・SHOPCOMABLE_402
;	・SHOP_COM_402
;	・GACHA_MASCHINE_SHOP
;
;	# ガチャ！
;	・GACHA_ONCE
;	・GACHA_SEARCH_DEVIL
;	・GACHA_SEARCH_ITEM
;
;=================================================
@SHOPCOMABLE_402
	RESULT:1 = 4
	IF FLAG:악마가챠모드 > 0
		RESULTS = 가챠！
		RETURN 1
	ENDIF
RETURN 0


;=================================================
@SHOP_COM_402
	CUSTOMDRAWLINE =
	CALL GACHA_MASCHINE_SHOP


;=================================================
@GACHA_MASCHINE_SHOP
PRINTFORML 
PRINTFORML 여기에는 가챠가챠가 놓여있다……
PRINTFORML 
$GACHA_LOOP_START
CUSTOMDRAWLINE =
PRINTFORM ＞현재의 픽업은
IF TALENT:MASTER:ガチャ > 0
	LOCAL = (DAY ) % 5
	SELECTCASE LOCAL
		CASE 0
			;なしの日
			PRINTFORM  없음
		CASE 1
			;性別ピックアップの日
			LOCAL:1 = (DAY + TIME - 1) % 2
			SELECTCASE LOCAL:1
				CASE 0
					PRINTFORM [여악마 픽업]
					FLAG:현재가챠토픽 = 11000140
				CASE 1
					PRINTFORM [남자 픽업]
					FLAG:현재가챠토픽 = 01000140
			ENDSELECT
		CASE 2
			;性格の日
			LOCAL:1 = (DAY + TIME - 1) % 8
			SELECTCASE LOCAL:1
				CASE 0
					PRINTFORM [우애 픽업]
					FLAG:현재가챠토픽 = 01000201
				CASE 1
					PRINTFORM [영맹 픽업]
					FLAG:현재가챠토픽 = 01000202
				CASE 2
					PRINTFORM [우둔 픽업]
					FLAG:현재가챠토픽 = 01000203
				CASE 3
					PRINTFORM [교만 픽업]
					FLAG:현재가챠토픽 = 01000204
				CASE 4
					PRINTFORM [교활 픽업]
					FLAG:현재가챠토픽 = 01000205
				CASE 5
					PRINTFORM [냉정 픽업]
					FLAG:현재가챠토픽 = 01000206
				CASE 6
					PRINTFORM [잔학 픽업]
					FLAG:현재가챠토픽 = 01000207
				CASE 7
					PRINTFORM [현명 픽업]
					FLAG:현재가챠토픽 = 01000208
			ENDSELECT
		CASE 3
			;민감の日
			LOCAL:1 = (DAY + TIME - 1) % 4
			SELECTCASE LOCAL:1
				CASE 0
					PRINTFORM [Ｃ민감 픽업]
					FLAG:현재가챠토픽 = 01000102
				CASE 1
					PRINTFORM [Ｂ민감 픽업]
					FLAG:현재가챠토픽 = 01000104
				CASE 2
					PRINTFORM [Ｖ민감 픽업]
					FLAG:현재가챠토픽 = 01000106
				CASE 3
					PRINTFORM [Ａ민감 픽업]
					FLAG:현재가챠토픽 = 01000108
			ENDSELECT
		CASE 4
			;속성ピックアップの日
			LOCAL:1 = (DAY + TIME - 1) % 4
			SELECTCASE LOCAL:1
				CASE 0
					PRINTFORM [라이트 악마 픽업]
					FLAG:현재가챠토픽 = 05000001
				CASE 1
					PRINTFORM [로우 악마 픽업]
					FLAG:현재가챠토픽 = 05000002
				CASE 2
					PRINTFORM [카오스 악마 픽업]
					FLAG:현재가챠토픽 = 05000003
				CASE 3
					PRINTFORM [다크 악마 픽업]
					FLAG:현재가챠토픽 = 05000004
			ENDSELECT
	ENDSELECT
ELSE
	PRINTFORM [초회 한정 무료 １０연]
ENDIF
PRINTFORML 입니다。
SIF FLAG:가챠레벨설정 < 5 || TALENT:MASTER:ガチャ <= 0
	FLAG:가챠레벨설정 = 5
SIF FLAG:가챠레벨설정 > 80
	FLAG:가챠레벨설정 = 80
PRINTFORML 가챠LV {FLAG:가챠레벨설정}　　　소지마카 {MONEY:1}
CUSTOMDRAWLINE =
LOCAL = FLAG:가챠레벨설정 * 2
IF TALENT:MASTER:ガチャ > 0
	IF TALENT:MASTER:ガチャ < DAY
		PRINTFORML [1] 단발 가챠　　－　
		SETCOLOR 0xBB00BB
		PRINTFORML 1일 1회 무료！
		RESETCOLOR
	ELSE
		PRINTFORML [1] 단발 가챠　　－　{LOCAL}마카
	ENDIF
	PRINTFORML [2] １０연 가챠　－　{LOCAL * 10}마카
	SIF FLAG:DEBUG
		PRINTFORML [777] ＳＲ악마 확정 디버그용 단발가챠　－　{LOCAL}마카
ELSE
	PRINTFORM [2] １０연 가챠　－　
	SETCOLOR 0xBB00BB
	PRINTFORML 초회 무료！
	RESETCOLOR
ENDIF
PRINTFORML [3] 배출 비율 확인
SIF TALENT:MASTER:ガチャ > 0
	PRINTFORML [4] 레벨 설정
;ガチャレベルLv80以上＋エクストライベントON時限定ガチャ
IF GETBIT(FLAG:EXTRA이벤트설정, 0) != 0 && FLAG:가챠레벨설정 == 80
	PRINTFORML [5] EXTRAイベントガチャ(Lv80限定)
ENDIF
PRINTFORML [100] 돌아가기
CUSTOMDRAWLINE =

IF GETBIT(FLAG:EXTRA이벤트설정, 0) != 0 && FLAG:가챠레벨설정 == 80
	IF TALENT:MASTER:ガチャ > 0 && FLAG:DEBUG
		CALL INPUTINT(1, 2, 3, 4, 5, 777, 100)
	ELSEIF TALENT:MASTER:ガチャ > 0
		CALL INPUTINT(1, 2, 3, 4, 5, 100)
	ELSE
		CALL INPUTINT(2, 3, 5, 100)
	ENDIF
ELSE
	IF TALENT:MASTER:ガチャ > 0 && FLAG:DEBUG
		CALL INPUTINT(1, 2, 3, 4, 777, 100)
	ELSEIF TALENT:MASTER:ガチャ > 0
		CALL INPUTINT(1, 2, 3, 4, 100)
	ELSE
		CALL INPUTINT(2, 3, 100)
	ENDIF
ENDIF

PRINTFORML 
SELECTCASE RESULT
	;１連ガチャ
	CASE 1
		IF TALENT:MASTER:ガチャ >= DAY
			LOCAL = FLAG:가챠레벨설정 * 2
			IF MONEY:1 < (LOCAL)
				PRINTFORMW 마카가 부족합니다.
				GOTO GACHA_LOOP_START
			ENDIF
		MONEY:1 -= LOCAL
		ELSE
			TALENT:MASTER:ガチャ = DAY
		ENDIF
		PRINTFORML 가챠가챠……
		PRINTFORMW 

		CALL GACHA_ONCE,0,FLAG:현재가챠토픽

		PRINTFORML 
		PRINTFORMW 가챠를 종료했습니다.
		GOTO GACHA_LOOP_START

	;１０連ガチャ
	CASE 2
		IF TALENT:MASTER:ガチャ > 0
			LOCAL = FLAG:가챠레벨설정 * 2
			IF MONEY:1 < (LOCAL * 10)
				PRINTFORMW 마카가 부족합니다.
				GOTO GACHA_LOOP_START
			ENDIF
			MONEY:1 -= LOCAL * 10
		ELSE
			TALENT:MASTER:ガチャ = DAY
		ENDIF
		PRINTFORML 가챠가챠가챠가챠가챠가챠가챠가챠가챠가챠……
		PRINTFORMW 

		;最初が악마確定、最後がＲ이상確定
		CALL GACHA_ONCE,1,FLAG:현재가챠토픽
		CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		CALL GACHA_ONCE,2,FLAG:현재가챠토픽

		PRINTFORML 
		PRINTFORMW 가챠를 종료했습니다.
		GOTO GACHA_LOOP_START

	;排出率の확인
	CASE 3
		LOCAL = FLAG:가챠레벨설정 * 2
		PRINTFORML 　　１％　……ＳＲ……　{FLAG:가챠레벨설정+1}～{FLAG:가챠레벨설정+10}LV의 「악마」
		PRINTFORML 　　４％　……　Ｒ……　{MAX(FLAG:가챠레벨설정/2,5)}～{FLAG:가챠레벨설정+5}LV이하의 「악마」
		PRINTFORML 　１５％　……　Ｎ……　{MAX(FLAG:가챠레벨설정/2,5)}LV이하의 「악마」
		PRINTFORML 　　２％　……ＳＲ……　{LOCAL*10}마카 이내에서 현재 교환 가능한 장비품・아이템
		PRINTFORML 　　８％　……　Ｒ……　{LOCAL*4}마카 이내에서 현재 교환 가능한 장비품・아이템
		PRINTFORML 　２０％　……　Ｎ……　{LOCAL}마카 이내에서 현재 교환 가능한 장비품・아이템
		PRINTFORML 　　５％　……ＳＲ……　{LOCAL*500}￥이내에서 현재 구입 가능한 장비품・아이템
		PRINTFORML 　１０％　……　Ｒ……　{LOCAL*200}￥이내에서 현재 구입 가능한 장비품・아이템
		PRINTFORML 　３５％　……　Ｎ……　{LOCAL*50}￥이내에서 현재 구입 가능한 장비품・아이템
		PRINTFORML 　
		PRINTFORML 　　・１０연 가챠는 반드시 악마가 하나, 또한 R 이상이 하나 선출됩니다.
		PRINTFORML 　　　그러나 R 아이템으로 N 아이템이 나와도 울지마시기 바랍니다.
		PRINTFORML 　　・선출 후, 악마는 ４분의１의 확률로 픽업 대상에서 선정합니다.
		PRINTFORML 　　　그러나 픽업 대상이 없는 경우에는 비 픽업 대상에서 선정합니다.
		PRINTFORML 　　　그래도 없는 경우에는 한 단계 아래 조건으로 다시 선정합니다.
		PRINTFORMW 
		SIF GETBIT(FLAG:EXTRA이벤트설정, 0) != 0 && FLAG:가챠레벨설정 == 80
			PRINTFORML 　　　EXTRAイベントガチャは、各抽選時１％の確率でEXTRA悪魔が排出される１０連ガチャです。
		GOTO GACHA_LOOP_START

	;ガチャ레벨の변경
	CASE 4
		PRINTFORML 가챠에서 나오는 악마의 레벨 및 아이템의 가격 상한을 설정합니다.
		PRINTFORML (최저５～최대 마스터 LV 또한 ８０까지)
		CALL INPUT_MANY(0,BASE:MASTER:LV, , "0/1/2/3/4/5")
		FLAG:가챠레벨설정 = RESULT
		GOTO GACHA_LOOP_START

	CASE 5
		IF TALENT:MASTER:ガチャ > 0
			LOCAL = FLAG:가챠레벨설정 * 2
			IF MONEY:1 < (LOCAL * 10)
				PRINTFORMW 魔貨が足りません。
				GOTO GACHA_LOOP_START
			ENDIF
			MONEY:1 -= LOCAL * 10
		ELSE
			TALENT:MASTER:ガチャ = DAY
		ENDIF
		PRINTFORML ガチャガチャガチャガチャガチャガチャガチャガチャガチャガチャ……
		PRINTFORMW 

		;それぞれ１％でエクストライベントガチャに飛ぶ。残り９９％は最初が悪魔確定、最後がＲ以上確定
		IF RAND:100 == 0
			CALL エクストライベントガチャ
		ELSE
			CALL GACHA_ONCE,1,FLAG:현재가챠토픽
		ENDIF
		IF RAND:100 == 0
			CALL エクストライベントガチャ
		ELSE
			CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		ENDIF
		IF RAND:100 == 0
			CALL エクストライベントガチャ
		ELSE
			CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		ENDIF
		IF RAND:100 == 0
			CALL エクストライベントガチャ
		ELSE
			CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		ENDIF
		IF RAND:100 == 0
			CALL エクストライベントガチャ
		ELSE
			CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		ENDIF
		IF RAND:100 == 0
			CALL エクストライベントガチャ
		ELSE
			CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		ENDIF
		IF RAND:100 == 0
			CALL エクストライベントガチャ
		ELSE
			CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		ENDIF
		IF RAND:100 == 0
			CALL エクストライベントガチャ
		ELSE
			CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		ENDIF
		IF RAND:100 == 0
			CALL エクストライベントガチャ
		ELSE
			CALL GACHA_ONCE,0,FLAG:현재가챠토픽
		ENDIF
		IF RAND:100 == 0
			CALL エクストライベントガチャ
		ELSE
			CALL GACHA_ONCE,2,FLAG:현재가챠토픽
		ENDIF

		PRINTFORML 
		PRINTFORMW ガチャを終了しました。
		GOTO GACHA_LOOP_START


	;もどる
	CASE 100
		RETURN
	;１連ガチャ
	CASE 777
		SIF !(FLAG:DEBUG)
			GOTO GACHA_LOOP_START
		IF TALENT:MASTER:ガチャ >= DAY
			LOCAL = FLAG:가챠레벨설정 * 2
			IF MONEY:1 < (LOCAL)
				PRINTFORMW 마카가 부족합니다.
				GOTO GACHA_LOOP_START
			ENDIF
		ELSE
			TALENT:MASTER:ガチャ = DAY
		ENDIF
		MONEY:1 -= LOCAL
		PRINTFORML SR확정 가챠……
		PRINTFORMW 

		CALL GACHA_ONCE,777,FLAG:현재가챠토픽

		PRINTFORML 
		PRINTFORMW 가챠를 종료했습니다.
		GOTO GACHA_LOOP_START

ENDSELECT



;=================================================
;みんな大好きガチャ処理　(ARG=1は악마確定でARG=2でＲ이상確定。　ARG:3=ピックアップ内容)
;=================================================
@GACHA_ONCE(ARG = -1, ARG:3 = -1)
LOCAL = 0
	IF ARG == 1
		LOCAL = RAND:2000000
	ELSEIF ARG == 2
		LOCAL = RAND:3000000
		IF LOCAL >= 1500000
			LOCAL += 5000000
		ELSEIF LOCAL >= 500000 && LOCAL < 1500000
			LOCAL += 2000000
		ENDIF
	ELSEIF ARG == 777
		LOCAL = RAND:100000
	ELSE
		LOCAL = RAND:10000000
	ENDIF
	;　１％　……ＳＲ……　{FLAG:가챠레벨설정+1}～{FLAG:가챠레벨설정+10}LVの「악마」
	IF   LOCAL < 100000
		IF !(LOCAL % 4)
			CALL GACHA_SEARCH_DEVIL, FLAG:가챠레벨설정+1, FLAG:가챠레벨설정+10, ARG:3
			IF RESULT > 0
				CALL ADD_NEW_COMPANION ,RESULT ,0 ,0
				IF RESULT
					LOCAL = RESULT:1
					SIF FLAG:DEBUG
						PRINTFORM {LOCAL, 5}
					TALENT:LOCAL:ガチャ = 1
					SIF FLAG:(20000+NO:LOCAL) < 100
						FLAG:(20000+NO:LOCAL) = 100
					SETCOLOR 0x00EEEE
					PRINTFORM [ＳＲ악마]
					RESETCOLOR
					PRINTFORML \@ CSTR:LOCAL:종족명 != "" ? %CSTR:LOCAL:종족명% # %STR:(ABL:LOCAL:종족)%\@　%CSTR:LOCAL:종족명% %CALLNAME:LOCAL%
					RETURN 0
				ENDIF
			ENDIF
		ENDIF
		CALL GACHA_SEARCH_DEVIL, FLAG:가챠레벨설정+6, FLAG:가챠레벨설정+10, 0
		IF RESULT > 0
			CALL ADD_NEW_COMPANION ,RESULT ,0 ,0
			IF RESULT
				LOCAL = RESULT:1
				SIF FLAG:DEBUG
					PRINTFORM {LOCAL, 5}
				TALENT:LOCAL:ガチャ = 1
				SIF FLAG:(20000+NO:LOCAL) < 100
					FLAG:(20000+NO:LOCAL) = 100
				SETCOLOR 0x00EEEE
				PRINTFORM [ＳＲ악마]
				RESETCOLOR
				PRINTFORML \@ CSTR:LOCAL:종족명 != "" ? %CSTR:LOCAL:종족명% # %STR:(ABL:LOCAL:종족)%\@　%CSTR:LOCAL:종족명% %CALLNAME:LOCAL%
				RETURN 0
			ENDIF
			;실패した場合は次に回す（仲間枠飽和とかも含む）
		ENDIF
	ENDIF

	;　４％　……　Ｒ……　{MAX(FLAG:가챠레벨설정/2,5)}～{FLAG:가챠레벨설정+5}LV以下の「악마」
	IF   LOCAL < 500000
		IF !(LOCAL % 4)
			CALL GACHA_SEARCH_DEVIL, MAX(FLAG:가챠레벨설정/2,5), FLAG:가챠레벨설정+5, ARG:3
			IF RESULT > 0
				CALL ADD_NEW_COMPANION ,RESULT ,0 ,0
				IF RESULT
					LOCAL = RESULT:1
					SIF FLAG:DEBUG
						PRINTFORM {RESULT:1, 5}
					TALENT:LOCAL:ガチャ = 1
					SIF FLAG:(20000+NO:LOCAL) < 100
						FLAG:(20000+NO:LOCAL) = 100
					SETCOLOR 0xEEEE00
					PRINTFORM [　Ｒ악마]　
					RESETCOLOR
					PRINTFORML \@ CSTR:LOCAL:종족명 != "" ? %CSTR:LOCAL:종족명% # %STR:(ABL:LOCAL:종족)%\@　%CSTR:LOCAL:종족명% %CALLNAME:LOCAL%
					RETURN 0
				ENDIF
			ENDIF
		ENDIF
		CALL GACHA_SEARCH_DEVIL, MAX(FLAG:가챠레벨설정/2,5), FLAG:가챠레벨설정+5, 0
		IF RESULT > 0
			CALL ADD_NEW_COMPANION ,RESULT ,0 ,0
			IF RESULT
				LOCAL = RESULT:1
				SIF FLAG:DEBUG
					PRINTFORM {LOCAL, 5}
				TALENT:LOCAL:ガチャ = 1
				SIF FLAG:(20000+NO:LOCAL) < 100
					FLAG:(20000+NO:LOCAL) = 100
				SETCOLOR 0xEEEE00
				PRINTFORM [　Ｒ악마]　
				RESETCOLOR
				PRINTFORML \@ CSTR:LOCAL:종족명 != "" ? %CSTR:LOCAL:종족명% # %STR:(ABL:LOCAL:종족)%\@ %CALLNAME:LOCAL%
				RETURN 0
			ENDIF
			;실패した場合は次に回す（仲間枠飽和とかも含む）
		ENDIF
	ENDIF

	;１５％　……　Ｎ……　{MAX(FLAG:가챠레벨설정/2,5)}LV以下の「악마」
	IF  LOCAL < 2000000
		IF !(LOCAL % 4)
			CALL GACHA_SEARCH_DEVIL, 1, MAX(FLAG:가챠레벨설정/2,5), ARG:3
			IF RESULT > 0
				CALL ADD_NEW_COMPANION ,RESULT ,0 ,0
				IF RESULT
					LOCAL = RESULT:1
					SIF FLAG:DEBUG
						PRINTFORM {LOCAL, 5}
					TALENT:LOCAL:ガチャ = 1
					SIF FLAG:(20000+NO:LOCAL) < 100
						FLAG:(20000+NO:LOCAL) = 100
					PRINTFORM [　Ｎ악마]　
					PRINTFORML \@ CSTR:LOCAL:종족명 != "" ? %CSTR:LOCAL:종족명% # %STR:(ABL:LOCAL:종족)%\@ %CALLNAME:LOCAL%
					RETURN 0
				ENDIF
			ENDIF
		ENDIF
		CALL GACHA_SEARCH_DEVIL, 1, MAX(FLAG:가챠레벨설정/2,5), 0
		IF RESULT > 0
			CALL ADD_NEW_COMPANION ,RESULT ,0 ,0
			IF RESULT
				LOCAL = RESULT:1
		SIF FLAG:DEBUG
			PRINTFORM {LOCAL, 5}
				TALENT:LOCAL:ガチャ = 1
				SIF FLAG:(20000+NO:LOCAL) < 100
					FLAG:(20000+NO:LOCAL) = 100
				PRINTFORM [　Ｎ악마]　
				PRINTFORML \@ CSTR:LOCAL:종족명 != "" ? %CSTR:LOCAL:종족명% # %STR:(ABL:LOCAL:종족)%\@ %CALLNAME:LOCAL%
				RETURN 0
			ENDIF
			;실패した場合は次に回す（仲間枠飽和とかも含む）
		ENDIF
	ENDIF

	;　２％　……ＳＲ……　{LOCAL*10}마카以内で現在交換가능な아이템
	IF  LOCAL < 2200000
		CALL GACHA_SEARCH_ITEM, FLAG:가챠레벨설정 * 2 *10 / 2, 1
		IF RESULT > 0
			LOCAL = RESULT
			SIF FLAG:DEBUG
				PRINTFORM {LOCAL, 5}
			SETCOLOR 0x00EEEE
			PRINTFORM [ＳＲ마카]
			RESETCOLOR
			PRINTFORM %ITEMNAME:LOCAL%
			CALL CHECK_ITEMMAX, LOCAL
			IF RESULT > 0
				SETCOLOR 0x888888
				PRINTFORM 　(가지지 못하고 폐기했습니다)
				RESETCOLOR
			ELSE
				CALL GET_ITEM, LOCAL, 1
			ENDIF
			PRINTFORML
			RETURN 0
		ENDIF
		;실패した場合は次に回す
	ENDIF

	;　８％　……　Ｒ……　{LOCAL*4}마카以内で現在交換가능な아이템
	IF  LOCAL < 3000000
		CALL GACHA_SEARCH_ITEM, FLAG:가챠레벨설정 * 2 *4 /2, 1
		IF RESULT > 0
			LOCAL = RESULT
			SIF FLAG:DEBUG
				PRINTFORM {LOCAL, 5}
			SETCOLOR 0xEEEE00
			PRINTFORM [　Ｒ마카]
			RESETCOLOR
			PRINTFORM %ITEMNAME:LOCAL%
			CALL CHECK_ITEMMAX, LOCAL
			IF RESULT > 0
				SETCOLOR 0x888888
				PRINTFORM 　(가지지 못하고 폐기했습니다)
				RESETCOLOR
			ELSE
				CALL GET_ITEM, LOCAL, 1
			ENDIF
			PRINTFORML
			RETURN 0
		ENDIF
		;실패した場合は次に回す
	ENDIF

	;２０％　……　Ｎ……　{LOCAL}마카以内で現在交換가능な아이템
	IF  LOCAL < 5000000
		CALL GACHA_SEARCH_ITEM, FLAG:가챠레벨설정 * 2 /2, 1
		IF RESULT > 0
			LOCAL = RESULT
			SIF FLAG:DEBUG
				PRINTFORM {LOCAL, 5}
			PRINTFORM [　Ｎ마카]
			PRINTFORM %ITEMNAME:LOCAL%
			CALL CHECK_ITEMMAX, LOCAL
			IF RESULT > 0
				SETCOLOR 0x888888
				PRINTFORM 　(가지지 못하고 폐기했습니다)
				RESETCOLOR
			ELSE
				CALL GET_ITEM, LOCAL, 1
			ENDIF
			PRINTFORML
			RETURN 0
		ENDIF
		;실패した場合は次に回す
	ENDIF

	;　５％　……ＳＲ……　{LOCAL*500}￥以内で現在구입가능な아이템
	IF  LOCAL < 5500000
		CALL GACHA_SEARCH_ITEM, FLAG:가챠레벨설정 * 2 * 500 /2, 0
		IF RESULT > 0
			LOCAL = RESULT
			SIF FLAG:DEBUG
				PRINTFORM {LOCAL, 5}
			SETCOLOR 0x00EEEE
			PRINTFORM [ＳＲ￥　]
			RESETCOLOR
			PRINTFORM %ITEMNAME:LOCAL%
			CALL CHECK_ITEMMAX, LOCAL
			IF RESULT > 0
				SETCOLOR 0x888888
				PRINTFORM 　(가지지 못하고 폐기했습니다)
				RESETCOLOR
			ELSE
				CALL GET_ITEM, LOCAL, 1
			ENDIF
			PRINTFORML
			RETURN 0
		ENDIF
		;실패した場合は次に回す
	ENDIF

	;１０％　……　Ｒ……　{LOCAL*200}￥以内で現在구입가능な아이템
	IF  LOCAL < 5500000
		CALL GACHA_SEARCH_ITEM, FLAG:가챠레벨설정 * 2 *200 /2, 0
		IF RESULT > 0
			LOCAL = RESULT
			SIF FLAG:DEBUG
				PRINTFORM {LOCAL, 5}
			SETCOLOR 0xEEEE00
			PRINTFORM [　Ｒ￥　]
			RESETCOLOR
			PRINTFORM %ITEMNAME:LOCAL%
			CALL CHECK_ITEMMAX, LOCAL
			IF RESULT > 0
				SETCOLOR 0x888888
				PRINTFORM 　(가지지 못하고 폐기했습니다)
				RESETCOLOR
			ELSE
				CALL GET_ITEM, LOCAL, 1
			ENDIF
			PRINTFORML
			RETURN 0
		ENDIF
		;실패した場合は次に回す
	ENDIF

	;３５％　……　Ｎ……　{LOCAL*50}￥以内で現在구입가능な아이템
	CALL GACHA_SEARCH_ITEM, FLAG:가챠레벨설정 * 2 *50 /2, 0
	IF RESULT > 0
		LOCAL = RESULT
			SIF FLAG:DEBUG
				PRINTFORM {RESULT, 4}
		PRINTFORM [　Ｎ￥　]
		PRINTFORM %ITEMNAME:LOCAL%
		CALL CHECK_ITEMMAX, LOCAL
		IF RESULT > 0
			SETCOLOR 0x888888
			PRINTFORM 　(가지지 못하고 폐기했습니다)
			RESETCOLOR
		ELSE
			CALL GET_ITEM, LOCAL, 1
		ENDIF
		PRINTFORML
		RETURN 0
	ENDIF
	;실패した場合は次に回す

	PRINTFORML  에러 발생
RETURN


;=================================================
;대상악마サーチ(ARG:1=下限레벨、ARG:2=上限레벨、ARG:3=ピックアップ内容)
;帰す値は仲魔になった악마の番地または아이템NO、실패したら-1帰す
;ピックアップ内容は、XYZZZZZZ　Y=1でZ番の소질、Y=5でアライメント(1:Light、2:Law、3:C、4:Dark)を見る
;ただしXが1の時はYの조건がそれではない時を見る
;=================================================
@GACHA_SEARCH_DEVIL, ARG:1=1, ARG:2=5, ARG:3 = -1

;LOCAL:1番が選出された악마のNO
LOCAL:1 = -1
;LOCAL:2番が候補になった악마の総数
LOCAL:2 = 0
;LOCAL:3番が候補になった악마のRAND抽選用
LOCAL:3 = 0
;LOCAL:4番はピックアップの真偽체크
LOCAL:4 = 0
;LOCAL:6はFORループ用
LOCAL:6 = 0

FOR LOCAL:6, 0, 1800
	SIF !EXISTCSV(LOCAL:6, 0)
		CONTINUE
	IF ARG:3 > 0
		SIF !CSVTALENT(LOCAL:6, (ARG:3 % 1000000), 0)
			CONTINUE
	ENDIF
	SIF CSVCFLAG(LOCAL:6,(GETNUM(CFLAG,"전서소환불가")),0) == 1
		CONTINUE
	SIF CSVCFLAG(LOCAL:6,(GETNUM(CFLAG,"합체조건있음")),0) == 1
		CONTINUE
	SIF CSVCFLAG(LOCAL:6,(GETNUM(CFLAG,"특수합체만")),0) == 1
		CONTINUE
	SIF CSVABL(LOCAL:6, GETNUM(ABL, "종족")) <= 0
		CONTINUE
	SIF CSVABL(LOCAL:6, GETNUM(ABL, "종족")) >= 45
		CONTINUE
	SIF CSVBASE(LOCAL:6,(GETNUM(BASE, "LV")),0) < ARG:1
		CONTINUE
	SIF CSVBASE(LOCAL:6,(GETNUM(BASE, "LV")),0) > ARG:2
		CONTINUE

	;こっからピックアップ検出
	IF ARG:3 > 0
		;LOCAL:4が0なら真の時、1なら偽の時スルー出来る。
		LOCAL:4 = ARG:3 / 10000000
		;1は소질
		IF (ARG:3 % 10000000) / 1000000 == 1
			SIF CSVTALENT(LOCAL:6, (ARG:3 % 1000000) , 0) && LOCAL:4 == 0
				CONTINUE
			SIF !CSVTALENT(LOCAL:6, (ARG:3 % 1000000) , 0) && LOCAL:4 == 1
				CONTINUE

		;5はアライメント
		ELSEIF (ARG:3 % 10000000) / 1000000 == 5
			;余り1はライト
			SIF (ARG:3 % 1000000) == 1 && CSVABL(LOCAL:6, GETNUM(ABL, "속성LD")) == 1 && LOCAL:4 == 0
				CONTINUE
			SIF (ARG:3 % 1000000) == 1 && !(CSVABL(LOCAL:6, GETNUM(ABL, "속성LD")) == 1) && LOCAL:4 == 1
				CONTINUE
			;余り2はロウ
			SIF (ARG:3 % 1000000) == 2 && CSVABL(LOCAL:6, GETNUM(ABL, "속성LC")) == 3 && LOCAL:4 == 0
				CONTINUE
			SIF (ARG:3 % 1000000) == 2 && !(CSVABL(LOCAL:6, GETNUM(ABL, "속성LC")) == 3) && LOCAL:4 == 1
				CONTINUE
			;余り3はカオス
			SIF (ARG:3 % 1000000) == 3 && CSVABL(LOCAL:6, GETNUM(ABL, "속성LC")) == 3 && LOCAL:4 == 0
				CONTINUE
			SIF (ARG:3 % 1000000) == 3 && !(CSVABL(LOCAL:6, GETNUM(ABL, "속성LC")) == 3) && LOCAL:4 == 1
				CONTINUE
			;余り4はダーク
			SIF (ARG:3 % 1000000) == 4 && CSVABL(LOCAL:6, GETNUM(ABL, "속성LD")) == 1 && LOCAL:4 == 0
				CONTINUE
			SIF (ARG:3 % 1000000) == 4 && !(CSVABL(LOCAL:6, GETNUM(ABL, "속성LD")) == 1) && LOCAL:4 == 1
				CONTINUE
		ENDIF

	ENDIF

	LOCAL:2++
	;ここまで通ったなら抽選チャンス
	IF LOCAL:1 < 0
		;これが初めてならそのまま입력
		LOCAL:1 = LOCAL:6
	ELSE
		LOCAL:3 = RAND:(LOCAL:2 * 100)
		SIF LOCAL:3 >= 100
			CONTINUE
		LOCAL:1 = LOCAL:6
	ENDIF
NEXT

;ガチャ실패を返す
SIF LOCAL:1 <= 0
	RETURN -1

;ここまで来たら성공なのでキャラ번호を返す
RETURN LOCAL:1

;=================================================
;대상아이템サーチ(ARG:1=金額上限、ARG:3=マニーか마카か)
;帰す値は아이템NO、실패したら-1帰す
;ARG:3は0で￥、1で마카
;ただしXが1の時はYの조건がそれではない時を見る
;=================================================
@GACHA_SEARCH_ITEM, ARG:1=25, ARG:3 = 0


;LOCAL:1番が選出された아이템のNO
LOCAL:1 = -1
;LOCAL:2番が選出された総数
LOCAL:2 = 0
;LOCAL:3番が候補になった아이템のRAND抽選用
LOCAL:3 = 0
;LOCAL:4番は0:￥か1:마카か。
LOCAL:4 = ARG:3
;LOCAL:5番はARG:1の記録
LOCAL:5 = ARG:1
;SIF LOCAL:5 < 15
;	LOCAL:5 = 15
;SIF LOCAL:5 < 75 && LOCAL:4 == 0
;	LOCAL:5 = 75
;LOCAL:6はFORループ用
LOCAL:6 = 0

;３分の１で장비、３分の２で消耗品。
LOCAL:3 = RAND:9999
IF (LOCAL:3 % 3) == 0
	FOR LOCAL:6,2000,VARSIZE("ITEM")
		SIF ITEMPRICE:(LOCAL:6) < 10 || ITEMPRICE:(LOCAL:6) > LOCAL:5
			CONTINUE
		RESULT = 0
		IF LOCAL:4 == 0
			TRYCALLFORM 구입조건_{LOCAL:6}
			SIF RESULT == 0
				CONTINUE
		ELSE
			TRYCALLFORM EQUIP_EXCHANGE_{LOCAL:6}
			SIF RESULT == 0
				CONTINUE
		ENDIF
		RESULT = 12
		TRYCALLFORM 장비부위_{(LOCAL:6)}
		SIF RESULT < 0 && RESULT > 7
			CONTINUE

		LOCAL:2++
		;ここまで通ったなら抽選チャンス
		IF LOCAL:1 < 0
			;これが初めてならそのまま입력
			LOCAL:1 = LOCAL:6
		ELSE
			LOCAL:3 = RAND:(LOCAL:2 * 100)
			SIF LOCAL:3 >= 100
				CONTINUE
			LOCAL:1 = LOCAL:6
		ENDIF
	NEXT
ELSE
	FOR LOCAL:6,1000,2000
		SIF ITEMPRICE:(LOCAL:6) < 10 || ITEMPRICE:(LOCAL:6) > LOCAL:5
			CONTINUE
		RESULT = 0
		IF LOCAL:4 == 0
			TRYCALLFORM 도구구입조건_{LOCAL:6 +2000}
			SIF RESULT == 0
				CONTINUE
		ELSE
			TRYCALLFORM ITEM_EXCHANGE_{LOCAL:6 +2000}
			SIF RESULT == 0	
				CONTINUE
		ENDIF

		LOCAL:2++
		;ここまで通ったなら抽選チャンス
		IF LOCAL:1 < 0
			;これが初めてならそのまま입력
			LOCAL:1 = LOCAL:6
		ELSE
			LOCAL:3 = RAND:(LOCAL:2 * 100)
			SIF LOCAL:3 >= 100
				CONTINUE
			LOCAL:1 = LOCAL:6
		ENDIF
	NEXT
ENDIF

;ガチャ실패を返す
SIF LOCAL:1 <= 0
	RETURN -1

;ここまで来たら성공なので아이템번호を返す
RETURN LOCAL:1
