;KR판 고유요소 추정, 기본 DEBUG 메뉴

@SHOPCOMABLE_997
RESULTS = 데이터 수정
RETURN 1

@SHOP_COM_997

PRINTL [1] 소지금/정크포인트 변경   
PRINTL [2] 소지 마카 변경   
PRINTL [3] 전원 체력·기력 회복  
PRINTL [4] 캐릭터 수정  
PRINTL [5] 캐릭터 추가
PRINTL [6] 일자 변경 처리
PRINTL [7] 밤/낮 변경
PRINTL [8] FLAG 조작  
PRINTL [9] ITEM 조작
PRINTL [10] 리셋 조작
PRINTL [100] 취소   

INPUT
IF RESULT == 1
	PRINTLC  [ 0] 소지금 변경
	PRINTLC  [ 1] 정크포인트 변경
	PRINTLC  [100] 취소
	
	INPUT
		IF RESULT == 0
			PRINTFORML 현재 소지금:{MONEY}Yen
			PRINTL 원하는 금액을 입력해 주세요(마이너스 값이라면 변경하지 않음)
			INPUT
			SIF RESULT < 0
			RETURN 0
			MONEY = RESULT
			PRINTFORMW 현재 소지금을 {MONEY}\으로 변경했습니다
			RETURN 0
		ELSEIF RESULT == 1
			PRINTFORML 현재 소지 JP:{MONEY:4}JP
			PRINTL 원하는 정크포인트를 입력해 주세요(마이너스 값이라면 변경하지 않음)
			INPUT
			SIF RESULT < 0
			RETURN 0
			MONEY:4 = RESULT
			PRINTFORMW 현재 소지 JP를 {MONEY:4}JP로 변경했습니다
			RETURN 0
		ELSEIF RESULT == 100
			RETURN 0
		ENDIF
ELSEIF RESULT == 2
	PRINTFORML 현재 소지 마카:{MONEY:1}마카
	PRINTL 원하는 마카를 입력해 주세요(마이너스 값이라면 변경하지 않음)
	INPUT
	SIF RESULT < 0
	RETURN 0
	MONEY:1 = RESULT
	PRINTFORMW 현재 소지 마카를 {MONEY:1}마카로 변경했습니다
	RETURN 0
ELSEIF RESULT == 3
	REPEAT CHARANUM
	BASE:COUNT:0 = MAXBASE:COUNT:0
	BASE:COUNT:1 = MAXBASE:COUNT:1
	CFLAG:COUNT:10 = 0
	REND
	PRINTW 전원의 체력·기력이 회복되었습니다
	RETURN 0
ELSEIF RESULT == 4
	IF TARGET == -1
	CALL SELECT_CHAR
	ENDIF
	IF TARGET  >= 0
	$PLACE_CHAR
	PRINTFORML  현재의 편집 대상：%CALLNAME:TARGET%
	PRINTLC  [ 0] 수동 CFLAG 편집   
	PRINTLC  [ 1] ABL 편집   
	PRINTLC  [ 2] EXP 편집   
	PRINTLC  [ 3] MARK 편집   
	PRINTLC  [ 4] 수동 TALENT 편집
	PRINTlC  [ 5] BASE 편집   
	PRINTlC  [ 6] JUEL 편집   
	PRINTLC  [ 8] TALENT 편집
	PRINTlC  [ 9] ABL、EXP、TALENT、JUEL 초기화   
	PRINTLC  [10] MAG 기본능력강화수치 수정
	PRINTLC  [11] 편집 대상 변경
	PRINTLC  [100] 취소
	PRINTL
	ENDIF
		$INPUT_LOOP
		INPUT
			IF RESULT >= 0 && RESULT <= 6 && TARGET >= 0
			ELSEIF RESULT == 8  && TARGET >= 0
				CALL EDIT_TALENT
				GOTO PLACE_CHAR
			ELSEIF RESULT == 9 && TARGET >= 0
				VARSET TALENT
				VARSET EXP
				VARSET ABL
				VARSET JUEL
				PRINTW 초기화 완료
				GOTO PLACE_CHAR
			ELSEIF RESULT == 10 && TARGET >= 0
				CALL DIMINISH_BASE,TARGET
				GOTO PLACE_CHAR
			ELSEIF RESULT == 11
				CALL SELECT_CHAR
				GOTO PLACE_CHAR
			ELSEIF RESULT == 100
				RESTART
			ELSE
				GOTO INPUT_LOOP
			ENDIF
			A = RESULT
			CALL DEBUG_FLAG
			
			GOTO PLACE_CHAR

ELSEIF RESULT == 5
	CALL SHOP_COM_192
ELSEIF RESULT == 6
	CALL EDIT_DATE
ELSEIF RESULT == 7
	CALL EDIT_TIME
ELSEIF RESULT == 8
	CALL EDIT_FLAG
ELSEIF RESULT == 9
	CALL EDIT_ITEM
ELSEIF RESULT == 10
	CALL SHOP_SOFTRESET
ELSEIF RESULT == 100
	RETURN 0
ENDIF

@SELECT_CHAR
;표시시키는 캐릭터를 추출(LOCAL:2에 인원수)
DRAWLINE
VARSET LOCAL, 0
LOCAL:1 = CHARANUM / 20

$PRINT_LIST
DRAWLINE
SIF TARGET >= 0
	PRINTFORML 현재의 선택 캐릭터 :%CALLNAME:TARGET%
PRINTFORML 누구를 대상으로 합니까?  ＜page. {LOCAL+1}＞
DRAWLINE
REPEAT CHARANUM
	SIF COUNT >= LOCAL * 20 && COUNT < (LOCAL+1) * 20
		PRINTFORML [{COUNT}]%NAME:COUNT%
REND
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ?  %" " * 16% # [1001]이전페이지\@
PRINTLC [1000]돌아온다
PRINTFORMLC \@(LOCAL >= LOCAL:1) ?  %" " * 16% # [1009]다음페이지\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:1
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF
TARGET = RESULT
PRINTFORML %조사처리(CALLNAME:TARGET,"를")% 대상으로 선택했습니다
RETURN 0
				

@DEBUG_FLAG
$FLAG_CONSIS
PRINTL 
S = 0
IF A == 0
	LOCALS:1 = CFLAG
	L = 9999
ELSEIF A == 1
	LOCALS:1 = ABL
		REPEAT 300
		LOCALS = %ABLNAME:COUNT%
		STRLENS LOCALS
		IF RESULT && (COUNT < 40 || COUNT >= 50)
			S += 1
			PRINTFORM   [{COUNT,2}] %LOCALS%:LV{ABL:COUNT,2}
			SIF S%4 == 0
				PRINTL 
		ENDIF
	REND
	SIF S%4
		PRINTL 
	L = 281
ELSEIF A == 2
	LOCALS:1 = EXP
	REPEAT 99
		LOCALS = %EXPNAME:COUNT%
		STRLENS LOCALS
		IF RESULT
			S += 1
			PRINTFORM  [{COUNT,2}] %LOCALS%:{EXP:COUNT,4}
			SIF S%3 == 0
				PRINTL 
		ENDIF
	REND
	SIF S%3
		PRINTL 
	L = 99
ELSEIF A == 3
	LOCALS:1 = MARK
	REPEAT 20
		LOCALS = %MARKNAME:COUNT%
		STRLENS LOCALS
		IF RESULT
			S += 1
			PRINTFORM   [{COUNT,2}] %LOCALS%:LV{MARK:COUNT}
			SIF S%3 == 0
				PRINTL 
		ENDIF
	REND
	SIF S%3
		PRINTL 
	L = 99
ELSEIF A == 4
	LOCALS:1 = TALENT
	REPEAT 600
		SIF TALENT:COUNT == 0
			CONTINUE

		LOCALS = %TALENTNAME:COUNT%
		STRLENS LOCALS
		IF RESULT
			S += 1
			PRINTFORM  [{COUNT,3}] %LOCALS%
			IF TALENT:COUNT >= 2
				PRINTFORM :{TALENT:COUNT,3}
			ELSE
				PRINTFORM     
			ENDIF
			SIF S%3 == 0
				PRINTL 
		ENDIF
	REND
	SIF S%3
		PRINTL 
	L = 999
ELSEIF A == 5
	LOCALS:1 = BASE
	REPEAT 100
		LOCALS = %BASENAME:COUNT%
		STRLENS LOCALS
		IF RESULT
			S += 1
			PRINTFORM  [{COUNT,2}] %LOCALS%:{BASE:COUNT,5}
			SIF S%3 == 0
				PRINTL 
		ENDIF
	REND
	SIF S%3
		PRINTL 
	L = 101
ELSEIF A == 6
	LOCALS:1 = JUEL
	REPEAT 101
		SIF COUNT == 3 || (COUNT >= 11 && COUNT <= 13)
			CONTINUE
		LOCALS = %PALAMNAME:COUNT%
		STRLENS LOCALS
		IF RESULT
			S += 1
			PRINTFORM   [{COUNT,3}] %LOCALS%의 구슬:{JUEL:COUNT,8}
			SIF S%3 == 0
				PRINTL 
		ELSEIF COUNT >= 20 && COUNT <= 24
			S += 1
			PRINTFORM   [{COUNT,3}] 
			SIF COUNT == 20
				PRINT 쾌Ｃ
			SIF COUNT == 21
				PRINT 쾌Ｖ
			SIF COUNT == 22
				PRINT 쾌Ａ
			SIF COUNT == 23
				PRINT 쾌Ｂ
			SIF COUNT == 24
				PRINT 쾌Ｍ
			PRINTFORM 절정:{JUEL:COUNT,8}
			SIF S%3 == 0
				PRINTL 
		ENDIF
	REND
	SIF S%3
		PRINTL 
	L = 101
ELSEIF A == 9
	LOCALS:1 = FLAG
	L = 9999
ENDIF
PRINTFORML [{L}] 돌아간다
PRINTFORML 변경하고 싶은 %LOCALS:1%입력(0～{L - 1}). {L}이상의 값을 입력하면 전의 화면으로 돌아감.
SIF A == 5
	PRINTFORML 또한 체력(BASE:0)과 기력(BASE:1)은 최대치도 함께 변경됨.

$INPUT_LOOP_1
INPUT

SIF RESULT < 0
	GOTO INPUT_LOOP_1
SIF RESULT >= L
	RETURN 0

B = RESULT

PRINTFORM %LOCALS:1%:{B} = 
LOCALS = 
IF A == 0
	PRINTFORM {CFLAG:B}
	PRINTC : 
	PRINTFORM %CFLAGNAME:RESULT%
ELSEIF A == 1
	PRINTFORM {ABL:B}
	LOCALS = %ABLNAME:B%
ELSEIF A == 2
	PRINTFORM {EXP:B}
	LOCALS = %EXPNAME:B%
ELSEIF A == 3
	PRINTFORM {MARK:B}
	LOCALS = %MARKNAME:B%
ELSEIF A == 4
	PRINTFORM {TALENT:B}
	LOCALS = %TALENTNAME:B%
ELSEIF A == 5
	PRINTFORM {BASE:B}
	LOCALS = %BASENAME:B%
ELSEIF A == 6
	PRINTFORM {JUEL:B}
	LOCALS = %PALAMNAME:B%
ELSEIF A == 9
	PRINTFORM {FLAG:B}
ENDIF
SIF STRLENS(LOCALS)
	PRINTFORM  (%LOCALS%)
PRINTFORML 의 변경
SIF A == 0
	PRINTL CFLAG는 음수의 값도 입력가능.
$INPUT_LOOP_2
INPUT

SIF RESULT < -10000 && A
	GOTO INPUT_LOOP_2

IF A == 0 
	CFLAG:B = RESULT
ELSEIF A == 1
	ABL:B = RESULT
ELSEIF A == 2
	EXP:B = RESULT
ELSEIF A == 3
	MARK:B = RESULT
ELSEIF A == 4
	TALENT:B = RESULT
ELSEIF A == 5
	SIF B == 0 || B == 1
		MAXBASE:B = RESULT
	BASE:B = RESULT
ELSEIF A == 6
	JUEL:B = RESULT
ELSEIF A == 9
	FLAG:B = RESULT
ENDIF
PRINTFORMW %LOCALS:1%:{B}을(를) {RESULT}(으)로 변경했습니다
GOTO FLAG_CONSIS

@EDIT_DATE
DRAWLINE
PRINTFORML 현재:{DAY}일\@(TIME == 0) ?  (낮) # (밤)\@
PRINTL 몇 일로 할지 입력하시오(0 이하라면 변경하지 않음)

INPUT
SIF RESULT < 1
	RETURN 0
DAY = RESULT
PRINTFORMW 신비하게도 오늘은 {DAY}일인 듯한 느낌이 드는 것 같기도…
CALL CALENDAR_CALC, DAY

@CALENDAR_CALC,ARG
VARSET LOCAL,0
;曜日を先に求める
DAY:7 = 1 + ARG % 7

;以下年月日を計算
LOCAL = ARG
DAY:4 = 1
LOCAL:12 = 0
;4000000年(1460970000日）ごとにまず計算
IF LOCAL > 1460970000
	LOCAL:12 += LOCAL / 1460970000
	LOCAL = LOCAL % 1460970000
ENDIF
DAY:4 += 4000000 * LOCAL:12
LOCAL:11 = 0
;次に40000年(14609700日）ごとに計算
IF LOCAL > 14609700
	LOCAL:11 += LOCAL / 14609700
	LOCAL = LOCAL % 14609700
ENDIF
DAY:4 += 40000 * LOCAL:11
LOCAL:10 = 0
;400年（146097日）ごとに繰り返すのがグレゴリオ暦
IF LOCAL > 146097
	LOCAL:10 += LOCAL / 146097
	LOCAL = LOCAL % 146097
ENDIF
DAY:4 += 400 * LOCAL:10
LOCAL:4 = 0
LOCAL:8 = 0
IF LOCAL % 146097 == 0 && DAY < 146098
	DAY:4 += 399
	DAY:5 = 12
	DAY:6 = 31
	RETURN 1
ENDIF
$NEXT_YEAR
IF LOCAL > (365 + LOCAL:8) 
	LOCAL:4 ++
	LOCAL -= (LOCAL:8) ? 366 # 365
	LOCAL:8 = (LOCAL:4 % 100 != 0 && LOCAL:4 % 4 == 0) ? 1 # 0
	GOTO NEXT_YEAR
ENDIF
DAY:4 += LOCAL:4
LOCAL:5 = 1
LOCAL:6 = 0
IF LOCAL <= 0
	DAY:4 --
	DAY:5 = 12
	DAY:6 = 31
	CALL CALENDAR_LEAP
	RETURN 1
ENDIF
CALL CALENDAR_LEAP
$NEXT_DAY
LOCAL --
LOCAL:6 ++
IF LOCAL > 0
	IF LOCAL:5 == 1 || LOCAL:5 == 3 || LOCAL:5 == 5 || LOCAL:5 == 7 || LOCAL:5 == 8 || LOCAL:5 == 10 || LOCAL:5 == 12
		IF LOCAL:6 > 30
			LOCAL:5 ++
			LOCAL:6 = 0
		ENDIF
	ELSEIF LOCAL:5 == 4 || LOCAL:5 == 6 || LOCAL:5 == 9 || LOCAL:5 == 11
		IF LOCAL:6 > 29
			LOCAL:5 ++
			LOCAL:6 = 0
		ENDIF
	ELSEIF DAY:8
		IF LOCAL:6 > 28
			LOCAL:5 ++
			LOCAL:6 = 0
		ENDIF
	ELSE
		IF LOCAL:6 > 27
			LOCAL:5 ++
			LOCAL:6 = 0
		ENDIF
	ENDIF
	GOTO NEXT_DAY
ENDIF
DAY:5 = LOCAL:5
DAY:6 = LOCAL:6

@CALENDAR_LEAP
DAY:8 = ((DAY:4 % 400 == 0) && (DAY:4 % 100 != 0) || (DAY:4 % 400 == 0)) ? 1 # 0

@DEFINE_FLAG, ARG
IF STRLENS(FLAGNAME:ARG) > 0
	PRINTS FLAGNAME:ARG
ELSE
	PRINT ――
ENDIF

@EDIT_FLAG
VARSET LOCAL, 0
LOCAL:1 = 199
$PRINT_LIST
DRAWLINE
PRINTFORML 현재 할당되어 있는 FLAG
PRINTFORML 어떤 것을 변경하겠습니까?  ＜page. {LOCAL+1}＞
DRAWLINE
REPEAT 10000
	SIF COUNT < LOCAL * 50 || COUNT >= (LOCAL + 1) * 50
		CONTINUE
	PRINTFORMLC [{COUNT, 4}] FLAG:{COUNT, 4} = {FLAG:COUNT}
REND
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ?  %" " * 17% # [10001]이전페이지\@
PRINTLC [10000]돌아온다
PRINTFORMLC \@(LOCAL >= LOCAL:1) ?  %" " * 17% # [10009]다음페이지\@
PRINTL 
PRINTFORMLC \@(LOCAL/10 <= 0) ?  %" " * 17% # [10011]10 페이지 앞으로\@
PRINTFORMLC %" " * 17%
PRINTFORMLC \@(LOCAL/10 >= LOCAL:1/10) ?  %" " * 17% # [10019]10 페이지 뒤로\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 10000
	RETURN 0
ELSEIF RESULT == 10001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 10009 && LOCAL < LOCAL:1
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT == 10011 && LOCAL/10 > 0
	LOCAL -= 10
	GOTO PRINT_LIST
ELSEIF RESULT == 10019 && LOCAL/10 < LOCAL:1/10
	LOCAL += 10
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 10000
	GOTO INPUT_LOOP
ENDIF
LOCAL:2 = RESULT
DRAWLINE
PRINTFORM FLAG:{LOCAL:2}(
CALL DEFINE_FLAG, LOCAL:2
PRINTFORML )의 변경(현재{FLAG:(LOCAL:2)})
PRINTFORML 수치를 입력(범위값 이외의 수치로 캔슬)
INPUT
SIF RESULT < 0
	RESTART
FLAG:(LOCAL:2) = RESULT
GOTO PRINT_LIST

@EDIT_TIME
DRAWLINE
IF TIME == 0
TIME = 1
PRINTFORMW 시간대를 {DAY}일 낮으로 변경했습니다
ELSEIF TIME == 1
TIME = 2
PRINTFORMW 시간대를 {DAY}일 저녁으로 변경했습니다
ELSEIF TIME == 2
TIME = 3
PRINTFORMW 시간대를 {DAY}일 밤으로 변경했습니다
ELSEIF TIME == 3
TIME = 0
PRINTFORMW 시간대를 {DAY}일 아침으로 변경했습니다
ENDIF

@EDIT_TALENT
LOCAL = 0
LOCAL:1 = 4
CALLF CLEAR_LIST
;표시시키는 소질을 추출
REPEAT 300
;존재하지 않는 소질은 배제
STRLENS TALENTNAME:COUNT
SIF RESULT == 0
	CONTINUE

CALLF SET_LIST, COUNT, 1
REND

$PRINT_LIST
DRAWLINE
PRINTFORML 어느 소질을 변경합니까?  ＜page. {LOCAL+1}＞
DRAWLINE
REPEAT 60
	LOCAL:2 = COUNT + LOCAL * 60
	SIF GET_LIST(LOCAL:2) == 0
		CONTINUE
	IF TALENT:(LOCAL:2) == 0
		PRINTFORMLC [{LOCAL:2, 3}]×【%TALENTNAME:(LOCAL:2)%】
	ELSEIF TALENT:(LOCAL:2) == 1
		PRINTFORMLC [{LOCAL:2, 3}]◎【%TALENTNAME:(LOCAL:2)%】
	ELSEIF TALENT:(LOCAL:2) > 1
		PRINTFORMLC [{LOCAL:2, 3}]○【%TALENTNAME:(LOCAL:2)%】
	ENDIF
REND
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ?  %" " * 16% # [1001]이전페이지\@
PRINTLC [1000]돌아온다
PRINTFORMLC \@(LOCAL >= LOCAL:1) ?  %" " * 16% # [1009]다음페이지\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:1
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 1000
	GOTO INPUT_LOOP
ELSEIF GET_LIST(RESULT) == 0
	GOTO INPUT_LOOP
ENDIF

SIF STRLENS(TALENTNAME:RESULT) == 0
	GOTO INPUT_LOOP
DRAWLINE
IF (RESULT == 0 || RESULT == 213) && TALENT:RESULT == 1
	PRINTFORMW \@(RESULT == 0) ?  재생 처녀로 했던 # 마음의 눈을 감았습니다\@
	TALENT:RESULT = 2
ELSEIF (RESULT == 122 || RESULT == 124 || RESULT == 220 || RESULT == 833) && TALENT:RESULT == 0
	CALL EDIT_ADVANCED_TALENT, RESULT
ELSE
	PRINTFORMW [%TALENTNAME:RESULT%]를 \@(TALENT:RESULT) ?  삭제 # 부가\@했습니다
	TALENT:RESULT = (TALENT:RESULT) ?  0 # 1
ENDIF
GOTO PRINT_LIST

@EDIT_ADVANCED_TALENT, ARG
PRINTFORML %TALENTNAME:ARG%의 값을 결정해 주세요[0-999] (현재{TALENT:ARG})
PRINTL [1000]돌아온다
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 1000
	GOTO INPUT_LOOP
ENDIF
PRINTFORMW %TALENTNAME:ARG%의 값을 {RESULT}로 했습니다
TALENT:ARG = RESULT

@HELP_LIST, ARG
; 생선이 고안중인 디버그 헬프리스트
; ARG 1 == CFLAG 설정 도움
; ARG 2 == TALNET 설정 도움

@SET_LIST(ARG, ARG:1)
#FUNCTION
RETURNF LIST_DATA("SET", ARG, ARG:1)

@CLEAR_LIST
#FUNCTION
RETURNF LIST_DATA("CLEAR")

@LIST_DATA(ARGS, ARG, ARG:1)
#FUNCTION
IF ARGS == "SET"
	LOCAL:ARG = ARG:1
	RETURNF ARG:1
ELSEIF ARGS == "GET"
	RETURNF LOCAL:ARG
ELSEIF ARGS == "CLEAR"
	VARSET LOCAL, 0
	RETURNF -1
ELSE
	RETURNF -1
ENDIF

@GET_LIST(ARG)
#FUNCTION
RETURNF LIST_DATA("GET", ARG)

@EDIT_ITEM
PRINTFORML 변경하고 싶은 아이템 입력. 9999 이상의 값을 입력하면 전의 화면으로 돌아감.
PRINTFORML 참고로 스킬 카드는 1만 번대
PRINTFORML [99999] 돌아온다
$INPUT_LOOP
INPUT
SIF RESULT < 0
	GOTO INPUT_LOOP
SIF RESULT >= 99999
	RETURN 0
CALL EDIT_ITEMNUM, RESULT

@EDIT_ITEMNUM, ARG
PRINTFORML %ITEMNAME:ARG%의 값을 결정해주세요[0-999] (현재{ITEM:ARG})
PRINTFORML [99999] 돌아온다
$INPUT_LOOP
INPUT
SIF RESULT < 0
	GOTO INPUT_LOOP
SIF RESULT >= 99999
	RETURN 0
PRINTFORMW %ITEMNAME:ARG%의 값을 {RESULT}로 했습니다
ITEM:ARG = RESULT

@DIMINISH_BASE,TARGET
#DIM ＣＰ보정
PRINTL □ 기본능력약화

;能力ＵＰに필요な値を計算
LOCAL:1 = CFLAG:TARGET:능력강화횟수
;/3 + 1
SELECTCASE FLAG:전투난이도
	CASE 1 TO 6
		LOCAL:10 = 250
	CASE 4,5,6
		LOCAL:10 = 100
ENDSELECT
SIF TARGET == MASTER
	LOCAL:10 *= 100
LOCAL:2 = 0
ＣＰ보정 = ＭＡＧ강화ＣＰ보정(TARGET)
FOR LOCAL, 0 ,10
	LOCAL:2 += (LOCAL:1/3+1) * (LOCAL:1/3+2)
	SIF LOCAL == 0
		LOCAL:3 = LOCAL:2*LOCAL:10 * ＣＰ보정 / 1000
	SIF LOCAL == 4
		LOCAL:4 = LOCAL:2*LOCAL:10 * ＣＰ보정 / 1000
	SIF LOCAL == 9
		LOCAL:5 = LOCAL:2*LOCAL:10 * ＣＰ보정 / 1000
	LOCAL:1++
NEXT
PRINTFORML 어느 능력을 수정합니까？
PRINTL  -1   -5  -10
FOR LOCAL,1,7
	PRINTFORML [{LOCAL}] [{LOCAL+10}] [{LOCAL+20}]%GET_BASESTATUS(LOCAL),5% {MAXBASE:TARGET:(GET_BASESTATUS(LOCAL))-CFLAG:TARGET:(GET_BASESTATUS(LOCAL) + "강화횟수"),4} + {CFLAG:TARGET:(GET_BASESTATUS(LOCAL) + "강화횟수"),4}
NEXT
DRAWLINE
PRINTL [0] 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF RESULT < 7 && RESULT > 0
	IF CFLAG:TARGET:(GET_BASESTATUS(RESULT) + "강화횟수") -1 < 0
		PRINTFORMW 이 이상 강화수치를 낮출 수 없습니다.
		RESTART
	ENDIF
	PRINTFORMW %NAME:TARGET%의 %조사처리(GET_BASESTATUS(RESULT),"가")% 1 내렸다
	CFLAG:TARGET:능력강화횟수 -= 1
	CFLAG:TARGET:(GET_BASESTATUS(RESULT) + "강화횟수") -= 1
	CALL CONTROL_MAG,TARGET,+LOCAL:3
	CFLAG:TARGET:강화사용ＭＡＧ -= LOCAL:3
ELSEIF RESULT < 17 && RESULT > 10
	IF CFLAG:TARGET:(GET_BASESTATUS(RESULT-10) + "강화횟수") -5 < 0
		PRINTFORMW 이 이상 강화수치를 낮출 수 없습니다.
		RESTART
	ENDIF
	PRINTFORMW %NAME:TARGET%의 %조사처리(GET_BASESTATUS(RESULT-10),"가")% 5 내렸다
	CFLAG:TARGET:능력강화횟수 -= 5
	CFLAG:TARGET:(GET_BASESTATUS(RESULT-10) + "강화횟수") -= 5
	CALL CONTROL_MAG,TARGET,+LOCAL:4
	CFLAG:TARGET:강화사용ＭＡＧ -= LOCAL:4
ELSEIF RESULT < 27 && RESULT > 20
	IF CFLAG:TARGET:(GET_BASESTATUS(RESULT-20) + "강화횟수") -10 < 0
		PRINTFORMW 이 이상 강화수치를 낮출 수 없습니다.
		RESTART
	ENDIF
	PRINTFORMW %NAME:TARGET%의 %조사처리(GET_BASESTATUS(RESULT-20),"가")% 10 내렸다
	CFLAG:TARGET:능력강화횟수 -= 10
	CFLAG:TARGET:(GET_BASESTATUS(RESULT-20) + "강화횟수") -= 10
	CALL CONTROL_MAG,TARGET,+LOCAL:5
	CFLAG:TARGET:강화사용ＭＡＧ -= LOCAL:5
ELSE
	GOTO INPUT_LOOP
ENDIF
CALL SYNC_STATUS,TARGET
LOCAL:1 = CFLAG:TARGET:능력강화횟수/3 + 1
;IF BASE:TARGET:ＭＡＧ < LOCAL:10*LOCAL:1*(LOCAL:1+1)*(900+MAXBASE:TARGET:CP)/1000
;	PRINTW ＭＡＧが발りなくなったので終了します。
;	RETURN 0
;ENDIF
RESTART