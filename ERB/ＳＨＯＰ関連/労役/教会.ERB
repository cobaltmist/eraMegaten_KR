;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:教会.ERB
;	Facility	:教会に関する関数群です。
;
;	Licence		:ライセンスフリー
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2014/09/17		----					ネトリス
;	002		2014/12/28		----					秩混値の変動が正しく行われていなかったのを修正、など
;	003		2014/04/22								L教会の報酬に女性テンプルナイト追加
;	004		2021/04/27		JK好き					協会で多胎OP時でも妊娠するように修正
;	005		2021/09/30		JK好き					勧誘時のレイアウト調整
;	006		2022/01/07		JK好き					属性変動が機能していなかった問題を修正
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;교회
;それぞれの教会の対応したアライメント調整をする
;메시아 교회ならＬ、ガイア教会はＣ、教会はＮに寄っていく
;収入はなく維持費がかかる（維持費は高めでもいいかも）
;[[점포:선전플래그]]:ARG を 경배치 として使う
;경배치は教会で奉仕活動をすることで得られ、個々の教会で独自に管理する
;경배치を消費する事でそれぞれの教会に対応した仲魔と交換する事が出来る
;
;==========================================================
;ARRANGE_CHARA_COMMODITY_MANAGEMENT_교회
; 봉사活動個別설정
; 초기설정は祈り
;<引数>
;	ARG = 점포番号
;<戻り値>
;	なし
;========================================================
@ARRANGE_CHARA_COMMODITY_MANAGEMENT_교회,ARG
#DIM LCOUNT

P = 0
$INPUT_LOOP_2
;표시させるキャラを抽出（Q:*に표시するキャラの登録番号、LOCAL:2に人数）
VARSET Q, -1
A = 0
FOR LCOUNT, 0, CHARANUM
	SIF CFLAG:LCOUNT:노역플래그 != 10 + ARG
		CONTINUE
	Q:A = LCOUNT
	A += 1
NEXT
LOCAL:2 = A

$PRINT_LIST
DRAWLINE
PRINTFORML 이용 방법을 고르세요　＜page.{P + 1}＞
DRAWLINE
PRINTFORML 　　　　　　　　　　　　　　　　　 기도　입봉사 성봉사
FOR LCOUNT, 0, 리스트표시수()
	LOCAL = Q:(LCOUNT + P * 리스트표시수())
	SIF LOCAL < 0 || CFLAG:LOCAL:이자리에없는플래그 == 1
		CONTINUE
	CALL ARRANGE_SETCOLOR, LOCAL

	CALL ARRANGE_TARGETSIGN, LOCAL

	CALL ARRANGE_CHARALIST, LOCAL
	CALL ARRANGE_FALLTALENT, LOCAL
	CALL ARRANGE_SEX, LOCAL
	
	FOR LOCAL:2, 1, 4
		PRINTFORM 　-　
		SIF CFLAG:LOCAL:개별성적서비스 != LOCAL:2
			SETCOLOR COLOR("灰色")
		PRINTFORM \@ CFLAG:LOCAL:개별성적서비스 == LOCAL:2 ? Ｏ # Ｘ \@
		RESETCOLOR
	NEXT
	
	PRINTL 
NEXT
RESETCOLOR
DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 16% # [1007]이전페이지\@
PRINTLC [1000]돌아간다
PRINTFORMLC \@(P >= (LOCAL:2 - 1) / 리스트표시수()) ? %" " * 16% # [1009]다음페이지\@

$INPUT_LOOP_1
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1007
	IF P > 0
		P -= 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP_1
	ENDIF
ELSEIF RESULT == 1009
	LOCAL = (LOCAL:2 - 1) / 리스트표시수()
	IF P < LOCAL
		P += 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP_1
	ENDIF
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MATCH(Q,RESULT) == 0
	GOTO INPUT_LOOP_1
ENDIF

LOCAL:1 = RESULT

CFLAG:(LOCAL:1):개별성적서비스 += 1
SIF CFLAG:(LOCAL:1):개별성적서비스 > 3
	CFLAG:(LOCAL:1):개별성적서비스 = 1

GOTO INPUT_LOOP_2


;==========================================================
;@EXTEND_MANAGEMENT_교회
; 教会の규모확장処理
;<引数>
;	ARG = 점포番号
;<戻り値>
;	なし
;==========================================================
@EXTEND_MANAGEMENT_교회, ARG
PRINTFORML 현재, 이 교회의 레벨은 {점포("규모", ARG)}입니다.

LOCAL = 5000 * 점포("규모", ARG)
IF 점포("규모", ARG) >= 5
	PRINTFORMW 이 교회는 더 이상 개축할 수 없습니다.
	RETURN 0
ELSEIF MONEY < LOCAL
	PRINTFORMW 개축하려면 ￥{LOCAL}이 필요합니다. 자금이 부족합니다.
	RETURN 0
ELSE
	PRINTFORML ￥{LOCAL}으로 개축합니다. 개축하겠습니까?
ENDIF

CALL INPUT_YN, "YES", "NO"
SIF RESULT == 1
	RETURN 0
CALL 점포연산("규모", ARG,1, "+")
MONEY -= LOCAL

;==========================================================
;MANAGEMENT_교회
; 教会での活動処理
;
;<引数>
;	ARG = 점포番号
;<사용変数>
;	속성변동치   = 各活動で変動した数値の保管
;	배치인수     = 教会に配置しているキャラの人数
;	최대참배인수 = 現在の교회규모での최대참배인수、最大値は教会に改装規模による
;	참배인수     = 相手にする人数
;	교회규모     = 참배인수と속성변동치の底上げに影響する
;	경배치       = [[점포:인기]]:ARG に収める数値をまとめて保管する
;<戻り値>
;	なし
;========================================================
@MANAGEMENT_교회, ARG
#DIM 속성변동치
#DIM 배치인수
#DIM 최대참배인수
#DIM 참배인수
#DIM 교회규모
#DIM 경배치
#DIM 입봉사횟수
#DIM Ｖ봉사횟수
#DIM Ａ봉사횟수


;교회の改装레벨を保存
교회규모 = 점포("규모", ARG)
;최대참배인수の설정
SELECTCASE 교회규모
	CASE 1
		최대참배인수 = 5
	CASE 2
		최대참배인수 = 10
	CASE 3
		최대참배인수 = 20
	CASE 4
		최대참배인수 = 40
	CASE 5
		최대참배인수 = 80
ENDSELECT

;配置されているキャラがいるか체크
LOCAL:1 = 0
FOR LOCAL, 0, CHARANUM
	;配置されていないキャラを弾く
	SIF CFLAG:LOCAL:노역플래그 != 10+ARG
		CONTINUE
	배치인수 += 1
NEXT
;誰も配置されていない場合は戻る
SIF 배치인수 == 0
	RETURN

;活動処理
;LOCAL = キャラ番号
;LOCAL:1 = 多목적に사용
FOR LOCAL, 0, CHARANUM
	;配置されていないキャラを弾く
	SIF CFLAG:LOCAL:노역플래그 != 10+ARG
		CONTINUE
	;초기化
	입봉사횟수 = 0
	Ｖ봉사횟수 = 0
	Ａ봉사횟수 = 0

	;봉사別処理
	SELECTCASE CFLAG:LOCAL:개별성적서비스
		;祈り
		CASE 1
			PRINTFORMW %조사처리(CALLNAME:LOCAL,"가")% 기도를 바치고 있다
			;祈りでの変化は0～1(MAX0～4)
			속성변동치 = RAND:(2+교회규모/2)
			경배치 += 1
			;이벤트구상
			T = TARGET
			TARGET = LOCAL
			TFLAG:13 = 65
			CALL KOJO_MESSAGE_EVENT
			TARGET = T
		;입봉사
		CASE 2
			;봉사する人数を決める
			참배인수 = RAND:최대참배인수
			;참배인수が0の場合はお祈りをする
			IF 참배인수 == 0
				PRINTFORMW 참배자가 없었기 때문에, %조사처리(CALLNAME:LOCAL,"는")% 하루종일 기도하였다
				;祈りでの変化は0～1(MAX0～4)
				속성변동치 = RAND:(2+교회규모/2)
				경배치 += 1
				;이벤트구상
				T = TARGET
				TARGET = LOCAL
				TFLAG:13 = 65
				CALL KOJO_MESSAGE_EVENT
				TARGET = T
				GOTO SKIP_1
			ENDIF
			
			;교회の종류によって分ける
			SELECTCASE 점포("종류",ARG)
				;메시아 교회
				CASE 13
					SELECTCASE RAND:(2+교회규모)
						CASE 1
							PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% {참배인수}명의 메시아교 신자들의 페니스에 정중한 봉사를 하였다
						CASEELSE
							PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% 방문한 {참배인수}명의 신자에게 입으로 봉사했다
					ENDSELECT
				;가이아教会
				CASE 14
					SELECTCASE RAND:2
						CASE 1
							PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% {참배인수}명의 가이아교 신자들의 페니스에 정중한 봉사를 하였다
						CASEELSE
							PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% 방문한 {참배인수}명의 신자에게 입으로 봉사했다
					ENDSELECT
				;교회
				CASE 15
					SELECTCASE RAND:2
						CASE 1
							PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% 미사로 방문한 {참배인수}명의 신자들의 페니스에 정중한 봉사를 하였다
						CASEELSE
							PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% 방문한 {참배인수}명의 신자에게 입으로 봉사했다
					ENDSELECT
			ENDSELECT
			;이벤트구상
			T = TARGET
			TARGET = LOCAL
			TFLAG:13 = 66
			CALL KOJO_MESSAGE_EVENT
			TARGET = T
			;취득경험計算
			TCVAR:LOCAL:획득펠라경험 = 참배인수
			;TCVAR:LOCAL:획득봉사쾌락경험 = 참배인수
			;혀놀림がある場合は정액경험にボーナス
			LOCAL:1 = 참배인수
			SIF TALENT:LOCAL:혀놀림
				LOCAL:1 += 1 + LOCAL:1 / 2
			TCVAR:LOCAL:획득정액경험 = LOCAL:1
			;崇拝値は1+획득정액경험
			경배치 += 1 + TCVAR:LOCAL:획득정액경험
			;획득경험표시
			CALL SHOW_ADD_EXP, LOCAL
			
			;입봉사での変化は0～2(MAX0～7)
			속성변동치 = RAND:(3+교회규모)

		;性봉사
		CASE 3
			;봉사する人数を決める
			참배인수 = RAND:최대참배인수
			;참배인수が0の場合はお祈りをする
			IF 참배인수 == 0
				PRINTFORMW 참배자가 없었기 때문에, %조사처리(CALLNAME:LOCAL,"는")% 하루종일 기도하였다
				;祈りでの変化は0～1(MAX0～4)
				속성변동치 = RAND:(2+교회규모/2)
				경배치 += 1
				;이벤트구상
				T = TARGET
				TARGET = LOCAL
				TFLAG:13 = 65
				CALL KOJO_MESSAGE_EVENT
				TARGET = T
				GOTO SKIP_1
			ENDIF
			
			;교회の종류によって分ける
			SELECTCASE 점포("종류",ARG)
				;메시아 교회
				CASE 13
					SELECTCASE RAND:2
						CASE 1
							PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% {참배인수}명의 메시아교 신자에게 몸으로 봉사했다
						CASEELSE
							PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% 방문한 {참배인수}명의 참배자에게 몸을 사용하여 봉사했다
					ENDSELECT
				;가이아教会
				CASE 14
					SELECTCASE RAND:2
						CASE 1
							PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% {참배인수}명의 가이아교 신자에게 몸으로 봉사했다
						CASEELSE
							PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% 방문한 {참배인수}명의 참배자에게 몸을 사용하여 봉사했다
					ENDSELECT
				;교회
				CASE 15
					SELECTCASE RAND:2
						CASE 1
							PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% 미사로 방문한 {참배인수}명의 신자에게 자신의 몸으로 봉사했다
						CASEELSE
							PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% 방문한 {참배인수}명의 참배자에게 몸을 사용하여 봉사했다
					ENDSELECT
			ENDSELECT
			
			;이벤트구상
			T = TARGET
			TARGET = LOCAL
			TFLAG:13 = 67
			CALL KOJO_MESSAGE_EVENT
			TARGET = T
			;취득경험計算
			;とりあえず一人に付き一発にしておく
			FOR LOCAL:1, 0, 참배인수
				SELECTCASE RAND:3
					CASE 1
						입봉사횟수 ++
					CASE 2
						Ｖ봉사횟수 ++
					CASEELSE
						Ａ봉사횟수 ++
				ENDSELECT
				
				;키스경험処理：適当に랜덤で
				SIF RAND:2 == 0
					TCVAR:LOCAL:획득키스경험 += 1
				
			NEXT
			TCVAR:LOCAL:획득펠라경험 += 입봉사횟수
			TCVAR:LOCAL:획득Ｖ경험 += Ｖ봉사횟수
			TCVAR:LOCAL:획득Ａ경험 += Ａ봉사횟수
			TCVAR:LOCAL:획득봉사쾌락경험 += Ｖ봉사횟수 + Ａ봉사횟수
			TCVAR:LOCAL:획득정액경험 += 입봉사횟수 + Ｖ봉사횟수 + Ａ봉사횟수
			TCVAR:LOCAL:획득성교경험 += Ｖ봉사횟수 + Ａ봉사횟수
			TCVAR:LOCAL:획득질사경험 += Ｖ봉사횟수
			경배치 += 1 + 입봉사횟수 + 2*(Ｖ봉사횟수 + Ａ봉사횟수)
			IF TALENT:LOCAL:혀놀림
				TCVAR:LOCAL:획득정액경험 += 입봉사횟수 / 2
				경배치 += 입봉사횟수 / 2
			ENDIF
			;妊娠用, SHOW_ADD_EXPの前に置かないと獲得膣射経験が0になる
			IF CFLAG:LOCAL:자궁내피임결계 == 0
				CFLAG:LOCAL:신자의질내사정에의한임신판정 += TCVAR:LOCAL:획득질사경험*2
				;자궁현재용량加算
				IF TALENT:LOCAL:오토코노코
					CFLAG:(LOCAL):자궁현재용량 += Ａ봉사횟수 * 5
				ELSEIF !IS_MALE(LOCAL)
					CFLAG:(LOCAL):자궁현재용량 += Ｖ봉사횟수 * 5
				ENDIF
				
			ENDIF
			;獲得経験表示
			CALL SHOW_ADD_EXP, LOCAL
			;체력の소비
			PRINTFORML 체력 - {참배인수*10}
			BASE:LOCAL:체력 -= 참배인수*10

			;性봉사での変化は1～4(MAX1～9)
			속성변동치 = 1 + RAND:(4+교회규모)

	ENDSELECT
	$SKIP_1
	;数値が1以上なら속성値変動
	IF LOCAL:1
		;교회によって変動が変わる
		SELECTCASE 점포("종류", ARG)
			CASE 13
				LOCAL:2 = 255
			CASE 14
				LOCAL:2 = 0
			CASE 15
				LOCAL:2 = 127
		ENDSELECT
		;キャラの속성値変動
		CALL CHANGE_CHARA_LC, LOCAL, LOCAL:2, 속성변동치
		CALL SYNC_STATUS, LOCAL
	ENDIF

	PRINTW
	DRAWLINE

NEXT
PRINTFORML 유지비로 ￥{500 * 교회규모 + 배치인수 * 300}만큼 지불했습니다
MONEY -= 500 * 교회규모 + 배치인수 * 300
[[점포:인기]]:ARG += 경배치
;==========================================================
;@교회_경배치_교환
; 경배치を交換するための処理
;<引数>
;	ARG = 점포番号
;<戻り値>
;	なし
;==========================================================
@교회_경배치_교환, ARG
;소지경배치と値段を比べるため
LOCAL:1 = 100
LOCAL:2 = 500
LOCAL:3 = 1000
LOCAL:4 = 2000
LOCAL:5 = 5000

$LOOP_1
PRINTFORML 권유할 자를 고르세요　　　경배치：{[[점포:인기]]:ARG}

SELECTCASE 점포("종류",ARG)
	;메시아 교회
	CASE 13
			PRINTFORML [1] 광신자　　　　　　　　　100
		SIF 점포("규모",ARG) > 1
			PRINTFORML [2] 부쳐　　　　　500
		SIF 점포("규모",ARG) > 2
			PRINTFORML [3] 템플나이트　　　1000
		SIF 점포("규모",ARG) > 3
			PRINTFORML [4] 여성 템플나이트　2000

	;가이아 교회
	CASE 14
			PRINTFORML [1] 특공대　　　　　　100
		SIF 점포("규모",ARG) > 1
			PRINTFORML [2] 파계승　　　　500
		SIF 점포("규모",ARG) > 2
			PRINTFORML [3] 어새신　　　　　1000
		SIF 점포("규모",ARG) > 3
			PRINTFORML [4] 오니죠로　　　2000
		SIF 점포("규모",ARG) > 4
			PRINTFORML [5] 가면성　　　5000
	;교회
	CASE 15
			PRINTFORML [1] 야쿠자　　　　　　　100
		SIF 점포("규모",ARG) > 1
			PRINTFORML [2] 펑크스　　　　　　500
		SIF 점포("규모",ARG) > 2
			PRINTFORML [3] 메탈　　　　　　　1000
		SIF 점포("규모",ARG) > 3
			PRINTFORML [4] 하이웨이스타　　2000

ENDSELECT

PRINTFORML [100] 돌아가다

SELECTCASE 점포("종류",ARG)
	;메시아 교회
	CASE 13
		CALL INPUTINT( 1, 2, 3, 4, 100)

	;가이아 교회
	CASE 14
		CALL INPUTINT( 1, 2, 3, 4, 5, 100)

	;교회
	CASE 15
		CALL INPUTINT( 1, 2, 3, 4, 100)

ENDSELECT

;戻るを選択した場合
IF RESULT == 100
	RETURN
;경배치が발りない場合はループ
ELSEIF LOCAL:RESULT > [[점포:인기]]:ARG
	PRINTFORMW 경배치가 충분하지 않습니다
	GOTO LOOP_1
ENDIF


SELECTCASE 점포("종류",ARG)
	;메시아 교회
	CASE 13
		SELECTCASE RESULT
			CASE 1
				PRINTFORMW 광신자가 추가 되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:광신자]]

			CASE 2
				PRINTFORMW 부쳐가 추가되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:부쳐]]

			CASE 3
				PRINTFORMW 템플나이트가 추가되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:템플나이트]]
				
			CASE 4
				PRINTFORMW 여성 템플나이트가 추가되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:템플나이트(여성)]]

		ENDSELECT

	;가이아 교회
	CASE 14
		SELECTCASE RESULT
			CASE 1
				PRINTFORMW 특공대가 추가되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:특공대]]

			CASE 2
				PRINTFORMW 파계승이 추가되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:파계승]]

			CASE 3
				PRINTFORMW 어새신이 추가되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:어새신]]

			CASE 4
				PRINTFORMW 오니죠로가 추가되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:오니죠로]]

			CASE 5
				PRINTFORMW 가면성이 추가되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:가면성]]

		ENDSELECT

	;교회
	CASE 15
		SELECTCASE RESULT
			CASE 1
				PRINTFORMW 야쿠자가 추가되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:야쿠자]]

			CASE 2
				PRINTFORMW 펑크스가 추가되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:펑크스]]

			CASE 3
				PRINTFORMW 메탈이 추가되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:메탈]]

			CASE 4
				PRINTFORMW 하이웨이스타가 추가되었습니다
				CALL ADD_NEW_COMPANION, [[キャラ:하이웨이스타]]

		ENDSELECT

ENDSELECT

;경배치支払い
[[점포:인기]]:ARG -= LOCAL:RESULT





