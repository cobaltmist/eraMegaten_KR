;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:SHOP_NEWGAME.ERB
;	Facility	:周回ニューゲームに関する関数群です
;
;	Licence		:ライセンスフリー
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;	002		2011/02/15		黒天					キャラの引継ぎで一括選択の항목を추가
;	003		2011/04/13		黒天					引継ぎ可能キャラが二人未満の場合、一括選択が표시できないよう변경
;													引継ぎ一括選択に対応する番号を0→999に변경
;													引継ぎ一括選択時に本当に一括변경を行うかの確認を추가
;	004		2011/05/01		Ｐ						主人を페르소나구사자から그외の特性に변경した場合、초기페르소나を削除するように
;	005		2011/05/23		Ｐ						引継ぎ時に던전フラグ:0-99:100-124(マップサイズ情報)を削除しないように
;	006		2011/06/24		Ｐ						マテリアルカードの引き継ぎ措置を변경
;	007		2011/09/02		旅人					デビサバ２用フラグの초기化を추가
;	008		2012/01/18		Ｐ						초기링크악마습득스킬を引継ぎ時に再설정するように
;													식노の스킬引継ぎの仕様を변경
;	009		2012/03/26								ＮＴＲフラグ、ＮＴＲ이벤트進行度の引継ぎリセット修正
;	010		2013/11/24		ひみつ					リスト표시数설정
;	011		2014/10/21		ネトリス				デビオクで사용している데이터のリセット処理を추가
;	012		2016/07/30		（U´・ω・｀）			ラン란다ンジョンを引き継ぎ可能に
;	013		2017/06/29		ああああ				元性別の扱いを변경
;	014		2017/05/10		kuma					周回ボーナスの調整
;	015		2017/09/19		kuma					周回ボーナスの調整　인기
;	016		2015/10/02		魔晶확장パッチ			마정무기の引き継ぎ処理を변경
;	017		2018/05/06		JK好き					FLAG:東方의뢰進行度がリセットされていなかったのを修正
;	018		2018/08/25		名無					저주의레드카펫추가
;	019		2019/04/20		TR(SAYA)				コスト制周回ボーナスの추가＆全キャラの장비を剥ぐ処理を추가
;	020		2019/05/29		名無し					ジャンクポイント関連추가(MONEY:4)
;	021		2019/08/29		空気					周回時、이능자・달인の能力ボーナス計算が間違っていたのを修正
;	022		2019/09/22		なんとなくの人			EP소비の引き継ぎ要素に경험치を추가
;	023		2019/10/15		ナナドラMOD				FLAG:ナナドラ의뢰進行度のリセット処理を추가
;	024		2019/10/30		ypa						신뢰系함락추가
;	025		2019/11/03		131						娘or息子マスター引き継ぎ処理を추가
;	026		2020/03/14		Jガン					娘or息子マスター引き継ぎ処理の修正
;	027		2020/06/20								FLAG:マヨナカアリーナ進行度の리셋処理を추가
;	028		2020/06/20		Schrott					아이템や무기防具の引き継ぎ時、『引き継げる최대数』が『아이템引き継ぎ可불가フラグ수치』であったものを、『최대EP』or『10』の低い方に
;	029		2020/08/08		kamedakeisuke(ﾟдﾟ)		劇場憑き악마系フラグの리셋処理
;	030		2020/08/12		スパロボガスキー		페르소나구사자引き継ぎ時の초기페르소나処理の수정
;	031		2020/09/01		スパロボガスキー		030の수정を取り消し、초기페르소나の능력値초기化のみを수정
;	032		2020/09/22		JK好き					周回ボーナス설정時の分母と分子が逆になっていたのを수정
;	033		2021/03/05		NEO						単発イベント用のフラグ消去加筆、そういえば以前にも加筆してここに書いてなかったわよ
;	034		2021/03/05		JK好き					周回時にFLAG:今周回信頼人数及びFLAG:信頼人数のリセット処理を追加
;	035		2021/03/16		JK好き					引継ぎあなたの能力ボーナス設定前にSYNC_STATUSを追加
;	036		2021/04/10		NEO						周回ボーナス設定に「受胎アイテムをリセット」「インストールソフト引継ぎ」を追加
;	037		2021/04/10		jガン					容量の処理追加
;	038		2021/06/22		軽木					「インストールソフト引継ぎ」を追加……とあったが、追加されてなかったので僭越ながら追加
;	039		2021/06/25		ロッド@カヤノソト		コロシアムイベント26_데미지コンテストのフラグ・記録リセットを追加
;	040		2021/06/30		lemukot					「異能者半魔あなた強化段階」のフラグリセット追加、Editを更新日時に合わせた
;	041		2021/07/17		Musue					ペルソナ強化フラグのリセット処理
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;=================================================================
;강하게뉴게임関連
;=================================================================
;引き継ぐ物
;악마解析度、合体解禁フラグ、素質・조교아이템、衣装、オートマッピング
;
;ボーナスが付くもの
;主人초기能力、초기ＣＯＭＰ용량、초기소지金、초기店인기
;=================================================================


@SHOPCOMABLE_777
RESULT:1 = 7
RESULTS = 뉴게임
SIF FLAG:5 == 9 || DAY > 50
	RETURN 1
RETURN 0

@SHOP_COM_777
PRINTFORML 극히 일부의 데이터를 계승해서、새로운 게임을 시작합니까？
PRINTL [0]시작한다 [1]그만둔다
$INPUT_LOOP
INPUT
IF RESULT == 1
	RETURN 0
ELSEIF RESULT == 0
	CALL START_NEWGAME
	RETURN 1
ELSE
	GOTO INPUT_LOOP
ENDIF


;=================================================================
;引継ぎ処理
;=================================================================
@START_NEWGAME
;주회횟수を+1
SIF FLAG:5 == 9
	FLAG:주회횟수 += 1
FLAG:경험ED수 = 0
FOR LOCAL,0,64
	SIF GETBIT(FLAG:발견ED , LOCAL)
		FLAG:경험ED수 ++
NEXT
FOR LOCAL,0,64
	SIF GETBIT(FLAG:발견ED2 , LOCAL)
		FLAG:경험ED수 ++
NEXT

PRINTL 주회 보너스를 설정합니까？
IF REQUIRE_ENDING_POINT()
	PRINTFORML [0] 설정을 변경한다(현재의 EP:{COUNT_ENDING_POINT()}/{REQUIRE_ENDING_POINT()}사용)
	PRINTFORML [1] 변경하지 않는다
ELSE
	PRINTFORML [0] 설정한다(현재의 EP:{COUNT_ENDING_POINT()})
	PRINTFORML [1] 설정하지 않는다
ENDIF
$LOOP_BONUS
INPUT
SELECTCASE RESULT
CASE 0
	CALL NEWGAME_BONUS
CASE 1
CASEELSE
	CLEARLINE 1
	GOTO LOOP_BONUS
ENDSELECT


;-----------------------------------------------------------------
;引き継がないものを초기化
;主人以外のキャラを削除
;-----------------------------------------------------------------
;先に、削除しないキャラを選択することも出来る。

;全員の장비を外す
;なんらかの理由でいないキャラは除外
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:この場に居ないフラグ
		CONTINUE
	CALL 全장비해제(LOCAL)
NEXT

CALL SELECT_YOME

FOR LOCAL,CHARANUM-1,0,-1
	SIF CFLAG:LOCAL:引継ぎフラグ == 0
		CALL キャラ삭제(LOCAL)
NEXT
CALL REFRESH_POS

;引き継いだキャラの레벨を１・能力をALL3に。습득스킬をＣＳＶから再読み込み。引継ぎフラグに応じて素質や能力변경
;인간キャラの場合元CSVの레벨通りになるのでコメントアウト
;FOR LOCAL,1,CHARANUM
;	SIF FLAG:5 == 9
;		CFLAG:LOCAL:引継ぎ횟수 += 1
;	IF (CSVTALENT(NO:LOCAL,230,0) == 0 && (NO:LOCAL == 0 || (NO:LOCAL > 4500 && NO:LOCAL < 4800) || NO:LOCAL > 4920)) || TALENT:LOCAL:230
;		BASE:LOCAL:LV = CSVBASE(NO:LOCAL,GETNUM(BASE,"LV"),0)
;		BASE:LOCAL:ＥＸＰ = 5*(BASE:LOCAL:LV-1)*(BASE:LOCAL:LV+1)*(BASE:LOCAL:LV)/3
;		BASE:LOCAL:ＭＡＧ = 0
;		CFLAG:LOCAL:초기LV = 1
;		FOR LOCAL:1,1,7
;			BASE:LOCAL:GET_BASESTATUS(LOCAL:1) = CSVBASE(NO:LOCAL,GETNUM(BASE,GET_BASESTATUS(LOCAL:1)),0)
;			CFLAG:LOCAL:(GET_BASESTATUS(LOCAL:1) + "강화횟수") = 0
;		NEXT
;		;このあとで処理する
;		;CFLAG:LOCAL:この場に居ないフラグ = 1
;		SIF CPOS(LOCAL) > 0
;			CALL REMOVE_POSITION,CPOS(LOCAL)
;		;적専用스킬を削除
;		FOR LOCAL:3,1,FLAG:스킬수+1
;			LOCALS = 스킬{LOCAL:3}
;			TRYCCALLFORM 적専用_{ABL:LOCAL:LOCALS}
;				SIF RESULT == 1
;					ABL:LOCAL:LOCALS = 0
;			CATCH
;			ENDCATCH
;		NEXT
;	ELSE
;		BASE:LOCAL:LV = 1
;		CFLAG:LOCAL:초기LV = 1
;		BASE:LOCAL:ＥＸＰ = 0
;		BASE:LOCAL:ＭＡＧ = 0
;		FOR LOCAL:1,1,7
;			BASE:LOCAL:GET_BASESTATUS(LOCAL:1) = 3
;			CFLAG:LOCAL:(GET_BASESTATUS(LOCAL:1) + "강화횟수") = 0
;		NEXT
;		BASE:LOCAL:(RAND:6 + 31) += 1
;	ENDIF

;---- MOD 031 EDIT START -------------------------

;引き継いだキャラの레벨を인간なら5、악마なら1にして능력をＣＳＶから読み込んでから레벨に合わせて増減、습득스킬をＣＳＶから再読み込み。引継ぎフラグに応じて소질や능력변경
FOR LOCAL,1,CHARANUM
	CFLAG:LOCAL:引継ぎ횟수 += 1
	CFLAG:LOCAL:전투참가불가능 = 0
	CALL SHOP_NEWGAME_INITCHARA, LOCAL
	CALL 장비강화_CSTR, "全해제：能力のみ", LOCAL
NEXT
;---- MOD 031 EDIT END   -------------------------


;-----------------------------------------------------------------
;이벤트フラグを초기化
;-----------------------------------------------------------------
;VARSET 던전フラグ:0:0,0
;던전・이벤트・의뢰・투기장フラグ초기化
FOR LOCAL, 0, 100
	FOR LOCAL:1, 0, 100
		던전フラグ:LOCAL:(LOCAL:1) = 0
		IF LOCAL:1 < 20
			이벤트フラグ:LOCAL:(LOCAL:1) = 0
			투기장フラグ:LOCAL:(LOCAL:1) = 0
			의뢰フラグ:LOCAL:(LOCAL:1) = 0
		ENDIF
	NEXT
NEXT

FLAG:초기파트너 = 0
FLAG:속성고정LD = 0
FLAG:속성고정LC = 0
FLAG:루트확정 = 0
FLAG:나오야루트 = 0
FLAG:백합의꽃루트 = 0
FLAG:벨신격파 = 0
;---- EDIT 007 ADD START -------------------------
FLAG:デビサバ２進行度 = 0
;---- EDIT 007 ADD END ---------------------------
FLAG:真IV進行度 = 0
FLAG:ナイト・テイル進行度 = 0
FLAG:트리슈조교 = 0
FLAG:던전출현1 = 0
FLAG:던전출현2 = 0
FLAG:투기장출현1 = 0
FLAG:투기장출현2 = 0
FLAG:이벤트출현1 = 0
FLAG:이벤트출현2 = 0
FLAG:의뢰출현1 = 0
FLAG:의뢰출현2 = 0
FLAG:東方의뢰進行度 = 0
FLAG:ナナドラ의뢰進行度 = 0
FLAG:マヨナカアリーナ進行度 = 0
CFLAG:MASTER:101 = 0
CFLAG:MASTER:102 = 0
CFLAG:MASTER:103 = 0
CFLAG:MASTER:104 = 0
CFLAG:MASTER:105 = 0
CFLAG:MASTER:110 = 0
CFLAG:MASTER:111 = 0
CFLAG:MASTER:112 = 0
CFLAG:MASTER:출산日 = 0
CFLAG:MASTER:父親の固有번호親 = 0
CFLAG:MASTER:娘の産みの親のキャラ번호親 = 0
CFLAG:MASTER:娘の産みの親の固有번호親 = 0
TALENT:MASTER:153 = 0
CFLAG:MASTER:발정임신 = 0
CFLAG:MASTER:父親失踪 = 0
;ふたごちゃんパッチ：最大受胎人数は記録的な部分があるので初期化しない
CFLAG:MASTER:今回受胎人数 = 0
CFLAG:MASTER:この場に居ないフラグ = 0




TALENT:MASTER:154 = 0

;뱅드림+P5 모드
FOR LOCAL, 0, 100
	메멘토스의뢰:LOCAL = 0
NEXT
FLAG:던전필터1 = 0
FLAG:던전필터2 = 0
FLAG:취소된ED = 0
FLAG:취소된ED2 = 0
FLAG:명성 = 0
FLAG:조교사명성 = 0
FLAG:투기장명성 = 0
FLAG:조직공헌도 = 0
FLAG:소속조직 = 0
FLAG:투기장랭크 = 0
SAVESTR:랜덤대전팀 =
CALL SELECT_COLOSSEUM_RANDOMBATTLE
;---- 闘技場26_데미지コンテスト！ -------------------------
YOUR_RANK = 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 ;現在の順位(レギュレーション1～10)　※レギュレーション4以降は予備です。
COLOSSEUM_26_RANKING_EVENT_FLG = 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 ;ランキング1位イベント完了フラグ(レギュレーション1～10)
COLOSSEUM_26_MAX_DAMAGE = 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 ;今周回の最大데미지(レギュレーションごとにハイスコアを格納する配列)　※次の周回へ進む際にリセットする。
;---- 闘技場26_데미지コンテスト！ -------------------------
TIME:1 = 0


;-----------------------------------------------------------------
;마정장비削除
;-----------------------------------------------------------------
IF GET_BONUS("마정무기") == 0
	FOR LOCAL,0,50
		IF DC:LOCAL:90 != 1
			FOR LOCAL:2,0,99
				DC:LOCAL:(LOCAL:2) = 0
			NEXT
		ENDIF
	NEXT
ENDIF
;VARSET DC:0:0,0

;-----------------------------------------------------------------
;점포解体
;-----------------------------------------------------------------
VARSET DE:0:0,0

;-----------------------------------------------------------------
;文字列フラグ削除
;一応、文字列フラグ管理で使ってる部分だけに限定
;-----------------------------------------------------------------
VARSET SAVESTR, "", 100, 500

;-----------------------------------------------------------------
;장비品・아이템・도리カドモン・インス토르ソフト削除
;-----------------------------------------------------------------
;主人の장비초기化
CALL 全장비해제,MASTER
FOR LOCAL,0,VARSIZE("EQUIP")
	EQUIP:MASTER:LOCAL = CSVEQUIP(NO:MASTER,LOCAL,0)
NEXT
FOR LOCAL,200,6000
	SELECTCASE LOCAL
		CASE 220 TO 250
			ITEM:LOCAL = ABS(ITEM:LOCAL)
		;一部除くインス토르ソフトを引継ぎ
		;ラン란다ンジョンを추가
		CASE 500 TO 503 , 508 TO 515 , 517 , 530 , 533 , 540 TO 542, 559
		CASE 2450 TO 2499
			SIF DC:(LOCAL-2450):90 != 1 && GET_BONUS("마정무기") == 0
				ITEM:LOCAL = 0
		;例外の아이템はここに記述
		CASEELSE
			TRYCCALLFORM 引継ぎ可不可_{LOCAL}
				SIF RESULT <= 0
					ITEM:LOCAL = 0
				RESULT = COUNT_ENDING_POINT()
				SIF COUNT_ENDING_POINT() >= 10
					RESULT = 10
				SIF ITEM:LOCAL > RESULT
					ITEM:LOCAL = RESULT
			CATCH
				;周回ボーナス処理を추가
				SELECTCASE LOCAL
				
				;引継不可(能力의향)
				CASE 1010,1011,1012,1013,1014,1015
				;引継不可(황금의벌꿀주,스피릿리브,겨우살이)
				CASE 1044,1198,1199

				;500-600(インストールソフト)
				CASE 500 TO 600
					SIF GET_BONUS("인스톨소프트")
						CONTINUE

				;270-318(宝石、페르소나강화カード、섀도우のドロップ)
				;1400-1449(合成素材)
				CASE 248, 250 TO 319 , 1400 TO 1449
					SIF GET_BONUS("소재아이템")
						CONTINUE

				;1000-1200(소비아이템)
				;5500-5599(특수탄丸)
				;겨우살이等も含まれるが아이템側のERBで対処
				CASE 1000 TO 1199 , 5500 TO 5599
					SIF GET_BONUS("소비아이템")
						CONTINUE

				;2000-2448(검)
				;2500-2948(총)
				CASE 2000 TO 2448 , 2500 TO 2948
					SIF GET_BONUS("무구아이템")
						CONTINUE

				;3000-3448(머리)
				;3500-3948(몸통体)
				;4000-4448(팔)
				;4500-4948(발)
				;5000-5448(アクセ)
				CASE 3000 TO 3449 , 3500 TO 3949 , 4000 TO 4449 , 4500 TO 4949 , 5000 TO 5449
					SIF GET_BONUS("방어구아이템")
						CONTINUE
				ENDSELECT
				ITEM:LOCAL = 0
				SIF 개조장비(LOCAL)
					CALL 개조장비해제,개조장비番号(LOCAL)
			ENDCATCH
	ENDSELECT
NEXT
;조교아이템を破棄するか
SIF GET_BONUS("조교아이템") == 0
	VARSET ITEM,0,0,100

;-----------------------------------------------------------------
;종족ごとの매각횟수を초기化
;-----------------------------------------------------------------
FOR LOCAL,30000,35021
	FLAG:LOCAL = 0
NEXT

;-----------------------------------------------------------------
;キャンセルしたEDをリセット
;-----------------------------------------------------------------
FLAG:취소된ED = 0
FLAG:취소된ED2 = 0
;-----------------------------------------------------------------
;日付、時間、조교대상や조수のリセット
;-----------------------------------------------------------------
DAY = 1
DAY:1 = 10
MONEY:2 = 50000
TIME = 0
TARGET = -1
ASSI = -1
FLAG:5 = 0
FLAG:던전탐색済み = 0
FLAG:콜로세움참가済み = 0
;SIF FLAG:연모人数 > FLAG:주회횟수*2
;	FLAG:연모人数 = FLAG:주회횟수*2
;SIF FLAG:음란人数 > FLAG:주회횟수*2
;	FLAG:음란人数 = FLAG:주회횟수*2
;SIF FLAG:복종人数 > FLAG:주회횟수*2
;	FLAG:복종人数 = FLAG:주회횟수*2
FLAG:今周回연모人数 = 0
FLAG:今周回음란人数 = 0
FLAG:今周回복종人数 = 0
FLAG:今周回信頼人数 = 0
FLAG:奴隷매각数 = 0

;今週回の帳簿リセット
VARSET FLAG, 0, 1403, 1406
VARSET FLAG, 0, 1413, 1416
VARSET FLAG, 0, 1423, 1426
VARSET FLAG, 0, 1433, 1436
FLAG:매각人数 = 0
FLAG:매각人数（연모친애） = 0
FLAG:매각人数（음란창부） = 0
FLAG:매각人数（복종예속） = 0
FLAG:식당総利益 = 0
FLAG:창관総利益 = 0
FLAG:공중변소総利益 = 0
FLAG:렌탈総利益 = 0
FLAG:劇場総利益 = 0
FLAG:コンサート総利益 = 0
;デビオクの内容もリセット
VARSET 中古キャラ매각済み:0:0, 0
VARSET 中古キャラ매각가격:0:0, 0
VARSET 中古キャラABL:0:0, 0
VARSET 中古キャラBASE:0:0, 0
VARSET 中古キャラCFLAG:0:0, 0
VARSET 中古キャラCFLAG2:0:0, 0
VARSET 中古キャラCFLAG3:0:0, 0
VARSET 中古キャラCFLAG이상경험記録:0:0, 0
VARSET 中古キャラEXP:0:0, 0
VARSET 中古キャラTALENT:0:0, 0
VARSET 中古キャラMARK:0:0, 0
VARSET 中古キャラ임신相손:0:0, 0
VARSET 中古キャラ매각先:0:0, 0
VARSET 中古キャラ매각日:0:0, 0
VARSET デビオク상품リスト:0:0, 0
VARSET デビオク予約フラグ:0:0, 0

;그외	独自変数の리셋
VARSET 보컬리스트・세이렌進行, 0
VARSET 시나리오라이터・지크프리트進行, 0
VARSET 『관객』디오니소스進行, 0
VARSET ほむほむvsゴトウ進行, 0
VARSET 料理イベント進行, 0
VARSET Reconnect_Ragnarok進行, 0

;그외 한판 개별이벤트 관련 리셋
VARSET 세라픽게이트진행도, 0

;集団ライブの変数もリセット
VARSET グループ人気
VARSET グループ収入

;-----------------------------------------------------------------
;難易度
;-----------------------------------------------------------------
CALL SET_DIFFICULT

;-----------------------------------------------------------------
;主人の能力설정
;-----------------------------------------------------------------
;CALL NEW_MASTER
;-----------------------------------------------------------------
;主人の조교・노역설정
;-----------------------------------------------------------------

;素質・能力・経験を弄くる
;周횟수が5以上の場合能力リセット時に旧당신を新規キャラとして加入させるか選択
PRINTFORML 주인의 소질・능력・경험을 계승합니까？
CALL INPUT_YN,"Yes","NO"

IF FLAG:경험ED수 > 4
	IF RESULT == 1
		PRINTFORML 옛주인(리셋 전의 소질・능력・경험을 보유)을 신규 캐릭터로 가입시킵니까？
		CALL INPUT_YN,"Yes","NO"
		;旧당신加入処理
		IF RESULT == 0
			CALL ADD_NEW_COMPANION, 4998, 500
			IF RESULT == 1
				LOCAL = CFLAG:(RESULT:1):キャラ固有の번호
				COPYCHARA 0, (RESULT:1)
				;呼び名
				IF CFLAG:LOCAL:당신の専攻分野  == 1
					NAME:(RESULT:1)     = 다크서머너
					CALLNAME:(RESULT:1) = 서머너
				ELSEIF CFLAG:LOCAL:당신の専攻分野  == 2
					NAME:(RESULT:1)     = 페르소나구사자서머너
					CALLNAME:(RESULT:1) = 페르소나구사자
				ELSEIF CFLAG:LOCAL:당신の専攻分野  == 3
					NAME:(RESULT:1)     = 이능자서머너
					CALLNAME:(RESULT:1) = 이능자
				ELSEIF CFLAG:LOCAL:당신の専攻分野  == 4
					NAME:(RESULT:1)     = 관광객
					CALLNAME:(RESULT:1) = 관광객
				ELSEIF CFLAG:LOCAL:당신の専攻分野  == 5
					NAME:(RESULT:1)     = 식노서머너
					CALLNAME:(RESULT:1) = 식노
				ELSEIF CFLAG:LOCAL:당신の専攻分野  == 6
					NAME:(RESULT:1)     = 달인서머너
					CALLNAME:(RESULT:1) = 달인
				ELSEIF CFLAG:LOCAL:당신の専攻分野  == 7
					NAME:(RESULT:1)     = 악마사냥꾼서머너
					CALLNAME:(RESULT:1) = 악마사냥꾼
				ELSEIF CFLAG:LOCAL:당신の専攻分野  == 8
					NAME:(RESULT:1)     = 악마인서머너
					CALLNAME:(RESULT:1) = 악마인
				ELSEIF CFLAG:LOCAL:당신の専攻分野  == 9
					NAME:(RESULT:1)     = 광대
					CALLNAME:(RESULT:1) = 광대
				ELSEIF CFLAG:LOCAL:당신の専攻分野  == 10
					NAME:(RESULT:1)     = 시프터
					CALLNAME:(RESULT:1) = 시프터
				ELSE
					NAME:(RESULT:1)     = 수수께끼의사람
					CALLNAME:(RESULT:1) = 수수께끼의사람
				ENDIF
				CFLAG:(RESULT:1):引継ぎフラグ = 1
				CFLAG:(RESULT:1):引継ぎ횟수   = 1
				CFLAG:(RESULT:1):포지션   = 0
				CFLAG:(RESULT:1):所属ＣＯＭＰ = 0
				CFLAG:(RESULT:1):합체불가     = 0
				CFLAG:(RESULT:1):キャラ固有の번호 = LOCAL
				NO:(RESULT:1) = 4998
				CALL SHOP_NEWGAME_INITCHARA, (RESULT:1)
			ENDIF
		ENDIF

		FOR LOCAL,0,26
			ABL:MASTER:LOCAL = CSVABL(NO:MASTER,LOCAL,0)
		NEXT
		FOR LOCAL,0,VARSIZE("EXP")
			EXP:MASTER:LOCAL = CSVEXP(NO:MASTER,LOCAL,0)
		NEXT
		FOR LOCAL,0,101
			JUEL:MASTER:LOCAL = 0
		NEXT
		VARSET CFLAG:MASTER:0 , 0 , 2001 , 2600
		CFLAG:MASTER:함락キャラ = 0
		;素質
		TALENT:MASTER:수수께끼의매력 = 0
		TALENT:MASTER:절륜 = 0
		TALENT:MASTER:음란 = 0
		TALENT:MASTER:창부 = 0
		TALENT:MASTER:모유체질 = 0
		TALENT:MASTER:인기 = 0
		FLAG:인기취득者数 = 0
		CFLAG:MASTER:第何子まで産んだか = 0
		CFLAG:MASTER:仕送り時してくれる人数親 = 0  
		;ふたごちゃんパッチ：最大人数はリセット
		CFLAG:MASTER:最大妊娠人数 = 0
	;	CFLAG:MASTER:キャラ固有の번호 = FLAG:今までのキャラ数
	;	FLAG:今までのキャラ数 += 1
		CFLAG:MASTER:요리인기 = 0
		CFLAG:MASTER:매춘인기 = 0
		CFLAG:MASTER:변기인기 = 0
		CFLAG:MASTER:劇場인기 = 0
		CFLAG:MASTER:아이돌인기 = 0
		IF TALENT:MASTER:남자 == 0 
			TALENT:MASTER:처녀 = 1
		ELSE
			CFLAG:MASTER:元처녀 = 1
			CFLAG:MASTER:元Ｖ감각 = 1
		ENDIF
		TALENT:MASTER:새드 = 0
		TALENT:MASTER:마조 = 0
		TALENT:MASTER:불결무시 = 0
		ITEM:【불결무시】 = 0
		TALENT:MASTER:조합지식 = 0
		ITEM:【조합지식】 = 0
		TALENT:MASTER:금단의지식 = 0
		ITEM:【금단의지식】 = 0
		TALENT:MASTER:깨달음 = 0
		ITEM:【서드・아이】 = 0
		TALENT:MASTER:촉수사역술 = 0
		ITEM:【촉수사역술】 = 0
		TALENT:MASTER:잉태희망 = 0

		EX:MASTER:0 = 0
		EX:MASTER:1 = 0
		EX:MASTER:2 = 0
		EX:MASTER:3 = 0
		FLAG:주인출신 = 0
		CFLAG:MASTER:이상경험記録 = 0
	ENDIF
ELSE
	IF RESULT == 1
		FOR LOCAL,0,26
			ABL:MASTER:LOCAL = CSVABL(NO:MASTER,LOCAL,0)
		NEXT
		FOR LOCAL,0,VARSIZE("EXP")
			EXP:MASTER:LOCAL = CSVEXP(NO:MASTER,LOCAL,0)
		NEXT
		FOR LOCAL,0,101
			JUEL:MASTER:LOCAL = 0
		NEXT
		VARSET CFLAG:MASTER:0 , 0 , 2001 , 2600
		CFLAG:MASTER:함락キャラ = 0
		;素質
		TALENT:MASTER:수수께끼의매력 = 0
		TALENT:MASTER:절륜 = 0
		TALENT:MASTER:음란 = 0
		TALENT:MASTER:창부 = 0
		TALENT:MASTER:모유체질 = 0
		TALENT:MASTER:인기 = 0
		FLAG:인기취득者数 = 0
		CFLAG:MASTER:第何子まで産んだか = 0
		CFLAG:MASTER:仕送り時してくれる人数親 = 0  
		;ふたごちゃんパッチ：最大人数はリセット
		CFLAG:MASTER:最大妊娠人数 = 0
	;	CFLAG:MASTER:キャラ固有の번호 = FLAG:今までのキャラ数
	;	FLAG:今までのキャラ数 += 1
		CFLAG:MASTER:요리인기 = 0
		CFLAG:MASTER:매춘인기 = 0
		CFLAG:MASTER:변기인기 = 0
		CFLAG:MASTER:劇場인기 = 0
		CFLAG:MASTER:아이돌인기 = 0
		IF TALENT:MASTER:남자 == 0 
			TALENT:MASTER:처녀 = 1
		ELSE
			CFLAG:MASTER:元처녀 = 1
			CFLAG:MASTER:元Ｖ감각 = 1
		ENDIF
		TALENT:MASTER:새드 = 0
		TALENT:MASTER:마조 = 0
		TALENT:MASTER:불결무시 = 0
		ITEM:【불결무시】 = 0
		TALENT:MASTER:조합지식 = 0
		ITEM:【조합지식】 = 0
		TALENT:MASTER:금단의지식 = 0
		ITEM:【금단의지식】 = 0
		TALENT:MASTER:깨달음 = 0
		ITEM:【서드・아이】 = 0
		TALENT:MASTER:촉수사역술 = 0
		ITEM:【촉수사역술】 = 0
		EX:MASTER:0 = 0
		EX:MASTER:1 = 0
		EX:MASTER:2 = 0
		EX:MASTER:3 = 0
		FLAG:주인출신 = 0
	ENDIF
ENDIF

;娘or息子マスター引き継ぎ
;---- EDIT 026 MOD START -------------------------
LOCAL:1 = 0
FOR LOCAL,0,CHARANUM
	IF TALENT:LOCAL:마스터의혈통 && FLAG:경험ED수 > 4
		LOCAL:1 = 0
		FOR LOCAL,0,CHARANUM
			SIF TALENT:LOCAL:마스터의혈통
				BREAK
			;SIF LOCAL == CHARANUM - 1
			;	RETURN 0
		NEXT

		PRINTL 딸 또는 아들에게 마스터를 인계하겠습니까？
		CALL INPUT_YN
		SIF RESULT == 0
			CALL MASTER_MUSUME
		BREAK
	ENDIF
NEXT
;---- EDIT 026 MOD END -------------------------
;ＴＳ경험が奇数の場合、元性別と現性別を別にする
;IF EXP:MASTER:ＴＳ경험 % 2 == 1
;	CFLAG:MASTER:元性別 = !TALENT:MASTER:남자
;ELSE
;	CFLAG:MASTER:元性別 = TALENT:MASTER:남자
;ENDIF

	;인기を초기化+ボーナス
	FLAG:요리인기 = MIN(FLAG:주회횟수 * 50,FLAG:요리인기,500)
	FLAG:매춘인기 = MIN(FLAG:주회횟수 * 50,FLAG:매춘인기,500)
	CFLAG:MASTER:요리인기 = MIN(FLAG:주회횟수 * 50,CFLAG:MASTER:요리인기,1000)
	CFLAG:MASTER:매춘인기 = MIN(FLAG:주회횟수 * 50,CFLAG:MASTER:매춘인기,500)
	CFLAG:MASTER:변기인기 = MIN(FLAG:주회횟수 * 50,CFLAG:MASTER:변기인기,500)
	CFLAG:MASTER:아이돌인기 = MIN(FLAG:주회횟수 * 50,CFLAG:MASTER:아이돌인기,1000)

PRINTW 주인의 속성을…
CALL INPUT_YN,"계승한다","초기화한다"
IF RESULT == 1
	CFLAG:MASTER:善悪치 = 128
	CFLAG:MASTER:秩混치 = 128
	ABL:MASTER:속성LD = 2
	ABL:MASTER:속성LC = 2
ENDIF
PRINTL 함락소질을 갖고 있는 인원 수를 이어받습니까？
PRINTL ※계승했을 경우에도、주회 횟수에 의해 감소되는 경우가 있습니다
CALL INPUT_YN,"Yes","No"
IF RESULT == 1
	FLAG:연모人数 = 0
	FLAG:음란人数 = 0
	FLAG:복종人数 = 0
	FLAG:信頼人数 = 0
	FLAG:奴隷매각総数 = 0
ENDIF

PRINTFORML 주인의 성별・체격 등의 설정을 계승합니까？
CALL INPUT_YN,"Yes","No"
SIF RESULT == 1
	CALL SET_MASTER_BASIC

;-----------------------------------------------------------------
;主人の전투特性
;-----------------------------------------------------------------
DRAWLINE
PRINTL 전투에 관한 요소는 모두 초기화됩니다
DRAWLINE
;스킬초기化
FOR LOCAL,1,FLAG:이능자스킬수+1
	LOCALS = 스킬{LOCAL}
	;例外を作りたければここで逃がす事
	IF TALENT:MASTER:마스터의혈통
	ELSE
		SELECTCASE ABL:MASTER:LOCALS
			CASEELSE
				ABL:MASTER:LOCALS = 0
		ENDSELECT
	ENDIF
NEXT

;能力値초기化
FOR LOCAL,2,VARSIZE("BASE")
	IF LOCAL != 7
		IF GET_BONUS("경험치")
			MAXBASE:MASTER:LOCAL = CSVBASE(NO:MASTER,LOCAL,0)
			BASE:MASTER:LOCAL = CSVBASE(NO:MASTER,LOCAL,0)
		ENDIF
	ENDIF
NEXT

FOR LOCAL,0,8
	CFLAG:MASTER:(1170+LOCAL) = 0
NEXT

;마장술・能力＆耐性初期化
CALL 장비강화_CSTR, "全해제：능력のみ", MASTER

;---- EDIT 031 MOD START -------------------------
;페르소나のステ설정 
FOR LOCAL, 0, 100
	IF DITEMTYPE:LOCAL:페르소나("장비상태")
		;페르소나のLvが11이상なら10まで下げる
		SIF DITEMTYPE:LOCAL:페르소나("NO") && DITEMTYPE:LOCAL:페르소나("LV") > 10
			DITEMTYPE:LOCAL:페르소나("LV") = 10
		;능력値を再설정。端数は四捨五入
		FOR LOCAL:7, 1, 7
			LOCAL:(LOCAL:7) = (CSVBASE(DITEMTYPE:LOCAL:페르소나("NO"), GETNUM(BASE,"힘")-1+LOCAL:7, 0) * (18 + DITEMTYPE:LOCAL:페르소나("LV") * 3 / 2) * 10) / (18 + CSVBASE(DITEMTYPE:LOCAL:페르소나("NO"), GETNUM(BASE,"LV"), 0) * 3 / 2)
			LOCAL:(LOCAL:7) = LOCAL:(LOCAL:7)%10 > 4 ? (LOCAL:(LOCAL:7)+5)/10 # LOCAL:(LOCAL:7)/10
		NEXT
		;それでも既定値미만なら補填
		WHILE SUMARRAY(LOCAL, 1, 7) < (18 + DITEMTYPE:LOCAL:페르소나("LV") * 3 / 2)
			LOCAL:((LOCAL:7)%6+1) += 1
			LOCAL:7++
		WEND
		FOR LOCAL:7, 1, 7
			DITEMTYPE:LOCAL:(페르소나("힘")-1+LOCAL:7) = LOCAL:(LOCAL:7)
		NEXT
;		DITEMTYPE:LOCAL:페르소나("LV") = 1
;		DITEMTYPE:LOCAL:페르소나("힘") = 3
;		DITEMTYPE:LOCAL:페르소나("지혜") = 3
;		DITEMTYPE:LOCAL:페르소나("마력") = 3
;		DITEMTYPE:LOCAL:페르소나("인내력") = 3
;		DITEMTYPE:LOCAL:페르소나("속도") = 3
;		DITEMTYPE:LOCAL:페르소나("운") = 3
		DITEMTYPE:LOCAL:페르소나("EXP") = 0
		DITEMTYPE:LOCAL:페르소나("랭크") = 0
		DITEMTYPE:LOCAL:페르소나("강화가능횟수") = 0
	ENDIF
NEXT
;---- EDIT 031 MOD END   -------------------------

;特性초기化
CALL SET_MASTER_MAJORING
;---- EDIT 004 MOD START -------------------------
;---- EDIT 041 MOD START -------------------------
IF TALENT:MASTER:페르소나구사자 == 0 && CFLAG:MASTER:초기페르소나넘버
	FOR LOCAL:1 , 0 , 50
		DITEMTYPE:(CFLAG:MASTER:초기페르소나넘버):(LOCAL:1) = 0
	NEXT
	CFLAG:MASTER:페르소나강화플래그 = 0
	CFLAG:MASTER:초기페르소나넘버 = 0
	ABL:MASTER:초기페르소나 = 0
ELSEIF TALENT:MASTER:페르소나구사자 && ABL:MASTER:초기페르소나 != ABL:MASTER:초기페르소나3 && !ABL:MASTER:초기페르소나2
	CFLAG:MASTER:페르소나강화플래그 = 0
ELSEIF TALENT:MASTER:페르소나구사자 && ABL:MASTER:초기페르소나 != ABL:MASTER:초기페르소나2
	CFLAG:MASTER:페르소나강화플래그 = 0
ENDIF
;---- EDIT 041 MOD END -------------------------
;---- EDIT 004 MOD END -------------------------

;---- EDIT 031 MOD CANCEL -------------------------
;-----------------------------------------------------------------
;페르소나削除
;主人公の페르소나決定に使いたいので、ここでやる
;よく考えたら0~10まで削除すればいい話でした
;-----------------------------------------------------------------
FOR LOCAL, 0, 12
	FOR LOCAL:1 , 0 , 50
		DITEMTYPE:LOCAL:(LOCAL:1) = 0
	NEXT
NEXT
;---- EDIT 027 MOD END   -------------------------

;-----------------------------------------------------------------
;主人の特性・まとめ
;-----------------------------------------------------------------
CALL SET_MASTER_NAME
CFLAG:MASTER:노역플래그 = 0
IF GET_BONUS("경험치")
	BASE:MASTER:ＥＸＰ = MAX(200, BASE:MASTER:ＥＸＰ)
ELSE
	BASE:MASTER:ＥＸＰ = 200
ENDIF
BASE:MASTER:ＭＡＧ = 3000
;포지션2に指定
;CFLAG:MASTER:포지션 = 2
;FLAG:포지션2 = 0
;CFLAG:MASTER:PT플래그 = 2
;普通に考えて、この時点で主人がパーティから居ない事態のほうがおかしい
LOCAL:1 = 0
;主人の能力ボーナスは見たEDの数-周回ボーナスコスト)*3
;ボーナス充当済みの이능자・달인以外は+12
;---- EDIT 021 MOD START -------------------------
CALL SYNC_STATUS,0
CALL STATUS_BONUS,MASTER,(TALENT:MASTER:이능자 || TALENT:MASTER:달인) ? (COUNT_ENDING_POINT()-REQUIRE_ENDING_POINT())*3 # 12+((COUNT_ENDING_POINT()-REQUIRE_ENDING_POINT())*3)
;---- EDIT 021 MOD END ---------------------------
;能力の同期化
CALL SYNC_STATUS,0
CALL HEALTH_CHARA,0
CALL REFRESH_POS
;-----------------------------------------------------------------
;ＣＯＭＰ용량は8+見たEDの数-周回ボーナスコスト
;-----------------------------------------------------------------
FLAG:ＣＯＭＰ용량 = 8+COUNT_ENDING_POINT()-REQUIRE_ENDING_POINT()
FLAG:魔貨によるＣ容量増加 = FLAG:ＣＯＭＰ용량 - 7;037
LOCAL:2 = COUNT_ENDING_POINT()-REQUIRE_ENDING_POINT()

;---- EDIT 018 ADD START -------------------------
;저주의레드카펫설정
EQUIP:MASTER:저주의레드카펫 = ITEM:저주의레드카펫
;---- EDIT 018 ADD END -------------------------

;-----------------------------------------------------------------
;소지金の調整
;-----------------------------------------------------------------
;￥
LOCAL:1 = MONEY / 100
LOCAL:1 = TABLE_CALC2(LOCAL:1, 1000, 90)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 2500, 80)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 5000, 70)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 10000, 60)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 25000, 50)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 50000, 25)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 100000, 10)
MONEY = LOCAL:1
;ＥＤ発見毎に+2000の초기소지金
;周回するたびに+500の초기소지金
;ただし周回ボーナスの最高額は100万までとする(そこまで周回を重ねるのも困難な話だが)
;3500円保障
MONEY += 1000 + 2000 * (LOCAL:2 + 1)
MONEY += MIN(500 * FLAG:주회횟수, 1000000)

;마카
LOCAL:1 = MONEY:1 / 100
LOCAL:1 = TABLE_CALC2(LOCAL:1, 20, 90)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 50, 80)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 100, 70)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 200, 60)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 500, 50)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 1000, 25)
LOCAL:1 = TABLE_CALC2(LOCAL:1, 2000, 10)
MONEY:1 = LOCAL:1
;ＥＤ発見毎に+25の초기소지마카
;周回するごとに発見ＥＤ×周횟수×5の초기소지마카
;ただし周回ボーナスの最高額は100万までとする(そこまで周回を重ねるのも困難な話だが)
MONEY:1 += 25 * LOCAL:2
MONEY:1 += MIN(LOCAL:2 * FLAG:주회횟수 * 5, 1000000)

;정크 포인트
;정크 포인트は持越しできない
MONEY:4 = 0

CALL OPENING

;=============================================================================
;テーブル計算汎用処理
;=============================================================================
;-------------------------------------------------
;テーブル計算その１
;-------------------------------------------------
;テーブル計算用
;ARG:0 = 代入すべき変数(以下元変数)より引き渡された数
;ARG:1 ・ ARG:2 = 元変数がARG:2より大きいならばARG:1を引いてARG:2を割る
@TABLE_CALC(ARG, ARG:1, ARG:2)
#FUNCTION
LOCAL = ARG
SIF LOCAL > ARG:1
	LOCAL = ARG:1 + (LOCAL - ARG:1) / ARG:2

RETURNF LOCAL

;-------------------------------------------------
;テーブル計算その２
;-------------------------------------------------
;テーブル計算用
;ARG:0 = 代入すべき変数(以下元変数)より引き渡された数
;ARG:1 ・ ARG:2 = 元変数がARG:2より大きいならばARG:1を引いそれに、ARG:2の100分の1の値を掛ける
@TABLE_CALC2(ARG, ARG:1, ARG:2)
#FUNCTION
LOCAL = ARG
SIF LOCAL > ARG:1
	LOCAL = ARG:1 + (LOCAL - ARG:1) * ARG:2 / 100

RETURNF LOCAL

;=============================================================================
;引き継ぎ嫁選択
;=============================================================================
@SELECT_YOME
#DIM LCOUNT

LOCAL:1 = 0
FOR LOCAL,0,CHARANUM
;---- EDIT 024 MOD START -------------------------
	; SIF TALENT:LOCAL:아내 || TALENT:LOCAL:남편 || TALENT:LOCAL:음마 || TALENT:LOCAL:노리개 || CFLAG:LOCAL:引継ぎ횟수
	SIF TALENT:LOCAL:아내 || TALENT:LOCAL:남편 || TALENT:LOCAL:음마 || TALENT:LOCAL:노리개 || TALENT:LOCAL:맹우 || CFLAG:LOCAL:引継ぎ횟수
;---- EDIT 024 MOD END -------------------------
		BREAK
	SIF LOCAL == CHARANUM - 1
		RETURN 0
NEXT

PRINTL 일부 캐릭터를 다음 주차로 계승합니까？
CALL INPUT_YN
IF RESULT == 1
	RETURN 0
ENDIF

P = 0
;표시させるキャラを抽出（Q:*に표시するキャラの登録番号、LOCAL:2に人数）
VARSET Q, -1
LOCAL:2 = 0
FOR LCOUNT, 0, CHARANUM
	SIF LCOUNT == MASTER
		CONTINUE
;---- EDIT 024 MOD START -------------------------
	; SIF (TALENT:LCOUNT:아내 || TALENT:LCOUNT:남편 || TALENT:LCOUNT:음마 || TALENT:LCOUNT:노리개) == 0 && CFLAG:LCOUNT:引継ぎ횟수 == 0
	SIF (TALENT:LCOUNT:아내 || TALENT:LCOUNT:남편 || TALENT:LCOUNT:음마 || TALENT:LCOUNT:노리개 || TALENT:LCOUNT:맹우) == 0 && CFLAG:LCOUNT:引継ぎ횟수 == 0
;---- EDIT 024 MOD END -------------------------
		CONTINUE
	Q:(LOCAL:2) = LCOUNT
	LOCAL:2 += 1
NEXT

$PRINT_LIST
LOCAL:10 = 0
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:引継ぎフラグ > 0
		LOCAL:10 += 1

NEXT

DRAWLINE
;PRINTFORML 引き継ぐキャラを選んでください　引き継ぎ人数：{LOCAL:10}/{1+FLAG:주회횟수/2}　＜page.{P}＞
PRINTFORML 계승할 캐릭터를 선택하십시오　＜page.{P + 1}＞
DRAWLINE
;---- EDIT 002 ADD START -------------
;대상キャラが二人以上の場合に全員を변경する選択肢を표시する。
SIF LOCAL:2 >= 2
	PRINTFORML [999] 계승 가능한 모든 캐릭터의 계승을 변경한다。
DRAWLINE
;---- EDIT 002 ADD END ---------------

FOR LOCAL, P * リスト표시数(), (P + 1) * リスト표시数()
	IF Q:(LOCAL) > -1
		PRINTFORM [{Q:LOCAL}] %CALLNAME:(Q:LOCAL)%   
		IF CFLAG:(Q:LOCAL):引継ぎフラグ == 1
			PRINTL ○ 계약・함락 소질을 이어받음
		ELSEIF CFLAG:(Q:LOCAL):引継ぎフラグ == 2
			PRINTL ○ 계약・함락 소질을 리셋
		ELSEIF CFLAG:(Q:LOCAL):引継ぎフラグ == 3
			PRINTL ○ 경험을 리셋
		ELSEIF CFLAG:(Q:LOCAL):引継ぎフラグ == 4
			PRINTL ○ 조교 능력을 리셋
		ELSE
			PRINTL × 이어받지 않는다
		ENDIF
	ENDIF
NEXT

DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 16% # [1007]이전 페이지\@
PRINTLC [1000] 이어받을 캐릭터 선택 종료
PRINTFORMLC \@(P >= (LOCAL:2 - 1) / リスト표시数()) ? %" " * 16% # [1009]다음 페이지\@

$INPUT_LOOP_1
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1007
	IF P > 0
		P -= 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP_1
	ENDIF
ELSEIF RESULT == 1009
	IF P < LOCAL
		P += 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP_1
	ENDIF
;---- EDIT 002 MOD START -------------
ELSEIF (RESULT < 0 || RESULT >= CHARANUM || FINDELEMENT(Q, RESULT) == -1) && RESULT != 999
;---- EDIT 002 MOD END ---------------
	GOTO INPUT_LOOP_1
ENDIF

LOCAL:5 = RESULT

;一括引継ぎの場合、間違い対策として確認を추가
IF RESULT == 999
	PRINTL 계승 가능한 모든 캐릭터의 계승 설정을 변경합니다。
ENDIF

;IF CFLAG:(LOCAL:5):引継ぎフラグ == 0 && LOCAL:10 >= 1+FLAG:주회횟수/2
;	GOTO INPUT_LOOP_1
;ENDIF

PRINTL [0] 이어받지 않는다
PRINTL [1] 계약・함락 소질을 이어받음
PRINTL [2] 계약・함락 소질을 리셋
PRINTL [3] 경험을 리셋
PRINTL [4] 조교 능력을 리셋

$INPUT_LOOP2
INPUT
SIF RESULT < 0 || RESULT > 4
	GOTO INPUT_LOOP_1

;---- EDIT 002 MOD START -------------
IF LOCAL:5 == 999
	;대상キャラが二人未満の場合は無効
	SIF LOCAL:2 < 2
		GOTO INPUT_LOOP_1
	;一括選択
	FOR LOCAL,0,LOCAL:2
		CFLAG:(Q:LOCAL):引継ぎフラグ = RESULT
	NEXT
ELSE
	;個別選択
	CFLAG:(LOCAL:5):引継ぎフラグ = RESULT
ENDIF
;---- EDIT 002 MOD END ---------------

GOTO PRINT_LIST

;周回ボーナスを何個受けてるか計算
;受けた数に応じて能力とCOMPへのボーナスが下がる
@REQUIRE_ENDING_POINT
#FUNCTION
VARSET LOCAL
FOR LOCAL:1,0,63
	SIF !GETBIT(FLAG:周回ボーナス,LOCAL:1)
		CONTINUE
	LOCAL += CHECK_ED_POINT(GETBIT_BONUS_NAME(LOCAL:1))
NEXT
RETURNF LOCAL
;VARSET LOCAL
;FOR LOCAL:1,0,64
;	SIF GETBIT(FLAG:周回ボーナス,LOCAL:1)
;		LOCAL++
;NEXT
;RETURNF LOCAL

@COUNT_ENDING_POINT
#FUNCTION
VARSET LOCAL
FOR LOCAL,0,64
	SIF GETBIT(FLAG:발견ED , LOCAL)
		LOCAL:1++
	SIF GETBIT(FLAG:발견ED2 , LOCAL)
		LOCAL:1++
NEXT
RETURNF LOCAL:1

;이상時は-1で返すとTRUE(!=0)扱いされて面倒そうなのでFALSE(0)で返す
;GET_BONUS("조교아이템")
;GET_BONUS("무구아이템")
;GET_BONUS("방어구아이템")
;GET_BONUS("소비아이템")
;GET_BONUS("合成소재")
;GET_BONUS("마정무기")
@GET_BONUS(ARGS)
#FUNCTION
LOCAL = GETBIT_BONUS_NO(ARGS)
SIF LOCAL == -1
	RETURNF 0
RETURNF GETBIT(FLAG:周回ボーナス,LOCAL)

;引継ぎに필요なEDポイント
;조교아이템	1点
;무기아이템	3点
;방어구아이템	3点
;소비아이템	1点
;소재아이템	2点
;마정무기		5点
;경험치			5点
;受胎アイテムリセット	10点　追加：受胎アイテム取得状況をリセットする。
;						要素としてデカめなのでポイントは多めに。たっちゃんをノヴァサイザー産む機械にしないためのポイント
;インストールソフト 4点 レベル制限がなく、引き継いですぐに使えるのでちょっと増やす。
;全て取得には20+10+4EP必要
@CHECK_ED_POINT(ARGS)
#FUNCTION
SELECTCASE ARGS
CASE "조교아이템","소비아이템"
	RETURNF 1
CASE "소재아이템"
	RETURNF 2
CASE "무구아이템","방어구아이템"
	RETURNF 3
CASE "인스톨소프트"
	RETURNF 4
CASE "마정무기","경험치"
	RETURNF 5
CASE "수태아이템리셋"
	RETURNF 10
ENDSELECT
RETURNF 666

@GETBIT_BONUS_NAME(ARG)
#FUNCTIONS
SELECTCASE ARG
CASE 0
	RETURNF "조교아이템"
CASE 1
	RETURNF "무구아이템"
CASE 2
	RETURNF "방어구아이템"
CASE 3
	RETURNF "소비아이템"
CASE 4
	RETURNF "소재아이템"
CASE 5
	RETURNF "마정무기"
CASE 6
	RETURNF "경험치"
CASE 7
	RETURNF "수태아이템리셋"
CASE 8
	RETURNF "인스톨소프트"
ENDSELECT
RETURNF "NONE"

@GETBIT_BONUS_NO(ARGS)
#FUNCTION
FOR LOCAL,0,64
	SIF GETBIT_BONUS_NAME(LOCAL) == ARGS
		RETURNF LOCAL
NEXT
RETURNF -1

@NEWGAME_BONUS
#DIM L_LINE
REDRAW 0
L_LINE = LINECOUNT
$BONUS_TOP
RESETCOLOR
DRAWLINE
PRINTFORML 계승할 요소를 선택해 주십시오
PRINTFORML 현재EP(Ending Point)를 {COUNT_ENDING_POINT()}점 가지고 있습니다
PRINTFORML 선택중인 요소를 계승하려면 {REQUIRE_ENDING_POINT()} 포인트가 필요합니다
IF COUNT_ENDING_POINT()-REQUIRE_ENDING_POINT() < 0
	PRINTFORML EP가 부족합니다
ELSE
	PRINTFORML 능력 보너스+{(COUNT_ENDING_POINT()-REQUIRE_ENDING_POINT())*3} COMP 보너스+{COUNT_ENDING_POINT()-REQUIRE_ENDING_POINT()}
ENDIF
SETCOLOR COLOR("RED")
PRINTFORML ※조건에 구애받지 않고 계승됩니다／계승되지 않는 아이템도 있습니다
RESETCOLOR
DRAWLINE
FOR LOCAL,0,64
	SIF GETBIT_BONUS_NAME(LOCAL) == "NONE"
		CONTINUE
		;한판 레이아웃 수정 12>15, 20>21
	LOCALS = %GETBIT_BONUS_NAME(LOCAL),15,LEFT%\({CHECK_ED_POINT(GETBIT_BONUS_NAME(LOCAL))}EP\)
	IF GETBIT(FLAG:周回ボーナス,LOCAL)
		SETCOLOR COLOR("AQUA")
		PRINTFORML [{LOCAL,2}] %LOCALS,21,LEFT% 계승한다
	ELSE
		PRINTFORML [{LOCAL,2}] %LOCALS,21,LEFT% 계승하지 않는다
	ENDIF
	RESETCOLOR
NEXT
PRINTFORML [98] 리셋
IF COUNT_ENDING_POINT()-REQUIRE_ENDING_POINT() < 0
	PRINTFORML [99] EP가 부족합니다
ELSE
	PRINTFORML [99] 설정완료
ENDIF
$INPUT_LOOP
INPUT
IF RESULT == 99 && COUNT_ENDING_POINT()-REQUIRE_ENDING_POINT() >= 0
	REDRAW 0
	RETURN
ELSEIF RESULT == 98
	FLAG:周回ボーナス = 0
ELSEIF GETBIT_BONUS_NAME(RESULT) != "NONE"
	INVERTBIT FLAG:周回ボーナス,RESULT
ENDIF
CLEARLINE LINECOUNT-L_LINE
GOTO BONUS_TOP

