
@SIAWASE_TICKET
LOCAL:11 = LINECOUNT
$LP
CLEARLINE LINECOUNT - LOCAL:11
VARSET RESULTS
VARSET X

PRINTFORML 무엇과 바꾸겠습니까？		행복티켓 {ITEM:361}매
DRAWLINE
CALL TICKET_EXCHANGE_표시,1,"스킬카드상자 LV1",5
SIF FLAG:組織貢献度 + FLAG:名声 * 100 >= 60000
	CALL TICKET_EXCHANGE_표시,2,"스킬카드상자 LV2",15
SIF FLAG:組織貢献度 + FLAG:名声 * 100 >= 210000
	CALL TICKET_EXCHANGE_표시,3,"스킬카드상자 LV3",35
SIF FLAG:組織貢献度 + FLAG:名声 * 100 >= 860000
	CALL TICKET_EXCHANGE_표시,4,"스킬카드상자 LV4",90
SIF FLAG:組織貢献度 + FLAG:名声 * 100 >= 2260000
	CALL TICKET_EXCHANGE_표시,5,"스킬카드상자 LV5",190
CALL TICKET_EXCHANGE_표시,6,"P성장카드상자",80



PRINTL 
PRINTL [1000] 행복티켓이란？
PRINTL [1001] 스킬카드상자에서 등장하는 스킬목록
CUSTOMDRAWLINE =
PRINTL [99] 돌아간다 [100] 가게를 나간다
INPUT
IF RESULT == 99
	VARSET RESULTS
	RETURN -1
ELSEIF RESULT == 100
	VARSET RESULTS
	RETURN 0
ELSEIF RESULT == 1000
	PRINTL 점주：
	PRINTW 「우리 가게에서 물건을 사면 받을수 있는 티켓이다」
	PRINTW 「￥1000 이상 물건을 사면 한장씩 주고 있지」
	PRINTW 「다만, JP로 물건을 사는건 포함되지 않으니 유의하라구」
	GOTO LP
ELSEIF RESULT == 1001
	FOR LOCAL:1,1,6
		CALL SKILL_STOCK_AD,LOCAL:1 * 3 - 2,LOCAL:1 * 3
		PRINTFORML 스킬카드상자 LV{LOCAL:1}
		FOR LOCAL, 0 ,A
			CALLFORM SKILL_NAME_{D:LOCAL},-1
			PRINTFORM %RESULTS,22%
			SIF LOCAL % 3 == 2
				PRINTL
		NEXT
		SETCOLOR 0xffff00
		SELECTCASE LOCAL:1
		CASE 2
			PRINTFORML %"시크릿 2종",22%
		CASE 3
			PRINTFORML %"시크릿 3종",22%
		CASE 4
			PRINTFORML %"시크릿 2종",22%
		CASE 5
			PRINTFORML %"시크릿 13종",22%
		ENDSELECT
		RESETCOLOR
		PRINTL
		PRINTW
	NEXT
	GOTO LP
ENDIF
SIF RESULT > 999
	GOTO LP
SIF X:RESULT < 1 || ITEM:361 < X:RESULT
	GOTO LP
LOCAL = RESULT
PRINTFORML {X:LOCAL}매 사용하여 %RESULTS:LOCAL%로 바꾸겠습니까?
PRINTL [0] 네 [1] 아니오
INPUT
SIF RESULT == 1
	GOTO LP
$ONEMORE
ITEM:361 -= X:LOCAL

SELECTCASE LOCAL
CASE 1
	CALL SKILL_STOCK_AD,1,3
	IF A < 1
		PRINTW 스킬을 취득하지 못했습니다.
		GOTO LP
	ENDIF
	LOCAL:1 = D:(RAND:A)
	CALLFORM SKILL_NAME_{LOCAL:1},-1
	CALL GET_ITEMS(ITEMNAME:(10000 + LOCAL:1),1)
	PRINTFORML %ITEMNAME:(10000 + LOCAL:1)%을 손에 넣었다
CASE 2
	CALL SKILL_STOCK_AD,4,6
	IF A < 1
		PRINTW 스킬을 취득하지 못했습니다.
		GOTO LP
	ENDIF
	D:(A++) = 53
	D:(A++) = 247
	LOCAL:2 = RAND:A
	LOCAL:1 = D:(LOCAL:2)
	SIF LOCAL:2 > A - 3
		PRINTFORML 시크릿！
	CALLFORM SKILL_NAME_{LOCAL:1},-1
	CALL GET_ITEMS(ITEMNAME:(10000 + LOCAL:1),1)
	PRINTFORML %ITEMNAME:(10000 + LOCAL:1)%을 손에 넣었다
CASE 3
	CALL SKILL_STOCK_AD,7,9
	IF A < 1
		PRINTW 스킬을 취득하지 못했습니다.
		GOTO LP
	ENDIF
	D:(A++) = 838
	D:(A++) = 1206
	D:(A++) = 1103
	LOCAL:2 = RAND:A
	LOCAL:1 = D:(LOCAL:2)
	SIF LOCAL:2 > A - 4
		PRINTFORML 시크릿！
	CALLFORM SKILL_NAME_{LOCAL:1},-1
	CALL GET_ITEMS(ITEMNAME:(10000 + LOCAL:1),1)
	PRINTFORML %ITEMNAME:(10000 + LOCAL:1)%을 손에 넣었다
CASE 4
	CALL SKILL_STOCK_AD,10,12
	IF A < 1
		PRINTW 스킬을 취득하지 못했습니다.
		GOTO LP
	ENDIF
	D:(A++) = 1320
	D:(A++) = 1408
	LOCAL:2 = RAND:A
	LOCAL:1 = D:(LOCAL:2)
	SIF LOCAL:2 > A - 3
		PRINTFORML 시크릿！
	CALLFORM SKILL_NAME_{LOCAL:1},-1
	CALL GET_ITEMS(ITEMNAME:(10000 + LOCAL:1),1)
	PRINTFORML %ITEMNAME:(10000 + LOCAL:1)%을 손에 넣었다
CASE 5
	CALL SKILL_STOCK_AD,13,15
	IF A < 1
		PRINTW 스킬을 취득하지 못했습니다.
		GOTO LP
	ENDIF
	LOCAL:2 = RAND:(A + 1)
	IF LOCAL:2 == A
		PRINTFORML 시크릿！
		IF !RAND:13
			LOCAL:1 = 59
		ELSEIF !RAND:12
			LOCAL:1 = 147
		ELSEIF !RAND:11
			LOCAL:1 = 249
		ELSEIF !RAND:10
			LOCAL:1 = 314
		ELSEIF !RAND:9
			LOCAL:1 = 718
		ELSEIF !RAND:8
			LOCAL:1 = 825
		ELSEIF !RAND:7
			LOCAL:1 = 908
		ELSEIF !RAND:6
			LOCAL:1 = 1016
		ELSEIF !RAND:5
			LOCAL:1 = 1516
		ELSEIF !RAND:4
			LOCAL:1 = 1613
		ELSEIF !RAND:3
			LOCAL:1 = 1714
		ELSEIF !RAND:2
			LOCAL:1 = 1814
		ELSE
			LOCAL:1 = 1914
		ENDIF
	ELSE
		LOCAL:1 = D:(LOCAL:2)
	ENDIF
	CALLFORM SKILL_NAME_{LOCAL:1},-1
	CALL GET_ITEMS(ITEMNAME:(10000 + LOCAL:1),1)
	PRINTFORML %ITEMNAME:(10000 + LOCAL:1)%을 손에 넣었다
CASE 6
	SELECTCASE RAND:100
	CASE IS < 1
		LOCAL:1 = 219
	CASE IS < 4
		LOCAL:1 = 218
	CASE IS < 10
		LOCAL:1 = 217
	CASE IS < 20
		LOCAL:1 = 216
	CASEELSE
		LOCAL:1 = 215
	ENDSELECT
	CALL GET_ITEMS(ITEMNAME:(LOCAL:1),1)
	PRINTFORML %ITEMNAME:(LOCAL:1)%을 손에 넣었다
ENDSELECT

IF ITEM:361 >= X:LOCAL
	PRINTFORML [0]그만둔다　[1]한 번 더（{ITEM:361}장 남음）
	ONEINPUT
	SIF RESULT == 1
		GOTO ONEMORE
ELSE
	PRINTFORMW
ENDIF
GOTO LP

@TICKET_EXCHANGE_표시,ARG,ARGS,ARG:1
;ARG	クリック番号
;ARGS	名前
;ARG:1	사용チケット数

;Xの配列に사용チケット数を入れる
;RESULTSの配列に使名前を入れる
SIF ITEM:361 < ARG:1
	SETCOLOR COLOR("RED")
PRINTFORM [{ARG,2}]
PRINTFORM %ARGS,20%　
PRINTFORML 　{ARG:1}장
RESETCOLOR
X:ARG = ARG:1
RESULTS:ARG = %ARGS%

@SKILL_STOCK_AD,ARG = 1,ARG:1 = 15,ARG:2 = 1,ARG:3 = 2500
;ARG:0　최저랭크　　　초기値1
;ARG:1　최대랭크　　　초기値15
;ARG:2　開始스킬番号　초기値1
;ARG:3　終了스킬番号　초기値2500
;Dの配列に스킬番号を入れていき
;A:0にDに入った스킬数を入れる
SIF ARG:3 > 3900
	ARG:3 = 3900
VARSET D
A = 0
FOR LOCAL, ARG:2 ,ARG:3
	;총～システム系はスキップ
	SIF LOCAL >= 2101 && LOCAL < 2400
		CONTINUE
	;장비지식はスキップ
	SIF LOCAL >= 2438 && LOCAL < 2443
		CONTINUE
	;가드킬、브레이크はスキップ
	SIF LOCAL == 30 || LOCAL == 128 || LOCAL == 227 || LOCAL == 338 || LOCAL == 732 || LOCAL == 831 || LOCAL == 932
		CONTINUE
	SIF LOCAL == 1031 || LOCAL == 1133 || LOCAL == 1231 || LOCAL == 1331 || LOCAL == 1431 || LOCAL == 1531 || LOCAL == 1631 || LOCAL == 1731 || LOCAL == 1831 || LOCAL == 1931
		CONTINUE
	SIF LOCAL == 31 || LOCAL == 129 || LOCAL == 228 || LOCAL == 339 || LOCAL == 733 || LOCAL == 832 || LOCAL == 933
		CONTINUE
	SIF LOCAL == 1032 || LOCAL == 1134 || LOCAL == 1232 || LOCAL == 1332 || LOCAL == 1432 || LOCAL == 1532 || LOCAL == 1632 || LOCAL == 1732 || LOCAL == 1832 || LOCAL == 1932
		CONTINUE
	;民族学、아이템숙련はスキップ
	SIF LOCAL == 2431 || LOCAL == 2430
		CONTINUE
	;영웅스킬はスキップ（継承可能だけど영웅からしか取れないやつ）
	SIF LOCAL == 55 || LOCAL == 564 || LOCAL == 2048 || LOCAL == 2012 || LOCAL == 222 || LOCAL == 39 || LOCAL == 56 || LOCAL == 617 || LOCAL == 57 || LOCAL == 241 || LOCAL == 356 || LOCAL == 146
		CONTINUE
	;ボス스킬はスキップ（継承可能だけどボスからしか取れないやつ）
	SIF LOCAL == 2019 || LOCAL == 2070 || LOCAL == 2018
		CONTINUE
	;아이템系はスキップ
	SIF LOCAL >= 3001 && LOCAL < 3401
		CONTINUE
	
	;계승타입による판정
	RESULT = 0
	TRYCALLFORM 적専用_{LOCAL}
	SIF RESULT
		CONTINUE
	;まず継承不能스킬か체크
	TRYCALLFORM 継承不能_{LOCAL}
	IF RESULT
		CONTINUE
	ELSE
		TRYCCALLFORM SKILL_SUCCESSION_TYPE_{LOCAL}
			SIF RESULT < 0 || 22 < RESULT
				CONTINUE
		CATCH
			CONTINUE
		ENDCATCH
	ENDIF
	CALLFORM SKILL_RANK_{LOCAL}
	SIF RESULT <= 0 || RESULT > 15
		CONTINUE
	SIF RESULT < ARG || RESULT > ARG:1
		CONTINUE
	D:A = LOCAL
	A++
NEXT


