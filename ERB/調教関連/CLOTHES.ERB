;着衣処理の改定
;着る服をCFLAGに格納して、カスタムできるようにする
;
;	Edit	Date			Author					Reason
;	000		20xx/xx/xx		----					新規作成
;	001		2022/06/07		Collared				UI整理プロジェクトにより配置変更
;-------------------------------------------------
@SHOPCOMABLE_208
RESULT:1 = 2
RESULTS = 의상방
RETURN 1

@SHOP_COM_208
CALL CLOTHES_SETTING_USUALLY

;=================================================
;착의に関するFUNCTION
;=================================================
;착의部位の번호
@GET_CLOTHES(ARGS)
#FUNCTION
RETURNF GETNUM(TEQUIP,ARGS) - GETNUM(TEQUIP,"모자")

@NAME_EXPOSE(ARG)
#FUNCTIONS
RETURNF TEQUIPNAME:(GETNUM(TEQUIP,"팔노출")+ARG)

@GET_CLOTHESNAME(ARG)
#FUNCTIONS
IF ARG < 12
	RETURNF TEQUIPNAME:(GETNUM(TEQUIP,"모자")+ARG)
ELSE
	RETURNF TEQUIPNAME:(GETNUM(TEQUIP,"그외2")+(ARG-12))
ENDIF

@CLOTHESNAMEF(ARG = -99,ARG:1)
#FUNCTIONS
SIF ARG == -99
	ARG = TARGET
; ;그외 확장패치 주석처리
; IF ARG:1 < 0 || ARG:1 > 16
IF ARG:1 < 0 || ARG:1 > 13
	PRINTFORM %CALLNAME:ARG%번의 의상명을 참조할 때에 부정한 번호 {ARG:1}이 의상의 부위로서 지정되었습니다
	PRINTFORM 의도하지 않은 동작・오류일 가능성이 높으므로 보고를 부탁드립니다
	RETURNF "잘못된 옷이름"
ENDIF
RETURNF ITEMNAME:(6000 + CFLAG:ARG:(GET_CLOTHESNAME(ARG:1)))

;=================================================
;노출状況の체크
;=================================================
@CHECK_EXPOSE(ARG)
#DIM 의상부위 , 1
#DIM 조사부위 , 1
#LOCALSIZE 14 ;17 그외 확장패치 주석처리
FOR 조사부위,0,8
	VARSET LOCAL
	TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = -1
	
	SIF (조사부위 == 3 || 조사부위 == 4) && TEQUIP:ARG:가슴앞열림 == 1
		CONTINUE
	
	FOR 의상부위,0,14 ;17 그외 확장패치 주석처리
		TRYCALLFORM CLOTHES_EXPOSE_{TEQUIP:ARG:(GET_CLOTHESNAME(의상부위))},조사부위+1
		SIF RESULT == 0
			LOCAL:의상부위 = 1
	NEXT

	;그외→겉옷→전신복→옷→하의→모자→신발→전신속옷→손→양말→下着上下
	; 그외 확장패치 주석처리
	; IF LOCAL:16
	; 	TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 16
	; ELSEIF LOCAL:15
	; 	TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 15
	; ELSEIF LOCAL:14
	; 	TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 14
	IF LOCAL:13
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 13
	ELSEIF LOCAL:12
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 12
	ELSEIF LOCAL:11
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 11
	ELSEIF LOCAL:10
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 10
	ELSEIF LOCAL:3
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 3
	ELSEIF LOCAL:1
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 1
	ELSEIF LOCAL:2
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 2
	ELSEIF LOCAL:0
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 0
	ELSEIF LOCAL:9
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 9
	ELSEIF LOCAL:7
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 7
	ELSEIF LOCAL:4
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 4
	ELSEIF LOCAL:8
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 8
	ELSEIF LOCAL:5
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 5
	ELSEIF LOCAL:6
		TEQUIP:ARG:(NAME_EXPOSE(조사부위)) = 6
	ENDIF
NEXT

@CHECK_EXP(ARG)
; FOR LOCAL,0,17 ; 그외 확장패치 주석처리
FOR LOCAL,0,14
		IF TEQUIP:ARG:GET_CLOTHESNAME(LOCAL)
			TRYCALLFORM CLOTHES_EXP_{TEQUIP:ARG:GET_CLOTHESNAME(LOCAL)},ARG
		ENDIF
NEXT

@CHECK_SOURCE(ARG)
; FOR LOCAL,0,17 ; 그외 확장패치 주석처리
FOR LOCAL,0,14
		IF TEQUIP:ARG:GET_CLOTHESNAME(LOCAL)
			TRYCALLFORM CLOTHES_SOURCE_{TEQUIP:ARG:GET_CLOTHESNAME(LOCAL)},ARG
		ENDIF
NEXT
;FOR LOCAL,0,80
;	LOCAL:2 = 0
;	FOR LOCAL:1,0,14
;		IF TEQUIP:ARG:GET_CLOTHESNAME(LOCAL:1)
;			TRYCALLFORM CLOTHES_EXP_{TEQUIP:ARG:GET_CLOTHESNAME(LOCAL:1)},LOCAL
;			LOCAL:2 += RESULT
;		ENDIF
;	NEXT
;	EXP:ARG:LOCAL = EXP:ARG:LOCAL + LOCAL:2
;	IF LOCAL:2 > 0
;		SIF ARG == TARGET
;			PRINTFORML %EXPNAME:LOCAL%+{LOCAL:2}
;		SIF ARG == ASSI
;			PRINTFORML %EXPNAME:LOCAL%+{LOCAL:2}(조수)
;		SIF ARG == MASTER
;			PRINTFORML %EXPNAME:LOCAL%+{LOCAL:2}(主人)
;	ENDIF
;NEXT

;=================================================
;촉각상황 체크
;=================================================
@촉각체크,ARG
#DIM 의상부위 , 1
#DIM 조사부위 , 1
#DIM 촉각 , 14 ;17 그외 확장패치 주석처리
#LOCALSIZE 3
#LOCALSSIZE 2

FOR 조사부위 , 0 , 5
	VARSET 촉각 , 1
	SELECTCASE 조사부위
		CASE 0
			LOCALS = Ｃ
			LOCALS:1 = 음순
			LOCAL = 7
			LOCAL:1 = 0
		CASE 1
			LOCALS = Ｖ
			LOCALS:1 = 음순
			LOCAL = 7
			LOCAL:1 = 1
		CASE 2
			LOCALS = Ａ
			LOCALS:1 = 둔부
			LOCAL = 8
			LOCAL:1 = 2
		CASE 3
			LOCALS = 유두
			LOCALS:1 = 유두
			LOCAL = 5
			LOCAL:1 = 3
		CASE 4
			LOCALS:1 = 유방
			LOCALS = 유방
			LOCAL = 4
			LOCAL:1 = 4
	ENDSELECT
	TEQUIP:ARG:(LOCALS + "촉각") = 1
	TEQUIP:ARG:("피복애무" + LOCALS) = 0
	TEQUIP:ARG:("옷내부애무" + LOCALS) = 0

	;노출している部位
	IF (TEQUIP:ARG:(LOCALS:1 + "노출")) == -1
		CONTINUE
	ENDIF

	FOR 의상부위 , 0 , 14 ;17 그외 확장패치 주석처리
		RESULT = 0
		TRYCALLFORM CLOTHES_EXPOSE_{TEQUIP:ARG:GET_CLOTHESNAME(의상부위)},LOCAL
		RESULT:1 = RESULT
		;가슴앞열림
		IF RESULT != 1 && (LOCAL == 4 || LOCAL == 5) && TEQUIP:ARG:가슴앞열림
			RESULT = 0
			TRYCALLFORM CLOTHES_OPENFRONT_{TEQUIP:ARG:GET_CLOTHESNAME(의상부위)}
			SIF RESULT
				RESULT:1 = 1
		ENDIF
		
		;노출
		IF RESULT:1 == 1
			촉각:의상부위 = 1
		;피복
		ELSE
			TRYCCALLFORM CLOTHES_촉각_{TEQUIP:ARG:GET_CLOTHESNAME(의상부위)},LOCAL:1
				IF RESULT == 0
					TEQUIP:ARG:(LOCALS + "촉각") = 0
					BREAK
				ENDIF
				촉각:의상부위 = RESULT
			CATCH
				TEQUIP:ARG:(LOCALS + "촉각") = 0
				BREAK
			ENDCATCH
		ENDIF
	NEXT
	SIF TEQUIP:ARG:(LOCALS + "촉각") == 0
		CONTINUE

	;上から伝わる
	LOCAL:2 = 0
	FOR 의상부위 , 0 ,14 ;17 그외 확장패치 주석처리
		;上から伝わらない옷
		IF 촉각:의상부위 != 1 && (촉각:의상부위 & 2 == 0)
			LOCAL:2 += 2
			BREAK
		ENDIF
		;上から伝わる옷
		SIF 촉각:의상부위 & 2
			LOCAL:2 += 1
	NEXT
	;着込みすぎ
	IF LOCAL:2 > 1
		TEQUIP:ARG:(LOCALS + "촉각") = 0
		CONTINUE
	;上から伝わる옷を一枚だけ着ている
	ELSEIF LOCAL:2 == 1
		TEQUIP:ARG:(LOCALS + "촉각") = 2
		TEQUIP:ARG:("피복애무" + LOCALS) = 1
	ENDIF

	;潜り込ませられる
	LOCAL:2 = 0
	FOR 의상부위 , 0 ,14 ;17 그외 확장패치 주석처리
		;潜り込ませられない
		IF 촉각:의상부위 != 1 && (촉각:의상부위 & 4 == 0)
			TEQUIP:ARG:(LOCALS + "촉각") = 0
			LOCAL:2 = 0
			BREAK
		ENDIF
		;潜り込ませられる
		SIF 촉각:의상부위 != 1 && (촉각:의상부위 & 4)
			LOCAL:2 += 1
	NEXT
	IF LOCAL:2 > 0
		SIF TEQUIP:ARG:(LOCALS + "촉각") == 1
			TEQUIP:ARG:(LOCALS + "촉각") = 0
		TEQUIP:ARG:(LOCALS + "촉각") += 4
		TEQUIP:ARG:("옷내부애무" + LOCALS) = 1
	ENDIF
NEXT

;=================================================
;Ｖ/Ａ비켜놓기가능の체크
;=================================================
@비켜놓기체크,ARG
#DIM 의상부위 , 1
#DIM 조사부위 , 1
#LOCALSIZE 1
#LOCALSSIZE 2
FOR 조사부위 ,0 , 2
	LOCALS = \@ 조사부위 == 0 ? Ｖ # Ａ \@
	LOCALS:1 = \@ 조사부위 == 0 ? 음순 # 둔부 \@
	TEQUIP:ARG:(LOCALS + "비켜놓기가능") = 1
	IF TEQUIP:ARG:(LOCALS:1 + "노출") == -1
		TEQUIP:ARG:(LOCALS + "비켜놓기가능") = 0
		CONTINUE
	ENDIF
	FOR 의상부위 , 0 , 14 ;17 그외 확장패치 주석처리
		RESULT = 0
		TRYCALLFORM CLOTHES_EXPOSE_{TEQUIP:ARG:GET_CLOTHESNAME(의상부위)},7+조사부위
		SIF RESULT == 1
			CONTINUE
		TRYCALLFORM CLOTHES_CROTCH_{TEQUIP:ARG:GET_CLOTHESNAME(의상부위)}
		IF RESULT != 2
			TEQUIP:ARG:(LOCALS + "비켜놓기가능") = 0
			BREAK
		ENDIF
	NEXT
NEXT
;=================================================
;おっぱいオープンの체크
;=================================================
@가슴오픈체크,ARG
#DIM 의상부위 , 1
#DIM オープン可否 , 14 ;17 그외 확장패치 주석처리
#LOCALSIZE 1

VARSET オープン可否 , 0
TEQUIP:ARG:가슴구조 = 0
FOR 의상부위 , 0 , 14 ;17 그외 확장패치 주석처리
	RESULT = 0
	TRYCALLFORM CLOTHES_EXPOSE_{TEQUIP:ARG:GET_CLOTHESNAME(의상부위)},4
	RESULT:1 = RESULT
	TRYCALLFORM CLOTHES_EXPOSE_{TEQUIP:ARG:GET_CLOTHESNAME(의상부위)},5
	SIF RESULT == 1 && RESULT:1 == 1
		CONTINUE
	TRYCCALLFORM CLOTHES_BREAST_{TEQUIP:ARG:GET_CLOTHESNAME(의상부위)}
		IF RESULT == 0
			TEQUIP:ARG:가슴앞열림 = 0
			RETURN 0
		ENDIF
		オープン可否:의상부위 = RESULT
	CATCH
		TEQUIP:ARG:가슴앞열림 = 0
		RETURN 0
	ENDCATCH
NEXT
;그외→겉옷→전신복→옷→하의→모자→신발→전신속옷→손→양말→下着上下
IF オープン可否:10
	TEQUIP:ARG:가슴구조 = オープン可否:10
ELSEIF オープン可否:3
	TEQUIP:ARG:가슴구조 = オープン可否:3
ELSEIF オープン可否:1
	TEQUIP:ARG:가슴구조 = オープン可否:1
ELSEIF オープン可否:2
	TEQUIP:ARG:가슴구조 = オープン可否:2
ELSEIF オープン可否:0
	TEQUIP:ARG:가슴구조 = オープン可否:0
ELSEIF オープン可否:9
	TEQUIP:ARG:가슴구조 = オープン可否:9
ELSEIF オープン可否:7
	TEQUIP:ARG:가슴구조 = オープン可否:7
ELSEIF オープン可否:4
	TEQUIP:ARG:가슴구조 = オープン可否:4
ELSEIF オープン可否:8
	TEQUIP:ARG:가슴구조 = オープン可否:8
ELSEIF オープン可否:5
	TEQUIP:ARG:가슴구조 = オープン可否:5
ELSEIF オープン可否:6
	TEQUIP:ARG:가슴구조 = オープン可否:6
ENDIF
SIF TEQUIP:ARG:가슴구조 == 6
	TEQUIP:ARG:가슴앞열림 = 1
;=================================================
;고간구조の체크
;TEQUIP:고간구조 BIT演算 6以降は스커트
;0 = 바지、1 = ビキニ타입、2 = 穴開き、3 앞열림、4 開閉가능
;10 = 具チラ 11 = ミニ 12 = 통상 13 = ロング 14 = タイト
;=================================================
@고간구조체크,ARG
#DIM 의상부위 , 1
#DIM 구조 , 14
#LOCALSIZE 1

VARSET 구조 , 0
TEQUIP:ARG:고간구조 = 0
TEQUIP:ARG:스커트걷어올림 = 0

;보임状況초기化
TEQUIP:ARG:음순보임 = TEQUIP:ARG:음순노출
TEQUIP:ARG:둔부보임 = TEQUIP:ARG:둔부노출
FOR 의상부위 , 0 , 14
	RESULT = 0
	;股間の構造
	TRYCCALLFORM CLOTHES_CROTCH_{TEQUIP:ARG:GET_CLOTHESNAME(의상부위)}
		IF RESULT <= 0
			CONTINUE
		ELSEIF RESULT == 3
			;스커트の場合、스커트の타입を고간구조に放り込む
			TRYCCALLFORM CLOTHES_SKIRT_{TEQUIP:ARG:GET_CLOTHESNAME(의상부위)}
				SIF RESULT == 0
					RESULT = 3
				SETBIT TEQUIP:ARG:고간구조 , 9 + RESULT
			CATCH
				SETBIT TEQUIP:ARG:고간구조 , 12
			ENDCATCH
;		ELSEIF INRANGE(RESULT,4,6)
;			RESULT -= 2
		ELSEIF TEQUIP:ARG:음순노출 == 의상부위
			SIF	RESULT > 3
				RESULT -= 1
			;スカート以外の場合、一番上の衣装であれば股間構造を放り込む
			SETBIT TEQUIP:ARG:고간구조 , RESULT - 1
		ENDIF
		구조:의상부위 = RESULT
	CATCH
	ENDCATCH
NEXT
;음순노출しているのであれば、가랑이の구조に穴開きがあれば放り込む
SIF TEQUIP:ARG:음순노출 == -1 && MATCH(구조,4)
	SETBIT TEQUIP:ARG:고간구조 , 2

;스커트の長さに基づく諸処理
;ロング
IF GETBIT(TEQUIP:ARG:고간구조,13)
	TEQUIP:ARG:스커트걷어올림 = 4
	TEQUIP:ARG:음순보임 = 0
	TEQUIP:ARG:둔부보임 = 0
;통상
ELSEIF GETBIT(TEQUIP:ARG:고간구조,12)
	TEQUIP:ARG:스커트걷어올림 = 3
	TEQUIP:ARG:음순보임 = 0
	TEQUIP:ARG:둔부보임 = 0
;タイト
ELSEIF GETBIT(TEQUIP:ARG:고간구조,14)
	TEQUIP:ARG:스커트걷어올림 = 1
	SIF !HAVE_PENIS(ARG)
		TEQUIP:ARG:음순보임 = 0
	TEQUIP:ARG:둔부보임 = 0
;ミニ
ELSEIF GETBIT(TEQUIP:ARG:고간구조,11)
	TEQUIP:ARG:스커트걷어올림 = 2
	SIF !HAVE_PENIS(ARG)
		TEQUIP:ARG:음순보임 = 0
	TEQUIP:ARG:둔부보임 = 0
;具チラ
ELSEIF GETBIT(TEQUIP:ARG:고간구조,10)
	TEQUIP:ARG:스커트걷어올림 = 1
ENDIF
;==================================================
;착의の初期化処理
;==================================================
@CLOTHES_INITIALIZE,ARG
#DIM LCOUNT
LOCAL = 1
FOR LCOUNT, 0, 14 ;17 그외 확장패치 주석처리
	IF CFLAG:ARG:(60+LCOUNT) == -1
		CFLAG:ARG:(GET_CLOTHESNAME(LCOUNT)) = 0
	ELSE
		CFLAG:ARG:(GET_CLOTHESNAME(LCOUNT)) = CFLAG:ARG:(60+LCOUNT) ? CFLAG:ARG:(60+LCOUNT) # CSVCFLAG(NO:ARG,40+LCOUNT,0)
	ENDIF
	SIF CFLAG:ARG:(GET_CLOTHESNAME(LCOUNT)) < 0
		CFLAG:ARG:(GET_CLOTHESNAME(LCOUNT)) = 0
	;당신が초기化대상の場合、그외2、그외3の衣装を強制的に外します
	IF NO:ARG == NO:MASTER
		SIF LCOUNT >= 12
			CFLAG:ARG:(GET_CLOTHESNAME(LCOUNT)) = 0
	ENDIF
	SIF CFLAG:ARG:GET_CLOTHESNAME(LCOUNT)
		CFLAG:ARG:23 |= LOCAL
	LOCAL *= 2
NEXT



@EVENTTRAIN
#PRI
CALL CLOTHES_TRAIN,MASTER
SIF TARGET >= 0
	CALL CLOTHES_TRAIN,TARGET
SIF ASSI >= 0
	CALL CLOTHES_TRAIN,ASSI



;복장の着用
@CLOTHES_TRAIN,ARG,ARG:1 = 0
#DIM LCOUNT
;ARG:1 == 1の場合、던전탐색の記録を체크する
FOR LCOUNT, 0, 14 ;17 그외 확장패치 주석처리
	TEQUIP:ARG:(GET_CLOTHESNAME(LCOUNT)) = CFLAG:ARG:(GET_CLOTHESNAME(LCOUNT))
NEXT
SIF ARG:1 == 1 && CFLAG:ARG:전투참가불가능
	RETURN 0
SIF ARG:1 == 1 && FLAG:탐색시초기착의설정 == 5 && CFLAG:ARG:PT플래그 > 0
	ARG:1 = 0
;초기착의설정に応じて、옷の설정をする
;전회설정
IF (FLAG:초기착의설정 == 4 && ARG:1 == 0) || (ARG:1 == 1 && FLAG:탐색시초기착의설정 == 4)
	LOCAL = 1
	FOR LCOUNT, 0, 14 ;그외 확장패치 주석처리
		SIF (CFLAG:ARG:(23+ARG:1) & LOCAL)  == 0
			TEQUIP:ARG:GET_CLOTHESNAME(LCOUNT) = 0
		LOCAL *= 2
	NEXT
	CALL 가슴오픈체크(ARG)
	CALL CHECK_EXPOSE(ARG)
	CALL 고간구조체크,ARG
	CALL 촉각체크(ARG)
	CALL 비켜놓기체크(ARG)
	LOCAL:1 = 0
	SIF  (TEQUIP:ARG:가슴구조 & 1 || TEQUIP:ARG:가슴구조 & 2 || TEQUIP:ARG:가슴구조 & 4 || TEQUIP:ARG:가슴구조 & 8 || TEQUIP:ARG:가슴구조 & 16) 
		LOCAL:1 = 1
	SIF GETBIT(CFLAG:ARG:23,12) && LOCAL:1
		TEQUIP:ARG:가슴앞열림 = 1
;전라
ELSEIF (FLAG:초기착의설정 == 3 && ARG:1 == 0) || (ARG:1 == 1 && FLAG:탐색시초기착의설정 == 3)
	FOR LCOUNT, 0, 14 ;17 그외 확장패치 주석처리
		TEQUIP:ARG:(GET_CLOTHESNAME(LCOUNT)) = 0
	NEXT
;속옷 차림
ELSEIF (FLAG:초기착의설정 == 2 && ARG:1 == 0) || (ARG:1 == 1 && FLAG:탐색시초기착의설정 == 2)
	TEQUIP:ARG:하의 = 0
	TEQUIP:ARG:옷 = 0
	TEQUIP:ARG:전신복 = 0
	TEQUIP:ARG:겉옷 = 0
	TEQUIP:ARG:손 = 0
	TEQUIP:ARG:신발 = 0
;반라
ELSEIF (FLAG:초기착의설정 == 1 && ARG:1 == 0) || (ARG:1 == 1 && FLAG:탐색시초기착의설정 == 1)
	TEQUIP:ARG:속옷（상） = 0
	TEQUIP:ARG:속옷（하） = 0
	TEQUIP:ARG:전신속옷 = 0
	TEQUIP:ARG:겉옷 = 0
	TEQUIP:ARG:손 = 0
	TEQUIP:ARG:신발 = 0
ENDIF
CALL 가슴오픈체크(ARG)
CALL CHECK_EXPOSE(ARG)
CALL 고간구조체크,ARG
CALL 촉각체크(ARG)
CALL 비켜놓기체크(ARG)



@EVENTTURNEND
CALL SAVE_CLOTHES,MASTER

SIF TARGET >= 0
	CALL SAVE_CLOTHES,TARGET
SIF ASSI >= 0
	CALL SAVE_CLOTHES,ASSI

@SAVE_CLOTHES,ARG,ARG:1 = 0
#DIM LCOUNT
	;착의상태の保存処理
	LOCAL = 1
	CFLAG:ARG:(23+ARG:1) = 0
	FOR LCOUNT, 0, 14 ;17 그외 확장패치 주석처리
		SIF TEQUIP:ARG:GET_CLOTHESNAME(LCOUNT)
			CFLAG:ARG:(23+ARG:1) |= LOCAL
		LOCAL *= 2
	NEXT
	SIF TEQUIP:ARG:가슴앞열림
		SETBIT CFLAG:ARG:(23+ARG:1),12


;-------------------------------------------------
;착의の표시
;-------------------------------------------------
@CLOTHES_CHANGE,ARG , ARG:1 = 0
;C = NO:TARGET
#DIM LINE,2
#DIM 가슴개방가능, 3
#DIM 고간개방가능, 3
#DIM CHARA, 1
#DIM 비표시, 3
#DIM 음순노출상태, 3
#DIM 유방노출상태, 3
#DIM 구상체크可, 1
#DIM LCOUNT
#LOCALSIZE 3
LINE = LINECOUNT
$START
CLEARLINE LINECOUNT - LINE
PRINTL 의상에 대해서 설정합니다
;特殊な착의상태を표시
;全身着か上下着着用で下着無しは반라（脱がすまたは着せる過程で반라상태にしていると脳内変換）
;IF (TEQUIP:6 > 0 || (TEQUIP:5 > 0 && TEQUIP:4 > 0)) && TEQUIP:3 == 0 && TEQUIP:2 == 0
;	PRINTL 【반라】
;	PRINTL 　
;全身着及び上下着が無く、下着を上下共につけている場合に표시
;ELSEIF TEQUIP:전신복 == 0 && TEQUIP:5 == 0 && TEQUIP:4 == 0 && TEQUIP:3 > 0 && TEQUIP:2 > 0
;	PRINTL 【속옷 차림】
;	PRINTL 　
;何もつけていない時に표시
;ELSEIF TEQUIP:6 == 0 && TEQUIP:5 == 0 && TEQUIP:4 == 0 && TEQUIP:3 == 0 && TEQUIP:2 == 0 && TEQUIP:1 == 0 && TEQUIP:0 == 0
;	PRINTL 【전라】
;	PRINTL 　
;ENDIF
;가슴ぼろん以外は가능
구상체크可 = 1
;착의名関数から착의を呼び出していく
FOR LOCAL:1, 0 , (ARG:1 == 1 ? 1 # 3)
	SIF LOCAL:1 == 0
		CHARA = ARG
	SIF LOCAL:1 == 1
		CHARA = MASTER
	SIF LOCAL:1 == 2
		CHARA = ASSI
	SIF CHARA < 0
		CONTINUE
	;구상の판정用に취득しておく
	음순노출상태:(LOCAL:1) = TEQUIP:CHARA:음순노출
	유방노출상태:(LOCAL:1) = TEQUIP:CHARA:유방노출
	;現在来ている옷を표시、PRINTLは改行用
	DRAWLINE
	PRINTFORM %CALLNAME:CHARA,36,LEFT% 
	SIF !비표시:(LOCAL:1)
		SETCOLOR COLOR("aqua")
			PRINTBUTTON @"[{LOCAL:1*100+98,3}] 표시　", LOCAL:1*100+98
	RESETCOLOR
	SIF 비표시:(LOCAL:1)
		SETCOLOR COLOR("aqua")
			PRINTFORML [{LOCAL:1*100+99,3}] 비표시
	RESETCOLOR
	SIF 비표시:(LOCAL:1)
		CONTINUE
	; \@ TEQUIP:ARG:가슴앞열림 ? [가슴개방中] #\@
	FOR LOCAL, 0, 14 ;17 그외 확장패치 주석처리
		SIF !CFLAG:CHARA:(GET_CLOTHESNAME(LOCAL))
			CONTINUE
		TRYCCALLFORM CLOTHES_NAME_{CFLAG:CHARA:(GET_CLOTHESNAME(LOCAL))}
			IF STRLENS(RESULTS) > 20
				SUBSTRING RESULTS, 0, 18
				RESULTS += "…"
			ENDIF
			PRINTFORM %"【"+GET_CLOTHESNAME(LOCAL)+"】",14,LEFT% %RESULTS,21,LEFT% 
			SIF TEQUIP:CHARA:(GET_CLOTHESNAME(LOCAL))
				SETCOLOR COLOR("aqua")
			PRINTBUTTON @"[{LOCAL+LOCAL:1*100,3}] 착의　", LOCAL+LOCAL:1*100
			RESETCOLOR
			SIF !TEQUIP:CHARA:(GET_CLOTHESNAME(LOCAL))
				SETCOLOR COLOR("aqua")
			PRINTFORM [{LOCAL+20+LOCAL:1*100,3}] 탈의
			RESETCOLOR
			SIF STAIN:CHARA:(LOCAL+10) & 1
				PRINT <애액>
			SIF STAIN:CHARA:(LOCAL+10) & 2
				PRINT <페니스>
			SIF STAIN:CHARA:(LOCAL+10) & 4
				PRINT <정액>
			SIF STAIN:CHARA:(LOCAL+10) & 8
				PRINT <애널>
			SIF STAIN:CHARA:(LOCAL+10) & 16
				PRINT <모유>
			SIF STAIN:CHARA:(LOCAL+10) & 32
				PRINT <점액>
			SIF STAIN:CHARA:(LOCAL+10) & 64
				PRINT <파과혈>
			PRINTL 
		CATCH
		ENDCATCH
	NEXT
	PRINTFORM %"【모든의복】",36,LEFT% 
	SIF SUMARRAY(TEQUIP:CHARA:0, GETNUM(TEQUIP, GET_CLOTHESNAME(0)), GETNUM(TEQUIP, GET_CLOTHESNAME(11))+1) == SUMARRAY(CFLAG:CHARA:0, GETNUM(CFLAG, GET_CLOTHESNAME(0)), GETNUM(CFLAG, GET_CLOTHESNAME(11))+1)
		SETCOLOR COLOR("aqua")
			PRINTBUTTON @"[{LOCAL:1*100+50,3}] 착의　", LOCAL:1*100+50
	RESETCOLOR
	SIF SUMARRAY(TEQUIP:CHARA:0, GETNUM(TEQUIP, GET_CLOTHESNAME(0)), GETNUM(TEQUIP, GET_CLOTHESNAME(11))+1) == 0
		SETCOLOR COLOR("aqua")
			PRINTFORML [{LOCAL:1*100+51,3}] 탈의
	RESETCOLOR
	;가슴
	SIF FLAG:SHOP커맨드 == [[SHOP:의상방]] || FLAG:SHOP커맨드 == [[SHOP:옵션]]
		BREAK
	PRINTFORM %"【가슴구조】",14,LEFT% 
	가슴개방가능:(LOCAL:1) = 0
	SIF (TEQUIP:CHARA:가슴구조 & (1 + 2 + 4 + 8 + 16))
		가슴개방가능:(LOCAL:1) = 1
	PRINTFORM \@가슴개방가능:(LOCAL:1) ? %"개방가능",21,LEFT% # %"개방불능",21,LEFT%\@ 
	IF 가슴개방가능:(LOCAL:1) == 0
		SETCOLOR COLOR("light-gray")
		PRINTBUTTON @"[{LOCAL:1*100+60,3}] 개방　", LOCAL:1*100+60
		PRINTFORM [{LOCAL:1*100+61,3}] 격납
	ELSE
		SIF TEQUIP:CHARA:가슴앞열림
			SETCOLOR COLOR("aqua")
		PRINTBUTTON @"[{LOCAL:1*100+60,3}] 개방　", LOCAL:1*100+60
		RESETCOLOR
		SIF !TEQUIP:CHARA:가슴앞열림
			SETCOLOR COLOR("aqua")
			PRINTFORM [{LOCAL:1*100+61,3}] 격납
	ENDIF
	RESETCOLOR
	PRINTL 
	;股間
		PRINTFORM %"【고간구조】",14,LEFT% 
	고간개방가능:(LOCAL:1) = 0
	;TEQUIP:CHARA:Ｖ비켜놓기가능を条件に追加
	SIF (TEQUIP:CHARA:고간구조 & (1 + 2 + 4 + 8 + 16)) && TEQUIP:CHARA:Ｖ비켜놓기가능
		고간개방가능:(LOCAL:1) = 1
	PRINTFORM \@고간개방가능:(LOCAL:1) ? %"비켜놓기 가능",21,LEFT% # %"비켜놓기 불가능",21,LEFT%\@ 
	IF 고간개방가능:(LOCAL:1) == 0
		SETCOLOR COLOR("light-gray")
		PRINTBUTTON @"[{LOCAL:1*100+70,3}] 비켜놓는다　", LOCAL:1*100+70
		PRINTFORM [{LOCAL:1*100+71,3}] 되돌린다
	ELSE
		SIF TEQUIP:CHARA:고간앞열림
			SETCOLOR COLOR("aqua")
		PRINTBUTTON @"[{LOCAL:1*100+70,3}] 비켜놓는다　", LOCAL:1*100+70
		RESETCOLOR
		SIF !TEQUIP:CHARA:고간앞열림
			SETCOLOR COLOR("aqua")
			PRINTFORM [{LOCAL:1*100+71,3}] 되돌린다
	ENDIF
	RESETCOLOR
	PRINTL 

NEXT
;FOR LCOUNT, 0, 14
;	IF TEQUIP:ARG:(GET_CLOTHESNAME(LCOUNT)) > 0
;		LOCAL = LCOUNT
;		PRINT 【
;		CALL CLOTHES_NAME,LOCAL,ARG
;		PRINTL 】
;	ENDIF
;NEXT
DRAWLINE
PRINTL [999] 돌아간다
;CUSTOMDRAWLINE ・

;-------------------------------------------------
;착의の설정
;-------------------------------------------------
;FOR LCOUNT, 0, 14
;	IF TEQUIP:ARG:(GET_CLOTHESNAME(LCOUNT))
;		PRINTFORM [{LCOUNT}] 
;		LOCAL = LCOUNT
;		CALL CLOTHES_NAME,LOCAL,ARG
;		IF ARG == PLAYER
;			PRINTL を脱ぐ
;		ELSE
;			PRINTL を脱がす
;		ENDIF
;	ENDIF
;NEXT
;FOR LCOUNT, 0, 14
;	IF TEQUIP:ARG:(GET_CLOTHESNAME(LCOUNT)) == 0 && CFLAG:ARG:(GET_CLOTHESNAME(LCOUNT))
;		PRINTFORM [{LCOUNT+12}] 
;		LOCAL = LCOUNT
;		CALL CLOTHES_NAME,LOCAL,ARG
;		PRINT を
;		
;		SELECTCASE LOCAL
;			CASE 0,4,5,11
;				PRINTL つける
;			CASE 1,3,7,10
;				IF ARG == PLAYER
;					PRINTL 着る
;				ELSE
;					PRINTL 着せる
;				ENDIF
;			CASE 2,6,8,9
;				IF ARG == PLAYER
;					PRINTL 穿く
;				ELSE
;;					PRINTL 穿かせる
;				ENDIF
;		ENDSELECT
;	ENDIF
;NEXT
;FOR LCOUNT, 0, 12
;	IF TEQUIP:ARG:(GET_CLOTHESNAME(LCOUNT)) > 0
;		IF ARG == PLAYER
;			PRINTL [30] すべて脱ぐ
;		ELSE
;			PRINTL [30] すべて脱がす
;		ENDIF
;		BREAK
;	ENDIF
;NEXT
;FOR LCOUNT, 0, 12
;	IF TEQUIP:ARG:(GET_CLOTHESNAME(LCOUNT)) == 0 && CFLAG:ARG:(GET_CLOTHESNAME(LCOUNT))
;		IF ARG == PLAYER
;			PRINTL [40] すべて着る
;		ELSE
;			PRINTL [40] すべて着せる
;		ENDIF
;		BREAK
;	ENDIF
;NEXT
;SIF  (TEQUIP:ARG:가슴구조 & 1 || TEQUIP:ARG:가슴구조 & 2 || TEQUIP:ARG:가슴구조 & 4 || TEQUIP:ARG:가슴구조 & 8 || TEQUIP:ARG:가슴구조 & 16) 
;	LOCAL:1 = 1
;SIF TEQUIP:ARG:가슴구조 > 0 && LOCAL:1 && FLAG:SHOP커맨드 != [[SHOP:의상방]] && FLAG:SHOP커맨드 != [[SHOP:옵션]]
;	PRINTFORML [98]\@ TEQUIP:ARG:가슴앞열림 ? 가슴을 집어넣는다 # 가슴을 드러낸다 \@
;PRINTL [100]돌아간다
LINE:1 = LINECOUNT
$INPUT_LOOP
CLEARLINE LINECOUNT - LINE:1
INPUT
SIF RESULT == 999
	RETURN 0
LOCAL = RESULT % 100
LOCAL:1 = (RESULT) / 100
CHARA = -1
SIF LOCAL:1 == 0
	CHARA = ARG
SIF LOCAL:1 == 1
	CHARA = MASTER
SIF LOCAL:1 == 2
	CHARA = ASSI
SIF CHARA == -1
	GOTO START
;가슴の開閉
;ELSEIF RESULT == 98 && TEQUIP:ARG:가슴구조 > 0 && LOCAL:1 && FLAG:SHOP커맨드 != [[SHOP:의상방]] && FLAG:SHOP커맨드 != [[SHOP:옵션]]
IF LOCAL == 98
	비표시:(LOCAL:1) = 0
ELSEIF LOCAL == 99 && RESULT > 0
	비표시:(LOCAL:1) = 1
ELSEIF (LOCAL == 60 || LOCAL == 61) && (TEQUIP:CHARA:가슴앞열림 == LOCAL % 10) && 가슴개방가능:(LOCAL:1)
	INVERTBIT TEQUIP:CHARA:가슴앞열림 , 0
	TFLAG:13 = 44
	SWAP CHARA , TARGET
	CALL KOJO_MESSAGE_EVENT
	SWAP TARGET , CHARA
	구상체크可 = 0
;모두 벗긴다
ELSEIF (LOCAL == 70 || LOCAL == 71) && (TEQUIP:CHARA:고간앞열림 == LOCAL % 10) && 고간개방가능:(LOCAL:1)
	INVERTBIT TEQUIP:CHARA:고간앞열림 , 0
	TFLAG:13 = 96
	SWAP CHARA , TARGET
	CALL KOJO_MESSAGE_EVENT
	SWAP TARGET , CHARA
	구상체크可 = 0

;すべて脱がせる
ELSEIF LOCAL == 51
	FOR LCOUNT, 0, 14 ;17 그외 확장패치 주석처리
		TEQUIP:CHARA:(GET_CLOTHESNAME(LCOUNT)) = 0
	NEXT
;모두 입힌다
ELSEIF LOCAL == 50
	FOR LCOUNT, 0, 14 ;17 그외 확장패치 주석처리
		TEQUIP:CHARA:(GET_CLOTHESNAME(LCOUNT)) = CFLAG:CHARA:(GET_CLOTHESNAME(LCOUNT))
	NEXT
;옷을 입히는 경우
ELSEIF LOCAL < 20
	IF TEQUIP:CHARA:(GET_CLOTHESNAME(LOCAL)) == 0 && CFLAG:CHARA:(GET_CLOTHESNAME(LOCAL))
		IF LOCAL == 3
			TEQUIP:CHARA:옷 = 0
			TEQUIP:CHARA:하의 = 0
		ELSEIF LOCAL == 1 || LOCAL == 2
			TEQUIP:CHARA:전신복 = 0
		ELSEIF LOCAL == 5 || LOCAL == 6
			TEQUIP:CHARA:전신속옷 = 0
		ELSEIF LOCAL == 7
			TEQUIP:CHARA:속옷（상） = 0
			TEQUIP:CHARA:속옷（하） = 0
		ENDIF
		TEQUIP:CHARA:(GET_CLOTHESNAME(LOCAL)) = CFLAG:CHARA:(GET_CLOTHESNAME(LOCAL))
	ELSE
		GOTO INPUT_LOOP
	ENDIF
	;입히는 처리 후에 텍스트를 부른다
	TRYCCALLFORM CLOTHES_MESSAGE_{NO:CHARA}, LOCAL, 1, CHARA
	CATCH
		CALL CLOTHES_MESSAGE,LOCAL,1,CHARA
	ENDCATCH
;옷을 벗기는 경우
ELSEIF LOCAL < 40
	LOCAL -= 20
	;입지 않은 경우 재입력
	SIF TEQUIP:CHARA:(GET_CLOTHESNAME(LOCAL)) == 0
		GOTO INPUT_LOOP
	;벗기는 처리 전에 텍스트를 부른다
	TRYCCALLFORM CLOTHES_MESSAGE_K{NO:CHARA}, LOCAL, 0, CHARA
	CATCH
		CALL CLOTHES_MESSAGE,LOCAL, 0, CHARA
	ENDCATCH
	TEQUIP:CHARA:(GET_CLOTHESNAME(LOCAL)) = 0
ELSE
	GOTO INPUT_LOOP
ENDIF

CALL 가슴오픈체크(CHARA)
CALL CHECK_EXPOSE(CHARA)
CALL 고간구조체크,CHARA
CALL 촉각체크(CHARA)
CALL 비켜놓기체크(CHARA)
IF 구상체크可
	SIF CHARA == TARGET
		LOCAL:1 = 0
	SIF CHARA == MASTER
		LOCAL:1 = 1
	SIF CHARA == ASSI
		LOCAL:1 = 2
	TFLAG:13 = 0
	IF 음순노출상태:(LOCAL:1) >= 0 && TEQUIP:CHARA:음순노출 == -1
		IF 유방노출상태:(LOCAL:1) >= 0 && TEQUIP:CHARA:유방노출 == -1
			TFLAG:13 = 70+LOCAL:1
		ELSE
			TFLAG:13 = 73+LOCAL:1
		ENDIF
	ELSE
		SIF 유방노출상태:(LOCAL:1) >= 0 && TEQUIP:CHARA:유방노출 == -1
			TFLAG:13 = 76+LOCAL:1
	ENDIF
	IF TFLAG:13
		SWAP CHARA , TARGET
		CALL KOJO_MESSAGE_EVENT
		SWAP TARGET , CHARA
	ENDIF
;	IF 음순노출상태:(LOCAL:1) == -1 && TEQUIP:CHARA:음순노출음순 >= 0
;		IF 유방노출상태:(LOCAL:1) == -1 && TEQUIP:CHARA:유방노출 >= 0
;		ELSE
;		ENDIF
;	ELSE
;		IF 유방노출상태:(LOCAL:1) == -1 && TEQUIP:CHARA:유방노출 >= 0
;		ENDIF
;	ENDIF
ENDIF
GOTO START
;RESTART

;-------------------------------------------------
;착의の상태によって、조교中のソースが若干変動する
;-------------------------------------------------
@EQUIP_CLOTHES
;착의상태によるソースを付加（손以外の１箇所ごとに노출+5、未함락時반감+5）
;EQUIPによる制御に변경するまで封印
;予定・가슴と바기나が노출するような組み合わせだとさらに＋5
IF TEQUIP:다리노출 == -1		;다리노출
	SOURCE:노출 += 3
	SOURCE:반감 += 3 * !함락()	;함락時は반감を加算しない。以下同様
ENDIF
IF TEQUIP:배꼽노출 == -1		;배꼽노출
	SOURCE:노출 += 3
	SOURCE:반감 += 3 * !함락()
ENDIF
IF TEQUIP:유방노출 == -1	;유방노출
	SOURCE:노출 += 3
	SOURCE:반감 += 3 * !함락()
ENDIF
IF TEQUIP:유두노출 == -1	;유두노출。유방は隠れているのに유두が出てたら+6
	SOURCE:노출 += (10 + (6 * (TEQUIP:유방노출 != -1)))
	SOURCE:반감 += (10 + (6 * (TEQUIP:유방노출 != -1))) * !함락()
ENDIF
IF TEQUIP:음순노출 == -1	;음순노출。다리は隠れているのに음순が出てたら+3、배꼽は隠れているのに음순が出てたら+3
	SOURCE:노출 += (10 + (3 * (TEQUIP:다리노출 != -1)) + (3 * (TEQUIP:배꼽노출 != -1)))
	SOURCE:반감 += (10 + (3 * (TEQUIP:다리노출 != -1)) + (3 * (TEQUIP:배꼽노출 != -1))) * !함락()
ENDIF
IF TEQUIP:둔부노출 == -1	;둔부노출
	SOURCE:노출 += 10
	SOURCE:반감 += 10 * !함락()
ENDIF
RETURN RESULT

;-------------------------------------------------
;옷の이름を関数で呼び出せるようにする。A+40の数値でCFLAGの数値を分岐
;-------------------------------------------------
@CLOTHES_NAME, ARG, ARG:1
PRINTFORM %ITEMNAME:(6000+CFLAG:(ARG:1):(GET_CLOTHESNAME(ARG)))%


;-------------------------------------------------
;착의の実行時の메시지処理
;-------------------------------------------------
@CLOTHES_MESSAGE, ARG, ARG:1, ARG:2
CALL CLOTHES_NAME, ARG, ARG:2
PRINT 를 
IF ARG:1	;着る/着せる
	SELECTCASE ARG
		CASE 0, 4, 5, 11, 12, 13	;모자/손/속옷（상）/그외
			PRINTW 붙였다
		CASE 1, 3, 7, 10	;옷/전신복/전신속옷/겉옷
			IF ARG:2 == PLAYER
				PRINTW 입었다
			ELSE
				PRINTW 덮어 씌웠다
			ENDIF
		CASE 2, 6, 8, 9		;하의/속옷（하）/양말/신발
			IF ARG:2 == PLAYER
				PRINTW 입었다
			ELSE
				PRINTW 입게 했다
			ENDIF
	ENDSELECT
;脱ぐ/脱がせる
ELSE
	;그외
	IF ARG == 11 || ARG == 12 || ARG == 13
		IF ARG:2 == PLAYER
			PRINTW 벗겼다
		ELSE
			PRINTW 벗게 했다
		ENDIF
	;그외以外
	ELSE
		IF ARG:2 == PLAYER
			PRINTW 벗었다
		ELSE
			PRINTW 벗겼다
		ENDIF
	ENDIF
ENDIF

;==============================================================
;옵션内の衣装関連항목、及び衣装변경기능
;==============================================================
;초기착의설정
@CLOTHES_SETTING
Z = 0
PRINTL 설정할 항목을 선택해 주세요
PRINTFORM 현재의 설정은 조교시는
SELECTCASE FLAG:초기착의설정
	CASE 0
		PRINTFORM 전부착용
	CASE 1
		PRINTFORM 반라
	CASE 2
		PRINTFORM 속옷 차림
	CASE 3
		PRINTFORM 전라
	CASE 4
		PRINTFORM 전회 조교 종료시
ENDSELECT
PRINTFORM 　、탐색시는
SELECTCASE FLAG:탐색시초기착의설정
	CASE 0
		PRINTFORM 전부착용
	CASE 1
		PRINTFORM 반라
	CASE 2
		PRINTFORM 속옷 차림
	CASE 3
		PRINTFORM 전라
	CASE 4
		PRINTFORM 전회 종료시
	CASE 5
		PRINTFORM 전회 조교 종료시
ENDSELECT
PRINTFORML 　입니다
PRINTL [1] - 조교 개시시의 착의 상태 설정
PRINTL [2] - 의상의 설정
PRINTL [3] - 탐색 개시시의 착의 상태 설정
PRINTL [0] - 돌아온다



$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN 1
ELSEIF RESULT == 1
	CALL CLOTHES_SETTING_TRAIN
ELSEIF RESULT == 2
	CALL CLOTHES_SETTING_USUALLY
ELSEIF RESULT == 3
	CALL CLOTHES_SETTING_DUNGEON
ELSE
	GOTO INPUT_LOOP
ENDIF
RESTART



@CLOTHES_SETTING_TRAIN
Z = 0
PRINTL 초기의 착의를 설정해 주세요
PRINTL [0] - 전부착용
PRINTL [1] - 반라
PRINTL [2] - 속옷 차림
PRINTL [3] - 전라
PRINTL [4] - 전회 종료시
$INPUT_LOOP
INPUT
IF RESULT == 0
	PRINTW 초기의 착의를 전부 착용으로 설정했습니다
	FLAG:초기착의설정 = 0
ELSEIF RESULT == 1
	PRINTW 초기의 착의를 반라로 설정했습니다
	FLAG:초기착의설정 = 1
ELSEIF RESULT == 2
	PRINTW 초기의 착의를 속옷 차림으로 설정했습니다
	FLAG:초기착의설정 = 2
ELSEIF RESULT == 3
	PRINTW 초기의 착의를 전라로 설정했습니다
	FLAG:초기착의설정 = 3
ELSEIF RESULT == 4
	PRINTW 초기의 착의를 전회 종료시의 상태로 설정했습니다
	FLAG:초기착의설정 = 4
ELSE
	GOTO INPUT_LOOP
ENDIF

@CLOTHES_SETTING_DUNGEON
PRINTL 초기의 착의를 설정해 주세요
PRINTL [0] - 전부 착용
PRINTL [1] - 반라
PRINTL [2] - 속옷 차림
PRINTL [3] - 전라
PRINTL [4] - 전회 종료시
PRINTL [5] - 전회 조교 종료시
$INPUT_LOOP
INPUT
IF RESULT == 0
	PRINTW 초기의 착의를 전부 착용으로 설정했습니다
	FLAG:탐색시초기착의설정 = 0
ELSEIF RESULT == 1
	PRINTW 초기의 착의를 반라로 설정했습니다
	FLAG:탐색시초기착의설정 = 1
ELSEIF RESULT == 2
	PRINTW 초기의 착의를 속옷 차림으로 설정했습니다
	FLAG:탐색시초기착의설정 = 2
ELSEIF RESULT == 3
	PRINTW 초기의 착의를 전라로 설정했습니다
	FLAG:탐색시초기착의설정 = 3
ELSEIF RESULT == 4
	PRINTW 초기의 착의를 전회 종료시의 상태로 설정했습니다
	FLAG:탐색시초기착의설정 = 4
ELSEIF RESULT == 5
	PRINTW 초기의 착의를 전회 조교 종료시의 상태로 설정했습니다
	FLAG:탐색시초기착의설정 = 5
ELSE
	GOTO INPUT_LOOP
ENDIF

@CLOTHES_SETTING_USUALLY
;キャラリストを표시・選択
CALL INPUT_CHARA_LIST("착의 설정을 할 캐릭터를 선택해 주세요","CASTING_CLOTHES_SETTING_USUALLY")
SIF RESULT == 1000
	RETURN 0
LOCAL = RESULT
CALL CLOTHES_SETTING_USUALLY_A,LOCAL

RESTART

@CASTING_CLOTHES_SETTING_USUALLY(ARG)
RETURN CFLAG:ARG:이자리에없는플래그 !| CFLAG:ARG:필터링플래그


@CLOTHES_SETTING_USUALLY_A,ARG
LOCAL:3 = RESULT, LINECOUNT
DO
	CLEARLINE LINECOUNT - LOCAL:4
	LOCAL:1 = -1, -1
	FOR LOCAL, ARG - 1, -1, -1
		IF CFLAG:LOCAL:이자리에없는플래그 !| CFLAG:LOCAL:필터링플래그
			LOCAL:1 = LOCAL	;이전캐릭터
			BREAK
		ENDIF
	NEXT
	FOR LOCAL, ARG + 1, CHARANUM
		IF CFLAG:LOCAL:이자리에없는플래그 !| CFLAG:LOCAL:필터링플래그
			LOCAL:2 = LOCAL	;다음캐릭터
			BREAK
		ENDIF
	NEXT
	PRINTL 의상 변경을 하고 싶은 부위를 선택해 주세요
	PRINTL
	PRINTFORML □ %NAME:ARG%의 의상
	DRAWLINE
	; 그외 확장패치 주석처리
	;FOR LOCAL, 0, 17
	FOR LOCAL, 0, 14
		PRINTFORML [{LOCAL, 3}] %GET_CLOTHESNAME(LOCAL), 14, LEFT%: \@ CFLAG:ARG:GET_CLOTHESNAME(LOCAL) ? %CLOTHESNAMEF(ARG, LOCAL)% # \@
	NEXT
	DRAWLINE

	PRINTL [ 99] 착의 설정 초기화
	PRINTFORMLC \@ LOCAL:1 >= 0 ? [107] 이전캐릭터 # %" " * 16% \@
	PRINTFORMLC [100] 돌아간다
	PRINTFORMLC \@ LOCAL:2 >= 0 ? [109] 다음캐릭터 # %" " * 16% \@
	DO
		INPUT
		; 그외 확장패치 주석처리
		; LOCAL = INRANGE(RESULT, 0, 16) || INRANGE(RESULT, 99, 100) || (RESULT == 107 && LOCAL:1 >= 0) || (RESULT == 109 && LOCAL:2 >= 0)
		LOCAL = INRANGE(RESULT, 0, 13) || INRANGE(RESULT, 99, 100) || (RESULT == 107 && LOCAL:1 >= 0) || (RESULT == 109 && LOCAL:2 >= 0)
		SIF !LOCAL
			CLEARLINE 1
	LOOP !LOCAL
	LOCAL = RESULT
	SELECTCASE RESULT
		CASE 0 TO 13 ;16 그외 확장패치 주석처리
			CALL CLOTHES_SETTING_PART, ARG, RESULT
		CASE 99
			CALL CLOTHES_INITIALIZE, ARG
			PRINTFORMW ＜%NAME:ARG%의 의상을 초기화했습니다＞
		CASE 107
			ARG = LOCAL:1
		CASE 109
			ARG = LOCAL:2
	ENDSELECT
LOOP LOCAL != 100
RETURN LOCAL:3



@CLOTHES_SETTING_PART, ARG, ARG:1
#LOCALSIZE 5
PRINTFORML %GET_CLOTHESNAME(ARG:1)%에게 무엇을 입히겠습니까?  （현재의 복장：\@ CFLAG:ARG:GET_CLOTHESNAME(ARG:1) ? %CLOTHESNAMEF(ARG,ARG:1)% # 미장비 \@）
DRAWLINE
LOCAL:3 = 24
LOCAL:1 = 0
VARSET G,-1

LOCAL:4 = ARG:1
SIF LOCAL:4 > 11
	LOCAL:4 = 11

;選別
FOR LOCAL,1,1500
	;SIF ITEM:(6000+LOCAL) < 1 && CSVCFLAG(NO:ARG,40+GET_CLOTHES(GET_CLOTHESNAME(ARG:1)),0) != LOCAL && CFLAG:ARG:(60+GET_CLOTHES(GET_CLOTHESNAME(ARG:1))) != LOCAL
	SIF ITEM:(6000+LOCAL) < 1 && CSVCFLAG(NO:ARG,40+GET_CLOTHES(GET_CLOTHESNAME(LOCAL:4)),0) != LOCAL && CFLAG:ARG:(60+GET_CLOTHES(GET_CLOTHESNAME(LOCAL:4))) != LOCAL
		CONTINUE
	RESULT = 12
	TRYCALLFORM CLOTHES_PART_{LOCAL}
	;SIF RESULT != ARG:1
	SIF RESULT != (LOCAL:4)
		CONTINUE
	RESULT = 1
	TRYCALLFORM CLOTHES_PREREQUISITE_{LOCAL},ARG
	SIF RESULT == 0
		CONTINUE
	;아이템名の長さを保存
	SIF STRLENS(ITEMNAME:(6000+LOCAL)) > LOCAL:3
		LOCAL:3 = STRLENS(ITEMNAME:(6000+LOCAL))
	G:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT
;표시
IF LOCAL:1
	FOR LOCAL , 0 , LOCAL:1
		PRINTFORM [{G:LOCAL,4}] 
		IF G:LOCAL == CFLAG:ARG:GET_CLOTHESNAME(ARG:1)
			LOCAL:2 = GETCOLOR()
			SETCOLOR COLOR("PASTEL-BLUE")
			PRINTFORM %ITEMNAME:(6000+G:LOCAL),LOCAL:3,LEFT%
			SETCOLOR LOCAL:2
		ELSE
			PRINTFORM %ITEMNAME:(6000+G:LOCAL),LOCAL:3,LEFT%
		ENDIF
		SIF LOCAL % 3 == 2
			PRINTL
	NEXT
ENDIF
SIF !LINEISEMPTY()
	PRINTL
PRINTL
PRINTL [   0] 착용하지 않는다
DRAWLINE
PRINT [ 100] 돌아간다


$INPUT_LOOP
INPUT
IF RESULT == 0
	CFLAG:ARG:GET_CLOTHESNAME(ARG:1) = 0
ELSEIF RESULT == 100
	RETURN 0
ELSEIF MATCH(G,RESULT,0) > 0
	;SIF ARG:1 == 1 || ARG:1 == 2
	;	CFLAG:ARG:전신복 = 0
	;IF ARG:1 == 3
	;	CFLAG:ARG:옷 = 0
	;	CFLAG:ARG:하의 = 0
	;ENDIF
	SIF ARG:1 == 5 || ARG:1 == 6
		CFLAG:ARG:전신속옷 = 0
	IF ARG:1 == 7
		CFLAG:ARG:속옷（상） = 0
		CFLAG:ARG:속옷（하） = 0
	ENDIF
		
	CFLAG:ARG:GET_CLOTHESNAME(ARG:1) = RESULT
	SIF CFLAG:ARG:GET_CLOTHESNAME(ARG:1)
		CFLAG:ARG:23 |= POWER(2,ARG:1)
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 0

