;-------------------------------------------------
;바이브
;道具사용コマンド、레벨2
;-------------------------------------------------
@COMNAME44
IF TEQUIP:13
	RESULTS = 바이브해제
ELSE
	RESULTS = 바이브
ENDIF
@ACT_COM44
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
IF TEQUIP:25
	PRINTL 촉수삽입
	TSTR:0 = 촉수삽입
	SELECTCOM = 161
ELSE
	PRINTL 바이브
	TSTR:0 = 바이브
ENDIF
;CALL KOJO_MESSAGE_COM

;V경험을수반하는커맨드のフラグ(처녀膜再生対応)
TFLAG:19 = 1

;着脱の処理
IF TEQUIP:13 == 0
	TEQUIP:13 = 1
	;촉수삽입は촉수の불결を추가
	IF TEQUIP:25
		CALL SET_STAIN("바기나", "점액", TARGET)
	ENDIF
ELSE
	TEQUIP:13 = 0
ENDIF

;조교者→조교대상
TFLAG:28 = 2
;-------------------------------------------------
;ソース추가処理
;-------------------------------------------------
;소비체력、기력
DOWNBASE:TARGET:체력 += 30
DOWNBASE:TARGET:기력 += 100

SOURCE:TARGET:도구쾌Ｖ = 250
SOURCE:TARGET:아픔 = 50
SOURCE:TARGET:노출 = 20
SOURCE:TARGET:일탈 = 40
SOURCE:TARGET:반감 = 40


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;억압、저항持ちには、억울ソース추가
SIF TALENT:32 || TALENT:34
	SOURCE:TARGET:울굴 += 40

;처녀だった場合は、고통のソースと반발のソースを추가する
IF TALENT:0 == 1
	SOURCE:TARGET:아픔 += 2000
	SOURCE:TARGET:반감 += 3000
ENDIF

;재생처녀だった場合は、고통のソースと반발のソースを추가する
IF TALENT:1 == 1
	SOURCE:TARGET:아픔 += 500
	SOURCE:TARGET:반감 += 1000
ENDIF

;Vへの고통は先に処理しておく
;EXP:Ｖ경험をみる（처녀の반감は別途処理済みなので생략）
IF EXP:0 < EXPLV:1
	TIMES SOURCE:TARGET:아픔 , 2.50
ELSEIF EXP:0 < EXPLV:2
	TIMES SOURCE:TARGET:아픔 , 0.80
	TIMES SOURCE:TARGET:반감 , 1.20
ELSEIF EXP:0 < EXPLV:3
	TIMES SOURCE:TARGET:아픔 , 0.40
ELSEIF EXP:0 < EXPLV:4
	TIMES SOURCE:TARGET:아픔 , 0.10
ELSEIF EXP:0 < EXPLV:5
	TIMES SOURCE:TARGET:아픔 , 0.00
ELSEIF EXP:0 >= EXPLV:5
	TIMES SOURCE:TARGET:아픔 , 0.00
ENDIF

;PALAM:윤활をみる
IF PALAM:4 < PALAMLV:1
	TIMES SOURCE:TARGET:아픔 , 1.20
	TIMES SOURCE:TARGET:반감 , 1.50
ELSEIF PALAM:4 < PALAMLV:2
	TIMES SOURCE:TARGET:아픔 , 0.70
	TIMES SOURCE:TARGET:반감 , 1.20
ELSEIF PALAM:4 < PALAMLV:3
	TIMES SOURCE:TARGET:아픔 , 0.40
ELSEIF PALAM:4 < PALAMLV:4
	TIMES SOURCE:TARGET:아픔 , 0.10
ELSEIF PALAM:4 >= PALAMLV:4
	TIMES SOURCE:TARGET:아픔 , 0.10
ENDIF


;-------------------------------------------------
;불결の処理
;-------------------------------------------------
;촉수삽입なら、촉수体液が질내に付く
SIF TEQUIP:25
	CALL SET_STAIN("질내", "점액", TARGET)

;-------------------------------------------------
;경험の処理
;-------------------------------------------------
;V경험
CALL ADDS_EXP("Ｖ경험", 1, TARGET)

;이상경험
IF TALENT:0 == 1 && TEQUIP:25 == 1
;	EXP:50 += 2
;	PRINTFORML %EXPNAME:50%＋２
	CALL ADDS_ABNORMAL_EXP("처녀상실（촉수）", TARGET)
ELSEIF TALENT:0 == 1
;	EXP:50 += 1
;	PRINTFORML %EXPNAME:50%＋１
	CALL ADDS_ABNORMAL_EXP("처녀상실（바이브）", TARGET)
ENDIF

;레즈경험
A = 1
IF IS_LESBIAN(TARGET,PLAYER)
	CALL ADDS_EXP("레즈경험", A, TARGET)
	CALL ADDS_EXP("레즈경험", A, PLAYER)
ELSEIF IS_GAY(TARGET,PLAYER)
	CALL ADDS_EXP("ＢＬ경험", A, TARGET)
	CALL ADDS_EXP("ＢＬ경험", A, PLAYER)
ENDIF

;촉수소환時、촉수경험の리셋をする
;SIF TEQUIP:25 == 1
;	T = 0

;의존도ベクトル
TFLAG:33 = 1


RETURN 1


;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM44
;바이브挿入中

;V경험을수반하는커맨드の플래그(처녀膜재생対応)
TFLAG:19 = 1

IF TEQUIP:25
	PRINTL ＜촉수삽입중＞
ELSE
	PRINTL ＜바이브삽입중＞
ENDIF

DOWNBASE:TARGET:체력 += 10
DOWNBASE:TARGET:기력 += 50

SOURCE:TARGET:도구쾌Ｖ += 125
SOURCE:TARGET:노출 += 10
SOURCE:TARGET:일탈 += 20

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;억압、저항持ちには、억울ソース추가
SIF TALENT:32 || TALENT:34
	SOURCE:TARGET:울굴 += 20

;Vへの고통は先に処理しておく
;EXP:Ｖ경험をみる（처녀はありえない）
A = 25
B = 20
IF EXP:0 < EXPLV:2
	TIMES A , 0.80
	TIMES B , 1.20
ELSEIF EXP:0 < EXPLV:3
	TIMES A , 0.40
ELSEIF EXP:0 < EXPLV:4
	TIMES A , 0.10
ELSEIF EXP:0 < EXPLV:5
	TIMES A , 0.00
ELSEIF EXP:0 >= EXPLV:5
	TIMES A , 0.00
ENDIF

;PALAM:윤활をみる
IF PALAM:4 < PALAMLV:1
	TIMES A , 1.20
	TIMES B , 1.50
ELSEIF PALAM:4 < PALAMLV:2
	TIMES A , 0.70
	TIMES B , 1.20
ELSEIF PALAM:4 < PALAMLV:3
	TIMES A , 0.40
ELSEIF PALAM:4 < PALAMLV:4
	TIMES A , 0.10
ELSEIF PALAM:4 >= PALAMLV:4
	TIMES A , 0.10
ENDIF
SOURCE:TARGET:아픔 += A
SOURCE:TARGET:반감 += B

;-------------------------------------------------
;경험の処理
;-------------------------------------------------
;자위実行時は경험＋１
IF SELECTCOM == 11
	CALL ADDS_EXP("Ｖ경험", 2, TARGET)
ELSE
	CALL ADDS_EXP("Ｖ경험", 1, TARGET)
ENDIF

;촉수경험加算
SIF TEQUIP:25 == 1
	CALL ADDS_EXP("촉수경험", 1, TARGET)
;	T += 1


RETURN 1

;===================================================
;조교타입
;===================================================
@COMTYPE_44
RETURN COMTYPE("도구계")


