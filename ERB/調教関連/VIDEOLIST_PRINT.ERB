;==============================
;저장내용이 있는 비디오 배열을 제외한 나머지 배열에 -1 대입하는 함수
;ARG - 슬롯번호
;==============================
@VIDEO_ARRAY_DUMMY
#LOCALSIZE 2
#LOCALSSIZE 2
#DIM DYNAMIC V_COUNT1
#DIM DYNAMIC V_COUNT2

FOR V_COUNT1, 0, 10
	SIF STRLENS(CSTR:TARGET:(V_COUNT1 + 100)) > 0
		CONTINUE
	FOR V_COUNT2, 0, 10
		CDFLAG:TARGET:(V_COUNT1 + 10):V_COUNT2 = -1
	NEXT
NEXT


;==============================
;비디오 리스트 출력 & 저장 함수
;==============================
@VIDEO_LIST_PRINT_AND_SAVE
#LOCALSIZE 2
#LOCALSSIZE 2
#DIM DYNAMIC V_COUNT
REDRAW 0
LOCAL = LINECOUNT

;저장된 배열 외에는 전부 더미를 입력한다
CALL VIDEO_ARRAY_DUMMY

FOR V_COUNT, 0, 10
	CALL VIDEO_SELECT_DETAIL_PRINT, V_COUNT
	DRAWLINE
NEXT
SIF TFLAG:63
	SETCOLOR COLOR("aqua")
PRINTFORML [10] 저장된 기록 삭제
SIF TFLAG:63
	RESETCOLOR
PRINTFORML [11] 저장하지 않는다(비디오를 판다)

$V_SLOT_INPUT_LOOP
INPUT
IF RESULT == 0 || RESULT == 1 || RESULT == 2 || RESULT == 3 || RESULT == 4 || RESULT == 5 || RESULT == 6 || RESULT == 7 || RESULT == 8 || RESULT == 9 
;선택한 슬롯에 비디오 타이틀 및 정보 저장
	IF TFLAG:63
	;비디오 삭제모드 --> 슬롯을 선택하면 해당 비디오 기록이 삭제된다
		;선택한 슬롯의 비디오 타이틀명을 지움
		CLEARLINE LINECOUNT - LOCAL
		CSTR:TARGET:(RESULT + 200) = 
		RESTART
	ELSE
	;통상대로 비디오 저장처리
		CALL VIDEO_SAVING, RESULT
		;이벤트구상呼び出し(ビデオ保有時）
		TFLAG:13 = 23
		CALL KOJO_MESSAGE_EVENT
		REDRAW 1 ;emuera 재(再)그리기 on
		TFLAG:63 = 0 ;비디오 삭제모드 off
	ENDIF
ELSEIF RESULT == 10
;저장기록 삭제모드 ON/OFF
	CLEARLINE LINECOUNT - LOCAL
	INVERTBIT TFLAG:63, 0
	RESTART
ELSEIF RESULT == 11
;비디오를 저장하지 않고 그냥 판다
	;입력된 비디오 타이틀명을 지움
	TSTR:4 = 
	PRINTFORML 조교시에 녹화한 비디오를 팔아、￥{TFLAG:36} 입수했습니다
	MONEY += TFLAG:36
	CFLAG:TARGET:비디오매각수 += 1
	CFLAG:TARGET:비디오매각합계금액 += TFLAG:36
	TFLAG:36 = 0
	;촬영 경과횟수 초기화
	TFLAG:70 = 0
	;이벤트구상呼び出し(ビデオ매각時）
	TFLAG:13 = 19
	CALL KOJO_MESSAGE_EVENT
	REDRAW 1 ;emuera 재(再)그리기 on
	TFLAG:63 = 0 ;비디오 삭제모드 off
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO V_SLOT_INPUT_LOOP
ENDIF

;==============================
;비디오 선택지 & 정보 출력 함수
;ARG - 비디오 슬롯번호(0~9번)
;==============================
@VIDEO_SELECT_DETAIL_PRINT, ARG
#LOCALSIZE 2
#LOCALSSIZE 2
#DIM DYNAMIC V_COUNT

;cdflag
;10번 - 1번 비디오 (0번 슬롯)
;11번 - 2번 비디오 (1번 슬롯)
;12번 - 3번 비디오 (2번 슬롯)
;13번 - 4번 비디오 (3번 슬롯)
;14번 - 5번 비디오 (4번 슬롯)
;15번 - 6번 비디오 (5번 슬롯)
;16번 - 7번 비디오 (6번 슬롯)
;17번 - 8번 비디오 (7번 슬롯)
;18번 - 9번 비디오 (8번 슬롯)
;19번 - 10번 비디오 (9번 슬롯)

;cstr
;200번 - 1번 비디오명
;201번 - 2번 비디오명
;202번 - 3번 비디오명
;203번 - 4번 비디오명
;204번 - 5번 비디오명
;205번 - 6번 비디오명
;206번 - 7번 비디오명
;207번 - 8번 비디오명
;208번 - 9번 비디오명
;209번 - 10번 비디오명


;비디오 타이틀명 표시
PRINTFORM [{ARG}]번 비디오 Slot: 
IF STRLENS(CSTR:TARGET:(ARG + 200)) > 0
;비디오 내용이 있음
	SETCOLOR 0xff3399
	PRINTFORML %CSTR:TARGET:(ARG + 200)%
	RESETCOLOR
ELSE
;빈 슬롯
	SETCOLOR 0x848484
	PRINTFORML -----
	RESETCOLOR
ENDIF

IF CDFLAG:TARGET:(ARG + 10):0 >= 0
;첫 행위커맨드 번호가 0번 이상일 경우, 비디오 행위커맨드 표시
	FOR V_COUNT, 0, 10
		SIF CDFLAG:TARGET:(ARG + 10):V_COUNT < 0
			BREAK
		IF V_COUNT > 0
		;두번째 이후 커맨드는 화살표를 표시한다
			PRINTFORM → %TRAINNAME:(CDFLAG:TARGET:(ARG + 10):V_COUNT)% 
		ELSE
			PRINTFORM %TRAINNAME:(CDFLAG:TARGET:(ARG + 10):V_COUNT)% 
		ENDIF
	NEXT
	PRINTFORML → 끝
ELSE
;그 외
	PRINTL    ☆(내용 없음)☆
ENDIF



;==============================
;방금 찍은 비디오 정보 출력 함수
;==============================
@VIDEO_DETAIL_PRINT
#LOCALSIZE 2
#LOCALSSIZE 2
#DIM DYNAMIC V_COUNT

;cdflag
;10번 - 1번 비디오 (0번 슬롯)
;11번 - 2번 비디오 (1번 슬롯)
;12번 - 3번 비디오 (2번 슬롯)
;13번 - 4번 비디오 (3번 슬롯)
;14번 - 5번 비디오 (4번 슬롯)
;15번 - 6번 비디오 (5번 슬롯)
;16번 - 7번 비디오 (6번 슬롯)
;17번 - 8번 비디오 (7번 슬롯)
;18번 - 9번 비디오 (8번 슬롯)
;19번 - 10번 비디오 (9번 슬롯)

;cstr
;100번 - 1번 비디오명
;101번 - 2번 비디오명
;102번 - 3번 비디오명
;103번 - 4번 비디오명
;104번 - 5번 비디오명
;105번 - 6번 비디오명
;106번 - 7번 비디오명
;107번 - 8번 비디오명
;108번 - 9번 비디오명
;109번 - 10번 비디오명


;비디오 타이틀명 표시
PRINTFORM 비디오 제목: 
SETCOLOR 0xf781be
PRINTFORML %TSTR:4%
RESETCOLOR
FOR V_COUNT, 0, TFLAG:70 - 1
	SIF CFLAG:TARGET:(1500 + V_COUNT) < 0
		CONTINUE
	IF V_COUNT > 0
	;두번째 이후 커맨드는 화살표를 표시한다
		PRINTFORM → %TRAINNAME:(CFLAG:TARGET:(1500 + V_COUNT))% 
	ELSE
		PRINTFORM %TRAINNAME:(CFLAG:TARGET:(1500 + V_COUNT))% 
	ENDIF
NEXT
PRINTFORML → 끝


