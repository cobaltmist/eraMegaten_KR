;-------------------------------------------------
;신발 벗기기
;-------------------------------------------------
@CHECK_COM_ABLE228
#LOCALSSIZE 1
;신발을 안 신고 있으면 불가능
IF TEQUIP:신발 == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF

@COMNAME228,ARG
#LOCALSSIZE 1
IF TEQUIP:주도권
;여자주도
	SELECTCASE CFLAG:신발
	CASE [[衣装:부츠]], [[衣装:하이부츠]], [[衣装:숏부츠]]
		RESULTS = 부츠 벗기
	CASE [[衣装:모카신]], [[衣装:명주신]]
		RESULTS = 고무신 벗기
	CASE [[衣装:펌프스]], [[衣装:하이힐]], [[衣装:핀힐]]
		RESULTS = 하이힐 벗기
	CASE [[衣装:스니커]], [[衣装:캔버스신발]]
		RESULTS = 운동화 벗기
	CASEELSE
		RESULTS = 신발 벗기
	ENDSELECT
ELSE
;남자주도
	SELECTCASE CFLAG:신발
	CASE [[衣装:부츠]], [[衣装:하이부츠]], [[衣装:숏부츠]]
		RESULTS = 부츠 벗기기
	CASE [[衣装:모카신]], [[衣装:명주신]]
		RESULTS = 고무신 벗기기
	CASE [[衣装:펌프스]], [[衣装:하이힐]], [[衣装:핀힐]]
		RESULTS = 하이힐 벗기기
	CASE [[衣装:스니커]], [[衣装:캔버스신발]]
		RESULTS = 운동화 벗기기
	CASEELSE
		RESULTS = 신발 벗기기
	ENDSELECT
ENDIF

@ACT_COM228
#LOCALSSIZE 1
CALL COMNAME228
LOCALS = %RESULTS%
PRINTFORML %LOCALS%
;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = %LOCALS%

;조교자→조교대상
TFLAG:28 = 2

;작업의 이행
TEQUIP:신발 = 0

TFLAG:13 = 44

;노출상태 체크 및 변수조정
CALL 가슴오픈체크(TARGET)
CALL CHECK_EXPOSE(TARGET)
CALL 촉각체크(TARGET)
CALL 고간구조체크, TARGET
CALL 비켜놓기체크(TARGET)

;-------------------------------------------------
;ソース추가処理
;-------------------------------------------------
;소비체력、기력
DOWNBASE:TARGET:체력 += 10
DOWNBASE:TARGET:기력 += 10

SOURCE:TARGET:달성 = 10
SOURCE:TARGET:노출 = 50
SOURCE:TARGET:굴종 = 80
SOURCE:TARGET:일탈 = 30
;연모나 친애가 아닐 경우, 무서움 및 반감이 증가
IF !(TALENT:3 || TALENT:6)
	SOURCE:TARGET:반감 = 150
	SOURCE:TARGET:무서움 = 120
ENDIF
;비디오 찍을때
IF TEQUIP:28
	SOURCE:TARGET:액체 += 50
	SOURCE:TARGET:욕정 += 100
	;연모나 친애가 아닐 경우, 반감이 증가
	SIF !(TALENT:3 || TALENT:6)
		SOURCE:TARGET:반감 += 200
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;억압、저항持ちには、억울ソース추가
SIF TALENT:32 || TALENT:34
	SOURCE:TARGET:울굴 += 150

;처녀、정조관념
IF TALENT:0
	TIMES SOURCE:TARGET:무서움 , 1.20
	TIMES SOURCE:TARGET:굴종 , 1.20
	;연모나 친애가 아닐 경우, 반감이 증가
	SIF !(TALENT:3 || TALENT:6)
		TIMES SOURCE:TARGET:반감 , 1.20
	SOURCE:TARGET:노출 += 300
	;정조관념일 경우, 노출 일탈이 증가
	IF TALENT:30
		TIMES SOURCE:TARGET:노출 , 2.00
		TIMES SOURCE:TARGET:일탈 , 2.00
	ENDIF
ENDIF

;-------------------------------------------------
;경험の処理
;-------------------------------------------------
;레즈경험

A = 1
IF IS_LESBIAN(TARGET,PLAYER)
	CALL ADDS_EXP("레즈경험", A, TARGET)
	CALL ADDS_EXP("레즈경험", A, PLAYER)
ELSEIF IS_GAY(TARGET,PLAYER)
	CALL ADDS_EXP("ＢＬ경험", A, TARGET)
	CALL ADDS_EXP("ＢＬ경험", A, PLAYER)
ENDIF

;의존도ベクトル
TFLAG:33 = 3

;의존도보정値(복종で1、예속で2）
IF TALENT:5 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:8 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1

@MESSAGE_COM228
#LOCALSSIZE 1
IF TEQUIP:주도권
;여자주도
	PRINTFORM %조사처리(CALLNAME:TARGET,"는")% 스스로 
	SELECTCASE CFLAG:신발
	CASE [[衣装:부츠]], [[衣装:하이부츠]], [[衣装:숏부츠]]
		PRINT 부츠를 한쪽씩 천천히 벗어 
	CASE [[衣装:모카신]], [[衣装:명주신]]
		PRINT 고무신을 손쉽게 한쪽씩 벗어 
	CASE [[衣装:펌프스]], [[衣装:하이힐]], [[衣装:핀힐]]
		PRINT 하이힐을 손쉽게 한쪽씩 벗어 
	CASE [[衣装:스니커]], [[衣装:캔버스신발]]
		PRINT 운동화를 한쪽씩 천천히 벗어 
	CASEELSE
		PRINT 신발을 한쪽씩 천천히 벗겨내서 
	ENDSELECT
	IF TEQUIP:양말
		PRINT 양말을 신은 두 발을 
	ELSE
		PRINT 두 맨발을 
	ENDIF
	PRINTW 드러냈다
ELSE
;남자주도
	IF TALENT:3 == 0 && TALENT:4 == 0 && TALENT:6 == 0 && TALENT:7 == 0 && TALENT:8 == 0 && TEQUIP:19 == 0
		PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% 저항하는 %CALLNAME:TARGET%의 
	ELSEIF TEQUIP:19 == 1
		PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% 묶여있는 %CALLNAME:TARGET%의 
	ELSE
		PRINTFORM %조사처리(CALLNAME:PLAYER,"는")% %CALLNAME:TARGET%의 
	ENDIF
	SELECTCASE CFLAG:신발
	CASE [[衣装:부츠]], [[衣装:하이부츠]], [[衣装:숏부츠]]
		PRINT 부츠를 한쪽씩 천천히 벗겨내서 
	CASE [[衣装:모카신]], [[衣装:명주신]]
		PRINT 고무신을 손쉽게 한쪽씩 벗겨내서 
	CASE [[衣装:펌프스]], [[衣装:하이힐]], [[衣装:핀힐]]
		PRINT 하이힐을 손쉽게 한쪽씩 벗겨내서 
	CASE [[衣装:스니커]], [[衣装:캔버스신발]]
		PRINT 운동화를 한쪽씩 천천히 벗겨내서 
	CASEELSE
		PRINT 신발을 한쪽씩 천천히 벗겨내서 
	ENDSELECT
	IF TALENT:3 == 0 && TALENT:4 == 0 && TALENT:6 == 0 && TALENT:7 == 0 && TALENT:8 == 0
		PRINT 억지로 
	ENDIF
	IF TEQUIP:양말
		PRINT 양말을 신은 두 발을 
	ELSE
		PRINT 두 맨발을 
	ENDIF
	IF TALENT:3 == 0 && TALENT:4 == 0 && TALENT:6 == 0 && TALENT:7 == 0 && TALENT:8 == 0
		PRINT 강제로 
	ENDIF	
	PRINTW 드러냈다
ENDIF

;===================================================
;조교타입
;===================================================
@COMTYPE_228
RETURN COMTYPE("특수커맨드계")

