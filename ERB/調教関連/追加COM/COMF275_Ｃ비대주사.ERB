;-------------------------------------------------
;Ｃ비대주사
;-------------------------------------------------
@CHECK_COM_ABLE275
#LOCALSSIZE 1
;클리가 사용 가능해야함
SIF !USE_CLI(TARGET)
	RETURN 0
;약물내성이라면 안 됨
SIF TALENT:약독내성
    RETURN 0
;이미 봉이 있는(남자나 후타나리)라면 안 됨
SIF HAVE_PENIS(TARGET)
	RETURN 0
;기계라면 안 됨
SIF TALENT:기계의처녀
    RETURN 0
;클리토리스를 성장시키지 않았다고 안 됨
;SIF BASE:음핵 == 0
;    RETURN 0
;클리토리스 링을 끼고 있으면(자) 안 됨
;SIF CFLAG:42 & 1
;   RETURN 0
;기교 3 이하의 ASSI의 경우 주사할 수 없다(바늘 다루기라면 OK)
IF ASSIPLAY
    SIF ABL:ASSI:기교 <= 3 && TALENT:ASSI:바늘다루기 == 0
        RETURN 0
ENDIF
;주사 세트와 음핵 성장제를 가지고 있는지, 아이템 없음 설정이 아니면 안 됨
SIF ITEM:주사기세트 == 0 || ITEM:음핵성장제 == 0
    RETURN 0
;음핵 성장제사용 중에는 
SIF TEQUIP:음핵성장제
    RETURN 0
;C계 장착도구 사용중은 
SIF TEQUIP:클리캡 || TEQUIP:오나홀 || TEQUIP:음핵전극 || TEQUIP:전극오나홀
    RETURN 0
;승마 중에는 
SIF TEQUIP:삼각목마
    RETURN 0
;목욕탕 플레이 중에는 
SIF TEQUIP:목욕탕
    RETURN 0
;촉수 조교 중에는 
SIF TEQUIP:촉수
    RETURN 0
;야외플레이 중에는 
SIF TEQUIP:야외플레이
    RETURN 0
RETURN 1

@ACT_COM275

PRINTL 클리 비대주사
TSTR:0 = Ｃ비대주사


;조교자→조교대상
TFLAG:28 = 2

;-------------------------------------------------
;ソース추가処理
;-------------------------------------------------
;조제 지식이 있으면 소비를 억제한다
LOCAL:3 = (TALENT:MASTER:조합지식) ?  1 # 0
SIF ASSI >= 0 && TALENT:ASSI:조합지식
    LOCAL:3 = 1

LOCAL:1 = (LOCAL:3) ?  750 # 1000
LOCAL:2 = (LOCAL:3) ?  600 # 800

DOWNBASE:체력 += LOCAL:1
DOWNBASE:기력 += LOCAL:2

SOURCE:쾌Ｃ = 1200
SOURCE:중독충족 = 500
SOURCE:일탈 = 2000

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:일탈 , 2.00
ELSEIF ABL:순종 == 1
    TIMES SOURCE:일탈 , 1.80
ELSEIF ABL:순종 == 2
    TIMES SOURCE:일탈 , 1.50
ELSEIF ABL:순종 == 3
    TIMES SOURCE:일탈 , 1.20
ELSEIF ABL:순종 == 4
    TIMES SOURCE:일탈 , 1.00
ELSE
    TIMES SOURCE:일탈 , 0.80
ENDIF

;ABL:C감각을 본다
IF ABL:Ｃ감각 == 0
    TIMES SOURCE:쾌Ｃ , 0.50
ELSEIF ABL:Ｃ감각 == 1
    TIMES SOURCE:쾌Ｃ , 0.75
ELSEIF ABL:Ｃ감각 == 2
    TIMES SOURCE:쾌Ｃ , 1.00
ELSEIF ABL:Ｃ감각 == 3
    TIMES SOURCE:쾌Ｃ , 1.25
ELSEIF ABL:Ｃ감각 == 4
    TIMES SOURCE:쾌Ｃ , 1.50
    EX:Ｃ절정 += RAND:2
ELSE
    TIMES SOURCE:쾌Ｃ , 2.00
    EX:Ｃ절정 += 1
ENDIF

;ABL:마조끼를 본다
IF ABL:마조끼 == 0
    TIMES SOURCE:중독충족 , 0.80
    TIMES DOWNBASE:0 , 1.20
ELSEIF ABL:마조끼 == 1
    TIMES SOURCE:중독충족 , 1.10
    TIMES DOWNBASE:0 , 1.10
ELSEIF ABL:마조끼 == 2
    TIMES SOURCE:중독충족 , 1.30
    TIMES DOWNBASE:0 , 1.00
ELSEIF ABL:마조끼 == 3
    TIMES SOURCE:중독충족 , 1.50
    TIMES DOWNBASE:0 , 0.90
ELSEIF ABL:마조끼 == 4
    TIMES SOURCE:중독충족 , 1.70
    TIMES DOWNBASE:0 , 0.80
ELSE
    TIMES SOURCE:중독충족 , 2.10
    TIMES DOWNBASE:0 , 0.70
ENDIF

;PALAM:고통을 본다
IF PALAM:고통 < PALAMLV:1
    SOURCE:아픔 = 3000
ELSEIF PALAM:고통 < PALAMLV:2
    SOURCE:아픔 = 3300
ELSEIF PALAM:고통 < PALAMLV:3
    SOURCE:아픔 = 3600
ELSEIF PALAM:고통 < PALAMLV:4
    SOURCE:아픔 = 4000
ELSE
    SOURCE:아픔 = 4500
ENDIF

;반항적
SIF TALENT:반항적
    TIMES SOURCE:아픔 , 1.10
;저항
SIF TALENT:저항
    TIMES SOURCE:아픔 , 1.10
;C민감
IF TALENT:Ｃ민감
    IF ABL:Ｃ감각 <= 3
        TIMES SOURCE:쾌Ｃ , 1.20
    ELSEIF ABL:Ｃ감각 == 4
        TIMES SOURCE:쾌Ｃ , 1.50
    ELSE
        TIMES SOURCE:쾌Ｃ , 1.80
        EX:Ｃ절정 += RAND:2
    ENDIF
;C둔감
ELSEIF TALENT:Ｃ둔감
    EX:Ｃ절정 -= (EX:Ｃ절정 > 1) ?  1 # 0
    IF ABL:Ｃ감각 <= 3
        TIMES SOURCE:쾌Ｃ , 0.60
    ELSEIF ABL:Ｃ감각 == 4
        TIMES SOURCE:쾌Ｃ , 0.70
    ELSE
        TIMES SOURCE:쾌Ｃ , 0.80
    ENDIF
ENDIF

;조교자가 바늘 다루기
SIF TALENT:PLAYER:바늘다루기
    TIMES SOURCE:아픔 , 0.90
;조교자가 유치
SIF TALENT:PLAYER:유치
    TIMES SOURCE:아픔 , 1.20



;음핵 성장제 1개 소비
ITEM:음핵성장제 -= 1

IF EX:Ｃ절정 < 5
    LOCAL:4 = 5 + RAND:10
ELSEIF EX:Ｃ절정 < 10
    LOCAL:4 = 10 + RAND:10
ELSEIF EX:Ｃ절정 < 20
    LOCAL:4 = 15 + RAND:15
ELSEIF EX:Ｃ절정 < 50
    LOCAL:4 = 20 + RAND:15
ELSEIF EX:Ｃ절정 < 100
    LOCAL:4 = 30 + RAND:15
ELSE
    LOCAL:4 = 50 + RAND:15
ENDIF
BASE:음핵 = MIN(BASE:음핵 + LOCAL:4, 250)

;음핵 성장제(주사) 효과 발휘
TEQUIP:음핵성장제 = 2

	
;-------------------------------------------------
;경험の処理
;-------------------------------------------------

;의존도ベクトル
TFLAG:33 = 3

IF !IS_MALE(TARGET) && !IS_MALE(PLAYER)
    TCVAR:획득레즈경험 += 2
ELSEIF IS_MALE(TARGET) && IS_MALE(PLAYER)
    TCVAR:획득ＢＬ경험 += 2
ENDIF

;의존도보정値(복종で1、예속で2）
IF TALENT:5 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:8 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1

;--------------------------------------------------
;음핵 성장제(주사) 효과 발휘중
;--------------------------------------------------
@EQUIP_COM275
LOCAL = 0
IF EX:Ｃ절정 < 10
    LOCAL = 1 + RAND:5
ELSEIF EX:Ｃ절정 < 50
    LOCAL = 3 + RAND:5
ELSEIF EX:Ｃ절정 < 100
    LOCAL = 5 + RAND:5
ELSE
    LOCAL = 7 + RAND:5
ENDIF

BASE:음핵 = MIN(BASE:음핵 + LOCAL, 250)
PRINTFORML 음핵의 성장 상태:{BASE:음핵}


@MESSAGE_COM275
PRINTL
PRINTFORML %조사선택(CALLNAME:TARGET, "는")% %CALLNAME:PLAYER%에게 지금 할 일을 듣자 
;괴조인격, 괴뢰, 정신 붕괴
IF TALENT:붕괴 || TALENT:허심
    PRINTW 아주 잠깐, 싫어하는 표정을 지은 것 같다….
;속박되고 있다
ELSEIF TEQUIP:밧줄
    PRINT 자유를 빼앗긴 모습으로 
    IF ABL:순종 < 3 || MARK:굴복각인 < 1 || MARK:반발각인 > 0
        IF TALENT:반항적 || TALENT:꿋꿋함 || TALENT:건방짐
            PRINTW 다부지게도 이쪽을 째려보고 있다….
        ELSEIF TALENT:겁쟁이 || TALENT:얌전함
            PRINTW 눈에 눈물을 머금고 무서워하고 있다….
        ELSE
            PRINTW 창백해진 표정으로 고개를 숙이고 있다….
        ENDIF
    ELSEIF TALENT:복종 || TALENT:예속
        PRINTW 어딘지 모르게 기대로 가득 찬 시선을 하고 있다….
    ELSE
        PRINTW 저항을 단념하고 고개를 숙였다….
    ENDIF
;속박되지 않았다
ELSE
    IF ABL:순종 < 3 || MARK:굴복각인 < 1 || MARK:반발각인 > 0
        PRINT 도망치려다 억지로 붙잡혀 
        IF TALENT:반항적 || TALENT:꿋꿋함 || TALENT:건방짐
            PRINTW 다부지게도 이쪽을 째려보고 있다.
        ELSEIF TALENT:겁쟁이 || TALENT:얌전함
            PRINTW 이빨이 부딪치는 소리가 나도록 떨고 있다.
        ELSE
            PRINTW 창백해진 표정을 하고 고개를 숙이고 있다….
        ENDIF
    ELSEIF TALENT:복종 || TALENT:예속
        PRINTW 주인의 명령에 따라, 스스로 보지를 크게 벌렸다….
    ELSE
        PRINTW 저항을 단념하고 스스로 보지를 벌리고 있다….
    ENDIF
ENDIF

PRINTL
PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 
;여체 광란보다 발췌입니다
SIF ABL:PLAYER:새드끼 > 3 || TALENT:PLAYER:새드
    PRINTL 주사를 놓으려면 마취를 해야 하잖냐는 농담을 던지며
PRINTFORM %CALLNAME:TARGET%의 
SIF TALENT:비대음핵 && BASE:음핵 > 150
    PRINT 거대한 
PRINTL 클리토리스에 가차 없이 주사바늘을 꽂았다….
PRINTFORMW %조사선택(CALLNAME:TARGET, "는")% 통증으로 몸을 떨고 있다.

PRINTL
PRINTL 효과가 나타나기 시작했는지 점착질의 음란한 소리를 내면서
SIF TALENT:비대음핵 && BASE:음핵 > 150
    PRINT 거대한 
PRINTL 클리토리스는 점점 부풀어간다.
PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 그 광경을 
IF TALENT:붕괴 || TALENT:허심
    PRINTW 보고 무엇을 생각하는 건지, 유리구슬 같은 눈동자로 뚫어져라 보고 있다.
ELSEIF ABL:순종 < 3 || MARK:굴복각인 < 1 || MARK:반발각인 > 0
    IF TALENT:반항적 || TALENT:꿋꿋함 || TALENT:프라이드높음
        PRINTW 보면서 당황하고 있다.
    ELSEIF TALENT:겁쟁이 || TALENT:얌전함
        PRINTW 보자 창백한 얼굴이 되었다.
    ELSE
        PRINTW 보지 않기 위해 시선을 돌리고 있다….
    ENDIF
ELSE
    PRINTW 지그시 지켜보고 있다….
ENDIF
PRINTL


;===================================================
;조교타입
;===================================================
@COMTYPE_275
RETURN COMTYPE("특수커맨드계")

