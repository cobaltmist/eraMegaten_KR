;-------------------------------------------------
;배란유도제
;-------------------------------------------------
@CHECK_COM_ABLE273
;조수 플레이중은 일단 금지(잠정)
SIF ASSIPLAY
    RETURN 0
;아이템을 가지고 있는지, 아이템 없음 설정이 아니면 안 됨
SIF ITEM:배란유도제 == 0
    RETURN 0
;약물내성의 경우 안 됨
SIF TALENT:약독내성
    RETURN 0
;조교자가 남자나 후타나리 나오지 않으면 안 됨
SIF HAVE_PENIS(PLAYER) == 0
    RETURN 0
;남자라면 안 됨
SIF TALENT:남자
    RETURN 0
;임신중이라면 
SIF TALENT:임신
    RETURN 0
;벌써 사용중이라면 사용할 수 없다
SIF TEQUIP:배란유도제 || CFLAG:배란유발제
    RETURN 0
;긴급 피임약을 사용 중이면 사용할 수 없다
SIF TEQUIP:피임약 || CFLAG:자궁내피임결계
    RETURN 0
;강제개구기 외에 다른 걸로 입이 막혀 있으면 
SIF TEQUIP:볼재갈
    RETURN 0
;촉수 조교 중에는 
SIF TEQUIP:촉수
    RETURN 0
;야외플레이 중에는
SIF TEQUIP:야외플레이
    RETURN 0
;여자주도일때 연모계 함락이 아닐 경우는 안됨
SIF TEQUIP:주도권 && !(TALENT:연모 || TALENT:친애)
	RETURN 0
RETURN 1

@ACT_COM273

PRINTL 배란유도제
TSTR:0 = 배란유도제


;조교者→조교대상
TFLAG:28 = 2

;-------------------------------------------------
;ソース추가処理
;-------------------------------------------------
;조제 지식이 있으면 소비를 억제한다
IF TALENT:MASTER:조합지식 || (ASSI >= 0 && TALENT:ASSI:조합지식)
    DOWNBASE:0 += 45
    DOWNBASE:1 += 45
ELSE
    DOWNBASE:0 += 100
    DOWNBASE:1 += 100
ENDIF

LOCAL:1 = 0

;연모인가 친애나 상애 때
IF TALENT:연모 || TALENT:친애
	SOURCE:정애 = 1500
	LOCAL:1 = 1
ENDIF
;복종이나 망신 때
IF TALENT:복종 || TALENT:예속
	SOURCE:복종 = 2000
	LOCAL:1 = 1
ENDIF
;음란 때
IF (TALENT:음란 || TALENT:창부)
	SOURCE:욕정 = 1800
	LOCAL:1 = 1
ENDIF
;함락 하고 있지는 않지만 순종 Lv5로 굴복 각인 3으로 반발 각인이 없을 때
IF LOCAL:1 == 0 && ABL:순종 == 5 && MARK:굴복각인 == 3 && MARK:반발각인 == 0
	SOURCE:액체 += 100
	SOURCE:굴종 = 1000
	LOCAL:1 = 1
ENDIF

;위의 각 조건에 들어맞지 않는 경우는 반발이 있다
IF LOCAL:1 == 0
	SOURCE:반감 = 1000
	LOCAL:2 = 400
	;ABL:순종을 본다
	IF ABL:순종 == 0
		TIMES LOCAL:2 , 6.00
	ELSEIF ABL:순종 == 1
		TIMES LOCAL:2 , 5.00
	ELSEIF ABL:순종 == 2
		TIMES LOCAL:2 , 4.00
	ELSEIF ABL:순종 == 3
		TIMES LOCAL:2 , 3.00
	ELSEIF ABL:순종 == 4
		TIMES LOCAL:2 , 2.00
	ENDIF
	SOURCE:반감 += LOCAL:2
	LOCAL:3 = 500
	;MARK:반발 각인을 본다
	IF MARK:반발각인 == 0
		TIMES LOCAL:3 , 2.00
	ELSEIF MARK:반발각인 == 1
		TIMES LOCAL:3 , 3.00
	ELSEIF MARK:반발각인 == 2
		TIMES LOCAL:3 , 4.00
	ELSEIF MARK:반발각인 >= 3
		TIMES LOCAL:3 , 5.00
	ENDIF
	SOURCE:무서움 += 1000
	SOURCE:반감 += LOCAL:3
ELSE
	SOURCE:액체 = 1500
ENDIF

TEQUIP:배란유도제 = 1
ITEM:배란유도제 -= 1

CFLAG:배란유도제 = 1
;-------------------------------------------------
;경험の処理
;-------------------------------------------------

;의존도ベクトル
TFLAG:33 = 3

A = 1
IF IS_LESBIAN(TARGET,PLAYER)
	CALL ADDS_EXP("레즈경험", A, TARGET)
	CALL ADDS_EXP("레즈경험", A, PLAYER)
ELSEIF IS_GAY(TARGET,PLAYER)
	CALL ADDS_EXP("ＢＬ경험", A, TARGET)
	CALL ADDS_EXP("ＢＬ경험", A, PLAYER)
ENDIF


RETURN 1


@MESSAGE_COM273
IF TEQUIP:주도권
;여자주도 -> 연모계 함락만 가능함
	PRINTFORML %조사처리(CALLNAME:TARGET,"는")% 요염하게 웃으면서 %CALLNAME:PLAYER%에게 보라는 듯이 배란유도제를 꺼내 먹었다.
	SETCOLOR 0xff0000
	PRINTFORMW 그것을 본 %조사처리(CALLNAME:PLAYER,"는")% 순간적으로 공포를 느꼈다!
	RESETCOLOR
ELSE
;남자주도
	PRINTFORM %CALLNAME:TARGET%에게 
	SIF ABL:PLAYER:기교 >= 3
		PRINT 입으로 
	SIF ABL:순종 <= 2
		PRINT 억지로 
	PRINTL 배란유도제를 먹였다.
	PRINTFORM \@ (EXP:출산경험 == 0) ? 먹인 약의 효능을 설명하자 # 무엇을 먹었는지 깨달은 \@ %조사선택(CALLNAME:TARGET, "는")% 
	;IF TALENT:135 || TALENT:136
	;    ;【난생】,【산란 체질】의 어느 쪽인지
	;    PRINTW 이상한 약을 먹인 것에 대한 반감을 숨길 수 없었다….
	IF TALENT:연모 || TALENT:친애
		;【연모】,【친애】,【상애】의 최악이어도 1개를 가진다
		PRINTFORMW %CALLNAME:PLAYER%의 아기를 가지길 기대한 듯, 어리광을 피우듯이 다가오고 있다.
	ELSEIF TALENT:복종 || TALENT:예속
		;【복종】,【낙인】의 어느 쪽인지
		PRINTW 질 내 사정을 황홀한 표정으로 기다리고 있다.
	ELSEIF TALENT:음란 || TALENT:창부
		;【예속】,【음란】의 어느 쪽인지
		PRINTW 금방이라도 질 내 사정해 주었으면 하는 것처럼 보지를 손가락으로 열고 있다.
	ELSEIF ABL:순종 >= 5 && MARK:굴복각인 == 3 && MARK:반발각인 == 0
		;종속 Lv5 또한 굴복 각인 Lv3 또한 반발 각인 Lv0
		PRINTW 포기했는지 다리를 벌리고 다음 행동을 기다리고 있다.
	ELSE
		PRINTFORMW %조사선택(CALLNAME:PLAYER, "를")% 심상치 않은 표정으로 째려보고 있다.
	ENDIF
ENDIF

;===================================================
;조교타입
;===================================================
@COMTYPE_273
RETURN COMTYPE("특수커맨드계")

