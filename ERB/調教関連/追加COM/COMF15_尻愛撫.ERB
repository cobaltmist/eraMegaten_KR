;
;	Edit	Date			Author					Reason
;	001		2021/01/28		eMさ者					V挿入継続時の分岐を追加
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#


;-------------------------------------------------
;엉덩이애무
;애무계コマンド　레벨2
;-------------------------------------------------

;-------------------------------------------------
;コマンド実行판정
;-------------------------------------------------
@CHECK_COM_ABLE15
;엉덩이애무実行판정
SIF !USE_HAND(PLAYER)
	RETURN 0

RETURN 1

@ACT_COM15
PRINTL 엉덩이애무
TSTR:0 = 엉덩이애무
;CALL KOJO_MESSAGE_COM

;조교자→조교대상
TFLAG:28 = 2

;-------------------------------------------------
;ソース추가処理
;-------------------------------------------------
;소비체력、기력
DOWNBASE:TARGET:체력 += 5
DOWNBASE:TARGET:기력 += 30

SOURCE:TARGET:정애 = 75
SOURCE:TARGET:성행위 = 30
SOURCE:TARGET:노출 = 10
SOURCE:TARGET:반감 = 20

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;억압、저항持ちには、억울ソース추가
SIF TALENT:32 || TALENT:34
	SOURCE:TARGET:울굴 += 20

;-------------------------------------------------
;吸精処理
;-------------------------------------------------
IF CFLAG:PLAYER:ＭＡＧ비피흡수 == 0
	LOCAL = (RAND:3+1) * BASE:LV
	CALL DRAIN_MAG,PLAYER,LOCAL,TARGET
ELSEIF CFLAG:TARGET:ＭＡＧ비피흡수 == 0
	LOCAL = (RAND:3+1) * BASE:LV
	CALL DRAIN_MAG,TARGET,LOCAL,PLAYER
ENDIF


;-------------------------------------------------
;경험の処理
;-------------------------------------------------
A = 3
IF IS_LESBIAN(TARGET,PLAYER)
	CALL ADDS_EXP("레즈경험", A, TARGET)
	CALL ADDS_EXP("레즈경험", A, PLAYER)
ELSEIF IS_GAY(TARGET,PLAYER)
	CALL ADDS_EXP("ＢＬ경험", A, TARGET)
	CALL ADDS_EXP("ＢＬ경험", A, PLAYER)
ENDIF

;의존도ベクトル
TFLAG:33 = 2

RETURN 1

@MESSAGE_COM15
IF TEQUIP:50 > 0
	PRINT 이어진 채 허리를 움직이면서, 
ENDIF
PRINTFORM %CALLNAME:TARGET%의 
;하의または전신복を着ている
IF TEQUIP:82 || TEQUIP:83
	IF TEQUIP:82
		CALL CLOTHES_NAME,2,TARGET
	ELSE
		CALL CLOTHES_NAME,3,TARGET
	ENDIF
	IF RAND:2 == 0
		PRINT 에 손을 밀어넣고、
			IF TALENT:유아체형
				PRINT 작은 엉덩이를 
			ELSE
				PRINT 탄력있는 엉덩이를 
			ENDIF
	ELSE
		PRINT 손가락으로 엉덩이를 잡고、
	ENDIF
;속옷（하）または전신속옷を着ている
ELSEIF TEQUIP:86 || TEQUIP:87
	IF TEQUIP:86
		CALL CLOTHES_NAME,6,TARGET
	ELSE
		CALL CLOTHES_NAME,7,TARGET
	ENDIF
	IF RAND:2 == 0
		PRINT 를 먹고있는 듯한、엉덩이를 
	ELSE
		PRINT 의 위에서 엉덩이를 잡고、
	ENDIF
ELSE
	IF RAND:2 == 0
		PRINT 엉덩이를 잡고、
	ELSE
		IF TALENT:유아체형
			PRINT 사랑스러운 엉덩이를 
		ELSE
			PRINT 육덕진 엉덩이를 
		ENDIF
	ENDIF
ENDIF
IF RAND:2 == 0
	PRINTW 부드럽게 주물렀다…
ELSE
	PRINTW 이리저리 쓰다듬었다…
ENDIF
;===================================================
;조교타입
;===================================================
@COMTYPE_15
RETURN COMTYPE("애무계")

