;-------------------------------------------------
;유두확장주사
;-------------------------------------------------
@CHECK_COM_ABLE276
;주사를 놀라면 젖꼭지가 보여야지
SIF !USE_NIPLE(TARGET)
    RETURN 0
;주인이 금단의 지식이 없으면 
SIF TALENT:MASTER:금단의지식 == 0
    RETURN 0
;남자라면 안됨
SIF TALENT:남자
    RETURN 0
;절벽이면 안됨
SIF TALENT:절벽
    RETURN 0
;확장유두라면 안됨
SIF TALENT:확장유두
    RETURN 0
;약물내성이라면 안 됨
SIF TALENT:약독내성
    RETURN 0
;기계라면 안 됨
SIF TALENT:기계의처녀
    RETURN 0
;기교 3 이하의 ASSI의 경우 주사할 수 없다(바늘 다루기라면 OK)
IF ASSIPLAY
    SIF ABL:ASSI:기교 <= 3 && TALENT:ASSI:바늘다루기 == 0
        RETURN 0
ENDIF
;주사 세트와 유두확장제를 가지고 있거나, 아이템 없음 설정이 아니면 안 됨
SIF (ITEM:주사기세트 == 0 || ITEM:유두확장제 == 0)
    RETURN 0
;B계 장착도구 사용중은 
SIF TEQUIP:유두캡 || TEQUIP:착유기 || TEQUIP:유방전극
    RETURN 0
;촉수 조교 중에는 
SIF TEQUIP:촉수
    RETURN 0
;야외플레이 중에는 
SIF TEQUIP:야외플레이
    RETURN 0
RETURN 1

@ACT_COM276

PRINTL 유두확장주사
TSTR:0 = 유두확장주사


;조교者→조교대상
TFLAG:28 = 2

;-------------------------------------------------
;ソース추가処理
;-------------------------------------------------
DOWNBASE:체력 += 500
DOWNBASE:기력 += 1000

SOURCE:쾌Ｂ = 500
SOURCE:일탈 = 3000
SOURCE:무서움 = 3000

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;ABL:순종을 본다
IF ABL:순종 == 0
    TIMES SOURCE:일탈 , 2.00
ELSEIF ABL:순종 == 1
    TIMES SOURCE:일탈 , 1.80
ELSEIF ABL:순종 == 2
    TIMES SOURCE:일탈 , 1.50
ELSEIF ABL:순종 == 3
    TIMES SOURCE:일탈 , 1.20
ELSEIF ABL:순종 == 4
    TIMES SOURCE:일탈 , 1.00
ELSE
    TIMES SOURCE:일탈 , 0.80
ENDIF

;ABL:B감각을 본다
IF ABL:Ｂ감각 == 0
    TIMES SOURCE:쾌Ｂ , 0.50
ELSEIF ABL:Ｂ감각 == 1
    TIMES SOURCE:쾌Ｂ , 1.00
ELSEIF ABL:Ｂ감각 == 2
    TIMES SOURCE:쾌Ｂ , 1.50
ELSEIF ABL:Ｂ감각 == 3
    TIMES SOURCE:쾌Ｂ , 2.00
    EX:3 += RAND:1
ELSEIF ABL:Ｂ감각 == 4
    TIMES SOURCE:쾌Ｂ , 2.50
ELSE
    TIMES SOURCE:쾌Ｂ , 3.00
ENDIF

;ABL:마조끼를 본다
IF ABL:마조끼 == 0
    TIMES DOWNBASE:체력 , 1.20
ELSEIF ABL:마조끼 == 1
    TIMES DOWNBASE:체력 , 1.10
ELSEIF ABL:마조끼 == 2
    TIMES DOWNBASE:체력 , 1.00
ELSEIF ABL:마조끼 == 3
    TIMES DOWNBASE:체력 , 0.90
ELSEIF ABL:마조끼 == 4
    TIMES DOWNBASE:체력 , 0.80
ELSE
    TIMES DOWNBASE:체력 , 0.70
ENDIF

;PALAM:고통을 본다
IF PALAM:고통 < PALAMLV:1
    SOURCE:아픔 = 3000
ELSEIF PALAM:고통 < PALAMLV:2
    SOURCE:아픔 = 3300
ELSEIF PALAM:고통 < PALAMLV:3
    SOURCE:아픔 = 3600
ELSEIF PALAM:고통 < PALAMLV:4
    SOURCE:아픔 = 4000
ELSE
    SOURCE:아픔 = 4500
ENDIF

;반항적
SIF TALENT:반항적
    TIMES SOURCE:아픔 , 1.10
;저항
SIF TALENT:저항
    TIMES SOURCE:아픔 , 1.10
;겁쟁이
SIF TALENT:10
    TIMES DOWNBASE:체력 , 1.30
;음유
IF TALENT:음유
    TIMES SOURCE:쾌Ｂ , 1.50
    TIMES SOURCE:일탈 , 1.50
    TIMES SOURCE:무서움 , 0.50
ENDIF
;B민감
IF TALENT:Ｂ민감
    IF ABL:Ｂ감각 <= 3
        TIMES SOURCE:쾌Ｂ , 1.80
    ELSEIF ABL:Ｂ감각 == 4
        TIMES SOURCE:쾌Ｂ , 2.00
        EX:3 += RAND:2
    ELSE
        TIMES SOURCE:쾌Ｂ , 2.20
        EX:3 += RAND:3
    ENDIF
;B둔감
ELSEIF TALENT:Ｂ둔감
    IF ABL:Ｂ감각 <= 3
        TIMES SOURCE:쾌Ｂ , 0.70
        EX:3 -= (EX:3 > 1) ?  1 # 0
    ELSEIF ABL:Ｂ감각 == 4
        TIMES SOURCE:쾌Ｂ , 0.75
        EX:3 -= (EX:3 > 1) ?  1 # 0
    ELSE
        TIMES SOURCE:쾌Ｂ , 0.80
    ENDIF
ENDIF

;조교자에게 바늘 다루기
SIF TALENT:PLAYER:바늘다루기
    TIMES SOURCE:아픔 , 0.90
;조교자에게 유치
SIF TALENT:PLAYER:유치
    TIMES SOURCE:아픔 , 1.20


	
;-------------------------------------------------
;경험の処理
;-------------------------------------------------

;의존도ベクトル
TFLAG:33 = 3

IF !IS_MALE(TARGET) && !IS_MALE(PLAYER)
    TCVAR:획득레즈경험 += 1
ELSEIF IS_MALE(TARGET) && IS_MALE(PLAYER)
    TCVAR:획득ＢＬ경험 += 1
ENDIF

;유두확장제 사용
ITEM:유두확장제 -= 1


;확장유두, 모유체질 획득
TALENT:확장유두 = 1
IF TALENT:모유체질 == 0
    TALENT:모유체질 = 1
ENDIF

;이상 경험
CALL ADDS_ABNORMAL_EXP("유두확장", TARGET)


RETURN 1

@MESSAGE_COM276
PRINTL
PRINTFORM %CALLNAME:TARGET%의 
SIF TALENT:Ｂ민감
    PRINT 심히 예민한 
PRINTL 유두 끝에 주사바늘을 꽂았다….
PRINTL
PRINTFORM %CALLNAME:TARGET%의 
IF TALENT:빈유
    PRINT 작은
ELSEIF TALENT:거유 || TALENT:폭유 || TALENT:마유
    PRINT 풍만한
ENDIF
PRINTL  가슴의 유두에 조금씩 변화가 일어나더니
PRINTW 유두구가 벌어지고 유선이 신축성을 얻어 모유를 흘려대는 구멍이 되었다.


;===================================================
;조교타입
;===================================================
@COMTYPE_276
RETURN COMTYPE("특수커맨드계")

