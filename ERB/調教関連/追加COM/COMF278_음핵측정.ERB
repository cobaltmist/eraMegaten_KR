;-------------------------------------------------
;음핵 측정
;-------------------------------------------------
@CHECK_COM_ABLE278

;클리가 사용 가능해야함
SIF !USE_CLI(TARGET)
	RETURN 0
;남자 또는 후타나리면 
SIF HAVE_PENIS(TARGET)
    RETURN 0
;Ｃ계 장착구 사용 중에는 
SIF TEQUIP:클리캡 || TEQUIP:오나홀 || TEQUIP:음핵전극 || TEQUIP:전극오나홀
    RETURN 0
;클리토리스를 성장시키지 않으면 
SIF BASE:음핵 == 0
    RETURN 0
;승마 중에는 
SIF TEQUIP:삼각목마
    RETURN 0
;욕실 플레이 중에는 
SIF TEQUIP:목욕탕
    RETURN 0
;촉수 조교 중에는 
SIF TEQUIP:촉수
    RETURN 0
;야외플레이 중에는 
SIF TEQUIP:야외플레이
    RETURN 0
RETURN 1

@ACT_COM278

PRINTL 음핵 측정
TSTR:0 = 음핵 측정


;조교자→조교대상
TFLAG:28 = 2

;-------------------------------------------------
;ソース추가処理
;-------------------------------------------------
DOWNBASE:체력 += 50
DOWNBASE:기력 += 90

SOURCE:노출 = 250
SOURCE:굴종 = 250
SOURCE:일탈 = 500

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;ABL:C감각을 본다
IF ABL:Ｃ감각 == 0
    SOURCE:쾌Ｃ = 100
    SOURCE:굴종 += 200
ELSEIF ABL:Ｃ감각 == 1
    SOURCE:쾌Ｃ = 200
    SOURCE:굴종 += 300
ELSEIF ABL:Ｃ감각 == 2
    SOURCE:쾌Ｃ = 350
    SOURCE:굴종 += 600
ELSEIF ABL:Ｃ감각 == 3
    SOURCE:쾌Ｃ = 700
    SOURCE:굴종 += 900
ELSEIF ABL:Ｃ감각 == 4
    SOURCE:쾌Ｃ = 1400
    SOURCE:굴종 += 1300
ELSE
    SOURCE:쾌Ｃ = 1800
    SOURCE:굴종 += 1700
ENDIF

;ABL:노출벽을 본다
IF ABL:노출증 == 0
    SOURCE:노출 += 1500
    SOURCE:불결 = 2000
ELSEIF ABL:노출증 == 1
    SOURCE:노출 += 1250
    SOURCE:불결 = 1000
ELSEIF ABL:노출증 == 2
    SOURCE:노출 += 750
    SOURCE:불결 = 500
ELSEIF ABL:노출증 == 3
    SOURCE:노출 += 500
    SOURCE:불결 = 200
ELSEIF ABL:노출증 == 4
    SOURCE:노출 += 250
    SOURCE:불결 = 100
ELSE
    SOURCE:불결 = 50
ENDIF

;PALAM:욕정을 본다
IF PALAM:욕정 < PALAMLV:1
    TIMES SOURCE:불결 , 1.80
ELSEIF PALAM:욕정 < PALAMLV:2
    TIMES SOURCE:불결 , 1.50
ELSEIF PALAM:욕정 < PALAMLV:3
    TIMES SOURCE:불결 , 1.20
ELSEIF PALAM:욕정 < PALAMLV:4
    TIMES SOURCE:불결 , 0.80
ELSE
    TIMES SOURCE:불결 , 0.50
ENDIF

;부끄럼쟁이·부끄럼없음
IF TALENT:35
    TIMES SOURCE:불결 , 1.20
ELSEIF TALENT:36
    TIMES SOURCE:불결 , 0.50
ENDIF
;C민감, 둔감을 본다
;쾌C자체의 체크는 다음에 정리해 준다
IF TALENT:Ｃ민감
    TIMES SOURCE:쾌Ｃ , 1.50
    TIMES SOURCE:굴종 , 1.50
    TIMES SOURCE:일탈 , 1.50
ELSEIF TALENT:Ｃ둔감
    TIMES SOURCE:쾌Ｃ , 0.60
    TIMES SOURCE:굴종 , 0.60
    TIMES SOURCE:일탈 , 1.50
ENDIF

;처녀, 정조관념
IF TALENT:처녀
    TIMES SOURCE:불결 , 1.20
    IF TALENT:처녀 == 1
        SOURCE:노출 += 2000
        TIMES SOURCE:굴종 , 2.00
        TIMES SOURCE:불결 , 2.00
        SIF TALENT:정조관념
            TIMES SOURCE:노출 , 2.00
    ENDIF
ENDIF

IF ABL:순종 < 3 && MARK:굴복각인 > 0 && MARK:반발각인 < 3
    ;EXP:자위 경험을 본다
    IF EXP:자위경험 < EXPLV:1
        TIMES SOURCE:불결 , 1.50
        TIMES SOURCE:굴종 , 0.20
    ELSEIF EXP:자위경험 < EXPLV:2
        TIMES SOURCE:불결 , 1.20
        TIMES SOURCE:굴종 , 0.50
    ELSEIF EXP:자위경험 < EXPLV:3
        TIMES SOURCE:불결 , 1.00
        TIMES SOURCE:굴종 , 0.80
    ELSEIF EXP:자위경험 < EXPLV:4
        TIMES SOURCE:불결 , 0.80
        TIMES SOURCE:굴종 , 1.00
    ELSEIF EXP:자위경험 < EXPLV:5
        TIMES SOURCE:불결 , 0.50
        TIMES SOURCE:굴종 , 1.20
    ELSE
        TIMES SOURCE:불결 , 0.20
        TIMES SOURCE:굴종 , 1.50
    ENDIF

    SIF ABL:노출증 >= 3
        TCVAR:획득자위경험 += 1
ENDIF
;-------------------------------------------------
;불결の処理
;-------------------------------------------------
IF TEQUIP:PLAYER:손 == 401 || TEQUIP:PLAYER:손 == 404 || TEQUIP:PLAYER:손 == 405 || TEQUIP:PLAYER:손 == 407 || TEQUIP:PLAYER:손 == 412 || TEQUIP:PLAYER:손 == 414
	CALL MOVE_STAIN("바기나", TARGET, "장갑", PLAYER)
ELSE
	CALL MOVE_STAIN("바기나", TARGET, "손", PLAYER)
ENDIF
;-------------------------------------------------
;吸精処理
;-------------------------------------------------
IF CFLAG:PLAYER:ＭＡＧ비피흡수 == 0
	LOCAL = (RAND:4+1) * BASE:LV
	CALL DRAIN_MAG,PLAYER,LOCAL,TARGET
ELSEIF CFLAG:TARGET:ＭＡＧ비피흡수 == 0
	LOCAL = (RAND:4+1) * BASE:LV
	CALL DRAIN_MAG,TARGET,LOCAL,PLAYER
ENDIF


;-------------------------------------------------
;경험の処理
;-------------------------------------------------

;의존도ベクトル
TFLAG:33 = 3

IF !IS_MALE(TARGET) && !IS_MALE(PLAYER)
    TCVAR:획득레즈경험 += 1
ELSEIF IS_MALE(TARGET) && IS_MALE(PLAYER)
    TCVAR:획득ＢＬ경험 += 1
ENDIF

RETURN 1

;-------------------------------------------------
;実行판정処理
;자위と同じ
;-------------------------------------------------
@COM_ORDER_278, ARG, ARG:1
IF TEQUIP:ARG:음문발동
	CALL ADD_COM_ORDER, "판정無視", "음문 발동중"
	RETURN 0
ENDIF
IF TEQUIP:ARG:밧줄
	CALL ADD_COM_ORDER, "판정無視", "결박"
	RETURN 0
ENDIF
CALL COM_ORDER_11, ARG, ARG:1
;-------------------------------------------------
;実行値
;-------------------------------------------------
@COM_ORDER_NUM_278
RETURN 50
;--------------------------------------------------
;명령 거부의 메시지
;--------------------------------------------------
;@DENIAL_MESSAGE_COM_278
;PRINTL
;PRINTFORM %CALLNAME:TARGET%의 
;IF HAVE_PENIS(TARGET)
;    PRINT 페니스의 길이
;ELSE
;    SIF TALENT:비대음핵
 ;       PRINT 거대한 
  ;  PRINT 클리토리스의 길이
;ENDIF
;PRINTL 를 측정하려고 했지만, 거절되었다.
;PRINTW 조금 더 조교가 필요한 것 같다.

;---------------------------------------------------------------------
;조교 커맨드로 사용
;---------------------------------------------------------------------
@PENIS_SMESSAGE
PRINTW  ·
PRINTW  ·
PRINTW  ·
;일단 한계치를 설정
IF BASE:음핵 >= 250
    BASE:음핵 = 250
    PRINTFORM 아무래도 %CALLNAME:TARGET%의 
    IF TALENT:비대음핵
        PRINT 클리토리스가 
    ELSE
        PRINT 페니스가 
    ENDIF
    PRINTW 더 이상 성장하는 건 무리일 듯 싶다….
;이상한 사이즈가 되면(자) AV계 커맨드로 확장 경험이 나오게 될……예정
;특수 커맨드 전용 커맨드 이외만으로도 대응할 수 있으면 좋겠다
ELSEIF BASE:음핵 > 200
    PRINTFORM %CALLNAME:TARGET%의 
    IF TALENT:비대음핵
        PRINT 클리토리스는 
    ELSE
        PRINT 페니스는 
    ENDIF
    PRINTW 이미 상식 밖의 사이즈가 돼버렸다….
    IF (CFLAG:음핵성장 & 4) == 0
        CFLAG:음핵성장 |= 4
		;이상경험
		CALL ADDS_ABNORMAL_EXP("거대음핵", TARGET)
    ENDIF
;음핵 비대 이벤트
ELSEIF BASE:음핵 >= 150
    PRINTFORM %CALLNAME:TARGET%의 
    IF HAVE_PENIS(TARGET)
        PRINT 페니스는 
    ELSE
        PRINT 클리토리스는 
    ENDIF
    PRINTW 발기한 페니스 정도 길이다.
    IF TALENT:비대음핵 == 0 && !HAVE_PENIS(TARGET)
        PRINTFORMW %CALLNAME:TARGET%의 클리토리스는 [비대 음핵]이 되었다.
        TALENT:비대음핵 = 1
        IF (CFLAG:음핵성장 & 2) == 0
            CFLAG:음핵성장 |= 2
			CALL ADDS_ABNORMAL_EXP("비대음핵", TARGET)
        ENDIF
    ENDIF
ELSEIF BASE:음핵 > 100
    PRINTFORM %CALLNAME:TARGET%의 
    IF HAVE_PENIS(TARGET)
        PRINT 페니스는 
    ELSE
        PRINT 클리토리스는 
    ENDIF
    PRINTW 보통 페니스 정도의 길이다.
ELSEIF HAVE_PENIS(TARGET)
    PRINTFORMW %CALLNAME:TARGET%의 페니스는 약간 좀 작은 크기다.
ELSEIF BASE:음핵 > 50
    PRINTFORMW %CALLNAME:TARGET%의 클리토리스는 새끼 손가락 만큼 성장하고 있다.
ELSE
    PRINTFORMW %CALLNAME:TARGET%의 클리토리스는 아직 보통 사이즈다.
ENDIF

;음핵 사이즈 표시 일시 보존 변수로 LOCAL:150을 사용
LOCAL:150 = BASE:음핵 * BASE:음핵 / 150
PRINTFORMW 대체로 {LOCAL:150 / 10}. {LOCAL:150 % 10}cm 일까…

;--------------------------------------------------
;조교시 메시지
;--------------------------------------------------
@MESSAGE_COM278
PRINTL
IF ABL:순종 < 3 && MARK:굴복각인 <= 2
    PRINTFORM %조사선택(CALLNAME:PLAYER, "는")% 
    IF TALENT:반항적 || TALENT:꿋꿋함 || TALENT:건방짐
        PRINTL 몸을 흔들어대며 저항하고 있는 
    ELSEIF TALENT:겁쟁이 || TALENT:얌전함
        PRINTL 눈을 들어 째려보고 있는 
    ELSE
        PRINTL 싫다는 듯이 고개를 젓고 있는 
    ENDIF
    SIF TALENT:유아체형
        PRINT 아담한 체격의 
    PRINTFORMW %조사선택(CALLNAME:TARGET, "를")% 껴안아 테이블의 위에 다리를 벌리게 앉혔다.
ELSE
    PRINTFORM %조사선택(CALLNAME:TARGET, "는")% 
    ;음란 or음핵/음경
    IF TALENT:음란 || TALENT:창부
        PRINTL 황홀한 표정으로 가랑이에 손을 가져가
        PRINTFORML 기대하는 듯한 눈동자로 %조사선택(CALLNAME:PLAYER, "를")% 바라보며
    ;연모 or망신 or상애 or친애
    ELSEIF TALENT:연모 || TALENT:친애 || TALENT:망신
        PRINTL 수치감에  떨리는 손을 억지로 움직이며
    ELSE
        PRINTL 얼굴을 새빨갛게 하고 흠칫 흠칫 손을 움직여
    ENDIF
        IF HAVE_PENIS(TARGET)
            PRINT 페니스를 
        ELSE
            SIF TALENT:비대음핵
                PRINT 거대한 
            PRINT 클리토리스를 
        ENDIF
    PRINTW 드러냈다….
ENDIF
PRINTL

PRINT 측정기에 
IF HAVE_PENIS(TARGET)
    PRINT 페니스를 
ELSE
    SIF TALENT:비대음핵
        PRINT 거대한 
    PRINT 클리토리스를 
ENDIF
PRINTL 끼워 
IF TALENT:음모없음
    PRINT 매끌매끌한 음부의 감촉을 즐기면서 
ELSEIF BASE:음모 > 50 && BASE:음모 < 251
    PRINT 털이 나기 시작해 까끌까끌 한 피부의 감촉을 즐기면서 
ELSE
    PRINT 음모의 감촉을 즐기며 
ENDIF
PRINTW 차분히 길이를 재었다….


CALL PENIS_SMESSAGE

;===================================================
;조교타입
;===================================================
@COMTYPE_278
RETURN COMTYPE("특수커맨드계")

