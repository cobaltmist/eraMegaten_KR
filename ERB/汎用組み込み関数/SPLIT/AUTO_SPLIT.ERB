;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:AUTO_SPLIT.ERB
;	Facility	:인수로 전달된 ARGS에서、특정 문자열 ARGS:1로 구분했을 경우의 ARG번째 문자열을 반환하는 함수 @AUTO_SPLIT
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2011/09/25		Ｎ鳥					新規作成
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;@AUTO_SPLIT
;ARGS   操作する文字列
;ARGS:1 ARGSを区切る文字列
;ARG 　 ARGSを区切った後で、操作する箇所。ARGS:2が指定されている場合、発見したARGS:2の場所+ARG番目の文字列を返す。
;ARGS:2 検索する文字列
;================================================
@AUTO_SPLIT(ARGS , ARGS:1 , ARG = 0 , ARGS:2 = "")
#FUNCTIONS
#LOCALSSIZE 200
#LOCALSIZE 1
#DIMS 保存文字列, 1
IF ARGS != "재이용"
	VARSET LOCALS
	SPLIT ARGS , ARGS:1 , LOCALS
	保存文字列 = %ARGS%
ENDIF
IF ARGS:2 != ""
	LOCAL = FINDELEMENT(LOCALS , ARGS:2 , , , 1)
	IF LOCAL != -1
		SIF ARG + LOCAL < 0
			PRINTFORML 에러：함수 @AUTO_SPLIT에서 {LOCAL}번째 문자열에 %조사처리(ARGS:2,"를")% 발견했습니다만、제 3 인수 {ARG}의 지정이 잘못되었습니다.
		RETURNF LOCALS:(LOCAL + ARG)
	ELSE
		RETURNF
	ENDIF
ELSE
	SIF ARG < 0
		PRINTFORML 에러：함수 @AUTO_SPLIT의 제 3인수로 {ARG}가 건네졌습니다
	RETURNF LOCALS:ARG
ENDIF

