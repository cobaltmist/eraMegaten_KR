;--------------------------------------------------
;커맨드 선택용 입력함수 @INPUT_SPLIT
;ARGS를 ARGS:1로 SPLIT해 선택지로 하는 입력함수
;ARGS     = 선택에 관한 설명문
;ARGS:1   = 선택지리스트
;ARGS:2   = 선택지리스트 구분
;ARGS:3   = "취소"에 대응하는 선택지（공백시 비표시）
;ARG:1    = 일렬로 표시할 항목 수
;ARG:2    = 최초에 표시할 페이지
;ARG:3    = 선택지의 시작번호
;ARG:4    = "다음페이지" 에 대응하는 선택지 번호
;ARG:5    =      "취소" 에 대응하는 선택지 번호
;ARG:6    = "이전페이지" 에 대응하는 선택지 번호
;RESULT   = 선택지의 번호
;RESULT:1 = 선택지가 포함된 페이지
;RESULTS  = 선택지의 텍스트
;-------------------------------------------------
@INPUT_SPLIT(ARGS,ARGS:1,ARGS:2,ARGS:3 = "　",ARG:1 = 1,ARG:2 = 0,ARG:3 = 1,ARG:4 = 1001,ARG:5 = 0,ARG:6 = 1003)
#LOCALSIZE 2
#LOCALSSIZE 1
#DIM 표시스타트,1
#DIM 항목문자수,1
#DIM 항목수,1
#DIM 항목자릿수,1
#DIM 페이지,1
#DIM 페이지엔드,1000
VARSET RESULTS , "　"
표시스타트 = 1
항목문자수 = 0
ARGS:1 = %ARGS:3%%ARGS:2%%ARGS:1%
SPLIT ARGS:1,ARGS:2,RESULTS
FOR LOCAL,1,VARSIZE("RESULTS")
	SIF RESULTS:LOCAL == "　"
		BREAK
	항목문자수 = MAX(항목문자수,STRLENS(RESULTS:LOCAL)+1)
	항목수 = LOCAL
NEXT
항목자릿수 = STRLENS(TOSTR(항목수+ARG:3))
WHILE 항목문자수+항목자릿수+2 >= 80 / ARG:1 && ARG:1 != 1
	ARG:1 --
WEND

VARSET 페이지엔드,-1
페이지 = 0
LOCAL:1 = 20*ARG:1
FOR LOCAL,1,항목수+2
	SIF RESULTS:LOCAL != ""
		LOCAL:1 --
	페이지엔드:페이지 = LOCAL
	SIF LOCAL:1 != 0
		CONTINUE
	LOCAL:1 = 20*ARG:1
	페이지 ++
NEXT	
페이지 = MIN(ARG:2,FINDELEMENT(페이지엔드,-1)-1)

$RE_DRAW
PRINTFORML %ARGS%
DRAWLINE
LOCAL:1 = ARG:1
FOR LOCAL,페이지 == 0 ? ARG:3 # 페이지엔드:(페이지-1),페이지엔드:페이지
	SIF RESULTS:LOCAL == ""
		CONTINUE
	PRINTFORM [{LOCAL,항목자릿수}]%RESULTS:LOCAL,항목문자수,LEFT%
	LOCAL:1 --
	SIF LOCAL:1 != 0
		CONTINUE
	PRINTL 
	LOCAL:1 = ARG:1
NEXT
$END_PRINT
SIF !LINEISEMPTY()
	PRINTL
DRAWLINE
PRINTFORMLC \@ 페이지 != 0                   ? [{ARG:4}]이전 페이지 # 　\@
PRINTFORMLC \@ RESULTS != "　"               ? [{ARG:5}]%RESULTS%  # 　\@
PRINTFORMLC \@ 페이지엔드:(페이지+1) != -1 ? [{ARG:6}]다음 페이지 # 　\@
PRINTL
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE ARG:4
		페이지 = MAX(0,페이지-1)
		GOTO RE_DRAW
	CASE ARG:6
		페이지 += 페이지엔드:(페이지+1) != -1 ? 1 # 0
		GOTO RE_DRAW
	CASE ARG:5
		LOCALS = %RESULTS:0%
	CASE ARG:3 TO ARG:3+항목수-1
		LOCALS = %RESULTS:MAX(RESULT-ARG:3+1,0)%
	CASEELSE
		GOTO INPUT_LOOP
ENDSELECT
SIF EQUALCHECK_STR(LOCALS,"","　")
	GOTO INPUT_LOOP
VARSET RESULTS
RESULTS = %LOCALS%
RETURN RESULT,페이지
